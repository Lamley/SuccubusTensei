;FileName_SYSTEM_DEBUG_Universal_eraTW.ERB -------------------------------- Rev1.06
;デバッグモード起動: @DEBUG_MENU_U
;Usage: Modify SHOP File to add "CALL DEBUG_MENU_U"
;
;Modify by Tsubasa (2015/5/28 4th Version)
;
;Original File: eraToho SYSTEM_DEBUG.ERB
;=============================================================================
;キャラリスト表示用の命令実装群
;=============================================================================
;-------------------------------------------------
;リストのキャラデータをセットする関数
;-------------------------------------------------
;CALLF SET_LIST, [リスト位置], [キャラ番号]
@SET_LIST(ARG, ARG:1)
#FUNCTION
RETURNF LIST_DATA("SET", ARG, ARG:1)

;-------------------------------------------------
;リストのキャラデータをセットする関数
;-------------------------------------------------
;[変数等] = GET_LIST([リスト位置])
@GET_LIST(ARG)
#FUNCTION
RETURNF LIST_DATA("GET", ARG)

;-------------------------------------------------
;内部で使用される関数
;-------------------------------------------------
;直接これを呼び出す必要はない
@LIST_DATA(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF ARG:1
ELSEIF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF -1
ELSE
	RETURNF -1
ENDIF

;-------------------------------------------------
;リストをクリアする関数
;-------------------------------------------------
;CALLF CLEAR_LIST
@CLEAR_LIST
#FUNCTION
RETURNF LIST_DATA("CLEAR")

;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;デバッグモード用の特殊指令
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;以下焼き芋弁当パッチ用のデバッグモードを起動しているときに使える特殊な処理をここで一括で管理
;大きく分けて調教中に使えるものとメイン菜單(SHOP)が開いているときに使用できるものに分かれる
;=============================================================================
;調教中に使用可能な指令
;=============================================================================
@DEBUG_MENU_U
DRAWLINE
PRINTFORML 已经{DAY}天
TARGET = 0
IF TARGET >= 0
	PRINTFORM %CALLNAME:TARGET% 選擇中　
ENDIF
PRINTFORML (現有{MONEY}圓)
DRAWLINE
PRINTL 你要準備做什麼？
PRINTL 
PRINTLC [ 0] - 角色編輯
PRINTLC [ 1] - 修改金錢
PRINTLC [ 2] - FLAG操作
PRINTLC [ 3] - 角色全回復
PRINTLC [ 4] - ITEM編輯
PRINTLC [97] - 全員妊娠加速
PRINTLC [98] - 全員變得経験豊富
PRINTLC [99] - 跳至下一天
PRINTLC [100] - 返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 0
	LOCAL = MASTER
	LOCAL:1 = TARGET
	LOCAL:2 = ASSI
	CALL EDIT_CHARA
	MASTER = LOCAL
	TARGET = LOCAL:1
	ASSI = LOCAL:2
ELSEIF RESULT == 1
	CALL EDIT_MONEY
ELSEIF RESULT == 2
	CALL EDIT_FLAG
ELSEIF RESULT == 3
	CALL EDIT_HEAL_ALL
ELSEIF RESULT == 4
	CALL EDIT_ITEM
ELSEIF RESULT == 97
	CALL PREGNANCY_FAST
ELSEIF RESULT == 98
	CALL EDIT_ALLSTATUS
ELSEIF RESULT == 99
	CALL NEXTDAY
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART

;--------------------------------------------------
;所持金変更処理
;--------------------------------------------------
@EDIT_MONEY
PRINTFORML (持有{MONEY:0}圓 or 持有{MONEY:1}圓 or 持有{MONEY:2}圓)
DRAWLINE
FOR LOCAL, 0, 3
	PRINTFORMLC [{LOCAL, 3}]MONEY:{LOCAL, 3}  = {MONEY:LOCAL}
NEXT
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 3
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML MONEY:{LOCAL} 修改為：(現在{MONEY:LOCAL})
	PRINTFORML 請輸入数値[0-99999999]（輸入無效數值則取消）
	INPUT
	SIF RESULT < 0 || RESULT > 99999999
		RESTART
	MONEY:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
;キャラ全快
;--------------------------------------------------
@EDIT_HEAL_ALL
REPEAT CHARANUM
	BASE:COUNT:0 = MAXBASE:COUNT:0
	BASE:COUNT:1 = MAXBASE:COUNT:1
REND
PRINTW 全員体力・气力已經回復

;--------------------------------------------------
;キャラ書き換え
;--------------------------------------------------
@EDIT_CHARA
;前後のキャラを表示できるか判定
LOCAL:1 = -1
LOCAL:2 = -1
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == TARGET
		CONTINUE
	IF LOCAL < TARGET
		LOCAL:1 = LOCAL
	ELSE
		LOCAL:2 = LOCAL
		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTFORML \@(TARGET < 0) ? 請選擇對象 # 現在「%CALLNAME:TARGET%」選擇中\@
DRAWLINE
PRINTFORMLC [  0] - \@(TARGET >= 0) ? 対象変更 # 対象選擇\@
IF TARGET >= 0
;	PRINTLC [  1] - 対象情報
	PRINTLC [  2] - ABL編輯
	PRINTLC [  3] - EXP編輯
	PRINTLC [  4] - MARK編輯
	PRINTLC [  5] - TALENT編輯
	PRINTLC [  6] - JUEL編輯
	PRINTLC [  7] - CFLAG操作
	PRINTLC [  8] - PALAM編輯
	PRINTLC [  9] - BASE編輯
	PRINTLC [  10] - MAXBASE編輯
ENDIF
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL:1 < 0) ? %" " * 16% # [1011]%CALLNAME:(LOCAL:1)% 編輯\@
PRINTFORMLC [1000]返回
PRINTFORMLC \@(LOCAL:2 < 0) ? %" " * 16% # [1019]%CALLNAME:(LOCAL:2)% 編輯\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1011 && LOCAL:1 >= 0
	TARGET = LOCAL:1
ELSEIF RESULT == 1019 && LOCAL:2 >= 0
	TARGET = LOCAL:2
ELSEIF RESULT == 0
	CALL SELECT_TARGET
ELSEIF TARGET < 0
	PRINTW 對象已經選擇。
;ELSEIF RESULT == 1
;	CALL PRINT_STATE(TARGET)
ELSEIF RESULT == 2
	CALL EDIT_ABL
ELSEIF RESULT == 3
	CALL EDIT_EXP
ELSEIF RESULT == 4
	CALL EDIT_MARK
ELSEIF RESULT == 5
	CALL EDIT_TALENT
ELSEIF RESULT == 6
	CALL EDIT_JUEL
ELSEIF RESULT == 7
	CALL EDIT_CFLAG
ELSEIF RESULT == 8
	CALL EDIT_PALAM
ELSEIF RESULT == 9
	CALL EDIT_BASE
ELSEIF RESULT == 10
	CALL EDIT_MAX_BASE
ENDIF
RESTART

@SELECT_TARGET
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
CALLF CLEAR_LIST
LOCAL:1 = CHARANUM / 20

$PRINT_LIST
DRAWLINE
SIF TARGET >= 0
	PRINTFORML 現在選擇角色是:%CALLNAME:TARGET%
PRINTFORML 你要選擇哪個對象？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT CHARANUM
	SIF COUNT >= LOCAL * 20 && COUNT < (LOCAL+1) * 20
		PRINTFORML [{COUNT}]%NAME:COUNT%
REND
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前頁\@
PRINTLC [1000]返回
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009]次頁\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

TARGET = RESULT
PRINTFORML %CALLNAME:TARGET% 已選擇

;--------------------------------------------------
; PALAM操作
;--------------------------------------------------
@EDIT_PALAM
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%PALAMNAME:LOCAL% ({PALAM:LOCAL}) 
NEXT
PRINTLC [199]PALAM 重置
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET PALAM, 0
	PRINTW PALAM已經全部重置
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML %PALAMNAME:LOCAL%修改為：(現在{PALAM:LOCAL})
	PRINTFORML 請輸入数値[0-9999999]（無効数値則取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	PALAM:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; BASE操作
;--------------------------------------------------
@EDIT_BASE
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {BASE:LOCAL}
NEXT
PRINTLC [199]BASE重置
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET BASE, 0
	PRINTW BASE已經全部重置
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(BASENAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL} # %BASENAME:LOCAL%\@ 修改為：(現在{BASE:LOCAL})
;	PRINTFORML %BASENAME:LOCAL%の変更(現在{BASE:LOCAL})
	PRINTFORML 請輸入数値[0-9999999]（無効数値則取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	BASE:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; ABL操作
;--------------------------------------------------
;ABL変更
@EDIT_ABL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%ABLNAME:LOCAL%LV ({ABL:LOCAL})  
NEXT
PRINTL 
PRINTLC [200]返回
PRINTLC [201]全ABL修改為99
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	ABL:LOCAL = 99
	NEXT
	PRINTW ABL 全部修改為99了
	RESTART
SIF RESULT < 0 || RESULT > 107
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML %ABLNAME:LOCAL%LV修改為(現在{ABL:LOCAL})
	PRINTFORML 請輸入数値[-999-999]（無効数値則取消）
	INPUT
	SIF RESULT < -999|| RESULT > 999
		RESTART
	ABL:LOCAL = RESULT
ENDIF

RESTART

;--------------------------------------------------
;素質編集
;--------------------------------------------------
@EDIT_TALENT
LOCAL = 0
LOCAL:1 = 4
CALLF CLEAR_LIST
;表示させる素質を抽出
REPEAT 1000
	;存在しない素質は排除
	STRLENS TALENTNAME:COUNT
	SIF RESULT == 0
		CONTINUE
	CALLF SET_LIST, COUNT, 1
REND
$PRINT_LIST
DRAWLINE
PRINTFORML 哪個素質要修改呢？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 60
	LOCAL:2 = COUNT + LOCAL * 60
	SIF GET_LIST(LOCAL:2) == 0
		CONTINUE
	IF TALENT:(LOCAL:2) == 0
		PRINTFORMLC [{LOCAL:2, 3}]×【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) == 1
		PRINTFORMLC [{LOCAL:2, 3}]◎【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) > 1
		PRINTFORMLC [{LOCAL:2, 3}]○【%TALENTNAME:(LOCAL:2)%】
	ENDIF
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前頁\@
PRINTLC [9999]返回
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009]次頁\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ELSEIF GET_LIST(RESULT) == 0
	GOTO INPUT_LOOP
ENDIF
SIF STRLENS(TALENTNAME:RESULT) == 0
	GOTO INPUT_LOOP
PRINTL [9999]返回
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML %TALENTNAME:(RESULT)%修改為
PRINTFORML 請輸入数値（-999 - 999, 請參照CSV文件進行）
INPUT
SIF RESULT < -999 || RESULT > 999
	RESTART
TALENT:(LOCAL:1) = RESULT
GOTO PRINT_LIST


;--------------------------------------------------
; MARK操作
;--------------------------------------------------
;MARK変更
@EDIT_MARK
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(MARKNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%MARKNAME:LOCAL% ({MARK:LOCAL})
NEXT
PRINTL [100]返回
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
SIF STRLENS(MARKNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %MARKNAME:LOCAL%LV修改為(現在{MARK:LOCAL})
PRINTFORML 請輸入数値[0-3]（無効数値則取消）
INPUT
SIF RESULT < 0 || RESULT >3
	RESTART
MARK:LOCAL = RESULT

RESTART

;--------------------------------------------------
; EXP操作
;--------------------------------------------------
;EXP変更
@EDIT_EXP
DRAWLINE
FOR LOCAL:1, 0, 100
	LOCAL = LOCAL:1
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%EXPNAME:LOCAL% ({EXP:LOCAL})　
NEXT
PRINTLC [200]返回
PRINTLC [201]EXP全部為999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	EXP:LOCAL = 999
	NEXT
	PRINTW EXP 全部最大化處理
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(EXPNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %EXPNAME:LOCAL%修改為(現在{EXP:LOCAL})
PRINTFORML 請輸入数値（負数則取消）
INPUT
SIF RESULT < 0
	RESTART
EXP:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; JUEL操作
;--------------------------------------------------
;JUEL変更
@EDIT_JUEL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%PALAMNAME:LOCAL%の珠({JUEL:LOCAL}) 
NEXT
PRINTLC [200]返回
PRINTLC [201]JUEL全部9999999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	JUEL:LOCAL = 9999999
	NEXT
	PRINTW JUEL最大化處理完畢
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(PALAMNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %PALAMNAME:LOCAL%の珠修改為：(現在{JUEL:LOCAL})
PRINTFORML 請輸入数値（負数則取消）
INPUT
SIF RESULT < 0
	RESTART
JUEL:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; CFLAG操作
;--------------------------------------------------
@EDIT_CFLAG
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 現在目前的CFLAG (MAX = 1000)
PRINTFORML 哪些需要修改？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] \@ (STRLENS(CFLAGNAME:COUNT) == 0) ? CFLAG:{COUNT, 3} # %CFLAGNAME:COUNT%\@ = {CFLAG:COUNT}
;	PRINTFORMLC [{COUNT, 3}] TFLAG:{COUNT, 3} = {CFLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前頁\@
PRINTLC [1000]返回
PRINTFORMLC \@(LOCAL >= 19) ? %" " * 16% # [1009]次頁\@
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < 19
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML \@ (STRLENS(CFLAGNAME:RESULT) == 0) ? CFLAG:{RESULT} # %CFLAGNAME:RESULT%\@ 修改為(現在{CFLAG:RESULT})
;PRINTFORML CFLAG:{RESULT}の変更(現在{CFLAG:RESULT})
PRINTFORML 請輸入数値（数値-999則取消）
INPUT
SIF RESULT == -999
	RESTART
CFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;TFLAG編集
;--------------------------------------------------
@EDIT_TFLAG
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 現在目前的TFLAG
PRINTFORML 哪些需要修改？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] TFLAG:{COUNT, 3} = {TFLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]返回
PRINTFORMLC \@(LOCAL >= 19) ? %" " * 16% # [1009]次のページ\@
PRINTL [1010]調教FLAG全部重置(要注意)
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < 19
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1010
	VARSET TFLAG, 0
	PRINTW 調教FLAG已經全部重置
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML TFLAG:{RESULT}修改為(現在{TFLAG:RESULT})
PRINTFORML 請輸入数値（負数為取消）
INPUT
SIF RESULT < 0
	RESTART
TFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;FLAG編集
;--------------------------------------------------
;説明付与
@DEFINE_FLAG, ARG
IF STRLENS(FLAGNAME:ARG) > 0
	PRINTS FLAGNAME:ARG
ELSE
	PRINT ――
ENDIF

@EDIT_FLAG
VARSET LOCAL, 0
LOCAL:1 = 199
$PRINT_LIST
DRAWLINE
PRINTFORML 現在目前的FLAG
PRINTFORML 哪些需要修改？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 10000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 4}] \@ (STRLENS(FLAGNAME:COUNT) == 0) ? FLAG:{COUNT, 4} # %FLAGNAME:COUNT%\@ = {FLAG:COUNT}
;	PRINTFORMLC [{COUNT, 4}] FLAG:{COUNT, 4} = {FLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 17% # [10001]前頁\@
PRINTLC [10000]返回
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 17% # [10009]次頁\@
PRINTL 
PRINTFORMLC \@(LOCAL/10 <= 0) ? %" " * 17% # [10011]10頁以前\@
PRINTFORMLC %" " * 17%
PRINTFORMLC \@(LOCAL/10 >= LOCAL:1/10) ? %" " * 17% # [10019]10頁以後\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 10000
	RETURN 0
ELSEIF RESULT == 10001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10011 && LOCAL/10 > 0
	LOCAL -= 10
	GOTO PRINT_LIST
ELSEIF RESULT == 10019 && LOCAL/10 < LOCAL:1/10
	LOCAL += 10
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 10000
	GOTO INPUT_LOOP
ENDIF
LOCAL:2 = RESULT
DRAWLINE
PRINTFORM FLAG:{LOCAL:2}(
CALL DEFINE_FLAG, LOCAL:2
PRINTFORML )修改為(現在{FLAG:(LOCAL:2)})
PRINTFORML 請輸入数値（負数則取消）
INPUT
SIF RESULT < 0
	RESTART
FLAG:(LOCAL:2) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
; MAXBASE操作
;--------------------------------------------------
@EDIT_MAX_BASE
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {MAXBASE:LOCAL}
;	PRINTFORMLC [{LOCAL, 3}]%BASENAME:LOCAL% ({MAXBASE:LOCAL}) 
NEXT
PRINTLC [200]返回
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL} # %BASENAME:LOCAL%\@ の上限修改(現在{MAXBASE:LOCAL})
	PRINTFORML 請輸入数値[0-9999999]（無効数値則取消）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	MAXBASE:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; ITEM操作
;--------------------------------------------------
;ITEMの有無をCSVから取得
@DISP_ITEM(ARG)
#FUNCTION
RETURNF (STRLENS(ITEMNAME:ARG)) ? 1 # 0

;ITEM変更
@EDIT_ITEM
DRAWLINE
FOR LOCAL:1, 0, 1000
	LOCAL = LOCAL:1
	SIF DISP_ITEM(LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%ITEMNAME:LOCAL% ({ITEM:LOCAL})　
NEXT
PRINTLC [9999]返回
PRINTL 
$INPUT_LOOP
INPUT
SIF RESULT == 9999
	RETURN 0
SIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
LOCAL = RESULT
SIF STRLENS(ITEMNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %ITEMNAME:LOCAL%修改為(現在{ITEM:LOCAL})
PRINTFORML 請輸入数値（負数則取消）
INPUT
SIF RESULT < 0
	RESTART
ITEM:LOCAL = RESULT
RESTART

;--------------------------------------------------
;全員妊娠加速
;--------------------------------------------------
@PREGNANCY_FAST
LOCAL = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出産経過日 > 0
		CALL BIRTH_FAST(LOCAL)
	SIF CFLAG:LOCAL:妊娠経過日数 > 0
		CALL PREGNANCY_UPUP(LOCAL)
NEXT

@PREGNANCY_UPUP(LOCAL)
IF CFLAG:LOCAL:妊娠経過日数 > 71
	CFLAG:LOCAL:妊娠経過日数 = 100
	PRINTFORMW %CALLNAME:LOCAL% 已生產。({CFLAG:LOCAL:妊娠経過日数})
ELSEIF CFLAG:LOCAL:妊娠経過日数 > 60 
	CFLAG:LOCAL:妊娠経過日数 = 71
	PRINTFORMW %CALLNAME:LOCAL% 要出產了。({CFLAG:LOCAL:妊娠経過日数})
ELSEIF CFLAG:LOCAL:妊娠経過日数 > 40 
	CFLAG:LOCAL:妊娠経過日数 = 60
	PRINTFORMW %CALLNAME:LOCAL% 流出母乳了。({CFLAG:LOCAL:妊娠経過日数})
ELSEIF CFLAG:LOCAL:妊娠経過日数 > 30 
	CFLAG:LOCAL:妊娠経過日数 = 40
	PRINTFORMW %NAME:LOCAL% 肚子已經很大了。({CFLAG:LOCAL:妊娠経過日数})
ELSEIF CFLAG:LOCAL:妊娠経過日数 > 20 
	CFLAG:LOCAL:妊娠経過日数 = 30
	PRINTFORMW %NAME:LOCAL% 能看得出肚子鼓起來了。({CFLAG:LOCAL:妊娠経過日数})
ELSEIF CFLAG:LOCAL:妊娠経過日数 > 0 
	CFLAG:LOCAL:妊娠経過日数 = 20
	PRINTFORMW %CALLNAME:LOCAL% 有身孕了。({CFLAG:LOCAL:妊娠経過日数})
ENDIF

@BIRTH_FAST(LOCAL)
IF CFLAG:LOCAL:出産経過日 > 60
	CFLAG:LOCAL:出産経過日 = 120
	PRINTFORMW 遠遠看著孩子 %CALLNAME:LOCAL%，%CALLNAME:MASTER% 祈禱一切健康。({CFLAG:LOCAL:出産経過日})
ELSEIF CFLAG:LOCAL:出産経過日 > 20
	CFLAG:LOCAL:出産経過日 = 60
	PRINTFORMW %CALLNAME:LOCAL%和%CALLNAME:MASTER% 不再喂奶了。({CFLAG:LOCAL:出産経過日})
ELSEIF CFLAG:LOCAL:出産経過日 > 0
	CFLAG:LOCAL:出産経過日 = 20
	PRINTFORMW 順利生產完成，%CALLNAME:LOCAL% 恢復元氣了。。({CFLAG:LOCAL:出産経過日})
ENDIF

;--------------------------------------------------
;全員経験豊富操作
;--------------------------------------------------
@EDIT_ALLSTATUS
LOCAL = 0
FOR LOCAL,1,CHARANUM
	FLAG:1007 = 1
	EXP:LOCAL:Ｃ経験 = 999
	EXP:LOCAL:Ｂ経験 = 999
	EXP:LOCAL:Ｖ経験 = 999
	EXP:LOCAL:Ａ経験 = 999
	EXP:LOCAL:手淫経験 = 999
	EXP:LOCAL:口淫経験 = 999
	EXP:LOCAL:乳交経験 = 999
	EXP:LOCAL:接吻経験 = 999
	EXP:LOCAL:奉仕快楽経験 = 999
	EXP:LOCAL:愛情経験 = 999
	EXP:LOCAL:苦痛快楽経験 = 999
	EXP:LOCAL:嗜虐快楽経験 = 999
	EXP:LOCAL:露出快楽経験 = 999
	EXP:LOCAL:絶頂経験 = 999
	TALENT:LOCAL:性別嗜好 = 0
	TALENT:LOCAL:濡れやすさ = 1
	TALENT:LOCAL:習得速度 = 1
	TALENT:LOCAL:快感応答 = 1
	TALENT:LOCAL:容易高潮 = 1
	TALENT:LOCAL:施虐狂 = 1
	TALENT:LOCAL:Ｃ感度 = 1
	TALENT:LOCAL:Ｖ感度 = 1
	TALENT:LOCAL:Ａ感度 = 1
	TALENT:LOCAL:Ｂ感度 = 1
;	TALENT:LOCAL:胸囲 = 1
	TALENT:LOCAL:Ｍ感度 = 1
	CFLAG:LOCAL:好感度 = 9999
	CFLAG:LOCAL:信頼度 = 9999
	CFLAG:LOCAL:子宝補正 = 50
	ABL:LOCAL:Ｖ感覚 = MAX(ABL:LOCAL:Ｖ感覚, 50)
	ABL:LOCAL:親密 = MAX(ABL:LOCAL:親密, 50)
	ABL:LOCAL:従順 = MAX(ABL:LOCAL:従順, 50)
	ABL:LOCAL:欲望 = MAX(ABL:LOCAL:欲望, 50)
	ABL:LOCAL:技巧 = MAX(ABL:LOCAL:技巧, 50)
	ABL:LOCAL:指 = MAX(ABL:LOCAL:指 , 50)
	ABL:LOCAL:舌 = MAX(ABL:LOCAL:舌, 50)
	ABL:LOCAL:胸 = MAX(ABL:LOCAL:胸, 50)
	ABL:LOCAL:腰 = MAX(ABL:LOCAL:腰, 50)
	ABL:LOCAL:膣 = MAX(ABL:LOCAL:膣, 50) 
	ABL:LOCAL:肛 = MAX(ABL:LOCAL:肛, 50)
NEXT
PRINTW 全員都很経験豊富。


;--------------------------------------------------
;NEXTDAY
;--------------------------------------------------
@NEXTDAY
DAY += 1
BEGIN AFTERTRAIN
