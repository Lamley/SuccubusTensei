;FileName_USERCOM.ERB ------------------------------ Rev1.00
;コマンド判定処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@USERCOM
#DIM 能力表示用ダミー
#DIM 装備部位
#DIM パンツ消費
#DIM なだめ判定
$LOOP_USERCOM
;一般コマンド(COMF)
IF RESULT >= 0 && RESULT < 800
	;帯出去判定
	IF RESULT == 351 && !CHK_DATENOW(CFLAG:MASTER:デート中)
		CALL COM_ABLE351
		SIF !RESULT
			RETURN 0
		PRINTL 帯出去
		STR:0 = 帯出去
		CALL COM351
		RETURN 0
	ENDIF
	;コマンド登録チェック
	SIF TRAINNAME:RESULT == ""
		RETURN 0
	;SELECTCOMとTFLAG:3(SELECTCOM退避)に入力値セット
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	;一部コマンドの特殊制限
	SELECTCASE SELECTCOM
		CASE 100 TO 107,140 TO 145
			IF TALENT:TARGET:妊娠
				PRINTW 不能対孕妇做这种事
				RETURN 0
;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 4)
;				CALL ＳＭ許可
			ENDIF
		CASE 60 TO 71
			IF TALENT:TARGET:妊娠 && (CFLAG:TARGET:903 <= 30 || CFLAG:TARGET:903 >= 71)
				PRINTFORMW 现在最好不要対%CALLNAME:TARGET%乱来比较好
				RETURN 0
;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 3)
;				CALL Ａ許可
			ENDIF
	ENDSELECT
	SIF GLOBAL_COMABLE2(SELECTCOM)
		RETURN 0
	;コマンド使用可能判定
	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		RETURN 0
	;実行制限(実行拒否等)チェック
	TRYCALLFORM CAN_COM{SELECTCOM}(1)
	IF RESULT == 0
		;実行値表示のため再度CAN_COM
		TRYCALLFORM CAN_COM{SELECTCOM}(0)
		RETURN 0
	ENDIF
	;ダミーTRAIN
	DOTRAIN 999
;履歴表示（未実装）
ELSEIF RESULT == 800
	CALL SHOW_EQUIP
	TFLAG:Info表示中 = 0
	RETURN 1
;汚れ表示
ELSEIF RESULT == 801
	CALL STAIN_INFO
	TFLAG:Info表示中 = 0
	RETURN 1
;TARGET変更
ELSEIF RESULT == 802
	IF !CFLAG:添い寝中
		CALL TARGET_CHANGE
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
;TARGETの状態表示
ELSEIF RESULT == 803
	IF TARGET == MASTER
		CALL SINGLE_CHARA_STATE(MASTER, "「調教中」")
	ELSE
		CALL TARGET_LIST_STATE
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;MASTERの状態表示
ELSEIF RESULT == 804
	CALL SINGLE_CHARA_STATE(MASTER, "「調教中」")
	TFLAG:Info表示中 = 0
	RETURN 1
;コマンドフィルタ
ELSEIF RESULT == 809
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 815 && FLAG:70 && !CFLAG:TARGET:睡眠 && AT_HOME(MASTER)
	IF BATHCHECK(CFLAG:MASTER:現在位置) || IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTFORMW 果然在这个地方不太合适
	ELSEIF TFLAG:運搬 != TARGET
		PRINTFORMW %CALLNAME:MASTER%背起了%CALLNAME:TARGET%
		TFLAG:運搬 = TARGET
	ELSE
		PRINTFORMW %CALLNAME:MASTER%放下了%CALLNAME:TARGET%
		TFLAG:運搬 = 0
	ENDIF
ELSEIF RESULT == 816
	FLAG:AA表示 = !FLAG:AA表示
;集合
ELSEIF RESULT == 817 && FLAG:70 && TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	CALL SYUGO()
ELSEIF RESULT == 817
	FLAG:挿絵表示 = !FLAG:挿絵表示
;服を脱がせる
ELSEIF RESULT == 820 && EQUIP:0 && !(TFLAG:102 == 3 && TFLAG:抱きつきモード)
	CALL 服を脱がせる
;内袴をくすねるコマンドの切り離し
ELSEIF RESULT == 821 && EQUIP:0 && EQUIP:TARGET:下半身内袴２
	IF !TARGET && !FLAG:70 && !CFLAG:MASTER:イタズラ
		CALL SIMATTYAUOJISAN
	ELSE
		SIF CFLAG:TARGET:291 == 0
			CALL 脱衣処理(TARGET, "くすねる")
	ENDIF
;上半身内袴を脱がす
ELSEIF RESULT == 822 && EQUIP:TARGET:0 && EQUIP:TARGET:上半身内袴１ != 0
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		IF EQUIP:TARGET:上半身上衣１ || EQUIP:TARGET:上半身上衣２ || EQUIP:TARGET:連衣裙 || EQUIP:TARGET:和服 || EQUIP:TARGET:連体内衣
			PRINTFORM 将%CALLNAME:TARGET%的%SHOW_TOP(TARGET,1)%袒露開来、
		ELSE
			PRINTFORM %CALLNAME:TARGET%的
		ENDIF
		PRINTFORMW %CLOTHNAME(7,EQUIP:TARGET:上半身内袴１)%直接脱掉
		CALL MESSAGE_脱衣上
		EQUIP:TARGET:上半身内袴１ = 0
		TEQUIP:TARGET:上半身着衣状況 = 1
		TEQUIP:TARGET:上半身はだけ状態 = 1
		EQUIP:MASTER:上半身内袴１ = 0
		TEQUIP:MASTER:上半身着衣状況 = 1
		TEQUIP:MASTER:上半身はだけ状態 = 1
		RETURN 0
	ENDIF
;内袴姿にする
ELSEIF RESULT == 823 && EQUIP:0 && (EQUIP:TARGET:14 != 0 || EQUIP:TARGET:15 != 0 || EQUIP:TARGET:16 != 0 || EQUIP:TARGET:17 != 0 || EQUIP:TARGET:18 != 0)
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		CALL 脱衣処理(TARGET, "内袴姿にする")
		CALL 脱衣処理(MASTER, "内袴姿にする")
	ENDIF
;身体測定する
ELSEIF RESULT == 824 && MEASUREMENTCHECK(CFLAG:TARGET:現在位置)
	IF CFLAG:TARGET:身体測定 != 2
		PRINTFORML 対%CALLNAME:TARGET%进行了身体測定
		PRINTFORMW ※　随着每次用能力显示而変動。进行固定时請选択結束
		CFLAG:TARGET:身体測定 = 2
		CALL MAKE_SIZE(TARGET)
	ELSE
		PRINTFORMW %CALLNAME:TARGET%的身体測定結束了
		CFLAG:TARGET:身体測定 = 1
	ENDIF
	RETURN 0
;内袴収奪コマンドし
ELSEIF RESULT == 825 && TFLAG:パンツ収奪
	IF !FLAG:70
		CALL SIMATTYAUOJISAN
	ELSE
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:LOCAL:ノーパン && EQUIP:LOCAL:下半身内袴２
				PRINTFORMW 将%CALLNAME:LOCAL%穿着的%PANTSNAME(EQUIP:LOCAL:下半身内袴２)%脱下纳入怀中
				CALL PANTS_GET(LOCAL)
			ENDIF
		NEXT
		TFLAG:Info表示中 = 0
		RETURN 0
	ENDIF
ELSEIF RESULT == 826
	IF TEQUIP:焦らしモード
		TEQUIP:焦らしモード = 0
	ELSE
		IF TCVAR:MASTER:イきそう
			PRINTFORMW 已経没有那么做的余裕了！
	 	ELSEIF (EX:20 + EX:21 + EX:22 + EX:23 + EX:24) > 0
	 		PRINTFORMW 在挑逗中
	 	ELSE
	 		TEQUIP:焦らしモード = 1
	 	ENDIF
	 ENDIF
;過激なイタズラ（未実装）
ELSEIF RESULT == 830 && CFLAG:イタズラ < 2
	PRINTFORML %CALLNAME:MASTER%将%CALLNAME:TARGET%的衣服慢慢地袒露開来
	EQUIP:0 = 0
	SIF CFLAG:TARGET:睡眠姦 < 2
	CFLAG:TARGET:睡眠姦 = 2
	CALL SET_PRANK(2,TARGET,1)
	CALL 脱衣処理(TARGET, "全裸", "「表示なし」")
	FOR LOCAL,1,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	NEXT
	RETURN 1
;test
;ELSEIF RESULT == 99999
;	TIME:5 = 4
;	RETURN 1
;抜け出す
ELSEIF RESULT == 850
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	DOTRAIN 999
;居場所察知=============================================================================================================================
ELSEIF RESULT == 811 && TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL HITOSAGASI()
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;====================================================
ELSEIF RESULT == 812 && TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL OHEYA_CHECK
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
ELSEIF RESULT == 813 && TALENT:MASTER:劣情察知 > 1 && !CFLAG:うふふ
	PRINTFORML 花费精力来寻找性欲高涨的少女。
	;CALL ASK_M("やめる",1,"発動",1,"触媒を使って発動",CSTR:MASTER:12 != "")
	CALL ASK_M("放棄",1,"発動",1,"使用触媒発動",FLAG:パンツ輸送)
	SELECTCASE RESULT
		CASE 0
			RETURN -1
		CASE 1,2
			PRINTFORMW %CALLNAME:MASTER%\@RESULT == 2 ?対着脱下的胖次、 # \@集中精神！
			SETCOLOR 255,100,255
			FONTBOLD
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:溜まってる度 >= 500
					PRINTFORML %CALLNAME:LOCAL%欲求不満…！
					SIF CFLAG:LOCAL:現在位置 == SUKIMA() && RESULT == 2
						PRINTFORML 然后，%CALLNAME:LOCAL%现在在家里！　……大概。
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
			IF RESULT == 1
				BASE:MASTER:気力 -= 500
			ELSE
				PRINTFORMW %CALLNAME:MASTER%在放松下来的时候，手中的胖次慢慢的融化在空气中了
				CSTR:MASTER:12 =
				FLAG:パンツ輸送 = 0
				VARSET PANTS_TEMP
				BASE:MASTER:気力 -= 50
			ENDIF
			SIF !FLAG:70
				TIME += 5
			TFLAG:Info表示中 = 0
			RETURN 1
	ENDSELECT
ELSEIF RESULT == 830
	IF !GETBIT (FLAG:1013,0)
			SETBIT FLAG:1013,0
	ELSE
		CLEARBIT FLAG:1013,0
	ENDIF
;フィルタ----------------------------
ELSEIF RESULT >= 831 && RESULT <= 843
	INVERTBIT FLAG:1013,(RESULT - 830)
	RETURN -1
;未完成---------------------------- 

;ターゲット変更
ELSEIF RESULT >=1001 && RESULT < (CHARANUM + 1000) && CFLAG:(RESULT - 1000):同室
	TFLAG:104 = (RESULT - 1000)
;一日を終える
;ELSEIF RESULT == 888
;	BEGIN AFTERTRAIN
;神社に誘う
ELSEIF RESULT == 990 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CALL 帰宅の時間経過
		CALL SET_DATE(99,TARGET)
		CFLAG:TARGET:現在位置 = ENTRANCE
		CFLAG:MASTER:現在位置 = ENTRANCE
		CFLAG:TARGET:デート中 = MAIN_MAP
		CFLAG:MASTER:デート中 = MAIN_MAP
		CFLAG:TARGET:同行 = 60
		CFLAG:MASTER:同行 = 60
		IF TARGET == 93 && !GETBIT (FLAG:プール, 0)
			TIME += 30
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 500,500)
		ENDIF
		PRINTFORMW 帯着有空的%CALLNAME:TARGET%一起回家了
		RETURN 1
	ENDIF
;倉庫施錠
ELSEIF RESULT == 991 
	IF CFLAG:MASTER:初期位置 == 53 && CFLAG:MASTER:現在位置 == 53
		FLAG:倉庫施錠 = !FLAG:倉庫施錠
		RETURN 0
	ELSEIF FLAG:倉庫施錠
		FLAG:倉庫施錠 = 0
		RETURN 0
	ENDIF
;デートに誘う
ELSEIF RESULT == 992 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CFLAG:MASTER:同行 = 60
		CFLAG:TARGET:同行 = 60
		CFLAG:TARGET:デート中 = CFLAG:MASTER:現在位置 / 100
		FLAG:デート相手 = TARGET
		TFLAG:デート前好感度 = CFLAG:TARGET:好感度
		TIME += 1
		PRINTFORMW 帯着有空的%CALLNAME:TARGET%去约会了
	ENDIF
;カレンダー
ELSEIF RESULT == 993
	CALL カレンダー
ELSEIF RESULT == 994
	CALL SHOW_HAVE_COLLECTION()
;時間停止解除
ELSEIF RESULT == 999 && FLAG:70 == 1 && !CFLAG:うふふ
	CALL 時間停止解除
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上衣脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
		;身体測定終了
		SIF CFLAG:LOCAL:身体測定 == 2
			CFLAG:LOCAL:身体測定 = 1
	NEXT
;逢瀬終了
ELSEIF RESULT == 999 && CFLAG:うふふ
	SIF CFLAG:MASTER:イタズラ == 2
		CALL SET_PRANK(0,TARGET,2)
	IF CFLAG:うふふ == 2
			SELECTCOM = RESULT
			TFLAG:3 = SELECTCOM
			DOTRAIN 999
	ELSE
		IF FLAG:70 == 1
			FOR LOCAL,0,CHARANUM
				SIF CFLAG:うふふ == 2
					CALL SET_DERAY(5,LOCAL)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ELSE
			FOR LOCAL,0,CHARANUM
				IF CFLAG:LOCAL:うふふ == 1 && LOCAL != 0
					SIF !FLAG:70 == 1
					CALL KOJO_MESSAGE_SEND("EVENT",15,LOCAL,2,0)
				ENDIF
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ENDIF
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		TEQUIP:上半身はだけ状態 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上衣脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
		RETURN 1
	ENDIF
;イタズラ終了
ELSEIF RESULT == 999 && CFLAG:イタズラ && !CFLAG:うふふ
	FOR LOCAL,0,CHARANUM
		CALL SET_PRANK(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上衣脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;添い寝終了
ELSEIF RESULT == 999 && CFLAG:添い寝中
	FOR LOCAL,0,CHARANUM
		CALL SET_TOGETHER(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	CALL KOJO_MESSAGE_SEND("EVENT",16,TARGET,2,0)
	;衰弱なら寝る
	SIF CFLAG:TARGET:衰弱
		CALL CHARA_SLEEP, TARGET,0

	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上衣脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;デート終了
ELSEIF RESULT == 999 && CHK_DATENOW(CFLAG:MASTER:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL 外出先から帰宅
	RETURN 1
ELSEIF RESULT == 3000
	TFLAG:PALAM非表示 = !TFLAG:PALAM非表示
ELSEIF RESULT == 3001
	TFLAG:Status非表示 = !TFLAG:Status非表示
	
;リソース表示関連
ELSEIF RESULT == 3100
	;表示設定の切り替え
	FLAG:画像表示設定 = !FLAG:画像表示設定
ELSEIF RESULT == 3101
	;画像表示人数 -
	FLAG:画像表示 = MAX(FLAG:画像表示-1,1)
	
ELSEIF RESULT == 3102
	;画像表示人数 +
	FLAG:画像表示 = MIN(FLAG:画像表示+1,10)
	
ELSEIF RESULT == 3103
	;表示位置切り替え
	FLAG:画像表示位置 = !FLAG:画像表示位置

ELSEIF RESULT == 3104
	;枠表示 ON/OFF
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 3105
	FLAG:立ち絵種類 = !FLAG:立ち絵種類
ELSEIF RESULT == 9999
	FOR LOCAL,1,CHARANUM
		PRINTFORMLC %CALLNAME:LOCAL% 現在位置：{CFLAG:LOCAL:現在位置}
	NEXT
ENDIF
TFLAG:Info表示中 = 0
RETURN 0

;FileName_USERCOM.ERB ------------------------------ Rev1.00
;TARGET変更処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TARGET_CHANGE
PRINTFORML 做了%CALLNAME:TARGET%的対手
PRINTL [0] - 返回
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL% (好感度:{CFLAG:LOCAL:2})
NEXT
INPUT
IF RESULT <= 0 || RESULT >= CHARANUM
	RETURN 0
ELSEIF CFLAG:RESULT:現在位置 == CFLAG:MASTER:現在位置
	TFLAG:104 = RESULT
ENDIF

;----------------------------------------------------------
;FileName_USERCOM.ERB ------------------------------ Rev1.00
;履歴表示処理（未実装）
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_EQUIP
WAIT