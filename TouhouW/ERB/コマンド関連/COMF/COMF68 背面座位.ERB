;-------------------------------------------------
;背面座位
;セックス系コマンド、レベル3
;-------------------------------------------------
@COM68
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT == 1
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 1200

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 1200

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 100
	;SOURCE:快Ｃ = 250
	SOURCE:苦痛 = 200

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 50
	DOWNBASE:気力 += 100

	;SOURCE:快Ｃ = 250
	SOURCE:快Ｖ = 350
	SOURCE:快Ｂ = 250
	SOURCE:情愛 = 350
	SOURCE:苦痛 = 400
	SOURCE:露出 = 200
	SOURCE:逸脱 = 400
	SOURCE:反感 = 800

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60

	CALL 特殊ソース処理

ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
CALL STAIN_膣内Ｐ挿入

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ経験 ++
EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
IF HETEROSEX(TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
	EXP:MASTER:時姦経験 += 1
ELSE
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
	EXP:Ｖ性交経験 ++
	IF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕 * 2
		;処女
		SIF TALENT:処女 && TALENT:処女 != -1 && TALENT:恋慕
			EXP:愛情経験 += 5
	ENDIF
ENDIF
RETURN 1

@CAN_COM68(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 用背面座位侵犯
ELSE
	;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
	IF !TEQUIP:縄
		SKIPDISP ARG
		PRINTL 背面座位
		A = 0
		S = 0
		;すべての命令に共通の要素を考慮
		;(従順が高いと命令に従いやすいなど)
		CALL COM_ORDER

		;ABL:欲望
		IF ABL:欲望
			SIF S
				PRINT  + 
			A += ABL:欲望 * 3
			PRINT 欲望
			PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
			S = 1
		ENDIF
		;ABL:Ｖ感覚
		IF ABL:Ｖ感覚
			SIF S
				PRINT  + 
			A += ABL:Ｖ感覚 * 2
			PRINT Ｖ感覚
			PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
			S = 1
		ENDIF
		;快楽刻印
		IF MARK:快楽刻印
			SIF S
				PRINT  + 
			A += MARK:快楽刻印 * 3
			PRINT 快楽刻印
			PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
			S = 1
		ENDIF
		;PALAM:潤滑（の不足）
		IF PALAM:潤滑 < PALAMLV:3
			PRINT  - 
			A -= 5
			PRINT 潤滑不足
			PRINTV '(,5,')
			S = 1
		ENDIF
		;PALAM:欲情
		IF GETPALAMLV(PALAM:欲情,5)
			SIF S
				PRINT  + 
			A += GETPALAMLV(PALAM:欲情,5) * 3
			PRINT 欲情
			PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
			S = 1
		ENDIF
		;害羞
		IF TALENT:羞恥心 > 0
			PRINT  - 
			A -= 2
			PRINT 害羞
			PRINTV '(,2,')
			S = 1
		ENDIF
		;否定快感
		IF TALENT:快感応答 < 0 && !TALENT:恋慕
			PRINT  - 
			A -= 5
			PRINT 否定快感
			PRINTV '(,5,')
			S = 1
		ENDIF
		;討厭男人（恋慕、親愛で無効）
		IF OTOKOGIRAI(TARGET)
			PRINT  - 
			A -= 12
			PRINT 討厭男人
			PRINTV '(,12,')
			S = 1
		ENDIF
		;恋慕
		IF TALENT:恋慕 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 15
			PRINT 恋慕
			PRINTV '(,15,')
			S = 15
		ENDIF
		;親愛
		IF TALENT:恋慕 > 1 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 20
			PRINT 親愛
			PRINTV '(,20,')
			S = 20
		ENDIF
		;処女
		LOCAL = MAX(10,40 - EXP:Ｖ経験)
		IF TALENT:処女 && TALENT:処女 != -1
			PRINT  - 
			A -= LOCAL
			PRINT 処女
			PRINTV '(,LOCAL,')
			S = 1
		;Ｖ経験
		ELSEIF EXP:Ｖ経験 < EXPLV:2
			PRINT  - 
			A -= 15
			PRINT Ｖ経験不足
			PRINTV '(,15,')
			S = 1
		ENDIF

		;調教者がFutanari
		IF (TALENT:PLAYER:2 & 3) == 3
			SIF S
				PRINT  + 
			A += 4
			PRINT Futanari
			PRINTV '(,4,')
			S = 1
		ENDIF

		;媚薬
		IF TCVAR:媚薬
			SIF S
				PRINT  + 
			A += 6
			PRINT 媚薬
			PRINTV '(,6,')
			S = 1
		ENDIF

		Y = 0
		;愛液の汚れ
		SIF STAIN:PLAYER:2 & 1
			Y += 1
		;精液の汚れ
		SIF STAIN:PLAYER:2 & 4
			Y += 3
		;アナルの汚れ
		SIF STAIN:PLAYER:2 & 8
			Y += 7
		SIF STAIN:PLAYER:2 & 16
			Y += 15
		Y = Y * (2 - TALENT:汚臭耐性) / 2

		;汚れあり
		IF Y
			PRINT  - 
			A -= Y
			PRINT 汚れあり
			;汚臭鈍感
			IF TALENT:汚臭耐性 > 0
				PRINT 、汚臭鈍感
			ELSEIF TALENT:汚臭耐性 < 0
				PRINT 、汚臭敏感
			ENDIF
			PRINTV '(,Y,')
			S = 1
		ENDIF
		;合計を表示(42以上で実行)
		PRINT  = 
		PRINTV A

		V = 42
		SIF A < V
			PRINT  < 
		SIF A == V
			PRINT  = 
		SIF A > V
			PRINT  > 
		PRINT 実行値
		PRINTV V
	ELSE
		SKIPDISP ARG
		PRINTL 束縛背面座位
	ENDIF
		WAIT
		SKIPDISP 0

	;実行できない
	SIF A < V
		RETURN 0

	;其他諸元判定
	IF ARG == 1
		SIF TFLAG:17 != TARGET
			TFLAG:16 = 0
		;初体験許すか判定
		IF GROUPMATCH(TALENT:TARGET:処女, 1, -1)
			CALL LOST_VIRGIN_STOP(TARGET)
			SIF RESULT
				RETURN 0
		ENDIF
		;危険日避妊要求判定
		CALL 避妊要求(TARGET)
		IF TFLAG:16 && !(TEQUIP:PLAYER:避孕套 || TCVAR:口服避孕薬)
			TFLAG:16 -= 1
			RETURN 0
		ENDIF
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE68
;背面座位実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(68)
	RETURN RESULT
;調教者が男人、Futanariの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:MASTER:勃起 < 1000
	RETURN 0
;振動棒挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;浴室PLAY中は塑料地墊が無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF 挿入不可(TARGET) == 1 && EXP:Ｖ経験 < 50 && !TALENT:PLAYER:禁断的知識
	RETURN 0
;さらに体格差があると禁断的知識が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断的知識 || EXP:Ｖ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者が男人、半人半妖、Futanari、もしくは穿戴式假陰茎がないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・百合属性4以下のASSIの場合はできない（施虐狂ならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:技巧 <= 4) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;処女かつ潤滑不足はだめ
SIF PALAM:潤滑 < PALAMLV:2 && TALENT:処女 && TALENT:処女 != -1
	RETURN 0
;対面座位、正常位からはダメ
SIF TEQUIP:体位 == 4 || TEQUIP:体位 == 1
	RETURN 0
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;室内でないかバスルームで塑料地墊がないとダメ
SIF !INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1

