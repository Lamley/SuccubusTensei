;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼6
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼6(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "誰来告訴我！")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"回答%CALLNAME:ARG%的疑問")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"\\500")
CASE "依頼実行コマンド名"
	IF IS_LIBRARY(CFLAG:MASTER:現在位置)
		CALLF MAKE_STR(V_NAME, "查資料")
	ELSE
		CALLF MAKE_STR(V_NAME, "進行解答")
	ENDIF
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依頼サブ"
	CALLF MAKE_INT(V_NAME, (ABL:ARG:教養 + RAND(1, 10)) * 10)
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 10)
CASE "依頼発生条件"
	SIF ABL:ARG:教養 <= ABL:MASTER:教養
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	;查資料はいつでも
	IF FLAG:70
	ELSEIF IS_LIBRARY(CFLAG:MASTER:現在位置)
		CALLF MAKE_INT(V_NAME, 1)
	;教えるのはCLIENTをTARGETにする必要がある
	ELSEIF TARGET == ARG
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般6(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  LIBRARY_ID
#DIMS LIBRARY_NAME
#DIM  CHK_SLOT
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
PRINTFORML 　　理解度：%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "実行委託時"
;実行委託時にはRESULTが意味を持ちます
;-2:キャンセル（選択肢があり、実行しなかった時）
;-1:実行して失敗
; 0:実行して成功
; 1:実行して依頼続行（未解決状態、時間経過あり）
	LIBRARY_ID = IS_LIBRARY(CFLAG:MASTER:現在位置)
	IF LIBRARY_ID
		IF INRANGE(LIBRARY_ID, 1, 99)
			LIBRARY_NAME '= STR:LIBRARY_ID
		ELSE
			LIBRARY_NAME '= STR:(8000 + LIBRARY_ID)
		ENDIF
		IF TARGET == CLIENT_ID
			PRINTFORML %CALLNAME:MASTER%為了回答%CALLNAME:TARGET%的疑問訪問了%LIBRARY_NAME%
			PRINTFORML 在那裡找到了有用的資料
			CALL ASK_M("やめておく", 1, "查資料", 1, "回答してみる", 1)
			SELECTCASE RESULT
			CASE 0
				RETURN -2
			CASE 1
				PRINTFORMW %CALLNAME:MASTER%和%CALLNAME:TARGET%在%LIBRARY_NAME%做着調查
				EXP:MASTER:学習経験 ++
				EXP:TARGET:学習経験 ++
				IRAI_SUB:CHK_SLOT -= ABL:MASTER:教養 + ABL:TARGET:教養 + 5
				IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
			CASE 2
				GOTO ANSWER
			ENDSELECT
		ELSE
			PRINTFORML %CALLNAME:MASTER%在%LIBRARY_NAME%做着調查
			EXP:MASTER:学習経験 ++
			IRAI_SUB:CHK_SLOT -= ABL:MASTER:教養 + 3
			IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
		ENDIF
		PRINTFORML 現在你的理解度
		PRINTFORMW 　%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
		RETURN 1
	ELSE
		$ANSWER
		PRINTFORML %CALLNAME:MASTER%回答了%CALLNAME:TARGET%的疑問
		IF IRAI_一般6_SUCCESS(CHK_SLOT)
			PRINTFORMW %CALLNAME:TARGET%的疑問冰消瓦解了！
			SOURCE:歓楽 = 300
			EXP:MASTER:学習経験 ++
			EXP:TARGET:学習経験 ++
			IF ITEM:103
				EXP:MASTER:学習経験 ++
				EXP:TARGET:学習経験 ++
			ENDIF
			DOWNBASE:MASTER:体力 = 70
			DOWNBASE:MASTER:気力 = 100
			DOWNBASE:体力 = 50
			DOWNBASE:気力 = 70
			TIME += 30
			RETURN 0
		ELSE
			PRINTFORMW %CALLNAME:TARGET%好像沒能理解……
			RETURN -1
		ENDIF
	ENDIF
CASE "依頼成功時"
;成功報告時の報酬など
	CALL CHANGE_CFLAG(2, TARGET, 20)
	CALL CHANGE_CFLAG(4, TARGET, 10)
	MONEY += 500
	PRINTFORMW %CALLNAME:MASTER%獲得了作為委託報酬的\\500
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT

@IRAI_一般6_SUCCESS(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF RAND:MAX(1, IRAI_SUB:CHK_SLOT) <= (1 + ABL:MASTER:教養) * 20
	RETURNF 1


@IRAI_一般6_DIFFICULTY(CHK_SLOT)
#FUNCTIONS
#DIM CHK_SLOT
SIF IRAI_SUB:CHK_SLOT <= (1 + ABL:MASTER:教養) * 20
	RETURNF "非常完美的讓対方理解了"
;難易度全体で差分をパーセント化したもの
SELECTCASE (IRAI_SUB:CHK_SLOT - ((1 + ABL:MASTER:教養) * 20)) * 100 / IRAI_SUB:CHK_SLOT
CASE 0, 1, 2, 3
	RETURNF "好像可以給出高明的回答"
CASE 4, 5, 6
	RETURNF "已経可以回答了"
CASE 7 TO 10
	RETURNF "能夠回答出来吧"
CASE 11 TO 33
	RETURNF "這樣算是理解了麼"
CASE 34 TO 50
	RETURNF "一定会有辦法的、大概"
CASE 51 TO 70
	RETURNF "很難的疑問"
CASEELSE
	RETURNF "完全無法推測"
ENDSELECT


