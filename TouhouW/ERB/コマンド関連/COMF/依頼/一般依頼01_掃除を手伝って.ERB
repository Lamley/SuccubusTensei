;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼1
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼1(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "幇忙做掃除")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"幇忙掃除%CALLNAME:ARG%的房間")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"\\500")
CASE "依頼実行コマンド名"
	CALLF MAKE_STR(V_NAME, "掃除")

CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 10)
CASE "依頼発生条件"
;自宅が主人公と同一マップのキャラのみ
	SIF MAIN_MAP == GET_MAPID(GET_CHARAHOME(ARG))
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
;その依頼者の自宅近辺にいるとき
;（基本条件としてまず依頼者と主人公が同一地点にいるはずなのでこれでよい）
	IF MAIN_MAP != GET_MAPID(GET_CHARAHOME(ARG))
	ELSEIF CFLAG:ARG:宴会参加
	ELSEIF YOGORE:(GET_CHARAHOME(ARG)) < 1000
	ELSEIF GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
CASE "依頼実行可能条件"
;依頼者とTARGETが同一であり、その依頼者の自宅にいるとき
;（基本条件としてまず依頼者と主人公が同一地点にいるはずなのでこれでよい）
	IF MAIN_MAP != GET_MAPID(GET_CHARAHOME(ARG))
	ELSEIF CFLAG:ARG:宴会参加
	ELSEIF YOGORE:(GET_CHARAHOME(ARG)) < 1000
	ELSEIF CFLAG:ARG:現在位置 == GET_CHARAHOME(ARG)
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般1(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  SWEEP_ID
#DIM  SWEEP_POWER
#DIM  PREV_YOGORE
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "実行委託時"
;実行委託時にはRESULTが意味を持ちます
;-2:キャンセル（選択肢があり、実行しなかった時）
;-1:実行して失敗
; 0:実行して成功
; 1:実行して依頼続行（未解決状態、時間経過あり）
;
;"実行委託時"に限っていえば、CHARAはTARGETと同一であり、必ずしもCLIENT_IDと一致するとは限りません
;個々のケースで確認する必要があります
;この依頼は「作業」扱いであり、CLIENTがTARGETになっている時のみ実行できます
;
;
;ほぼ掃除コマンドのコピーだが必ず手伝ってくれて掃除効果が高い
	PRINTFORMW %CALLNAME:MASTER%和%CALLNAME:TARGET%合作清掃了房間
	PRINTFORML 
	SWEEP_ID = CFLAG:MASTER:現在位置
	SWEEP_POWER = (2 + ABL:MASTER:清掃技能 + 3 * TALENT:MASTER:清掃中毒) * (5 - TALENT:MASTER:61) / 5
	PREV_YOGORE = YOGORE:SWEEP_ID
	YOGORE:SWEEP_ID = YOGORE:SWEEP_ID / (SWEEP_POWER + 1 + ABL:清掃技能)
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, 9, 10)
		YOGORE:(54) = 0
		;PRINTL 順手把虫笼也清扫干浄了
		;PRINTFORML 因為只要挪開針妙丸的私人物品再倒过来抖一抖就行了所以很簡単
	EX:MASTER:今日の掃除回数 += PREV_YOGORE - YOGORE:SWEEP_ID
	EXP:清掃経験 ++
	EXP:MASTER:清掃経験 ++

	DOWNBASE:MASTER:体力 = 50
	DOWNBASE:MASTER:気力 = 100
	DOWNBASE:気力 = 100
	SOURCE:歓楽 = 500
	SOURCE:征服 = 200
	TIME += 20
	RETURN 0
CASE "依頼成功時"
;成功報告時の報酬など
	MONEY += 500
	CALL CHANGE_CFLAG(2, TARGET, 20)
	CALL CHANGE_CFLAG(4, TARGET, 10)
	PRINTFORMW %CALLNAME:MASTER%獲得了作為委託報酬的\\500
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT
