;-------------------------------------------------
;移動
;-------------------------------------------------
@COM400
#DIM 入浴拒否
#DIM 虫かご
#DIM 忍び込み
#DIM 立ち止まり対象
#DIM BUTTON戻る
#DIM BUTTON切替左 
#DIM BUTTON切替中 
#DIM BUTTON切替右
#DIM 入れません
BUTTON戻る = MAXROOM + 1
BUTTON切替左 = MAXROOM + 2
BUTTON切替中 = MAXROOM + 4
BUTTON切替右 = MAXROOM + 4
入れません = 0

IF IN_HOME(TFLAG:195) != 1 && NEXTCOM != -1
	NEXTCOM = -1
	RETURN 0
ENDIF
;人数カウントをリセット
VARSET COUNTCHARA
;部屋の人数カウントを設定
FOR LOCAL,1,CHARANUM
	COUNTCHARA:(CFLAG:LOCAL:現在位置) ++
NEXT
入浴拒否 = 0
CALL TURN_RESET

DRAWLINE
;
;	// 自宅マップの表示を新描画方式に
;

CALL DRAW_MAP


PRINTL 
PRINTFORM 現在{(CFLAG:MASTER:現在位置)}:
PRINTFORMS NAME_FROM_PLACE(CFLAG:MASTER:現在位置)
PRINTL 位于这里
SIF !CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:MASTER:311) && CFLAG:MASTER:現在位置 != CFLAG:MASTER:311
	PRINTL [0] - 返回私室
FOR LOCAL, MINROOM(), MAXROOM
	SIF !CAN_MOVE(CFLAG:MASTER:現在位置, LOCAL) & 1
		CONTINUE
	SIF NAME_FROM_PLACE(LOCAL) == ""
		CONTINUE
	SELECTCASE FLAG:移動先表示
		CASE 2
			PRINTFORM [{LOCAL}] - 
			PRINTFORMSL NAME_FROM_PLACE(LOCAL)
		CASE 1
			PRINTFORM [{LOCAL}]-%NAME_FROM_PLACE(LOCAL),5,LEFT% 
		CASE 0
	ENDSELECT
NEXT

IF !CAN_MOVE(CFLAG:MASTER:現在位置, ENTRANCE) & 1 && CFLAG:MASTER:現在位置 != ENTRANCE
	PRINTFORM [{ENTRANCE}] - 
	PRINTFORMSL NAME_FROM_PLACE(ENTRANCE)
ENDIF
PRINTFORM [{BUTTON戻る}] - 返回　　
PRINTBUTTON "＜]", BUTTON切替左
PRINTBUTTON "マップ切り替え",BUTTON切替中
PRINTFORM ({TFLAG:マップ切り替え})
PRINTBUTTON "[＞",BUTTON切替右
SIF TALENT:MASTER:居場所察知 || TALENT:MASTER:汚部屋察知
	PRINTFORM 　　[200] - 察知模式
IF FLAG:察知モード == 1
	PRINT (居)
ELSEIF FLAG:察知モード == 2
	PRINT (汚)
ENDIF
SIF TALENT:MASTER:居場所察知 && FLAG:察知モード == 1
	PRINTFORM [811] - 居場所察知　　
SIF TALENT:MASTER:汚部屋察知 && FLAG:察知モード == 2
	PRINTFORM [812] - 汚部屋察知　　
PRINTFORM [813] - 移動先表示切替　　

IF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
	PRINTL
	PRINT （走过去都很辛苦…）
ELSEIF TALENT:MASTER:100 < -4 && CFLAG:MASTER:同行
	PRINTL
	PRINT （搭順风车真開心）
ENDIF
$INPUT_LOOP
INPUTS
SIF RESULTS == ""
	GOTO INPUT_LOOP
RESULT = TOINT(RESULTS) - (TOINT(RESULTS) == 0 && RESULTS != "0")
IF RESULT == -1
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, MINROOM() , MAXROOM
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
IF RESULT == BUTTON戻る || RESULT == CFLAG:MASTER:現在位置
	NEXTCOM = -1
	RETURN -1
ELSEIF RESULT == 813
	FLAG:移動先表示 += 1
	SIF FLAG:移動先表示 > 2
		FLAG:移動先表示 = 0
	RESTART
ELSEIF RESULT == BUTTON切替左
	TFLAG:マップ切り替え -= 1
	SIF TFLAG:マップ切り替え <= 0
		TFLAG:マップ切り替え = MAP_PAGES
	RESTART
ELSEIF RESULT == BUTTON切替右
	TFLAG:マップ切り替え += 1
	SIF TFLAG:マップ切り替え > MAP_PAGES
		TFLAG:マップ切り替え = 1
	RESTART
;MASTER的房間
ELSEIF RESULT == 0
	IF IN_HOME(CFLAG:MASTER:初期位置) != 1
		PRINTW 初期位置が不正です。私室に変更します。
		CFLAG:MASTER:初期位置 = デフォルト初期位置
	ENDIF
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, CFLAG:MASTER:初期位置)
	RETURN
;居場所察知
ELSEIF RESULT == 811 && TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL HITOSAGASI()
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
		RETURN
	ENDIF
	RESTART
ELSEIF RESULT == 812 && TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL OHEYA_CHECK
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 锁上了
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
		RETURN
	ENDIF
	RESTART
ELSEIF RESULT == 200
	IF TALENT:MASTER:居場所察知 && TALENT:MASTER:汚部屋察知
		FLAG:察知モード += 1
		SIF FLAG:察知モード >= 3
			FLAG:察知モード = 0
	ELSEIF TALENT:MASTER:居場所察知
		FLAG:察知モード = !FLAG:察知モード
	ELSEIF TALENT:MASTER:汚部屋察知
		FLAG:察知モード = (!FLAG:察知モード) * 2
	ENDIF
	RESTART
;移動範囲外
ELSEIF IN_HOME(RESULT) != 1
	TFLAG:マップ切り替え = 0
	PRINTW 移動範囲外
	CLEARLINE 2
	REUSELASTLINE 
	GOTO INPUT_LOOP
;移動不能な
ELSEIF MAP_CAN_MOVE(RESULT) == 0
	TFLAG:マップ切り替え = 0
	IF TFLAG:移動不能メッセージ == 999
		PRINTFORMW 锁上了
		CLEARLINE 2
		REUSELASTLINE 
	ELSE
		TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
	ENDIF
	TFLAG:移動不能メッセージ = 0
	GOTO INPUT_LOOP
ELSE
;ELSEIF CAN_SKIPMOVE(MAIN_MAP)	もう全部スキップムーブにする
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	RETURN
ENDIF
IF FLAG:70 == 1
	SIF TALENT:MASTER:100 < -4
	BASE:MASTER:TSP -= 15
	BASE:MASTER:TSP -= 5
	IF TFLAG:運搬
		BASE:MASTER:TSP -= 3
		CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
	ENDIF
ELSE
	;小人同士で同行しててもいつも街道の速度になるのは小人の知恵のおかげだ
	SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
	TIME += 7
	TIME += 3
	IF CFLAG:93:同行 && !GETBIT (FLAG:プール, 0)
		TIME += 2
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5,500)
	ENDIF
ENDIF
	;隠密解除処理
;	要検証
;IF CFLAG:MASTER:隠密中
;	FOR LOCAL, 1, CHARANUM
;		;MASTERの前ターン位置が私室だった場合
;		IF (CFLAG:LOCAL:311 == CFLAG:MASTER:前ターン位置) || (CFLAG:LOCAL:現在位置 == CFLAG:MASTER:311 && CFLAG:MASTER:前ターン位置 == CFLAG:MASTER:311)
;			CFLAG:LOCAL:隠密中 = 0
;			CFLAG:LOCAL:イタズラ = 0
;		ENDIF
;		;MASTERの現在位置が私室だった場合
;		SIF ((CFLAG:MASTER:現在位置 == CFLAG:LOCAL:311) && CFLAG:LOCAL:睡眠) || ((CFLAG:LOCAL:現在位置 == CFLAG:MASTER:311 && CFLAG:MASTER:現在位置 == CFLAG:MASTER:311) && CFLAG:LOCAL:睡眠)
;			CFLAG:LOCAL:隠密中 = 1
;	NEXT
;	;私室以外は隠密出来ない…ハズ
;	IF CFLAG:MASTER:現在位置 < 13
;		CFLAG:MASTER:隠密中 = 0
;		CFLAG:MASTER:イタズラ = 0
;	ENDIF
;ENDIF
PREVCOM = 400
STR:0 = 移動
;NEXTCOMに何かしらの値が入っている場合はそれを書き換えない
;そうでないばあい-1でリセット
SIF NEXTCOM == 0
	NEXTCOM = -1
RETURN 0

;移動
@COM_ABLE400
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:うふふ == 2
	RETURN 0
SIF CFLAG:MASTER:イタズラ
	RETURN 0
SIF CFLAG:MASTER:添い寝中
	RETURN 0
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	RETURN 0
;自宅でない
SIF AT_HOME(MASTER) == 0
	RETURN 0
RETURN 1

@SKIP_MOVE(ARG, 最終目的地)
#DIM 暫定目的地,10
#DIM 暫定目的地決定回数
#DIM 最終目的地
#DIM 直前位置
#DIM 出発地点
#DIM 経過時間
#DIM 中継地点,20
#DIM 中継回数
#DIM RELAY_COUNT

;ARG:0	出発位置
;ARG:1	最終目的地
VARSET LOCAL
VARSET 中継地点
暫定目的地 = 最終目的地
経過時間 = 0
中継回数 = 0
TFLAG:マップ切り替え = 0
IF (CAN_MOVE(CFLAG:MASTER:現在位置, 暫定目的地) & 1)
	直前位置 = ARG
	経過時間 = 1
	CFLAG:MASTER:現在位置 = 最終目的地
	GOTO STOP
ELSE
	TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(最終目的地)
	直前位置 = RESULT
ENDIF

出発地点 = CFLAG:MASTER:現在位置

FOR 中継回数,0,20
	WHILE !(CAN_MOVE(出発地点, 暫定目的地) & 1)
		TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(暫定目的地,出発地点)
		[IF_DEBUG]
		PRINTFORML {暫定目的地}に直接行けないから{RESULT}を目指すよ	
		[ENDIF]
		暫定目的地 = RESULT
	WEND
	[IF_DEBUG]
	SETCOLOR C_YELLOW
	PRINTFORML {中継回数+1}個めの暫定目的地を%NAME_FROM_PLACE(暫定目的地)%に決定
	RESETCOLOR
	[ENDIF]
	経過時間 += 1
	中継地点:中継回数 = 暫定目的地
	SIF CAN_MOVE(暫定目的地,最終目的地) & 1
		BREAK
	出発地点 = 暫定目的地
	暫定目的地 = 最終目的地
NEXT

FOR RELAY_COUNT,1,中継回数 + 1
	LOCAL:3 = 0
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == 中継地点:(RELAY_COUNT-1)
			LOCAL:3 = LOCAL
			BREAK
		ENDIF
	NEXT
	PRINTFORM {RELAY_COUNT}番目の中継地の%NAME_FROM_PLACE(中継地点:(RELAY_COUNT-1))%
	IF LOCAL:3
		RESULT = 0
		TRYCALLFORM M_KOJO_K{(LOCAL:3)}
		PRINTFORML 里好像有%CALLNAME:(LOCAL:3)%
		IF FLAG:立ち止まり == 2
			;立ち止まらない
		ELSEIF FLAG:立ち止まり == 1 && !RESULT
			;口上がある場合のみ
		ELSE
			CALL ASK_M("打声招呼后継续移動",!FLAG:70,"无视",1,"停下来",1)
			IF RESULT == 2
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,1,0)
				経過時間 = RELAY_COUNT
				CFLAG:MASTER:現在位置 = 中継地点:(RELAY_COUNT - 1)
				RESULT = 中継地点:RELAY_COUNT
				GOTO STOP
			ELSEIF RESULT == 1
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,3,0)
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,2,0)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 谁也不在
	ENDIF
NEXT

経過時間 = 中継回数 + 1
CFLAG:MASTER:現在位置 = 最終目的地
RESULT = 最終目的地
$STOP
IF IN_TOILET(RESULT)
	CALL 廁所侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF BATHCHECK(RESULT)
	LOCAL:1 = IN_ROOM_MEMBER("MIN", RESULT, "CFLAG", 6)
	IF !LOCAL:1 && !CFLAG:MASTER:同行
		PRINTFORMW 进入浴場的%CALLNAME:MASTER%泡在了浴池里
	ELSEIF LOCAL:1 && 浴室追い出し条件(LOCAL:1) && !CFLAG:MASTER:同行
		PRINTFORMW 被在浴場里的%CALLNAME:(LOCAL:1)%赶出来了…
		CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
		CFLAG:MASTER:現在位置 = 直前位置
	ELSEIF LOCAL:1 && !CFLAG:MASTER:同行
		IF FLAG:70 == 1
			PRINTFORMW %CALLNAME:(LOCAL:1)%在入浴中的样子…
		ELSE
			PRINTFORMW 与在浴場里的%CALLNAME:(LOCAL:1)%対上了视线…
			CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)
		ENDIF
		CFLAG:MASTER:現在位置 = 最終目的地
	ELSE
		FOR LOCAL, 1, CHARANUM
			IF 浴室追い出し条件(LOCAL) && CFLAG:LOCAL:同行
				PRINTFORMW 共同入浴的邀請被%CALLNAME:LOCAL%拒絶了
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ELSEIF LOCAL:1 && 浴室追い出し条件(LOCAL:1)
				PRINTFORMW 被在浴場里的%CALLNAME:(LOCAL:1)%赶出来了…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ENDIF
		NEXT
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:同行
				PRINTFORMW %CALLNAME:LOCAL%好像想一起进入浴場的样子
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
				BREAK
			ENDIF
		NEXT
		CFLAG:MASTER:現在位置 = 最終目的地
	ENDIF
ELSEIF RESULT == 54
	CALL 虫かご侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF LOCK:(RESULT)
	LOCAL:4 = RESULT
	IF FLAG:70 == 1
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW 好像%CALLNAME:LOCAL%正在睡覚的样子
			ENDIF
		NEXT
		;移動成立
		CFLAG:MASTER:現在位置 = 最終目的地
	ELSEIF FLAG:70 == 0
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW 好像%CALLNAME:LOCAL%正在睡覚的样子
			ELSEIF CFLAG:LOCAL:311 == RESULT && !CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORML 好像%CALLNAME:LOCAL%正在睡覚的样子
				PRINTFORMW 听到了一点動静…
			ENDIF
			IF CFLAG:LOCAL:同行
				LOCAL:2 = 1
			ELSE
				LOCAL:2 = 0
			ENDIF
		NEXT
		IF LOCAL:2
			CFLAG:MASTER:現在位置 = 直前位置
		ELSEIF LOCAL:4 != CFLAG:MASTER:初期位置
			CALL ASK_YN("潜入房间","算了吧")
			IF RESULT == 0
				CALL 部屋に忍び込む(LOCAL:4)
				GOTO END
			ELSEIF RESULT == 1
				CFLAG:MASTER:現在位置 = 直前位置
			ENDIF
		ENDIF
	ENDIF
ENDIF
$END
[IF_DEBUG]
PRINTFORML {経過時間}回移動したよ
[ENDIF]


IF FLAG:70 == 1
	SIF TALENT:MASTER:100 < -4
		BASE:MASTER:TSP -= 15 * 経過時間
	BASE:MASTER:TSP -= 5 * 経過時間
	IF TFLAG:運搬
		BASE:MASTER:TSP -= 3 * 経過時間
		CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
	ENDIF
ELSE
	SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
		TIME += 7 * 経過時間
	TIME += 3 * 経過時間
	IF CFLAG:93:同行 && !GETBIT (FLAG:プール, 0)
		TIME += 2 * 経過時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (5 * 経過時間), 500)
	ENDIF
ENDIF

PREVCOM = 400
STR:0 = 移動
;NEXTCOMに何かしらの値が入っている場合はそれを書き換えない
;そうでないばあい-1でリセット
SIF NEXTCOM == 0
	NEXTCOM = -1