;-------------------------------------------------
;道具画面
;-------------------------------------------------
@COM490
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復前精力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復後精力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復前精力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIM 回復後精力２
#DIM 対象
#DIM 全員
#DIM 使用道具
回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
回復前精力１ = BASE:MASTER:精力
回復前体力２ = BASE:TARGET:体力
回復前気力２ = BASE:TARGET:気力
回復前精力２ = BASE:TARGET:精力
VARSET LOCAL
VARSET LOCALS
対象 = MASTER
PRINTL 要使用什么呢？
$LOOP
DRAWLINE
FOR LOCAL,120,599
	IF 対象
		SIF 使用可能道具(LOCAL) == 2 && ITEM:LOCAL
			PRINTFORML [{LOCAL}] %ITEMNAME:LOCAL% {ITEM:LOCAL}個
	ELSE
		SIF 使用可能道具(LOCAL) && ITEM:LOCAL
			PRINTFORML [{LOCAL}] %ITEMNAME:LOCAL% {ITEM:LOCAL}個
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTFORML [900] 收集的胖次自動进入间隙的開関（現在\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@）
IF TARGET && SHIRAHU(TARGET)
	SIF 対象
		SETCOLOR C_YELLOW
	PRINTFORMLC [998] 让%CALLNAME:TARGET%使用
	RESETCOLOR
ENDIF

PRINTLC [999] 算了
INPUT
使用道具 = RESULT
IF DRUG(使用道具,対象)
	PRINTL 今天还是算了吧
	GOTO LOOP
ENDIF
IF ITEM_SWEETS(使用道具) && !TIME_PROGRESS(TCVAR:対象:デザート空腹時刻)
	PRINTL 我还不想吃
	GOTO LOOP
ENDIF
IF RESULT == 900
	FLAG:Cスキマ自動仕様 = !FLAG:Cスキマ自動仕様
	PRINTFORMW 把自動仕様設為了\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@
	GOTO LOOP
ENDIF
SELECTCASE 使用道具
	CASE 120
		IF 対象 == MASTER
			PRINTFORML 吃%ITEMNAME:使用道具%
			PRINTL ……一股廉价的味道
			CALL RECOVER(MASTER,50,"体力")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 121
		IF 対象 == MASTER
			PRINTFORML 吃%ITEMNAME:使用道具%
			CALL RECOVER(MASTER,300,"体力")
			CALL RECOVER(MASTER,300,"気力")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 122
		IF 対象 == MASTER
			PRINTFORML 吃%ITEMNAME:使用道具%
			PRINTFORML 好吃！
			CALL RECOVER(MASTER,500,"体力")
			CALL RECOVER(MASTER,500,"気力")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 123
		IF 対象 == MASTER
			PRINTFORML 吃%ITEMNAME:使用道具%
			PRINTFORML 可以感受到爱！
			CALL RECOVER(MASTER,1000,"体力")
			CALL RECOVER(MASTER,1000,"気力")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 212
			PRINTFORML 吃%ITEMNAME:使用道具%
			LOCAL:1 ++
			CALL RECOVER(MASTER,100,"体力")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
	CASE 213
		SIF TARGET && SHIRAHU(TARGET)
			PRINT 二人坐在一起吃着
			PRINTDATA
				DATAFORM 『冰淇淋』
				DATAFORM 『冷凍蜜柑』
				DATAFORM 『帯两个柄的苏打冰淇淋』
				DATAFORM 『二个一組的蛋蛋冰淇淋』
				DATAFORM 『棒棒冰』
				DATAFORM 『papico冰棒』
				DATAFORM 『涂練乳的牛奶冰棒』
				DATAFORM 『红豆冰棒』
				DATAFORM 『西瓜冰棒』
				DATAFORM 『蜜柑冰棒』
				DATAFORM 『黑雷神巧克力冰棒』
				DATAFORM 『雪見大福』
			ENDDATA
		PRINTFORML 。
			CALL RECOVER(MASTER,150,"気力")
			TCVAR:MASTER:デザート空腹時刻 = 1440 * DAY + TIME + 180
			IF  TARGET && SHIRAHU(TARGET)
				CALL RECOVER(TARGET,150,"気力")
				TCVAR:デザート空腹時刻 = 1440 * DAY + TIME + 180
			ENDIF
		LOCAL:1 ++
	CASE 215
		PRINTFORML 使用%ITEMNAME:使用道具%
		SETBIT TCVAR:MASTER:特別なお茶, 0
		LOCAL:1 ++
	CASE 352
		PRINTFORML 吃%ITEMNAME:使用道具%
		CALL RECOVER(対象,50,"気力")
		SIF 対象 == (NO:9)
			CALL RECOVER(対象,50,"気力")
		LOCAL:1 ++
	CASE 364
		IF FLAG:70 == 1
			PRINTFORML 吃%ITEMNAME:使用道具%
			CALL RECOVER(MASTER,100,"気力")
		ELSE
			PRINTFORML 一起吃%ITEMNAME:使用道具%
			FOR 全員, 0, CHARANUM
				SIF CFLAG:全員:現在位置 != CFLAG:MASTER:現在位置
					CONTINUE
				SIF !SHIRAHU(全員)
					CONTINUE
				CALL RECOVER(全員,100,"気力")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用道具%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500
		PRINTFORML 喝%ITEMNAME:使用道具%
		PRINTL 不好喝！再来一杯！
		CALL RECOVER(対象,100,"体力")
		CALL RECOVER(対象,200,"気力")
		LOCAL:1 ++
	CASE 501
			PRINTFORML 喝%ITEMNAME:使用道具%
			CALL RECOVER(対象,200,"体力")
			CALL RECOVER(対象,100,"気力")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 502
			PRINTFORML 喝%ITEMNAME:使用道具%
			CALL RECOVER(対象,300,"精力")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 503
			PRINTFORML 喝%ITEMNAME:使用道具%
			PRINTL 干劲満満、状态十足！
			CALL RECOVER(対象,500,"体力")
			CALL RECOVER(対象,300,"気力")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 510
			PRINTFORML 喝%ITEMNAME:使用道具%
			PRINTL 肝機能増强了、変得不容易酔了
			TFLAG:酔い止め = 1
			LOCAL:1 ++
	CASE 520
			IF !TFLAG:キスマーク
				PRINTFORML 现在使用也没有意义
				GOTO LOOP
			ELSE
				PRINTFORML %CALLNAME:MASTER%用%ITEMNAME:使用道具%把吻痕隠藏起来了
				TFLAG:绷帯 = 1
				LOCAL:1 ++
			ENDIF
	CASE 550
		IF !TIME_PROGRESS(TCVAR:対象:空腹時刻)
			PRINTFORML %CALLNAME:対象%的肚子還不餓
			GOTO LOOP
		ELSE			
			PRINTFORML 吃%ITEMNAME:使用道具%
			CALL 満腹度上昇(対象,"軽食")
			CALL RECOVER_PERMIL(対象,40,"体力")
			CALL RECOVER_PERMIL(対象,80,"気力")
			CALL RECOVER_PERMIL(対象,50,"精力")
			LOCAL:1 ++
		ENDIF
	CASE 998
		IF 対象
			対象 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			対象 = TARGET
		ENDIF
		GOTO LOOP
	CASE 999
		RETURN -1
	CASEELSE
		GOTO LOOP
ENDSELECT
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:使用道具 -= 1
	回復後体力１ = BASE:対象:体力
	回復後気力１ = BASE:対象:気力
	回復後精力１ = BASE:対象:精力
	回復後体力２ = BASE:対象:体力
	回復後気力２ = BASE:対象:気力
	回復後精力２ = BASE:対象:精力
	IF !対象
		SIF ABS(回復後体力１ - 回復前体力１)
			PRINTFORML 体力+{ABS(回復後体力１ - 回復前体力１)}（%CALLNAME:対象%）
		SIF ABS(回復後気力１ - 回復前気力１)
			PRINTFORML 気力+{ABS(回復後気力１ - 回復前気力１)}（%CALLNAME:対象%）
		SIF ABS(回復後精力１ - 回復前精力１)
			PRINTFORML 精力+{ABS(回復後精力１ - 回復前精力１)}（%CALLNAME:対象%）
	ELSE
		SIF ABS(回復後体力２ - 回復前体力２)
			PRINTFORML 体力+{ABS(回復後体力２ - 回復前体力２)}（%CALLNAME:対象%）
		SIF ABS(回復後気力２ - 回復前気力２)
			PRINTFORML 気力+{ABS(回復後気力２ - 回復前気力２)}（%CALLNAME:対象%）
		SIF ABS(回復後精力２ - 回復前精力２)
			PRINTFORML 精力+{ABS(回復後精力２ - 回復前精力２)}（%CALLNAME:対象%）
	ENDIF
	RETURN 0
ENDIF

@使用可能道具(ARG)
;1だとMASTERのみ、2だと主人公以外にも使える
#FUNCTION
SELECTCASE ARG
	CASE 120 TO 123, 215, 364, 500, 510, 520
		RETURNF 1
	CASE 212, 213, 352, 501 TO 503, 550
		RETURNF 2
	CASEELSE
		RETURNF 0
ENDSELECT

@DRUG(ARG,ARG:1)
#FUNCTION
SIF GROUPMATCH(ARG,501,502,503) && TCVAR:(ARG:1):お薬
	RETURNF 1

@ITEM_SWEETS(ARG)
#FUNCTION
SIF GROUPMATCH(ARG,120,121,123,124,212,213)
	RETURNF 1


;道具
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
SIF CFLAG:MASTER:うふふ
	RETURN 0
RETURN 1