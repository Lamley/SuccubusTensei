;-------------------------------------------------
;碰杯
;-------------------------------------------------
@COM334
;成否の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	TFLAG:193 = 1
	TFLAG:98 = 2
	LOCAL:2 = 80
	EXP:MASTER:会話経験 ++
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
	TFLAG:98 = -1
	LOCAL:2 = 20
ELSE
	;残りは成功
	TFLAG:193 = 0
	TFLAG:98 = 0
	LOCAL:2 = 60
ENDIF

EXP:MASTER:会話経験 ++

IF CFLAG:妊娠自覚状態 == 1 || TALENT:酒耐性 != -2
	LOCAL:3 = (MAXBASE:酒気 + MAXBASE:MASTER:酒気) * (LOCAL:2 + RAND:41) / 2000
	LOCAL:4 = (MAXBASE:酒気 + MAXBASE:MASTER:酒気) * (LOCAL:2 + RAND:41) / 2000
	BASE:酒気 += LOCAL:3
	BASE:MASTER:酒気 += LOCAL:4

	;酒を勧めるほど早く満足して去って行っちゃうのは寂しいのでコメントアウト
	;BASE:工作量 = BASE:工作量 - LOCAL:3
ENDIF

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 100

;所要時間
TIME += 30

DOWNBASE:気力 += 10

;信頼
TFLAG:98 += 4

;固定で獲得するソース
SOURCE:歓楽 = 700

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 50)
ELSEIF ABL:従順 <= 3
	SOURCE:歓楽 += 100 + (ABL:従順 * 40)
ELSEIF ABL:従順 <= 5
	SOURCE:歓楽 += 200 + (ABL:従順 * 50)
ELSEIF ABL:従順 <= 8
	SOURCE:歓楽 += 400 + (ABL:従順 * 70)
ELSEIF ABL:従順 <= 11
	SOURCE:歓楽 += 500 + (ABL:従順 * 60)
ELSE
	SOURCE:歓楽 += 800 + (ABL:従順 * 40)
ENDIF

;ABL:親密チェック
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 60)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 120 + (ABL:親密 * 60)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 300 + (ABL:親密 * 60)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 600 + (ABL:親密 * 50)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 900 + (ABL:親密 * 50)
ELSE
	SOURCE:歓楽 += 1400 + (ABL:親密 * 30)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF

SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

SOURCE:受動 = 150 + 150 * ABL:従順
SOURCE:征服 = 150 + 150 * ABL:施虐属性

SELECTCASE ABL:MASTER:話術技能
	CASE 0
		TIMES SOURCE:歓楽 , 0.60
		TIMES SOURCE:征服 , 0.60
		TIMES SOURCE:受動 , 0.60
	CASE 1
		TIMES SOURCE:歓楽 , 0.70
		TIMES SOURCE:征服 , 0.70
		TIMES SOURCE:受動 , 0.70
	CASE 2
		TIMES SOURCE:歓楽 , 0.85
		TIMES SOURCE:征服 , 0.85
		TIMES SOURCE:受動 , 0.85
	CASE 3
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
	CASE 4
		TIMES SOURCE:歓楽 , 1.10
		TIMES SOURCE:征服 , 1.10
		TIMES SOURCE:受動 , 1.10
	CASEELSE
		TIMES SOURCE:歓楽 , 1.20
		TIMES SOURCE:征服 , 1.20
		TIMES SOURCE:受動 , 1.20
ENDSELECT
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE334
;碰杯実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(334)
	RETURN RESULT
;仕事内容が宴会以外
SIF CFLAG:職種 != ENKAI_SANKA
	RETURN 0
;工作量(宴会で飲む残り量)がゼロ
SIF BASE:工作量 <= 0
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1
