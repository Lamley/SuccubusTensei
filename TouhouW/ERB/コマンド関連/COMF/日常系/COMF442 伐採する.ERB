;伐採
@COM442
#DIM 採取数

採取数 = 1 + ABL:MASTER:戦闘能力
IF TARGET && !FLAG:70
	採取数 += ABL:TARGET:戦闘能力
	DOWNBASE:体力 = 100
	DOWNBASE:気力 = 100
ENDIF

IF FLAG:70
	BASE:MASTER:TSP -= 200
ELSE
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 60
ENDIF

PRINTFORMW %CALLNAME:MASTER%砍起树来ー
PRINTFORMW 嘿～嘿～吼ー
PRINTFORMW 嘿～嘿～吼ー
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW 得到了{採取数}木材

ITEM:木材 += 採取数

SELECTCASE RAND:20
	CASE 0 TO 4
		LOCAL = 377
	CASE 5
		LOCAL = 4
	CASEELSE
		LOCAL = 0
ENDSELECT
IF LOCAL
	PRINTFORMW 用木头做了些东西…
	PRINTFORMW 得到了%ITEMNAME:LOCAL%
	ITEM:LOCAL ++
ENDIF

RETURN 1

@COM_ABLE442
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(419)
	RETURN RESULT
SIF CFLAG:うふふ == 2
	RETURN 0
SIF !伐採可能()
	RETURN 0
RETURN 1

@伐採可能()
#FUNCTION
IF !IN_HOME(CFLAG:MASTER:現在位置)
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 森林的入口,古木的大樹,山麓樹海
			RETURNF 1
	ENDSELECT
ENDIF
RETURNF 0