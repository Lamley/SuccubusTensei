;吃飯
@COM414
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２

回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
IF TARGET
	回復前体力２ = BASE:TARGET:体力
	回復前気力２ = BASE:TARGET:気力
	LOCAL = RAND:(100 - ABL:MASTER:料理技能 * 2)
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 99
		LOCAL:1 = 99
	IF LOCAL <= 9
		;10％で大成功
		EXP:MASTER:会話経験 ++
		TFLAG:193 = 1
		TFLAG:98 = 2
	ELSEIF LOCAL >= LOCAL:1
		;10～1％で失敗
		TFLAG:193 = -1
		TFLAG:98 = -2
	ELSE
		;残りは成功
		TFLAG:193 = 0
		TFLAG:98 = 0
	ENDIF
	;信頼
	TFLAG:98 += 3
	SIF ABL:MASTER:料理技能 > 1
		TFLAG:98 ++ 
	SIF ABL:MASTER:料理技能 > 3
		TFLAG:98 ++ 
	
	;イタズラを仕込まれていない
	IF TCVAR:MASTER:308 < 8
		;味付けがツボだと失敗しない
		IF CFLAG:TARGET:味覚嗜好 & TCVAR:MASTER:308
			TFLAG:193 = MAX(TFLAG:193,0)
			;信頼にも修正
			TFLAG:98 += 2
		ENDIF
	ENDIF
	SELECTCASE DISHTYPE
		CASE 1

		CASE 2,4,5

		CASE 3

	ENDSELECT
	;その場に沒穿キャラがいるか確認
	IF DISHNAME == "涮涮鍋"
		FOR LOCAL, 1, CHARANUM
			SIF EQUIP:LOCAL:6 == 0 && CFLAG:LOCAL:現在位置 == CFLAG:PLAYER:現在位置
				TFLAG:194 = 1
		NEXT
	ENDIF
	;特殊な吃飯フラグ
	SIF DISHNAME == "金吉獣料理"
		TFLAG:194 = 34
	SIF DISHNAME == "奶酪番茄沙拉"
		TFLAG:194 = 30
	SIF DISHNAME == "興奮劑高湯"
		TFLAG:194 = 31
	SIF DISHTYPE == 4
		TFLAG:194 = 2

	;その場に居るTARGET全員で回す
	LOCAL:1 = TARGET
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):睡眠
			CONTINUE
		TARGET = TARGET:LOCAL
		CALL COM414_1
	NEXT
	TARGET = LOCAL:1
	回復後体力２ = BASE:TARGET:体力
	回復後気力２ = BASE:TARGET:気力
ENDIF

;軽食
IF DISHTYPE == 1
	CALL 吃飯(MASTER)
	TIME += 20
	SIF CFLAG:同行
		CFLAG:同行 += 20
;料理が涮涮鍋で沒穿キャラがいるとノーパン涮涮鍋に
ELSEIF TFLAG:194 == 1
	BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + MAXBASE:MASTER:体力 / 3 , MAXBASE:MASTER:体力)
	BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + MAXBASE:MASTER:気力 / 2 , MAXBASE:MASTER:気力)
	BASE:MASTER:精力 = MIN(BASE:MASTER:精力 + MAXBASE:MASTER:精力 / 2 , MAXBASE:MASTER:精力)
	;吃飯をしたフラグ
	CALL 満腹度上昇(MASTER,"主食")
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ELSEIF DISHTYPE == 2
	CALL 吃飯(MASTER)
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ELSEIF DISHTYPE == 3
	CALL 吃飯(MASTER)
	TIME += 20
	SIF CFLAG:同行
		CFLAG:同行 += 20
ELSEIF DISHTYPE == 4 || DISHTYPE == 5
	CALL 吃飯(MASTER)
	TIME += 40
	SIF CFLAG:同行
		CFLAG:同行 += 40
ENDIF
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
CALL RESET_DISH
DRAWLINE
PRINTFORML 体力+{ABS(回復後体力１ - 回復前体力１)}（%CALLNAME:MASTER%）
PRINTFORML 気力+{ABS(回復後気力１ - 回復前気力１)}（%CALLNAME:MASTER%）
IF TARGET
	PRINTFORML 体力+{ABS(回復後体力２ - 回復前体力２)}（%CALLNAME:TARGET%）
	PRINTFORML 気力+{ABS(回復後気力２ - 回復前気力２)}（%CALLNAME:TARGET%）
ENDIF
RETURN 1

@COM414_1
#DIM 空腹
空腹 = 0
IF DISHTYPE == 3
	SIF TIME_PROGRESS(TCVAR:デザート空腹時刻)
		空腹 = 1
ELSE
	SIF TIME_PROGRESS(TCVAR:空腹時刻)
		空腹 = 1
ENDIF
;満腹でない
IF 空腹
	;固定で獲得するソース
	SELECTCASE DISHSPOIL
		CASE 0
			SOURCE:歓楽 = 800
		CASE 1
			SOURCE:歓楽 = 400
		CASE 2
			SOURCE:歓楽 = 100
			SOURCE:不潔 = 400
			SOURCE:反感 = 400
	ENDSELECT
	;イタズラを仕込まれていない
	IF TCVAR:MASTER:308 < 8
		;味覚嗜好を見る
		SIF CFLAG:TARGET:味覚嗜好 & TCVAR:MASTER:308
			SOURCE:歓楽 += 200
	ENDIF

	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 700 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1500 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 3000 + (ABL:従順 * 30)
	ENDIF

	;ノーパン涮涮鍋で露出追加
	SIF TFLAG:194 == 1
		SOURCE:露出 += 1000 
	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0


	SOURCE:受動 = 200 + 100 * ABL:従順
	SOURCE:征服 = 200 + 100 * ABL:施虐属性

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
		CALL KIGEN_CHANGE(TARGET,30,1)
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
		IF TALENT:心情 < 0
			CALL KIGEN_CHANGE(TARGET,100,1)
		ELSE
			CALL KIGEN_CHANGE(TARGET,50,1)
		ENDIF
	ENDIF
SOURCE:歓楽 = SOURCE:歓楽 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
SOURCE:征服 = SOURCE:征服 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
SOURCE:受動 = SOURCE:受動 * (ABL:MASTER:料理技能 + DISHQUALITY * 2) / 5
	IF CFLAG:行動 == 5
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
		TFLAG:98 += 2
	ENDIF
	CALL 吃飯(TARGET)
ENDIF
SIF TALENT:食量大
	TIMES SOURCE:歓楽 , 1.50

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE414
;吃飯実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(414)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;料理
SIF DISHNAME == ""
	RETURN 0
;軽食以外は屋内じゃなきゃ無理
SIF FLAG:20 >= 10 && !INROOM(CFLAG:MASTER:現在位置)
	RETURN 0
;食べたばかり
IF DISHTYPE == 3
	SIF !TIME_PROGRESS(TCVAR:MASTER:デザート空腹時刻)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:MASTER:空腹時刻)
		RETURN 0
ENDIF
SIF CFLAG:MASTER:うふふ
	RETURN 0
RETURN 1

