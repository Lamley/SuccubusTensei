@COM330
#DIM MEKURI
;掀起裙子
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
ELSE
IF FLAG:甲斐性無
	PRINTFORMW %CALLNAME:MASTER%沒有那樣的胆量
	RETURN -1
ENDIF
;固定で獲得するソース
SOURCE:露出 = 300
SOURCE:反感 = 700
IF FLAG:異常気象 == 6
	MEKURI = MAX(0,5000 - 500 * ABL:親密)
	SELECTCASE MARK:反発刻印
		CASE 0
			MEKURI /= 2
		CASE 2
			MEKURI *= 2
		CASE 3
			MEKURI *= 4
	ENDSELECT
	[IF_DEBUG]
	PRINTFORMW 掀裙子{MEKURI}
	[ENDIF]
	CUP:快Ｖ += MEKURI
ENDIF
;好感度をみる
SELECTCASE CFLAG:2
	CASE 1000 TO 3000
		SOURCE:情愛 += CFLAG:2
		SOURCE:反感 -= (ABL:親密 * 10)
	CASE 3001 TO 5000
		SOURCE:情愛 += 100 + (CFLAG:2 - 500) / 5
		SOURCE:歓楽 += 100 + (CFLAG:2 - 500) / 4
		SOURCE:欲情 += (ABL:親密 * 10)
		SOURCE:露出 += (ABL:親密 * 15)
		SOURCE:反感 -= (ABL:親密 * 15)
	CASE IS > 5000
		SOURCE:情愛 += 500 + (CFLAG:2 - 5000) / 7
		SOURCE:歓楽 += 500 + (CFLAG:2 - 5000) / 4
		IF !TALENT:恋慕
			SOURCE:欲情 += (ABL:親密 * 15)
			SOURCE:露出 += (ABL:親密 * 20)
			SOURCE:反感 -= (ABL:親密 * 25)
		ELSE
			SOURCE:欲情 += (ABL:親密 * 20)
			SOURCE:露出 += (ABL:親密 * 30)
			SOURCE:反感 -= (ABL:親密 * 40)
		ENDIF
ENDSELECT
SIF SOURCE:反感 < 0
	SOURCE:反感 = 0
SIF SOURCE:情愛 < 0
	SOURCE:情愛 = 0

LOCAL = 0
;好感度が低いと好感度低下
IF CFLAG:2 <= 100
	TFLAG:193 = -5
	DOWNBASE:MASTER:体力 -= ABL:戦闘能力 * 100
	TFLAG:掀裙子すぎ += RAND:30
ELSEIF CFLAG:2 <= 1000
	TFLAG:193 = -3
	DOWNBASE:MASTER:体力 -= ABL:戦闘能力 * 30
	TFLAG:掀裙子すぎ += RAND:20
ELSEIF CFLAG:2 <= 3000
	TFLAG:193 = -1
	TFLAG:掀裙子すぎ += RAND:10
ELSEIF CFLAG:2 <= 5000
	TFLAG:193 = 0
	TFLAG:掀裙子すぎ += RAND:10
ELSE
	TFLAG:193 = 1
	TFLAG:掀裙子すぎ += RAND:5
ENDIF

SOURCE:受動 = 100 + 150 * ABL:従順
SOURCE:征服 = 100 + 150 * ABL:施虐属性

;二人きりだと効果アップ、そうでないと露出以外ダウン=============
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:同室
		LOCAL:2 ++
NEXT
IF LOCAL:2 == 1
	TIMES SOURCE:欲情 , 1.20
	TIMES SOURCE:露出 , 0.80
	LOCAL:3 = 1
ELSE
	TIMES SOURCE:欲情 , 0.50
	TIMES SOURCE:露出 , 2.00
	TIMES SOURCE:反感 , 1.50
ENDIF
;情緒の影響=====================================================
IF BASE:情緒 == MAXBASE:情緒
	TIMES SOURCE:欲情 , 1.20
	TIMES SOURCE:露出 , 1.20
	TIMES SOURCE:反感 , 0.50
ELSEIF BASE:情緒 < 600 && BASE:情緒
	TIMES SOURCE:欲情 , 1.00
	TIMES SOURCE:露出 , 1.00
	TIMES SOURCE:反感 , 1.00
ELSEIF BASE:情緒 <= 200
	TIMES SOURCE:欲情 , 0.80
	TIMES SOURCE:露出 , 0.80
	TIMES SOURCE:反感 , 1.50
ENDIF

TIME += 5
IF CHK_DATENOW(CFLAG:TARGET:デート中)
	EXP:MASTER:約会経験 ++
	EXP:TARGET:約会経験 ++
	CALL DATE_GOES_ON(1)
ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE330
;掀裙子実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(330)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;睡眠中
	SIF CFLAG:睡眠
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;デート中
	SIF CHK_DATENOW(CFLAG:デート中) && CFLAG:現在位置 != OMANEKIBEYA()
		RETURN 0
	;添い寝中
	SIF CFLAG:添い寝中
		RETURN 0
ENDIF
;服装制限
SIF !(EQUIP:裙子 || EQUIP:連衣裙 || EQUIP:和服) || EQUIP:0 == 0
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF !TARGET
	RETURN 0
;押し倒され中の制御
SIF GETBIT (TCVAR:26,7)
	RETURN 0
RETURN 1

