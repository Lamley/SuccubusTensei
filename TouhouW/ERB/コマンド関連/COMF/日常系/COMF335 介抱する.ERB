;-------------------------------------------------
;照料
;-------------------------------------------------
@COM335
;成否の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	TFLAG:193 = 1
	TFLAG:98 = 2
	LOCAL:2 = 6
	EXP:MASTER:会話経験 ++
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
	TFLAG:98 = -1
	LOCAL:2 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
	TFLAG:98 = 0
	LOCAL:2 = 2
ENDIF

SIF RAND:2
	EXP:MASTER:会話経験 ++

;ランダムに(0～2)+(成功補正)%の酒気減少
LOCAL:3 = MAX(MAXBASE:酒気 * (RAND:3 + LOCAL:2) / 100, 0)
BASE:酒気 = MAX(BASE:酒気 - LOCAL:3, 0)

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:気力 += 100

;所要時間
TIME += 30

DOWNBASE:気力 += 10

;信頼
TFLAG:98 += 10

;固定で獲得するソース
SOURCE:歓楽 = 400

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 50)
ELSEIF ABL:従順 <= 3
	SOURCE:歓楽 += 100 + (ABL:従順 * 40)
ELSEIF ABL:従順 <= 5
	SOURCE:歓楽 += 200 + (ABL:従順 * 50)
ELSEIF ABL:従順 <= 8
	SOURCE:歓楽 += 400 + (ABL:従順 * 70)
ELSEIF ABL:従順 <= 11
	SOURCE:歓楽 += 500 + (ABL:従順 * 60)
ELSE
	SOURCE:歓楽 += 800 + (ABL:従順 * 40)
ENDIF

;ABL:親密チェック
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 50)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 80 + (ABL:親密 * 50)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + (ABL:親密 * 50)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 500 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 800 + (ABL:親密 * 40)
ELSE
	SOURCE:歓楽 += 1200 + (ABL:親密 * 30)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF

SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

SOURCE:受動 = 200 + 200 * ABL:従順
SOURCE:征服 = 40 + 40 * ABL:施虐属性
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE335
;照料実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(335)
	RETURN RESULT
;仕事内容が酔いつぶれ以外
SIF CFLAG:職種 != 酔いつぶれる
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1
