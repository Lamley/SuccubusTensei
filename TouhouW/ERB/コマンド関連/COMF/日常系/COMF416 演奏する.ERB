;-------------------------------------------------
;演奏
;-------------------------------------------------
@COM416
#DIM 参加人数
#DIM BAND_BONUS

TFLAG:使用楽器 = 0
PRINTL 要使用什么楽器呢……
FOR LOCAL,30,35
	SIF MUSIC_ABLE(LOCAL)
		PRINTFORML [{LOCAL}] - %ITEMNAME:LOCAL%
NEXT
PRINTL [100] - 返回
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 100
		TFLAG:使用楽器 = 0
		RETURN -1
	CASE 30 TO 34
		IF !MUSIC_ABLE(RESULT)
			GOTO INPUT_LOOP
		ELSE
			TFLAG:使用楽器 = RESULT - 29
		ENDIF
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= (9 + ABL:MASTER:音楽技能)
	;10％で大成功
	EXP:MASTER:演奏経験 ++
	EXP:MASTER:演奏経験 += 5
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	EXP:MASTER:演奏経験 ++
	TFLAG:193 = 0
ENDIF

DOWNBASE:MASTER:体力 = 300
DOWNBASE:MASTER:気力 = 300

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
参加人数 = 0
FOR LOCAL,1,CHARANUM
	TCVAR:LOCAL:演奏参加 = 0
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM416_1
	SIF TCVAR:LOCAL:演奏参加
		参加人数 ++
NEXT

SELECTCASE 参加人数
	CASE 0
		BAND_BONUS = 0
	CASE 1
		BAND_BONUS = 1
	CASE 2,3
		BAND_BONUS = 2
	CASEELSE
		BAND_BONUS = 4
ENDSELECT

FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	SOURCE:LOCAL:歓楽 = SOURCE:LOCAL:歓楽 * (2 + BAND_BONUS) / 2
	SOURCE:LOCAL:征服 = SOURCE:LOCAL:征服 * (2 + BAND_BONUS) / 2
NEXT
TFLAG:好感度ボー茄子 = BAND_BONUS * 50
TARGET = LOCAL:1

TIME += 30
RETURN 1


@COM416_1

DOWNBASE:気力 += 100

;歓楽強度
SOURCE:歓楽 = 700 + ABL:MASTER:音楽技能 * 300 + ABL:親密 * 50
;征服強度
SOURCE:征服 = 100 * (ABL:MASTER:音楽技能 + 1) + ABL:親密 * 30

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
ENDIF
;音楽技能で歓楽がさらに増減
IF ABL:MASTER:音楽技能 >= 5
	TIMES SOURCE:歓楽 , 3.00
ELSEIF ABL:MASTER:音楽技能 == 4
	TIMES SOURCE:歓楽 , 2.00
ELSEIF ABL:MASTER:音楽技能 == 3
	TIMES SOURCE:歓楽 , 1.00
ELSEIF ABL:MASTER:音楽技能 == 2
	TIMES SOURCE:歓楽 , 0.50
ELSE
	TIMES SOURCE:歓楽 , 0.10
ENDIF
;[音楽知識]を持っている場合、歓楽増加
SIF TALENT:MASTER:音楽知識
	TIMES SOURCE:歓楽 , 2.00
;音感のあるなしでもう少し増減
IF TALENT:MASTER:音感 == 2
	TIMES SOURCE:歓楽 , 2.00
ELSEIF TALENT:MASTER:音感 == 1
	TIMES SOURCE:歓楽 , 1.50
ELSEIF TALENT:MASTER:音感 == -1
	TIMES SOURCE:歓楽, 0.50
ENDIF
;楽器持ちキャラ、プリリバ三姉妹と九十九姉妹と雷鼓がTARGET

IF TARGET > 0 && TALENT:TARGET:音楽知識 == 1
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 1
		EXP:演奏経験 ++
		PRINTFORMW %CALLNAME:TARGET%開始演奏着楽器！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:演奏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;歌唱キャラ、米斯蒂亜と響子、艾倫がTARGET
ELSEIF TARGET > 0 && TALENT:TARGET:音楽知識 == 2
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 2
		EXP:歌唱経験 ++
		PRINTFORMW %CALLNAME:TARGET%配合着%CALLNAME:MASTER%的旋律唱起了歌！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:歌唱経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;舞踏キャラ（まだ未定
[SKIPSTART]
ELSEIF TARGET > 0 && TALENT:TARGET:音楽知識 == 3
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 3
		EXP:舞踏経験 ++
		PRINTFORMW %CALLNAME:TARGET%配合着%CALLNAME:MASTER%的旋律跳起了舞！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:舞踏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
[SKIPEND]
ELSEIF TARGET > 0 && (ABL:MASTER:音楽技能 <= ABL:TARGET:音楽技能 || TALENT:TARGET:音楽知識)
	EXP:MASTER:演奏経験 ++
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE416
;演奏実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(416)
	RETURN RESULT
	
;いずれの楽器（30～34）も使えない
LOCAL:1 = 0
FOR LOCAL,30,35
	SIF MUSIC_ABLE(LOCAL)
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	RETURN 0

;楽器三種のいずれかを持っていても13,24,30浴室と14,40廁所では演奏できない
;SIF CFLAG:MASTER:現在位置 == 13 || CFLAG:MASTER:現在位置 == 14 || CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 30 || CFLAG:MASTER:現在位置 == 39 || CFLAG:MASTER:現在位置 == 40
SIF (IN_TOILET(CFLAG:MASTER:現在位置) || BATHCHECK(CFLAG:MASTER:現在位置)) && GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
;睡眠中
;いっそ無理やり起床させるという案もあったり
SIF CFLAG:睡眠 && !CFLAG:添い寝中 || CFLAG:添い寝中
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

@MUSIC_ABLE(ARG)
#FUNCTION
SIF !ITEM:ARG
	RETURNF 0
SELECTCASE ARG
;電子琴、吉他、ペット
	CASE 30,32,33
		RETURNF 1
;鋼琴
;博麗神社居間、紅魔館の広場と廃洋館、地霊殿の広場、寺子屋に設置しているという形です。
	CASE 31
		SIF GROUPMATCH(CFLAG:MASTER:現在位置,GET_MAP_REPLACEMENT(9),GET_MAP_REPLACEMENT(222),GET_MAP_REPLACEMENT(309),GET_MAP_REPLACEMENT(335))
			RETURNF 1
;バイオリン
	CASE 34
		SIF ABL:MASTER:音楽技能 >= 2
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 0

@ACCOMPANY_ABLE(ARG)
#FUNCTION
SIF BASE:ARG:気力 < 200
	RETURNF 0
SIF TIME_PROGRESS(TCVAR:ARG:演奏日時) < 90
	RETURNF 0
SIF ABL:MASTER:音楽技能 < 3
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:ARG:行動 == 5
	RETURNF 0

RETURNF 1