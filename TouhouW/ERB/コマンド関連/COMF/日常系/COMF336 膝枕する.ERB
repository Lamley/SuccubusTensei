@COM336
#DIM 回復前体力
#DIM 回復前気力
#DIM 回復後体力
#DIM 回復後気力
;膝枕
IF TIME_PROGRESS(TFLAG:膝枕した) < 45
	PRINTFORMW 如果不動一下脚，过一段时间会発麻
	RETURN -1
ENDIF
IF TALENT:MASTER:体型 < TALENT:体型 - 1
	PRINTFORMW 体格差别太大，很累
	RETURN -1
ENDIF


DOWNBASE:気力 += 50

DOWNBASE:MASTER:気力 += 100

LOCAL = ABL:親密 * 10 + TALENT:恋慕 * 50 + TALENT:幼稚 * 30 - TALENT:羞恥心 * 20 - TALENT:自尊心 * 20
SIF BASE:体力 > MAXBASE:体力 * 4 / 5
	LOCAL -= 30
SIF BASE:体力 < MAXBASE:体力 * 2 / 5
	LOCAL += 30
LOCAL = MAX(LOCAL,10)
IF RAND:LOCAL > 120
	TFLAG:193 = 1
ELSEIF RAND:LOCAL < 30
	TFLAG:193 = -1
ELSE
	TFLAG:193 = 0
ENDIF

;固定で獲得するソース
SOURCE:情愛 = 250
SOURCE:受動 = 70
SOURCE:反感 = 150
SIF TFLAG:193 == -1
	SOURCE:反感 += 150

;ABL:親密をみる
IF ABL:親密 <= 5
	SOURCE:情愛 += (ABL:親密 * 25)
ELSEIF ABL:親密 <= 8
	SOURCE:情愛 += 600 + (ABL:親密 * 30)
	SOURCE:露出 += 200 + (ABL:欲望 * 15)
ELSEIF ABL:親密 <= 11
	SOURCE:情愛 += 800 + (ABL:親密 * 30)
	SOURCE:恭順 += ABL:親密 * 40
	SOURCE:露出 += 300 + (ABL:欲望 * 20)
ELSE
	SOURCE:情愛 += 1000 + (ABL:親密 * 40)
	SOURCE:恭順 += ABL:親密 * 50
	SOURCE:露出 += 700 + (ABL:欲望 * 25)
ENDIF



SOURCE:受動 = 150 + 300 * ABL:従順

IF TFLAG:193 == -1
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:露出 , 0.40
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:受動 , 1.00
	TIMES SOURCE:露出 , 0.90
ELSE
	TIMES SOURCE:情愛 , 1.50
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:露出 , 1.80
	TIME += 20
	DOWNBASE:体力 -= 150
	DOWNBASE:気力 -= 300
	DOWNBASE:MASTER:気力 -= 50
ENDIF
TIME += 20
CALL SET_DERAY(6,TARGET)
;膝枕フラグ
TFLAG:膝枕した = 1440 * DAY + TIME
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE336
;膝枕実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(336)
	RETURN RESULT
;馴れ合い強度2
SIF TCVAR:MASTER:馴れ合い < 2
	RETURN 0
;室内のみ
SIF !(INROOM(CFLAG:MASTER:現在位置))
	RETURN 0
;浴室場
SIF BATHROOM()
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;イタズラ中、添い寝中は無理
SIF CFLAG:イタズラ || CFLAG:添い寝中
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;六九式体勢
SIF TEQUIP:六九式
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

