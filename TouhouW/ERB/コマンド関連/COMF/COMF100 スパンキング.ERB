;-------------------------------------------------
;打屁股
;SM系コマンド、レベル1
;-------------------------------------------------
@COM100
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 60
	SOURCE:苦痛 = 450

ELSE

	DOWNBASE:体力 += 60
	DOWNBASE:気力 += 20

	SOURCE:苦痛 = 450
	SOURCE:恐怖 = 400
	SOURCE:逸脱 = 50
	SOURCE:反感 = 500
	IF TALENT:幼稚
		SOURCE:苦痛 += 350
		SOURCE:恐怖 += 200
		SOURCE:逸脱 += 50
	ENDIF
ENDIF
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 = SOURCE:苦痛 * (10 + 5 * MAX(GETPALAMLV(PALAM:苦痛,5) - 1, 0)) / 10
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
IF HETEROSEX(TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
ENDIF
ELSE
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
ENDIF
ENDIF

EXP:PLAYER:嗜虐快楽経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE100
;打屁股実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(100)
	RETURN RESULT
;従順3未満のASSIの場合打屁股はできない（施虐狂ならＯＫ）
IF ASSIPLAY
	SIF ABL:ASSI:従順 < 3 && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;下半身着衣
SIF TEQUIP:0 & 8
	RETURN 0
RETURN 1

