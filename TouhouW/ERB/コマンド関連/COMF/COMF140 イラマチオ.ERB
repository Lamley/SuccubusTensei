;-------------------------------------------------
;強制口交
;ハードな調教コマンド、レベル2
;-------------------------------------------------
@COM140

;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 強制口交
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 4
;強度
TCVAR:PLAYER:4 = 2000
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 100
	SOURCE:苦痛 = 100

ELSE

	DOWNBASE:体力 += 150
	DOWNBASE:気力 += 200

	SOURCE:達成 = 500
	SOURCE:苦痛 = 200
	SOURCE:屈従 = 1500
	SOURCE:逸脱 = 700
	SOURCE:反感 = 700

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*40 + 100


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 700
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の口⇔調教者のＰの汚れが移動
STAIN:0 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:0

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1 || CFLAG:イタズラ
IF HETEROSEX(TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
ENDIF
ELSE
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
ENDIF
	;フェラ経験
	EXP:口淫経験 ++
	;愛情経験(受虐属性5以上限定）
	SIF TALENT:恋慕 && ASSIPLAY == 0 && ABL:受虐属性 >= 5
		EXP:愛情経験 += 1
ENDIF
RETURN 1

@CAN_COM140(ARG)
SKIPDISP ARG
PRINTL 強制口交
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:イタズラ

ELSE

	A = 0
	S = 0

	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 1
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:奉仕精神 * 4
		PRINT 奉仕精神
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		A += ABL:精液中毒 * 3
		PRINT 精液中毒
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
		S = 1
	ENDIF

	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 1
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 1
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
		S = 1
	ENDIF

	;害羞
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 1
		PRINT 害羞
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭鈍感
	IF TALENT:汚臭耐性 > 0
		SIF S
			PRINT  + 
		A += 1
		PRINT 汚臭鈍感
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭敏感
	IF TALENT:汚臭耐性 < 0
		PRINT  - 
		A -= 3
		PRINT 汚臭敏感
		PRINTV '(,3,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		A += 6
		PRINT 献身的
		PRINTV '(,6,')
		S = 1
	ENDIF
	;否定快感（淫乱、娼婦で無効）
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 1
		PRINT 否定快感
		PRINTV '(,1,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 恋慕
		PRINTV '(,5,')
		S = 5
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF

	;調教者がFutanari
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 8
		PRINT Futanari
		PRINTV '(,8,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15
	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(36以上で実行)
	PRINT  = 
	PRINTV A

	V = 36
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE140
;強制口交実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(140)
	RETURN RESULT
;調教者が男人かFutanariじゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0
;口枷使用中は不可
SIF TEQUIP:口枷
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;従順、欲望の合計が6以上ないとダメ
SIF ABL:従順 + ABL:欲望 < 6
	RETURN 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;体格差がある場合は主人が禁断的知識持ちじゃないとダメ
SIF ABS(TALENT:体型 - TALENT:PLAYER:体型) > 2 && (TALENT:MASTER:禁断的知識 == 0)
	RETURN 0
;新妻PLAY中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

