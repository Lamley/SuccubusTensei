;-------------------------------------------------
;鞭
;SM系コマンド、レベル2
;-------------------------------------------------
@COM101
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
BASE:MASTER:TSP -= 80
SOURCE:苦痛 = 750

ELSE

DOWNBASE:体力 += 80
DOWNBASE:気力 += 50

SOURCE:苦痛 = 1500
SOURCE:恐怖 = 1200
SOURCE:逸脱 = 100
SOURCE:反感 = 600
ENDIF
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 = SOURCE:苦痛 * (10 + 5 * MAX(GETPALAMLV(PALAM:苦痛,5) - 1, 0)) / 10
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
IF HETEROSEX(TARGET,PLAYER)
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
ENDIF
ELSE
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
ENDIF
ENDIF

EXP:PLAYER:嗜虐快楽経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE101
;鞭実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(101)
	RETURN RESULT
;道具を持っているか、道具なし設定でないとダメ
SIF ITEM:13 == 0 && NOITEM == 0
	RETURN 0
;従順4未満のASSIの場合鞭はできない（施虐狂ならＯＫ）
IF ASSIPLAY
	SIF ABL:ASSI:従順 < 4 && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;全裸でないとダメ（襪子、特殊は除く）
SIF TEQUIP:0 || TEQUIP:1
	RETURN 0
;浴室PLAY中はダメ
SIF TEQUIP:31
	RETURN 0
;新妻PLAY中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

