;-------------------------------------------------
;進入房間
;-------------------------------------------------
@COM698
#DIM 部屋の汚れ
#DIM 浮気
#DIM 嫉妬

;別の子とデート中
IF FLAG:デート相手 != TARGET && FLAG:デート相手 != 0
	PRINTFORM 虽然想进入%OMANEKI_PLACE()%、却被%CALLNAME:(FLAG:デート相手)%死死地用眼神睨视着
	RETURN -1
ENDIF

;見られてないかチェック
浮気 = 0
嫉妬 = 0
FOR LOCAL,1,CHARANUM
	IF LOCAL != TARGET && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		SIF LOCAL == 60
			嫉妬 = 60
		SIF TALENT:LOCAL:恋慕 && CFLAG:LOCAL:信頼度 < 1000
			浮気 = LOCAL
	ENDIF
NEXT
;琶露西が出現し4人も住んでいる地霊殿はかなり難易度が高い
;連動キャラも駆けつけてくるので二股してると大変
IF 嫉妬 || 浮気
	PRINTFORM 虽然想进入%OMANEKI_PLACE()%、
	IF 嫉妬
		PRINTFORMW 却被面露悪鬼般表情的%CALLNAME:嫉妬%给干涉了
	ELSE
		PRINTFORMW 却被面露惊讶之色的%CALLNAME:浮気%给叫住了
	ENDIF
	RETURN -1
ENDIF

CALL OMANEKI_ENTER(TARGET)

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;進入房間
@COM_ABLE698

;一括管理
SIF GLOBAL_COMABLE(698)
	RETURN RESULT
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;自宅にいない
SIF CFLAG:MASTER:現在位置 != CFLAG:自宅位置
	RETURN 0
;とりあえず恋慕だけ条件
SIF !TALENT:恋慕
	RETURN 0
;外出してない
SIF !CHK_DATENOW(CFLAG:MASTER:デート中)
	RETURN 0
;別の子とデート中,面白いのでコマンドは実行できるように
;SIF FLAG:デート相手 != TARGET && FLAG:デート相手 != 0
;	RETURN 0
;怒ってたらだめ（バグ回避用でもある）
SIF BASE:憤怒 > 50
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;-------------------------------------------------
;外泊処理
;-------------------------------------------------
@OMANEKI_ENTER(ARG)
#DIM 部屋の汚れ

;お招きフラグにARGを代入
CFLAG:ARG:お招き = ARG
CFLAG:MASTER:お招き = ARG

;初期位置をOMANEKIBEYA()=98にする
CFLAG:ARG:初期位置 = OMANEKIBEYA()
;移動させる
CFLAG:ARG:現在位置 = OMANEKIBEYA()
CFLAG:MASTER:現在位置 = OMANEKIBEYA()

;部屋の汚れ具合
;一度入っていたら参照する
IF TCVAR:ARG:待客室の汚れ > 0
	YOGORE:(OMANEKIBEYA()) = TCVAR:ARG:待客室の汚れ
ELSE
	;最初に入る時は汚臭耐性依存（敏感以下=最高,普通=中,鈍感=低,無視=ゴミの山）
	部屋の汚れ = MAX(1999 + (TALENT:ARG:61 * 2000), 0) 
	YOGORE:(OMANEKIBEYA()) = 部屋の汚れ
	TCVAR:ARG:待客室の汚れ = 部屋の汚れ
ENDIF