;吃飯（デート）
@COM610
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２

回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
回復前体力２ = BASE:TARGET:体力
回復前気力２ = BASE:TARGET:気力
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:会話経験 ++
	EXP:MASTER:約会経験 ++
	EXP:TARGET:約会経験 ++
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF
	TFLAG:98 += 2

DOWNBASE:気力 += 10

;固定で獲得するソース
SOURCE:歓楽 = 800
SOURCE:情愛 = 300

;ABL:欲望をみる
IF ABL:欲望 <= 1
	SOURCE:歓楽 += (ABL:欲望 * 60)
ELSEIF ABL:欲望 <= 3
	SOURCE:歓楽 += 300 + (ABL:欲望 * 60)
ELSEIF ABL:欲望 <= 5
	SOURCE:歓楽 += 600 + (ABL:欲望 * 60)
ELSEIF ABL:欲望 <= 8
	SOURCE:歓楽 += 900 + (ABL:欲望 * 75)
ELSEIF ABL:欲望 <= 11
	SOURCE:歓楽 += 1500 + (ABL:欲望 * 75)
ELSE
	SOURCE:歓楽 += 2500 + (ABL:欲望 * 30)
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 <= 1
	SOURCE:情愛 += (ABL:奉仕精神 * 30)
ELSEIF ABL:奉仕精神 <= 3
	SOURCE:情愛 += 200 + (ABL:奉仕精神 * 30)
ELSEIF ABL:奉仕精神 <= 5
	SOURCE:情愛 += 500 + (ABL:奉仕精神 * 30)
ELSEIF ABL:奉仕精神 <= 8
	SOURCE:情愛 += 700 + (ABL:奉仕精神 * 45)
ELSEIF ABL:奉仕精神 <= 11
	SOURCE:情愛 += 1200 + (ABL:奉仕精神 * 45)
ELSE
	SOURCE:情愛 += 1800 + (ABL:奉仕精神 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 750 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 4
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


SOURCE:受動 = 200 + 100 * ABL:従順
SOURCE:征服 = 200 + 100 * ABL:施虐属性

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:情愛 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:情愛 , 1.50
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
ENDIF
SIF TALENT:食量大
	TIMES SOURCE:歓楽 , 1.50



CALL RECOVER_PERMIL(MASTER,75,"体力")
CALL RECOVER_PERMIL(MASTER,150,"気力")
CALL RECOVER_PERMIL(TARGET,75,"体力")
CALL RECOVER_PERMIL(TARGET,150,"気力")

TIME += 60
EXP:MASTER:約会経験 ++
EXP:TARGET:約会経験 ++
CALL 満腹度上昇(TARGET,"主食")
CALL 満腹度上昇(MASTER,"主食")
MONEY -= 3000
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
回復後体力２ = BASE:TARGET:体力
回復後気力２ = BASE:TARGET:気力
DRAWLINE
PRINTFORML 体力+{ABS(回復後体力１ - 回復前体力１)}（%CALLNAME:MASTER%）
PRINTFORML 気力+{ABS(回復後気力１ - 回復前気力１)}（%CALLNAME:MASTER%）
PRINTFORML 体力+{ABS(回復後体力２ - 回復前体力２)}（%CALLNAME:TARGET%）
PRINTFORML 気力+{ABS(回復後気力２ - 回復前気力２)}（%CALLNAME:TARGET%）
RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;請吃飯
@COM_ABLE610
;一括管理
SIF GLOBAL_COMABLE(610)
	RETURN RESULT
;相手が居ない
SIF !TARGET
	RETURN 0
;デート中以外
SIF !CHK_DATENOW(CFLAG:12)
	RETURN 0
SIF !DATE_PARTY(CFLAG:MASTER:現在位置)
	RETURN 0
SIF CFLAG:行動 == 5
	RETURN 0
;食べたばかり
SIF !TIME_PROGRESS(TCVAR:312)
	RETURN 0
SIF MONEY < 3000
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

