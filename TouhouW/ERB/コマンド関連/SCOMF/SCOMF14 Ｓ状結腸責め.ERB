;-------------------------------------------------
;S状結腸責め
;Ａセックス系派生コマンド
;-------------------------------------------------
@SCOM14
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 2
;強度
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 1600

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:快Ａ = 500
	SOURCE:苦痛 = 450

ELSE

	DOWNBASE:体力 += 100
	DOWNBASE:気力 += 120

	SOURCE:快Ａ = 500
	SOURCE:情愛 = 150
	SOURCE:苦痛 = 900
	SOURCE:露出 = 160
	SOURCE:逸脱 = 200
	SOURCE:反感 = 700

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 700
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:PLAYER:2 |= STAIN:4
STAIN:PLAYER:2 |= STAIN:7
STAIN:4 |= STAIN:PLAYER:2
STAIN:7 |= STAIN:PLAYER:2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
IF FLAG:70 == 1
	EXP:MASTER:時姦経験 += 1
ELSEIF CFLAG:MASTER:イタズラ == 2
	EXP:MASTER:睡眠姦経験 += 1
	EXP:TARGET:睡眠姦経験 += 1
ELSE
	EXP:Ａ性交経験 ++
	;愛情経験
	SIF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE74
;玩弄Ｓ状結腸
SIF !TFLAG:100
	RETURN 0
SIF !MASTER_POSE(SET_A,SET_P,1)
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(74)
	RETURN RESULT
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
SIF TEQUIP:TARGET:51 != MASTER
	RETURN 0
RETURN 1


;-------------------------------------------------
;選択可能判定
;-------------------------------------------------
;玩弄Ｓ状結腸
@COM_ABLE514
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(514)
	RETURN RESULT
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
RETURN 1

