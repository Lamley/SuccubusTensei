;--------------------------------------------------------------------------------------------------
;eraTW東風谷早苗口上UPDATE
;
;※注意　本体バージョン更新の際には
;　　　　『@M_KOJO_UPDATE_K31』の「#DIM CONST 早苗口上ver」の更新を忘れないこと！！
;--------------------------------------------------------------------------------------------------

;------M_KOJO_K31_UPDATE.ERB-----------------------------------------------------------------------
;2017/4/26　Ver 0.03.00　「M_KOJO_K31_イベント.ERB」より分離
;--------------------------------------------------------------------------------------------------
;※※※※※※※※※※※※※※※※※※※※※※※※※※
;"UPDATE判定"
;※※※※※※※※※※※※※※※※※※※※※※※※※※
@M_KOJO_UPDATE_K31
;口上独自のUPDATE用
;ENCOUNTER専用に独自分岐フラグを仕込んだ場合等にどうぞ
;UPDATE判定でCFLAGを使う場合、キャラ番号の指定が必要になります

;現在のverはver0.03です
#DIM CONST 早苗口上ver = 3


#DIM 現在のver整数
#DIM 現在のver小数
#DIM 早苗口上ver整数
#DIM 早苗口上ver小数


;ver0.02以前にはこのUPDATE CHECKはなかったので
SIF !CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	CFLAG:31:K31_CFLAG_SYSTEM_VERSION = 2

;PRINTFORM中に%を登場させて変な感じにさせたくないのでここで変換
現在のver整数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION / 100
現在のver小数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION % 100
早苗口上ver整数 = 早苗口上ver / 100
早苗口上ver小数 = 早苗口上ver % 100

CALL M_KOJO_COLOR_K31
;アップデート時の頭の文章
PRINTL 
PRINTL --早苗口上UPDATE CHECK--
RESETCOLOR

;『素質の変更』を行っている場合早苗の清掃技能は0になるが
;本体のUPDATEによってCSVの値が読み込まれてしまうため再修正を行う
;ほんとはUPDATE書き換えて、早苗だけ無効にしたいけどバグの温床になりそうなんでやめる
IF CFLAG:31:3121 == 1
	;---清掃技能　( D 1 → E 0)---
	;---清掃経験　( 20 → 4)---
	;流石に、料理までつけると素質盛りすぎ状態なので（技能にEがひとつもないとかいう状態）清掃技能に下方修正
	;既に技能,経験が育っている場合は変更しない
	IF ABL:31:40 == 1 && EXP:31:70 < 21
		PRINTL 
		PRINTL 本体のUPDATEにより、早苗の技能が上書きされたので再修正します
		PRINTL 
		PRINT 	清掃技能(
		CALL PRINT_RANK(ABL:31:40,"MAX_6")
		PRINTFORM  {ABL:31:40} →
		CALL PRINT_RANK(0,"MAX_6")
		RESETCOLOR
		PRINTL  0)
		
		PRINTFORML 	清掃経験( {EXP:31:70} → 4)
		ABL:31:40 = 0
		EXP:31:70 = 4
	ENDIF
ENDIF
;--------------------------------------------------------------------------------------------
;早苗口上のバージョンチェック
;--------------------------------------------------------------------------------------------
;同じバージョンの場合
IF 早苗口上ver == CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	PRINTFORML %"現在：Ver " + TOSTR(現在のver整数) + "." + TOSTR(現在のver小数,"D2")%
	PRINTFORML %"適用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	PRINTL 
	PRINTFORML 最新のバージョンが適用されています アップデートの必要はありません
	RETURN 1
ENDIF

;ダウングレードしようとしてる～
IF 早苗口上ver < CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	SETCOLOR C_RED
	PRINTFORML 【注意！】現在適用されているものよりも以前のバージョンが適用されようとしています
	RESETCOLOR
	PRINTL 
	PRINTFORML %"現在：Ver " + TOSTR(現在のver整数) + "." + TOSTR(現在のver小数,"D2")%
	SETCOLOR C_RED
	PRINTFORML %"適用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	RESETCOLOR
	PRINTL 
	PRINTL アップデートを続行しますか？
	PRINTL -YESは基本的にこの分岐に入る原因がわかっている場合のみ使用してください-
	PRINTL *バグが発生しても知らんよ*
	PRINTFORML [0]-YES(強制的に適用バージョンへの変更を行います)
	PRINTFORML [1]-NO
	INPUT
	IF !RESULT
		PRINTL アップデートを続行します　バグの発生にお気をつけ下さい
	ELSE
		PRINTL アップデートを中断します　最新バージョンを取得して下さい
		RETURN 1
	ENDIF
ENDIF

IF 早苗口上ver > CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	SETCOLOR C_YELLOW
	PRINTFORML バージョンのアップデートを行います
	RESETCOLOR
	PRINTL 
	PRINTFORML %"現在：Ver " + TOSTR(現在のver整数) + "." + TOSTR(現在のver小数,"D2")%
	CALL M_KOJO_COLOR_K31
	PRINTFORML %"適用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	RESETCOLOR
ENDIF

;####################################################
;ここからバージョンのアップデートが始まる
;####################################################
;----------------------------------------------------
;素質の変更(Ver 0.03より)
;----------------------------------------------------

IF CFLAG:31:3121 == 0 && (ABL:31:44 < 2 || EXP:31:79 < 200 || EXP:31:76 < 50 || EXP:31:70 < 50)
	PRINTL 
	PRINTL -----素質技能の変更-----
	;要するに、今のままだと口上の内容と技能・素質が噛み合わない場合があるのでそれが気になる人はどうぞ
	PRINTL 口上内容との整合性のため早苗さんの素質技能を変更することが出来ます
	PRINTL 変更してもしなくても、システム上に影響をおよぼすことはありません
	;PRINTL 『この子メシマズのくせになんか語ってる…』が気になる方は変更をどうぞ
	PRINTL 
	PRINTL ※変更される素質・能力
	
	;---[音楽知識]付与(正確には、楽器知識)---
	;音楽技能がBであることにともなって追加
	;流石にみすちーとか九十九姉妹と同じレベルで何もないというのも
	;なんか[演奏]の地の文が変化するらしいのでコメントアウト
	;自力で取れるらしいので頑張ろう(演奏経験1000+技巧4以上)
	;IF !TALENT:31:音楽知識
	;	PRINTL [音楽知識]追加
	;	PRINTL 
	;ENDIF

	;---演奏経験　( 0 → 200)---
	;音楽技能がBであることにともなって追加
	SIF EXP:31:79 < 200
		PRINTFORML 	演奏経験( {EXP:31:79} → 200)

	;---料理技能　( E 0 → C 2)---
	;音楽技能と間違えられたクッソ哀れな子
	;流石に可哀想なので追加
	IF ABL:31:44 < 2
		PRINT 	料理技能(
		CALL PRINT_RANK(ABL:31:44,"MAX_6")
		PRINTFORM  {ABL:31:44} →
		CALL PRINT_RANK(2,"MAX_6")
		RESETCOLOR
		PRINTL  2)
	ENDIF

	;---料理経験　( 0 → 50)---
	;料理技能がCであることにともなって追加
	;料理技能をつけようとして音楽技能がついてるっぽい早苗さんに料理技能及び料理経験をつける
	;3人揃って技能0のメシマズ神社救済法案
	SIF EXP:31:76 < 50
		PRINTFORML 	料理経験( {EXP:31:76} → 50)
	
	;---清掃技能　( D 1 → E 0)---
	;---清掃経験　( 20 → 4)---
	;流石に、料理までつけると素質盛りすぎ状態なので（技能にEがひとつもないとかいう状態）清掃技能に下方修正
	;既に技能,経験が育っている場合は変更しない
	IF ABL:31:40 == 1 && EXP:31:70 < 21
		PRINT 	清掃技能(
		CALL PRINT_RANK(ABL:31:40,"MAX_6")
		PRINTFORM  {ABL:31:40} →
		CALL PRINT_RANK(0,"MAX_6")
		RESETCOLOR
		PRINTL  0)
		
		PRINTFORML 	清掃経験( {EXP:31:70} → 4)
	ENDIF
	
	
	PRINTL 
	PRINTFORML [0]-保留する		(この場では変更しません、この選択肢はバージョン変更のときに再び登場します)
	PRINTFORML [1]-変更する		(この選択肢は再び登場しません)
	PRINTFORML [2]-変更させない	(この選択肢は再び登場しません)
	INPUT
	SELECTCASE RESULT
	;保留しました
	CASE 0
		CFLAG:31:3121 = 0
		PRINTL 保留しました
		PRINTW 
	;変更しました
	CASE 1
		CFLAG:31:3121 = 1
		PRINTL 変更しました
		
		;変更内容はここに記述
		;SIF !TALENT:31:音楽知識
		;	TALENT:31:音楽知識 = 1
		SIF EXP:31:79 < 200
			EXP:31:79 = 200
		SIF ABL:31:44 < 2
			ABL:31:44 = 2
		SIF EXP:31:76 < 50
			EXP:31:76 = 50
		IF ABL:31:40 == 1 && EXP:31:70 < 21
			ABL:31:40 = 0
			EXP:31:70 = 4
		ENDIF
		PRINTW 
	;変更させません
	CASEELSE
		CFLAG:31:3121 = -1
		PRINTL 変更させません
		PRINTW 
	ENDSELECT
	;============================================================
	;[1]、[2]を選んだけれどもどうしてももう一度選択肢を選びたい時
	;1.CFLAG:31:3121 = 0 にする
	;============================================================
ENDIF
;----------------------------------------------------
;面識の剥奪(Ver 0.01より)
;----------------------------------------------------
;口上的には「最初から」みたいなもの　バージョンアップで仕様が変わった場合や、他のルートに行きたい時とかにどうぞ
;本当はフラグ等のリセットは初対面時に行っている
IF CFLAG:31:18 != 0
	PRINTL 
	PRINTL -----面識について-----
	PRINTL 主人公は、既に早苗さんと面識があります
	PRINTL 「口上をアップデートした」または、「初めて導入した」場合面識のリセットを強くオススメします
	PRINTL また、口上的には「最初から」のようなもので、口上内での進行具合・フラグも全て初期化されます
	PRINTL バージョンアップ時の「readme.txt」にて面識のリセットが強く推奨されている場合
	PRINTL 最新バージョンにおいて、進行具合・フラグに改変を行った可能性があります
	PRINTL (従って、上記条件でリセットしなかった場合のバグは仕様です)
	PRINTL 
	PRINTFORML [0]-リセットするぜ
	PRINTFORML [1]-いやどす
	INPUT
	IF !RESULT
		CFLAG:31:18 = 0
		PRINTL -面識をリセットしました-　早苗さんとは初対面になります
	ELSE
		PRINTL -面識をリセットしませんでした-
	ENDIF
ENDIF

CFLAG:31:3120 = 早苗口上ver

;PRINTFORM中に%を登場させて変な感じにさせたくないのでここで変換
現在のver整数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION / 100
現在のver小数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION % 100

PRINTL 
PRINTL アップデートを完了しました

CALL M_KOJO_COLOR_K31
PRINTFORML 適用バージョン　%"Ver " + TOSTR(現在のver整数) + "." + TOSTR(現在のver小数,"D2")%
RESETCOLOR
PRINTL 

RETURN 1
