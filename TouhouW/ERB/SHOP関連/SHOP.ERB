;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニュー画面表示
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@SHOW_SHOP
;起床前画面表示中フラグ
TFLAG:100 = 0
;---------------------
;起床時間自動補正、最低睡眠時間は4時間
LOCAL:1 = CFLAG:MASTER:就寝時間 - DAY * 1440
IF TIME:3 >= LOCAL:1 && (TIME:3 - LOCAL:1) < 240
	PRINTL 睡眠時間太短了。至少需要睡4个小时以上
	TIME:3 = ((LOCAL:1 / 60) + 4) * 60
	SIF (LOCAL:1 % 60)
		TIME:3 += 60
	SIF TIME:3 >= 1440
		TIME:3 -= 1440
	PRINTFORMW 起床時間设定为（%時刻表示(TIME:3)%）了
ENDIF
;---------------------
DRAWLINE
PRINTFORML {DAY}日目(%GET_DAY()%)　所持金:\\%TOSTR(MONEY,"#,###")%
PRINTL

CALL BASE_BAR(MASTER, "体力", 28, "緑")
PRINTFORML 
CALL BASE_BAR(MASTER, "気力", 28, "青")
PRINTFORML 
PRINTFORML (TSP:{BASE:MASTER:8}/{MAXBASE:MASTER:8})
PRINTL

;PRINT 体力
;CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
;PRINTFORML  ({BASE:MASTER:体力,5}/{MAXBASE:MASTER:体力,5})  
;PRINT 気力
;CALL PRINT_COLORBAR, BASE:MASTER:1,MAXBASE:MASTER:1,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
;PRINTFORML  ({BASE:MASTER:1,5}/{MAXBASE:MASTER:1,5})  

PRINTFORM 自宅位置：%STR:(5000+MAIN_MAP)%
IF FLAG:気絶中断
	PRINTFORML 気絶中	
ELSE
	PRINTFORML 
ENDIF
PRINTFORML 起床時間：%時刻表示(TIME:3)%
PRINTL

SETCOLOR 103,228,126
PRINTL 【主菜単】
RESETCOLOR
PRINTLC [100] - 睜開眼睛
PRINTLC [102] - 居住環境設定
SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	PRINTLC [110] - 搬家
PRINTL
PRINTL

PRINTLC [101] - 能力表示
PRINTLC [103] - 隙間商店
PRINTL
PRINTLC [105] - 実績解除
SIF FLAG:700
	PRINTLC [106] - 收集
PRINTL
PRINTL

SETCOLOR 103,228,126
PRINTL 【系统菜単】
RESETCOLOR
PRINTLC [107] - 重置委托
PRINTLC [700] - 重置服装
PRINTL
PRINTLC [400] - OPTION

SIF SAVESTR:本体バージョン != eraTW_Version
	SETCOLOR C_RED
PRINTLC [500] - UPDATE
RESETCOLOR

PRINTL
PRINTL

PRINTLC [200] - SAVE
PRINTLC [300] - LOAD
;PRINTLC [999] - 修改器
PRINTL

SIF CM_SELL_AMT
	PRINTFORML [600] - 籌码清算（{CM_SELL_AMT}籌码）

DRAWLINE

;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニューRESULT処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@USERSHOP
;起床
IF RESULT == 100
	BEGIN TRAIN
	RETURN 1
;能力表示
ELSEIF RESULT == 101
	DEBUGCLEAR
	CALL SHOW_CHARA_LIST("存在するキャラリスト", "体力気力", "「情報表示」「条件変更」")
;	CALL SHOW_CHARADATA
;收藏
ELSEIF RESULT == 106 && FLAG:700 && !FLAG:気絶中断
	CALL SHOW_COLLECTION
;通信販売
ELSEIF RESULT == 103 && !FLAG:気絶中断
	CALL ITEM_BUY
;設定
ELSEIF RESULT == 102 && !FLAG:気絶中断
	CALL SCHEDULE
ELSEIF RESULT == 105 && !FLAG:気絶中断
	CALL BONUS
ELSEIF RESULT == 107 && !FLAG:気絶中断
	CALL IRAI_RESET
ELSEIF RESULT == 110 && CFLAG:MASTER:現在位置 != OMANEKIBEYA()&& !FLAG:気絶中断
	CALL SET_MAINHOME
;SAVE
ELSEIF RESULT == 200
	SAVEGAME
;LOAD
ELSEIF RESULT == 300
	LOADGAME
;OPTION
ELSEIF RESULT == 400
	CALL OPTION
ELSEIF RESULT == 500
	CALL UPDATE
ELSEIF RESULT == 600 && CM_SELL_AMT
	CALL CM_BALANCE
ELSEIF RESULT == 700
	CALL RESET_ALL_CLOTHES
ELSEIF RESULT == 999
	CALL DEBUG_MENU_U
ENDIF
