;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;MASTER生成処理
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_CHARAMAKE(ARG)
;NOは適当に設定
NO:ARG = 0

;性別(1=女性 2= 男性 3=Futanari)
TALENT:ARG:2 = 1
;MASTERは初期性別＝男
SIF ARG == 0
	TALENT:ARG:2 = 2
;気力、体力
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0
MAXBASE:ARG:1 = 2000
BASE:ARG:1 = MAXBASE:ARG:1

;キャラクタ生成
;EX_APPEND_CHARACTER
;キャラを追加したときにいちいちここをいじる必要の無いように
;キャラクタ数上限はERHファイルで定義
FOR LOCAL,1,キャラクタ数上限
	EXISTCSV LOCAL
	SIF RESULT == 1
		ADDCHARA LOCAL
NEXT

;モード選択
$INPUT_LOOP
PRINTL ★★請选択游戏模式★★
PRINTL [0]START [1]扮演模式
INPUT
IF RESULT == 0
	FLAG:5 = 0
ELSEIF RESULT == 1
	$LOOP_NARIKIRI
	FOR LOCAL,1,CHARANUM
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTL
	DRAWLINE
	PRINTL [900] 取消
	PRINTL 要変成誰呢？
	INPUT
	SELECTCASE RESULT
		CASE 1 TO CHARANUM
			NAME:MASTER = %NAME:RESULT%
			CALLNAME:MASTER = %CALLNAME:RESULT%
			FOR LOCAL,0,200
				TALENT:MASTER:LOCAL = TALENT:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,55
				ABL:MASTER:LOCAL = ABL:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,14
				MAXBASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
				BASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
			NEXT
			FOR LOCAL,0,100
				EXP:MASTER:LOCAL = EXP:RESULT:LOCAL
			NEXT
			CFLAG:MASTER:7 = RESULT
			CFLAG:MASTER:初期位置 = CFLAG:RESULT:初期位置
			CFLAG:RESULT:出禁 = 1
			FLAG:なりきり = RESULT
			PRINTFORMW 以%CALLNAME:RESULT%的身份来開始游戏
			IF !TALENT:MASTER:処女
				PRINTL 要変成処女吗？
				PRINTL [0]是 [1]否
				INPUT
				IF RESULT == 0
					TALENT:MASTER:処女 = 1
					EXP:MASTER:Ｖ経験 = 0
					ABL:MASTER:Ｖ感覚 = 0
					EXP:MASTER:Ｖ性交経験 = 0
					EXP:MASTER:Ｖ拡張経験 = 0
					EXP:MASTER:精液経験 = 0
				ENDIF
			ENDIF
			CALL CLOTHES_CUSTOM
			RETURN 0
		CASE 900
			GOTO INPUT_LOOP
		CASEELSE
			GOTO LOOP_NARIKIRI
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

;名前決定
CALL NAME_CUSTOM(ARG)

;ステータス設定
CALL CUSTOM_TERMINAL(ARG)

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;MASTERステータス設定処理
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_TERMINAL(ARG)
;排他処理
;MASTERが女、Futanari時の排他
IF TALENT:ARG:2 & 1
	ABL:ARG:断袖属性 = 0
	ABL:ARG:断袖中毒 = 0
	EXP:ARG:断袖経験 = 0
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
;MASTERが男時の排他
ELSE
	ABL:ARG:Ｖ感覚 = 0
	ABL:ARG:百合属性 = 0
	ABL:ARG:百合中毒 = 0
	EXP:ARG:百合経験 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:乳交経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:胸囲 = -2
ENDIF
;チ○コ付いてりゃ精力セット
IF TALENT:ARG:2 & 2
	MAXBASE:ARG:精力 = 1000
	BASE:ARG:精力 = 1000
	MAXBASE:ARG:勃起 = 1500
ELSE
	TALENT:ARG:非童貞 = 0
	MAXBASE:ARG:精力 = 0
	BASE:ARG:精力 = 0
ENDIF
;処女ならV系経験無し
IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
;非処女はV経験１
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

;===========
;画面表示
;===========
DRAWLINE
PRINTSL NAME:ARG
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
PRINT 気力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
PRINTL 
;能力・性技表示
CALL PRINT_STATE_ABL(ARG)
PRINTL 
;素質表示
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
;経験表示
CALL PRINT_STATE_EXP(ARG)
PRINTL
;------------------------------
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL 这样真的可以吗？
;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG,LOCAL:101)
;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ELSEIF LOCAL:100 == 1006
	CALL CLOTHES_CUSTOM
ENDIF
;------------------------------
DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ? 是 # 決定\@  [1001] - 名字的変更  [1002] - 体力、気力的設定
PRINTL [1003] - 素質的設定  [1004] - 能力的設定  [1005] - 経験的設定
INPUT
;最終決定
IF RESULT == 1000 && LOCAL:100 == 1000
	SIF NO:MASTER == NO:ARG
		CALL CLOTHES_CUSTOM
	RETURN 0
;設定系入力
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF

;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 2500
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 2500
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
;素質設定
ELSEIF LOCAL:100 == 1003
	;ページ送り
	SIF RESULT == 998 && LOCAL:101
		LOCAL:101 --
	SIF RESULT == 999
		LOCAL:101 ++
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;陥落素質ほか
	IF NO:ARG == NO:MASTER
		SIF 選べない素質(RESULT)
			RESTART
	ELSE
		SIF 選べない素質2(RESULT, NO:ARG)
			RESTART
	ENDIF
	;性別
	IF RESULT == 2
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;性別嗜好
	ELSEIF RESULT == 81
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1 || RESULT == 6
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
		;オンオフ型で別途追加が必要な素質
		;母乳体質
		IF RESULT == 149
			IF TALENT:ARG:RESULT
				MAXBASE:ARG:母乳 = 2000
			ELSE
				MAXBASE:ARG:母乳 = 0
			ENDIF
		ENDIF
	;体型
	ELSEIF RESULT == 100
		;体型が「小人(-5)」と「巨人(3)」の場合を独立処理
		IF TALENT:ARG:RESULT == -5
			TALENT:ARG:RESULT = -2
		ELSEIF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -5
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;量的素質
	ELSE
		;性別
		IF LOCAL == 2
			IF TALENT:ARG:RESULT == 3
				TALENT:ARG:RESULT = 1
			ELSE
				TALENT:ARG:RESULT ++
			ENDIF
		;端まで行くと-1を掛ける
		ELSEIF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;中毒関係
	SIF LOCAL >= 30
		RESTART
	;奇数(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
	;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		RESTART
	ENDIF
	PRINTFORM 請輸入%EXPNAME:LOCAL%      
	PRINTBUTTON "[中止]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT,30)
ELSEIF RESULT == 1006
ENDIF
RESTART

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;名前の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@NAME_CUSTOM(ARG)
PRINTL 請输入人物的名字
IF STRLENS(NAME:ARG)
	PRINTFORM 現在的名字：%NAME:ARG%      
	PRINTBUTTON "[不変更]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 請输入人物的称呼名
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在的CALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[不変更]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;ベースの設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 2500
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
PRINT 気力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 2500
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;素質の設定
;CALL		USER
;ARG		0:MASTERキャラNO, 1:ページ数
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	IF NO:ARG == NO:MASTER
		SIF 選べない素質(LOCAL)
			CONTINUE
	ELSE
		SIF 選べない素質2(LOCAL, NO:ARG)
			CONTINUE
	ENDIF
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
		IF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		IF !TALENT:ARG:LOCAL == 0
			SETCOLOR 150,150,150
		ELSEIF (TALENT:ARG:2 == 1 || TALENT:ARG:2 == 0)
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;無接吻経験
	ELSEIF LOCAL == 6
		IF TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		IF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
			SETCOLOR 150,150,150
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS,35,LEFT%
	LOCAL:1 ++
	IF LOCAL:1 % 2 == 0
		SETCOLOR 150,150,150
		PRINTL
	ENDIF
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 察看前一頁                 ",998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 察看前一頁",35,LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 3
	PRINTBUTTON "[999] - 察看下一頁",999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 察看下一頁",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;能力の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;経験の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	SIF LOCAL == 57 || (60 <= LOCAL && LOCAL <= 65)
		CONTINUE
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;服装の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CLOTHES_CUSTOM(ARG)
;主人公以外なら変更しない
SIF NO:ARG != NO:MASTER || FLAG:なりきり
	RETURN 0
;MASTERの性別判定
IF TALENT:ARG:2 & 1
	CFLAG:ARG:7 = 2
ELSE
	CFLAG:ARG:7 = 1
ENDIF
PRINTFORML 請選択平時的装扮。
DRAWLINE
$LOOP
PRINTL [0] - 全裸
PRINTL [1] - 一套男装
PRINTL [2] - 一套女装
PRINTL [3] - 一套執事服
PRINTL [4] - 一套女僕服
PRINTL [5] - 一套神官服
PRINTL [6] - 一套作務衣
PRINTL [7] - 一套道士服
PRINTL [8] - 一套学生服
PRINTL [9] - 一套女学生服
INPUT
SELECTCASE RESULT
	CASE 0
	PRINTFORMW 攻撃太多了！
	GOTO LOOP
	CASE 1 TO 6
		CFLAG:MASTER:7 = RESULT
	CASE 7 TO 9
		CFLAG:MASTER:7 = RESULT + 1
	CASEELSE
		GOTO LOOP
ENDSELECT
RETURN 0

@選べない素質(ARG)
#FUNCTION
SELECTCASE ARG
	CASE IS >= 169
		RETURNF 1
	CASE 3 TO 9, 16, 34, 50, 51, 52, 53, 55, 72 TO 79, 84, 85, 86, 92, 93, 94, 120 TO 122, 130, 132 TO 136, 137, 138, 141, 144, 145, 149, 152 TO 162
		RETURNF 1
	;女以外無理
	CASE 146
		SIF TALENT:MASTER:性別 != 1
			RETURNF 1
	;男だと無理
	CASE 142
		SIF TALENT:MASTER:性別 == 2
			RETURNF 1
	CASE 156
		SIF TALENT:MASTER:性別 == 2 || !TALENT:MASTER:幼稚
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@選べない素質2(ARG, ARG:1)
#FUNCTION
SELECTCASE ARG
	CASE IS >= 169
		RETURNF 1
	CASE 3 TO 5, 7 TO 9, 16, 17, 34, 55, 73 TO 79, 84, 120 TO 122, 152 TO 155, 159 TO 162
		RETURNF 1
	CASE 156
		SIF TALENT:(ARG:1):性別 == 2 || !TALENT:(ARG:1):幼稚
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;===========
;キャラカスタム
;===========
@CHARA_CUSTOM

	PRINTL 可以変更人物的設定
	FOR LOCAL, 0, CHARANUM
		IF FLAG:なりきり
			SIF LOCAL == FLAG:なりきり
				CONTINUE
		ENDIF
		PRINTFORMLC [{LOCAL}] - %CALLNAME:LOCAL%
	NEXT
	PRINTL [999] 設定完了
	INPUT

	IF RESULT >= 0 && RESULT < CHARANUM
		SIF RESULT != FLAG:なりきり
			CALL CUSTOM_TERMINAL(RESULT)
	ELSEIF RESULT == 999
		PRINTL 可以吗？
		PRINTL [0] 是
		PRINTL [1] 否
		PRINTL
		ONEINPUT
		IF RESULT == 0
			RETURN
		ENDIF
	ENDIF
	RESTART
RETURN 0

@LIMITED_CUSTOM(ARG)
$LOOP
PRINTFORML [0] 変更体型            現在的体型: %GET_TALENTNAME(100,TALENT:ARG:100)%
PRINTFORML [1] 変更胸囲            現在的胸囲: %GET_TALENTNAME(105,TALENT:ARG:105)%
PRINTFORM [2] 感度上昇             
	SELECTCASE TCVAR:ARG:感度上昇部位
		CASE 101 TO 104,106
			PRINTFORML %TALENTNAME:(TCVAR:ARG:感度上昇部位)%選択中
		CASEELSE
			PRINTL
	ENDSELECT
DRAWLINE
PRINTL [100] 終了
INPUT
IF RESULT == 0
	PRINTLC [0] 小人
	PRINTLC [3] 幼児体型
	PRINTLC [4] 矮小体型
	PRINTLC [5] 普通
	PRINTLC [6] 長身
	PRINTLC [7] 巨躯
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	SELECTCASE RESULT
		CASE 0, 3 TO 7
			TALENT:ARG:100 = RESULT - 5
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 1
	PRINTLC [0] 絶壁
	PRINTLC [1] 貧乳
	PRINTLC [2] 普乳
	PRINTLC [3] 巨乳
	PRINTLC [4] 爆乳
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 4
			TALENT:ARG:105 = RESULT - 2
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 2
	PRINTL 可以提升某一个部位的感度
	PRINTFORMLC [101] Ｃ感度
	PRINTFORMLC [102] Ｖ感度
	PRINTFORMLC [103] Ａ感度
	PRINTFORMLC [104] Ｂ感度
	PRINTFORMLC [106] Ｍ感度
	PRINTL
	DRAWLINE
	PRINTL [100] 返回
	INPUT
	IF RESULT >= 101 && RESULT <= 104 || RESULT == 106
		IF TALENT:ARG:RESULT > 0
			PRINTW 已経是敏感了
		ELSE
			TCVAR:ARG:感度上昇部位 = RESULT
		ENDIF
	ENDIF
	GOTO LOOP
ENDIF