;きょうのぱんつ処理の洗練
;ARGのはくことができるパンツを抽選する
;元々の今日のぱんつ関数だと処理上限がないのでシャッフルする
;FISHER_YATES_SHAFFLEつかおうとしたらF関数内部でCALLすんのムリだったでござる
@今日のぱんつ(ARG)
#FUNCTION
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM TODAYS_PANTS
#DIM RAND_PANTS, MAXPANTS
#DIM P_TABLE

VARSET RAND_PANTS , 0
FOR LOCAL:0 , 0 , MAXPANTS
	RAND_PANTS:(LOCAL:0) = LOCAL:0
NEXT
FOR LOCAL:0 , 0 , MAXPANTS - 1
	LOCAL:1 = RAND(LOCAL:0 , MAXPANTS)
	SWAP RAND_PANTS:(LOCAL:0), RAND_PANTS:(LOCAL:1)
NEXT

FOR P_TABLE, 0, MAXPANTS
	IF GET_INT(ARG, "下半身内袴_ずらし可能", RAND_PANTS:P_TABLE, "はける")
		TODAYS_PANTS = RAND_PANTS:P_TABLE
		BREAK
	ELSE
		CONTINUE
	ENDIF
	SIF P_TABLE == MAXPANTS - 1
		THROW %CALLNAME:ARG%のぱんつ抽選に失敗
NEXT
RETURNF TODAYS_PANTS

;互換用
@PANTSNAME(ARG)
#FUNCTIONS
RETURNF GET_STR(, CLOTHES_PARTS_TO_CATEGORY("下半身内袴２"), ARG, "名前")

@PANTS_DESCRIPTION(ARG)
#FUNCTIONS
RETURNF GET_STR(, CLOTHES_PARTS_TO_CATEGORY("下半身内袴２"), ARG, "描写")

@PANTS_GET(ARG,ARGS)
#DIM 種類
種類 = EQUIP:ARG:下半身内袴２
CALL PANTS_GETTING(ARG, 種類)

CFLAG:ARG:ノーパン = 1
EQUIP:ARG:下半身内袴２ = 0
TEQUIP:ARG:下半身着衣状況 = 1

SIF ARGS == "もらう"
	CFLAG:ARG:ノーパン = 2

@PANTS_GETTING(ARG, 種類)
#DIM 種類
#DIM スキマ送り成否
スキマ送り成否 = 0
PANTS_TEMP:ARG:種類 += 1
IF ITEM:收藏隙間
	IF FLAG:Cスキマ自動仕様
		CALL PANTS_SUKIMA(ARG, 種類)
		スキマ送り成否 = RESULT
	ELSEIF FLAG:パンツ輸送 == 0
		PRINTFORML 要使用收藏隙間麼？（剩余：{ITEM:收藏隙間}）
		CALL ASK_YN
		IF !RESULT
			CALL PANTS_SUKIMA(ARG, 種類)
			スキマ送り成否 = RESULT
		ENDIF
	ENDIF
ENDIF
SIF !スキマ送り成否
	FLAG:パンツ輸送 = 1
RETURN スキマ送り成否

@PANTS_SUKIMA(ARG, 種類)
#DIM 種類
#DIM スキマ送り成否
スキマ送り成否 = 0
IF PANTS_TEMP:ARG:種類
	CFLAG:ARG:(種類 + 100) += PANTS_TEMP:ARG:種類 
	PANTS_TEMP:ARG:種類 = 0
	PRINTFORMW %CALLNAME:MASTER%打開了收藏隙間、把%CALLNAME:ARG%的%PANTSNAME(種類)%放了進去
	ITEM:收藏隙間 -= 1
	スキマ送り成否 = 1
ENDIF
RETURN スキマ送り成否

@SHOW_HAVE_COLLECTION
IF ITEM:收藏隙間 == 0
	CALL DISPLAY_COLLECTION(1,0)
	PRINTW 
ELSE
	PRINTFORMW 要使用收藏隙間麼？（剩余：{ITEM:收藏隙間}）
	DRAWLINE
	CALL DISPLAY_COLLECTION(1,1)
	DRAWLINE
	PRINTFORML [0] 不使用
	INPUT
	LOCAL = RESULT
	CALL PANTS_SUKIMA(LOCAL / 100, LOCAL % 100)
	;パンツ輸送対策
	SIF RESULT
		CALL SPEND_HAVE_COLLECTION(LOCAL)
ENDIF
