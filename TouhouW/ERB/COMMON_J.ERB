@GET_SUCCESS_RATE
#FUNCTION
VARSET LOCAL
;親密
LOCAL += ABL:親密 * 5
LOCAL += GETPALAMLV(PALAM:恭順,5) * 3

;叛逆.坦率
LOCAL -= TALENT:態度 * 5
;堅強
SIF TALENT:胆量 > 0
	LOCAL -= 5

;自尊心高
SIF TALENT:自尊心 > 0
	LOCAL -= 15
;自尊心低
SIF TALENT:自尊心 < 0
	LOCAL += 5
;喜歡引人注目
SIF TALENT:喜歡引人注目
	LOCAL += 2
;圧抑
SIF TALENT:自己愛 < 0
	LOCAL -= 10
;抵抗
SIF TALENT:抵抗
	LOCAL -= 10
;容易高潮
SIF TALENT:容易高潮
	LOCAL += 10
;性別嗜好
SIF OTOKOGIRAI(TARGET)
	LOCAL -= 7

;調教者の素質
;魅惑
SIF TALENT:PLAYER:魅惑
	LOCAL += 6
;謎之魅力
SIF TALENT:PLAYER:謎之魅力
	LOCAL += 6

;相性チェック
R = NO:PLAYER
IF RELATION:R > 0 && RELATION:R < 30
	LOCAL -= 10
ELSEIF RELATION:R > 0 && RELATION:R < 70
	LOCAL -= 6
ELSEIF RELATION:R > 0 && RELATION:R < 100
	LOCAL -= 3
ELSEIF RELATION:R >= 100 && RELATION:R < 130
	LOCAL += 3
ELSEIF RELATION:R >= 100 && RELATION:R < 170
	LOCAL += 6
ELSEIF RELATION:R >= 100 && RELATION:R >= 170
	LOCAL += 10
ENDIF

;好感度
LOCAL += CFLAG:好感度 / 200
;信頼度
LOCAL += CFLAG:信頼度 / 20

;思慕
SIF TALENT:思慕
	LOCAL += 20
;恋慕
SIF TALENT:恋慕
	LOCAL += 40
LOCAL += (CFLAG:MASTER:310 - CFLAG:TARGET:310) / 500
LOCAL += TFLAG:幸運補正 * 10
SIF LOCAL < 0
	LOCAL = 0

DEBUGPRINTFORML SUCCESS_RATE{LOCAL}

RETURNF LOCAL
