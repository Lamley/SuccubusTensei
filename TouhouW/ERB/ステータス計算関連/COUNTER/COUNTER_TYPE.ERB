@COUNTER_TYPE(ARGS,ARG)
;TFLAG:102 =1で日常、=2で通常うふふ、=3で相手からの押し倒し
#FUNCTION
VARSET LOCAL
SELECTCASE ARGS
	CASE "ソフト"
		SIF TFLAG:102 == 1 && TCVAR:ARG:馴れ合い >= 2
			LOCAL = 100
	CASE "ベリーソフト"
		IF TFLAG:102 == 1
			LOCAL = 100
			SIF !TALENT:ARG:恋慕 && !TALENT:ARG:思慕
				LOCAL -= 50
		ENDIF
	CASE "コミュ"
		SIF TFLAG:102 == 1
			LOCAL = 100
	CASE "着衣"
		IF TFLAG:102 == 3 && !TFLAG:抱きつきモード
			LOCAL = 100
			;脱衣により着衣セクハラは制限される
			SIF TEQUIP:PLAYER:0 < 2
				LOCAL -= 10
			SIF TEQUIP:PLAYER:1 < 1
				LOCAL -= 10
		ENDIF
	CASE "脱衣愛撫"
		IF TFLAG:102 == 3 && !TFLAG:抱きつきモード
			LOCAL = 80
			IF BASE:ARG:情緒 > 900
				LOCAL += 10
			ELSEIF BASE:ARG:情緒 > 500
				LOCAL += 5
			ENDIF
			IF BASE:ARG:理性 <= 0
				LOCAL += 15
			ELSEIF BASE:ARG:理性 <= 500
				LOCAL += 10
			ENDIF
			;素質でガチセクハラ率が変動
			SIF TALENT:ARG:性的興味 > 0
				LOCAL += 3
			SIF TALENT:ARG:貞操 < 0
				LOCAL += 3
			SIF TALENT:ARG:羞恥心 < 0
				LOCAL += 3
			SIF TALENT:ARG:献身的
				LOCAL += 3
			SIF TALENT:ARG:自制心
				LOCAL -= 3
			SIF TALENT:ARG:性的興味 < 0
				LOCAL -= 5
			SIF TALENT:ARG:難以越過的底線
				LOCAL -= 5
			SIF TALENT:ARG:貞操 > 0
				LOCAL -= 5
			SIF TALENT:ARG:羞恥心 > 0
				LOCAL -= 3
			LOCAL += 2 * (CFLAG:ARG:タチ + CFLAG:ARG:積極性)
		ENDIF
	CASE "脱衣強要"
		IF TFLAG:102 == 3 && !TFLAG:抱きつきモード
			LOCAL = COUNTER_TYPE("脱衣愛撫",ARG)
			IF LOCAL
				SIF TALENT:ARG:淫乱
					LOCAL += 5
				SIF TALENT:ARG:自尊心 > 0
					LOCAL += 3
				SIF TALENT:ARG:羞恥心 < 0
					LOCAL += 3
				SIF TALENT:ARG:性的興味 > 0
					LOCAL += 3
				SIF TALENT:ARG:献身的
					LOCAL -= 3
				SIF TALENT:ARG:難以越過的底線
					LOCAL -= 5
				SIF TALENT:ARG:自制心
					LOCAL -= 3
				SIF TALENT:ARG:羞恥心 > 0
					LOCAL -= 3
				SIF TALENT:ARG:応答 < 0
					LOCAL -= 3
				SIF TALENT:ARG:胆量 < 0
					LOCAL -= 3
				LOCAL += 2 * (CFLAG:ARG:誘惑 - CFLAG:ARG:積極性)
			ENDIF
			;女同士だと確率低下
			IF (TALENT:MASTER:性別 & 1) && (TALENT:ARG:性別 & 1)
				IF TALENT:ARG:性別 & 2
					LOCAL = LOCAL * 3 / 5
				ELSE
					LOCAL = LOCAL * 2 / 5
				ENDIF
			ENDIF
		ENDIF
	CASE "性交", "責め"
		IF TFLAG:102 == 3 && !TFLAG:抱きつきモード
			LOCAL = COUNTER_TYPE("脱衣愛撫",ARG)
			IF LOCAL
				SIF TALENT:ARG:淫乱
					LOCAL += 5
				SIF TALENT:ARG:献身的 && TALENT:ARG:恋慕
					LOCAL += 3
				IF TALENT:ARG:恋慕
					IF TALENT:ARG:恋人
						LOCAL += 5
					ELSE
						LOCAL += 3
					ENDIF
				ENDIF
				SIF TALENT:ARG:難以越過的底線
					LOCAL -= 5
				SIF TALENT:ARG:自制心
					LOCAL -= 3
				LOCAL += 2 * (CFLAG:ARG:積極性 + CFLAG:ARG:タチ)
				SIF TALENT:MASTER:処女
					LOCAL -= 5
				SIF TALENT:ARG:処女 == 1 && !TALENT:ARG:恋慕
					LOCAL = 0
				SIF TALENT:ARG:無知
					LOCAL = 0
			ENDIF
		ENDIF
	CASE "おねだり"
		IF TFLAG:102 == 3 && GETPALAMLV(PALAM:ARG:欲情,10) > 3 && !TFLAG:抱きつきモード
			LOCAL = GETPALAMLV(PALAM:ARG:欲情,10)
			IF LOCAL
				SIF TALENT:ARG:淫乱
					LOCAL:1 += 5
				SIF TALENT:ARG:自尊心 > 0
					LOCAL:1 -= 3
				SIF TALENT:ARG:羞恥心 < 0
					LOCAL:1 += 3
				SIF TALENT:ARG:性的興味 > 0
					LOCAL:1 += 3
				SIF TALENT:ARG:難以越過的底線
					LOCAL:1 -= 5
				SIF TALENT:ARG:自制心
					LOCAL:1 -= 3
				SIF TALENT:ARG:羞恥心 > 0
					LOCAL:1 -= 3
				SIF TALENT:ARG:応答 < 0
					LOCAL:1 -= 3
				SIF TALENT:ARG:胆量 < 0
					LOCAL:1 -= 3
				LOCAL:1 += CFLAG:ARG:誘惑 * 2 - CFLAG:ARG:積極性 - CFLAG:ARG:害羞
				LOCAL = LOCAL * (12 + LOCAL:1)
			ENDIF
		ENDIF
	;CASE "挿入中"
	;	SIF TFLAG:102 == 3 && (TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0) && !TFLAG:抱きつきモード
	;		LOCAL = 999
	CASE "抱きつき"
		IF TFLAG:102 == 3 && TFLAG:抱きつきモード
			LOCAL = 100
		ENDIF
ENDSELECT
RETURNF LOCAL

@SETTING_PERSONALITY(ARG)
CFLAG:ARG:害羞 = TALENT:ARG:喜歡引人注目 + TALENT:ARG:自己愛 - TALENT:ARG:自制心 - TALENT:ARG:貞操 - 2 * TALENT:ARG:羞恥心
CFLAG:ARG:誘惑 = TALENT:ARG:魅力 + TALENT:ARG:魅惑 + TALENT:ARG:謎之魅力 + TALENT:ARG:胸囲 + TALENT:ARG:体型 - TALENT:ARG:貞操 + TALENT:ARG:小悪魔 - TALENT:ARG:幼稚+ 3 * FLAG:甲斐性無
CFLAG:ARG:積極性 = TALENT:ARG:胆量 + TALENT:ARG:陽気／陰気 - TALENT:ARG:冷漠 - TALENT:ARG:感情缺乏 + TALENT:ARG:性的興味 - TALENT:ARG:難以越過的底線 + TALENT:ARG:献身的
CFLAG:ARG:タチ = TALENT:ARG:応答 + TALENT:ARG:自尊心 + TALENT:ARG:献身的 + 2 * (TALENT:ARG:施虐狂 - TALENT:ARG:受虐狂) + 3 * FLAG:甲斐性無
CFLAG:ARG:性格傾向 = 素質計算(ARG)