@SHIBORITORI(射精キャラ番号, 被射精キャラ番号)
#DIM 射精キャラ番号
#DIM 被射精キャラ番号
#DIMS 射精箇所1
#DIMS 射精箇所2

;なんかDIMとか使って微妙に分かりにくいかもしれないけど、これは最初に射精確定後処理に入れようとしてやっぱり止めた時の名残なので
;気にしなくていいです　地の文が最初に表示されちゃう

IF 射精キャラ番号 == MASTER
;主人公が突っ込んでる場合
	IF Ｓ度判定(被射精キャラ番号,射精キャラ番号) < RAND:(MAX((100 - ABL:被射精キャラ番号:欲望 * 5), 20))
		IF TALENT:被射精キャラ番号:献身的 && BASE:射精キャラ番号:気力 == 0
			PRINTFORMW %CALLNAME:被射精キャラ番号%担心%CALLNAME:射精キャラ番号%的身体、放開了%CALLNAME:射精キャラ番号%的陰茎……
		ELSE
			PRINTFORMW %CALLNAME:被射精キャラ番号%在%CALLNAME:射精キャラ番号%射精結束后、露出満足的微笑将%CALLNAME:射精キャラ番号%陰茎抜了出来……
		ENDIF
		CALL 挿入解除(被射精キャラ番号)
		TFLAG:106 = 0
	ELSE
		IF TALENT:被射精キャラ番号:施虐狂
			IF TALENT:射精キャラ番号:受虐狂
				IF BASE:被射精キャラ番号:気力 > 0
					IF BASE:射精キャラ番号:気力 == 0
						PRINTFORML %CALLNAME:被射精キャラ番号%完全没有放过%CALLNAME:射精キャラ番号%的意思
						PRINTFORMW %CALLNAME:被射精キャラ番号%的膣肉紧紧缠住%CALLNAME:射精キャラ番号%的陰茎、為了索求更多的射精開始蠕動起来……
					ELSE
						PRINTFORMW %CALLNAME:被射精キャラ番号%像不让逃走似的牢牢抓住%CALLNAME:射精キャラ番号%的手臂、開始剧烈地晃起了腰……
					ENDIF
				ELSEIF BASE:射精キャラ番号:気力 == 0
					PRINTFORMW %CALLNAME:被射精キャラ番号%逆推了精疲力尽的%CALLNAME:射精キャラ番号%、開始剧烈地晃起了腰……
				ELSE
					PRINTFORMW %CALLNAME:被射精キャラ番号%確認%CALLNAME:射精キャラ番号%射精結束之后、開始间不容発地晃起了腰……
				ENDIF
			ELSEIF BASE:被射精キャラ番号:気力 > 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%為了榨取%CALLNAME:射精キャラ番号%更多的精液而再次開始晃起了腰……
			ELSEIF BASE:射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%逆推了精疲力尽的%CALLNAME:射精キャラ番号%、再次開始晃起了腰……
			ELSE
				PRINTFORMW %CALLNAME:被射精キャラ番号%確認%CALLNAME:射精キャラ番号%射精結束之后、開始间不容発地再次晃起了腰……
			ENDIF
		ELSE
			IF TALENT:被射精キャラ番号:献身的 && BASE:射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%因為还没有満足、一辺向%CALLNAME:射精キャラ番号%道歉一辺再次開始晃起了腰……
			ELSE
				PRINTFORMW %CALLNAME:被射精キャラ番号%在%CALLNAME:射精キャラ番号%射精結束之后、亲了亲%CALLNAME:射精キャラ番号%的脸颊就又開始晃起了腰……
			ENDIF
		ENDIF
	ENDIF
ELSE
;主人公に突っ込んでる場合
	IF TCVAR:TARGET:射精した箇所フラグ == 1
		射精箇所1 = 膣内
		射精箇所2 = 膣道
	ELSE
		射精箇所1 = 菊穴
		射精箇所2 = 菊穴
	ENDIF

	IF Ｓ度判定(射精キャラ番号, 被射精キャラ番号) - 引き抜き精力補正(射精キャラ番号) < RAND:(MAX((100 - ABL:被射精キャラ番号:欲望 * 5), 20))
		IF TALENT:射精キャラ番号:献身的 && BASE:被射精キャラ番号:気力 == 0
			PRINTFORMW %CALLNAME:射精キャラ番号%擔心%CALLNAME:被射精キャラ番号%的身体、從%CALLNAME:被射精キャラ番号%的%射精箇所2%中抜出了肉棒……
		ELSE
			PRINTFORMW %CALLNAME:射精キャラ番号%終於在%CALLNAME:被射精キャラ番号%的%射精箇所1%射完了精液、露出満足的微笑抜出了肉棒……
		ENDIF
		CALL 挿入解除(被射精キャラ番号)
		CALL TOUCH_SET(0, 0, 射精キャラ番号, 1)
	ELSE
		IF TALENT:射精キャラ番号:施虐狂
			IF TALENT:被射精キャラ番号:受虐狂
				IF BASE:射精キャラ番号:気力 > 0
					IF BASE:被射精キャラ番号:気力 == 0
						PRINTFORML %CALLNAME:射精キャラ番号%完全没有放过%CALLNAME:被射精キャラ番号%的意思
						PRINTFORMW %CALLNAME:射精キャラ番号%的肉棒在瞬間就重新恢復了硬度、開始抽插着%CALLNAME:被射精キャラ番号%的%射精箇所2%……
					ELSE
						PRINTFORMW %CALLNAME:射精キャラ番号%像不让逃跑似的牢牢抓住%CALLNAME:被射精キャラ番号%的手臂、開始剧烈地耸動起腰身……
					ENDIF
				ELSEIF BASE:射精キャラ番号:気力 == 0
					PRINTFORMW %CALLNAME:射精キャラ番号%逆推了精疲力尽的%CALLNAME:被射精キャラ番号%、開始剧烈地耸動起腰身……
				ELSE
					PRINTFORMW %CALLNAME:射精キャラ番号%射精結束后、抱住依偎过来的%CALLNAME:被射精キャラ番号%、再次動起了腰……
				ENDIF
			ELSEIF BASE:射精キャラ番号:気力 > 0
				PRINTFORMW %CALLNAME:射精キャラ番号%為了再次在%CALLNAME:被射精キャラ番号%的%射精箇所1%射入更多的精液再次動起了腰……
			ELSEIF BASE:被射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:射精キャラ番号%逆推了精疲力尽的%CALLNAME:被射精キャラ番号%、再次動起了腰……
			ELSE
				PRINTFORMW %CALLNAME:射精キャラ番号%射精結束后、抱住依偎过来的%CALLNAME:被射精キャラ番号%、再次動起了腰……
			ENDIF
		ELSE
			IF TALENT:射精キャラ番号:献身的 && BASE:被射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:射精キャラ番号%因為还没有満足、一辺向%CALLNAME:被射精キャラ番号%道歉一辺再次開始動起了腰……
			ELSE
				PRINTFORMW %CALLNAME:射精キャラ番号%在%CALLNAME:被射精キャラ番号%的%射精箇所1%射完之後、親了親%CALLNAME:被射精キャラ番号%的臉頰之後再次動起了腰……
			ENDIF
		ENDIF
	ENDIF
ENDIF

@Ｓ度判定(攻め,受け)
#FUNCTION

#DIM 攻め
#DIM 受け

VARSET LOCAL

LOCAL += ABL:攻め:施虐属性 * 5
LOCAL -= ABL:攻め:受虐属性 * 5
LOCAL += ABL:受け:受虐属性 * 5
LOCAL -= ABL:受け:施虐属性 * 5

IF TALENT:攻め:施虐狂
	IF TALENT:受け:受虐狂
		IF BASE:攻め:気力 > 0
			IF BASE:受け:気力 <= 0
				LOCAL += 999
			ELSE
				LOCAL += 90
			ENDIF
		ELSEIF BASE:受け:気力 <= 0
			LOCAL += 75
		ELSE
			LOCAL += 60
		ENDIF
	ELSEIF BASE:攻め:気力 > 0
		IF BASE:受け:気力 <= 0
			LOCAL += 50
		ELSE
			LOCAL += 30
		ENDIF
	ELSEIF BASE:受け:気力 <= 0
		LOCAL += 20
	ELSE
		LOCAL += 10
	ENDIF
ELSE
	IF TALENT:受け:受虐狂
		IF BASE:攻め:気力 > 0
			IF BASE:受け:気力 <= 0
				LOCAL += 75
			ELSE
				LOCAL += 60
			ENDIF
		ELSEIF BASE:受け:気力 <= 0
			LOCAL += 50
		ELSE
			LOCAL += 35
		ENDIF
	ELSE
		SIF BASE:受け:気力 <= 0
			LOCAL += 10
		SIF BASE:攻め:気力 > 0
			LOCAL += 10
	ENDIF
ENDIF

;叛逆
SIF TALENT:攻め:態度 == 1
	LOCAL += 5

;自制心
SIF TALENT:攻め:自制心
	LOCAL -= 10

;接受快感
IF TALENT:攻め:快感応答 == -1
	LOCAL += 10
;接受快感
ELSEIF TALENT:攻め:快感応答 == 1
	LOCAL -= 5
ENDIF

SIF TALENT:攻め:献身的 && BASE:受け:気力 <= 0
	LOCAL /= 5
SIF TALENT:受け:施虐狂
	LOCAL /= 2

RETURNF MAX(LOCAL, 0)

@引き抜き精力補正(ARG)
#FUNCTION
RETURNF (MAXBASE:ARG:精力 - BASE:ARG:精力) * 50 / MAXBASE:ARG:精力 + 5
