@押し倒され
#DIM 人の気配
#DIM 押し倒され判定
#DIM 抱きつき値
#DIM 押し倒され値
#DIM 抱きつき優先

;MASTERの位置から見て他的房間にだれかいるかどうか
人の気配 = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		CONTINUE
	SIF CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:LOCAL:現在位置) & 2
		人の気配 ++
NEXT

IF FLAG:押し倒し
	押し倒され判定 = 0
	押し倒され判定 += ABL:TARGET:戦闘能力 - ABL:MASTER:戦闘能力
	押し倒され判定 += TALENT:TARGET:体型 - TALENT:MASTER:体型
	押し倒され判定 += ABL:TARGET:施虐属性 + ABL:MASTER:受虐属性
	押し倒され判定 -= TALENT:MASTER:胆量 + TALENT:MASTER:態度 + TALENT:MASTER:応答 + TALENT:MASTER:自尊心 + TALENT:MASTER:難以越過的底線 + TALENT:MASTER:抵抗 + TALENT:MASTER:貞操
	押し倒され判定 += (CFLAG:TARGET:弱み握り * 2) + (TALENT:TARGET:施虐狂 * 3) + (TALENT:MASTER:受虐狂 * 3) + (TCVAR:TARGET:発情 * 3)
	IF CFLAG:TARGET:溜まってる度 < 600
		押し倒され判定 = 0
	ELSEIF CFLAG:TARGET:溜まってる度 < 800
		押し倒され判定 += 1
	ELSEIF CFLAG:TARGET:溜まってる度 < 1000
		押し倒され判定 += 2
	ELSE
		押し倒され判定 += 3
	ENDIF
ENDIF

IF CFLAG:うふふ == 1
	TFLAG:102 = 2
ELSEIF CFLAG:うふふ == 2
	TFLAG:102 = 3
ELSEIF TFLAG:102 == 3 && CFLAG:MASTER:うふふ == 2
	;既に処理済みなのでスキップ
ELSE
	TFLAG:102 = 1
ENDIF

IF !(TFLAG:102 == 3 && CFLAG:MASTER:うふふ == 2)
	TFLAG:抱きつきモード = 0
ENDIF

;抱きつきor押し倒されの処理
;デート中は発生しない
IF !CHK_DATENOW(CFLAG:TARGET:デート中)
	;抱きつきと押し倒され、どちらが優先して実行されるかを判定。抱きつきは女同士限定
	IF (TALENT:MASTER:性別 & 1) && (TALENT:性別 & 1)
		抱きつき値 = 抱きつき判定(TARGET)
	ELSE
		抱きつき値 = 0
	ENDIF
	押し倒され値 = ENDURE(TARGET)

	抱きつき優先 = (抱きつき値 >= 1 && 抱きつき値 > 押し倒され値 + 40 && !(押し倒され値 >= 600 && BASE:理性 <= 300))

	;抱きつきが優先の場合
	IF 抱きつき優先
		IF !CFLAG:うふふ && TFLAG:102 != 3 && 抱きつき値 >= RAND:250 + 250 && TCVAR:抱きつき禁止時間 <= 0
			;室外では抱きつかれない
			IF (!人の気配 && INROOM(CFLAG:MASTER:現在位置) && AT_HOME(MASTER)) || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
				;同室にいるのが1人のみの場合(抱きつきは複数人不可)
				IF GET_TARGETNUM() == 1
					PRINTL
					PRINTFORMW %CALLNAME:MASTER%抱住了%CALLNAME%！
					CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,1,1)
					TFLAG:抱きつき解放ポイント = 2 + RAND:(MIN(ABL:欲望, 10) / 2 + 2) + MIN(CFLAG:溜まってる度, 1000) / 200
					CFLAG:うふふ = 2
					CFLAG:MASTER:うふふ = 2
					TFLAG:102 = 3
					TFLAG:抱きつきモード = 1
				ENDIF
			ENDIF
		ENDIF
	;押し倒されが優先の場合
	ELSE
		IF ((!CFLAG:うふふ && TFLAG:102 != 3) || (TFLAG:102 == 3 && TFLAG:抱きつきモード)) && 押し倒され値 >= RAND:300 + 300
			;室外では押し倒されない
			IF (!人の気配 && INROOM(CFLAG:MASTER:現在位置) && AT_HOME(MASTER)) || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
				;同室にいるのが1人のみの場合または複数人に押し倒されたとき
				IF GET_TARGETNUM() == 1 || 複数押し倒され() == 1
					;押し倒されない効果
					IF FLAG:押し倒し && 押し倒され判定 <= 0
						PRINTL
						PRINTFORML 突然被%CALLNAME:TARGET%抱了过来！
						PRINTFORM %CALLNAME:TARGET%
						IF PALAM:TARGET:欲情 >= PALAMLV:10
							PRINT 闪闪発光的
						ELSE
							PRINT 水润的
						ENDIF
						PRINTFORML 眼睛注视过来
					;ディレイ中は襲い掛かってこない
					ELSEIF CFLAG:TARGET:ディレイ > 0

					ELSE
						PRINTL
						PRINTFORMW %CALLNAME:MASTER%被推倒了！
						;CALL KOJO_EVENT(12, TARGET)
						CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,1,0)
						FOR LOCAL,1,CHARANUM
							CFLAG:(TARGET:LOCAL):うふふ = 2
						NEXT
						CFLAG:MASTER:うふふ = 2
						TFLAG:102 = 3
						TFLAG:抱きつきモード = 0
						TFLAG:押し倒され開始時間 = TIME + DAY * 1440
					ENDIF
				ENDIF
				IF INROOM(CFLAG:MASTER:現在位置) == 2
					CALL DATUI_SHOES(MASTER,0)
					CALL DATUI_SHOES(TARGET,0)
				ENDIF
			;室外(情人用宿屋と情人旅館除く)で押し倒され条件を満たした場合
			ELSEIF CFLAG:MASTER:現在位置 != 260 && CFLAG:MASTER:現在位置 != 950
				PRINTL
				SELECTCASE CFLAG:TARGET:溜まってる度
					CASE IS < 600
						PRINTFORML %CALLNAME:TARGET%扯了扯袖子
						PRINTFORMW 似乎想去没人的地方的样子
					CASE IS < 800
						PRINTFORMW %CALLNAME:TARGET%依偎在%CALLNAME:MASTER%身上、抬頭凝視着你…
					CASE IS < 1000
						PRINTFORML %CALLNAME:TARGET%依偎在%CALLNAME:MASTER%身上、\@ BATHROOM(CFLAG:現在位置) ? 伸了進去 # 隔着衣服 \@軽軽的撫摸着股間…
						PRINTFORMW %CALLNAME:TARGET%用眼神诉説着情欲
						PALAM:TARGET:潤滑 += 500
					CASEELSE
						PRINTFORML %CALLNAME:TARGET%満臉通紅的掛在%CALLNAME:MASTER%身上、把%CALLNAME:MASTER%的手拉到了自己那湿透的私処…
						PRINTFORMW %CALLNAME:TARGET%已経忍耐不住想要立刻和%CALLNAME:MASTER%做愛的樣子
						PALAM:TARGET:潤滑 += 2000
				ENDSELECT
			ENDIF
		ENDIF
	ENDIF
ENDIF


;複数押し倒されの判定
@複数押し倒され()
#FUNCTION
#DIM 判定値
#DIM キャラ別判定値
#DIM 相性補正
判定値 = 0
FOR LOCAL,1,CHARANUM
	IF CFLAG:LOCAL:同室 == 1
		;まずは同室の全員が押し倒しの判定を通ること(元の関数で1％以上押し倒しの可能性があること)
		SIF ENDURE(LOCAL) < 450
			RETURNF 0
		;判定を通ったらキャラ別判定値に入れる
		キャラ別判定値 = ENDURE(LOCAL)
		DEBUGPRINTFORML 補正前：({LOCAL}) {ENDURE(LOCAL)}
		;ここからキャラ別判定値を補正していく
		;百合属性での補正(3未満にはペナルティ)
		SIF ABL:LOCAL:百合属性 == 0
			TIMES キャラ別判定値, 0
		SIF ABL:LOCAL:百合属性 == 1
			TIMES キャラ別判定値, 0.50
		SIF ABL:LOCAL:百合属性 == 2
			TIMES キャラ別判定値, 0.80
		;3以上では(1+0.1*(百合属性 - 3))倍
		SIF ABL:LOCAL:百合属性 >= 3
			キャラ別判定値 = キャラ別判定値 * (100 + 10 * (ABL:LOCAL:百合属性 - 3)) / 100
		DEBUGPRINTFORML 百合属性補正後：({LOCAL}) {キャラ別判定値}
		;百合中毒での補正 (1+0.1*百合中毒)倍
		キャラ別判定値 = キャラ別判定値 * (100 + 10 * ABL:LOCAL:百合中毒) / 100
		DEBUGPRINTFORML 百合中毒補正後：({LOCAL}) {キャラ別判定値}
		;相性での補正
		;仲のいいキャラがいると一緒にMASTERを押し倒しやすく(完全に趣味)
		相性補正 = 0
		FOR LOCAL:2,1,CHARANUM
			;同室のキャラを探り各キャラに対して相性値を見ていく
			IF CFLAG:(LOCAL:2):同室 == 1
				SIF LOCAL == LOCAL:2
					CONTINUE
				;相性補正 += RELATION:LOCAL:(LOCAL:2)
				;ほんとは上の一文だけで済ませたいんだけど......
				;_Replace.csvのRELATIONの初期値が働かない(泣)
				;なので苦肉の策として0は100とみなす(暴論)
				IF RELATION:LOCAL:(LOCAL:2) == 0
					相性補正 += 100
				ELSE
					相性補正 += RELATION:LOCAL:(LOCAL:2)
				ENDIF
			ENDIF
		NEXT
		;平均値をとる(小数点以下切捨て) 当人を除いた(GET_TARGETNUM() - 1)人で割る
		;0除算注意(無いと思うけど)
		相性補正 /= GET_TARGETNUM() - 1
		キャラ別判定値 = キャラ別判定値 * 相性補正 / 100
		DEBUGPRINTFORML 相性補正後：({LOCAL}) {キャラ別判定値}
		;補正後一位角色別判定値を全体の判定値に入れる
		判定値 += キャラ別判定値
	ENDIF
NEXT
;平均値をとる(小数点以下切捨て)
;0除算がこわいぃ
判定値 /= GET_TARGETNUM()
DEBUGPRINTFORML 判定値：{判定値}
;判定値が最大だとして、確率は「(400+200*同室の人数)分の150」に設定(人数増えると難易度上昇)
SIF RAND:(400 + 200 * GET_TARGETNUM()) + 450 <= 判定値
	RETURNF 1
RETURNF 0

;==============================================================
;FileName_ATTITUDE.ERB ------------------------------- Rev1.00
;押し倒し率判定
;CALL		USER
;ARG		キャラNO
;RETURN		0:押し倒し不成立, 1:押し倒し成立
;COMMENT	お茶濁し系関数。出来が悪い。


;-----------------------------------------------------------
@ENDURE(ARG)
#FUNCTION
;素質により判定変化
LOCAL = CFLAG:ARG:合意判定
LOCAL += (EX:ARG:Ｃ寸止め + EX:ARG:Ｖ寸止め + EX:ARG:Ａ寸止め + EX:ARG:Ｂ寸止め + EX:ARG:Ｍ寸止め) * 20
;抱き着くと押し倒され率上昇
SIF SELECTCOM == 311
	LOCAL += 30
SIF TALENT:ARG:処女
	LOCAL -= 50
SIF TALENT:ARG:恋慕
	LOCAL = LOCAL + 50
SIF CFLAG:ARG:生解禁 == 2
	LOCAL = LOCAL + 50
SIF CFLAG:ARG:無理矢理膣射
	LOCAL -= 100
SIF TALENT:ARG:胆量 < 0 || TALENT:ARG:応答 < 0 || TALENT:ARG:冷漠 || TALENT:ARG:羞恥心 > 0
	LOCAL = LOCAL - 30
SIF TALENT:ARG:性的興味 > 0 || TALENT:ARG:羞恥心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:ARG:自制心 || TALENT:ARG:性的興味 < 0
	LOCAL = LOCAL - 20
SIF TALENT:ARG:貞操 < 0 || TALENT:ARG:小悪魔
	LOCAL = LOCAL + 20
SIF TALENT:ARG:貞操 > 0 || TALENT:ARG:難以越過的底線
	LOCAL = LOCAL / 2
SIF OTOKOGIRAI(ARG) && (TCVAR:ARG:発情 || TCVAR:ARG:媚薬)
	LOCAL = LOCAL / 2
SIF TALENT:ARG:難以越過的底線 && TALENT:ARG:恋慕
	LOCAL = LOCAL * 12 / 5
SIF TALENT:ARG:性別嗜好 == 3 || MARK:ARG:反発刻印 == 3
	LOCAL = 0
SIF TFLAG:120 && TFLAG:120 != ARG
	LOCAL = 0
SIF !SHIRAHU(ARG)
	LOCAL = 0
SIF CFLAG:ARG:行動 == 5
	LOCAL = 0
SIF CFLAG:ARG:ディレイ
	LOCAL = 0
SIF CFLAG:ARG:添い寝中
	LOCAL = 0
SELECTCASE CFLAG:ARG:溜まってる度
	CASE IS >= 1000
		LOCAL *= 3
	CASE IS >= 800
		LOCAL = LOCAL * 3 / 2
	CASE IS >= 600
		LOCAL = LOCAL * 5 / 4
	CASE IS >= 400
	CASE IS >= 200
		LOCAL /= 2
	CASE IS >= 100
		LOCAL /= 4
	CASEELSE
		LOCAL = 0
ENDSELECT

;体力に依存して押し倒し率減少

SELECTCASE BASE:ARG:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT

;理性に依存して押し倒し率減少
IF BASE:ARG:理性 > 800
	LOCAL = LOCAL / 5
ELSEIF BASE:ARG:理性 > 600
	LOCAL = LOCAL / 2
ELSE
	;Do Notting
ENDIF
SELECTCASE BASE:ARG:情緒
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 500
		LOCAL /= 2
	CASE 501 TO 800
	CASEELSE
		LOCAL = LOCAL / 3 * 2
ENDSELECT
SIF FLAG:甲斐性無
	LOCAL = LOCAL * 2
SIF CFLAG:ARG:ブチギレ
	LOCAL = 0
RETURNF LOCAL

;==============================================================
;抱きつき判定
;==============================================================
@抱きつき判定(ARG)
#FUNCTION
;素質により判定変化
LOCAL = CFLAG:ARG:合意判定
SIF TALENT:ARG:恋慕
	LOCAL = LOCAL + 50
SIF CFLAG:ARG:無理矢理膣射
	LOCAL -= 100
;抱き着くと抱き着き判定上昇
SIF SELECTCOM == 311
	LOCAL += 30
SIF TALENT:ARG:胆量 < 0 || TALENT:ARG:応答 < 0 || TALENT:ARG:冷漠 || TALENT:ARG:羞恥心 > 0
	LOCAL = LOCAL - 30
SIF TALENT:ARG:性的興味 > 0 || TALENT:ARG:羞恥心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:ARG:自制心 || TALENT:ARG:性的興味 < 0
	LOCAL = LOCAL - 20
SIF TALENT:ARG:貞操 < 0 || TALENT:ARG:小悪魔
	LOCAL = LOCAL + 20
SIF TALENT:ARG:貞操 > 0 || TALENT:ARG:難以越過的底線
	LOCAL = LOCAL / 2
SIF OTOKOGIRAI(ARG)
	LOCAL = LOCAL / 2
SIF TALENT:ARG:性別嗜好 == 3 || MARK:ARG:反発刻印 == 3
	LOCAL = 0
SIF TFLAG:120 && TFLAG:120 != ARG
	LOCAL = 0
SIF !SHIRAHU(ARG)
	LOCAL = 0
SIF CFLAG:ARG:行動 == 5
	LOCAL = 0
SIF CFLAG:ARG:ディレイ
	LOCAL = 0
SIF CFLAG:ARG:添い寝中
	LOCAL = 0
SELECTCASE CFLAG:ARG:溜まってる度
	CASE IS >= 1000
		LOCAL *= 2
	CASE IS >= 800
		LOCAL = LOCAL * 7 / 5
	CASE IS >= 600
		LOCAL = LOCAL * 6 / 5
	CASE IS >= 400
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 150
		LOCAL /= 4
	CASEELSE
		LOCAL = 0
ENDSELECT

;体力に依存して押し倒し率減少
SELECTCASE BASE:ARG:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT

SELECTCASE BASE:ARG:情緒
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 500
		LOCAL = LOCAL * 2 / 3
	CASE 501 TO 800
	CASEELSE
		LOCAL = LOCAL * 3 / 2
ENDSELECT
SIF FLAG:甲斐性無
	LOCAL = LOCAL * 2
RETURNF LOCAL
