;射精確定後の処理
@EJAC_CHECK_AFTER(キャラ登録番号)
#DIM キャラ登録番号
#DIM 射精回数
#DIM 過剰射精

VARSET 射精回数
VARSET 過剰射精
;キャラ登録番号 射精する人
IF !TCVAR:キャラ登録番号:イきそう
	IF BASE:キャラ登録番号:射精 > MAXBASE:キャラ登録番号:射精
		IF BASE:キャラ登録番号:勃起 < 1000
			BASE:キャラ登録番号:射精 = MAXBASE:キャラ登録番号:射精 - 1
		ELSE
			TCVAR:キャラ登録番号:イきそう = 1
		ENDIF
	ENDIF
	RETURN 0
ELSEIF TEQUIP:キャラ登録番号:焦らしモード && キャラ登録番号 != MASTER
	TCVAR:キャラ登録番号:焦らし回数 += 2
	RETURN 0
ENDIF

;射精(絶頂)回数
射精回数 = MIN(BASE:キャラ登録番号:射精 / MAXBASE:キャラ登録番号:射精, (100 + BASE:キャラ登録番号:精力) / 100)


IF NOWEX:キャラ登録番号:射精量 > BASE:キャラ登録番号:精力
	NOWEX:キャラ登録番号:射精量 = (NOWEX:キャラ登録番号:射精量 - BASE:キャラ登録番号:精力) / 2 + BASE:キャラ登録番号:精力
	過剰射精 = NOWEX:キャラ登録番号:射精量 - BASE:キャラ登録番号:精力
ENDIF

NOWEX:キャラ登録番号:射精量 =BASE:キャラ登録番号:射精 * 10 / (ABL:キャラ登録番号:Ｃ感覚 / 2 + 10) / 100

NOWEX:キャラ登録番号:射精量 = NOWEX:キャラ登録番号:射精量 * 10 / (ABL:キャラ登録番号:Ｃ感覚 / 2 + 10)

SIF 過剰射精
	DOWNBASE:キャラ登録番号:体力 += 過剰射精 * 3


BASE:キャラ登録番号:精力 = MAX(0, BASE:キャラ登録番号:精力 - NOWEX:キャラ登録番号:射精量)
TCVAR:キャラ登録番号:イきそう = 0
STAIN:キャラ登録番号:Ｐ |= 4

;BASEの処理
BASE:キャラ登録番号:射精 = 0

CSTR:キャラ登録番号:射精後処理用 = %CSTR:キャラ登録番号:射精判定用%
TCVAR:キャラ登録番号:射精した箇所フラグ = TCVAR:キャラ登録番号:射精箇所

;異常経験、射精経験、精液経験
;女性器がある場合？
SIF !EXP:キャラ登録番号:射精経験 && TALENT:キャラ登録番号:性別 & 1
	EXP:キャラ登録番号:異常経験 += 1

EXP:キャラ登録番号:絶頂経験 ++ 
EXP:キャラ登録番号:射精経験 += 射精回数

NOWEX:キャラ登録番号:射精 += 射精回数
EX:キャラ登録番号:射精 += 射精回数
TCVAR:キャラ登録番号:焦らし回数 = 0
TEQUIP:キャラ登録番号:焦らしモード = 0
SIF AT_HOME(キャラ登録番号)
	YOGORE:(CFLAG:キャラ登録番号:現在位置) += 射精回数 * 50


DOWNBASE:キャラ登録番号:体力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 2, 50, 300)
DOWNBASE:キャラ登録番号:気力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 2, 50, 300)

;MASTERでなければBASEやSOURCE等の処理を入れる
SIF キャラ登録番号 == MASTER
	RETURN 0

IF EXP:キャラ登録番号:射精経験 < EXPLV:1
	SOURCE:キャラ登録番号:露出 += 5000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 2500 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 200 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:2
	SOURCE:キャラ登録番号:露出 += 2500 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 2000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 150 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:3
	SOURCE:キャラ登録番号:露出 += 1000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 800 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 100 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:4
	SOURCE:キャラ登録番号:露出 += 800 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 600 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 50 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:5
	SOURCE:キャラ登録番号:露出 += 400 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 400 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 10 * (射精回数 + 1) / 2
ELSE
	SOURCE:キャラ登録番号:露出 += 200 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 250 * (射精回数 + 1) / 2
ENDIF

;-------------------------------------------------
;調教対象の射精チェック
;-------------------------------------------------
@TARGET_EJAC_CHECK(キャラ登録番号)
#DIM キャラ登録番号

;男性器がなければ帰る
SIF !(TALENT:キャラ登録番号:性別 & 2)
	RETURN 0

LOCAL = (CUP:キャラ登録番号:快Ｃ + CUP:キャラ登録番号:快Ｖ + CUP:キャラ登録番号:快Ａ + CUP:キャラ登録番号:快Ｂ) / 4

;自制心
SIF TALENT:キャラ登録番号:自制心
	LOCAL /= 2

;接受快感
SIF TALENT:キャラ登録番号:快感応答 > 0
	TIMES LOCAL , 1.20

;否定快感（淫乱、娼婦で無効）
SIF TALENT:キャラ登録番号:快感応答 < 0 && !TALENT:キャラ登録番号:淫乱
	TIMES LOCAL , 0.80

;媚薬
SIF TCVAR:キャラ登録番号:媚薬
	LOCAL *= 2

;利尿剤
SIF TCVAR:キャラ登録番号:利尿剤
	LOCAL /= 2
;睡眠中
SIF CFLAG:キャラ登録番号:睡眠
	LOCAL /= 2
BASE:キャラ登録番号:射精 += LOCAL
SIF BASE:キャラ登録番号:射精 > MAXBASE:キャラ登録番号:射精
	BASE:キャラ登録番号:射精 = MAXBASE:キャラ登録番号:射精 - 1

;射精処理のキャラ別ターミナル
@SAMEN_SHOOT(射精したキャラ番号)
#DIM 射精したキャラ番号
#DIM 射精されたキャラ番号

FOR 射精されたキャラ番号, 0, CHARANUM
	SIF SAMEN_DIRECTION2(射精したキャラ番号, 射精されたキャラ番号)
		CALL SAMEN_SHOOT2(射精したキャラ番号, 射精されたキャラ番号)
NEXT

;部位別処理
@SAMEN_SHOOT2(射精キャラ番号, 被射精キャラ番号)
#DIM 射精キャラ番号
#DIM 被射精キャラ番号
#DIM 無理矢理反感
#DIM 無理矢理恐怖
#DIM 信頼舞茄子
#DIM 好感度舞茄子
#DIM 欲求不満解消係数

IF 射精キャラ番号 == MASTER && CFLAG:被射精キャラ番号:うふふ == 2
	SOURCE:被射精キャラ番号:達成 += NOWEX:射精キャラ番号:射精 * 150
	SOURCE:被射精キャラ番号:征服 += NOWEX:射精キャラ番号:射精 * 150
	SIF !(NOWEX:被射精キャラ番号:Ｃ絶頂 + NOWEX:被射精キャラ番号:Ｖ絶頂 + NOWEX:被射精キャラ番号:Ａ絶頂 + NOWEX:被射精キャラ番号:Ｂ絶頂 + NOWEX:被射精キャラ番号:Ｍ絶頂)
		SOURCE:被射精キャラ番号:征服 += NOWEX:射精キャラ番号:射精 * 100
ENDIF
IF TEQUIP:射精キャラ番号:避孕套
	;ゴムを外す
	TEQUIP:射精キャラ番号:避孕套 = 0
	SELECTCASE TCVAR:射精キャラ番号:射精した箇所フラグ
		CASE 1
			CFLAG:被射精キャラ番号:溜まってる度 += ABL:被射精キャラ番号:膣射中毒 * NOWEX:射精キャラ番号:射精 * 15
		CASE 2
			CFLAG:被射精キャラ番号:溜まってる度 += ABL:被射精キャラ番号:肛射中毒 * NOWEX:射精キャラ番号:射精 * 15
	ENDSELECT
	;接触のリセット
	CALL 挿入解除(被射精キャラ番号)
	TFLAG:ゴム内 = NOWEX:射精キャラ番号:射精量
ELSE

;TCVAR:12 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛門奉仕 10=道具 11=触手）
SELECTCASE TCVAR:射精キャラ番号:射精した箇所フラグ
	;膣内
	CASE 1
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精 * 2
		EXP:被射精キャラ番号:膣射経験 += NOWEX:射精キャラ番号:射精 * 2
		STAIN:被射精キャラ番号:Ｖ |= 4
		STAIN:被射精キャラ番号:膣内 |= 4
		TCVAR:被射精キャラ番号:Vに中出しされたフラグ = 2
		TCVAR:被射精キャラ番号:Vに中出ししたキャラの番号 = 射精キャラ番号
		IF FLAG:時間停止 == 1
			CFLAG:被射精キャラ番号:膣内射精 = 2
		ELSE
			CFLAG:被射精キャラ番号:膣内射精 = 1
		ENDIF
		NOWEX:被射精キャラ番号:膣内精液 = NOWEX:射精キャラ番号:射精量
		EX:被射精キャラ番号:膣内精液 += NOWEX:被射精キャラ番号:膣内精液
		欲求不満解消係数 = 1000 / MIN(100 + ABL:被射精キャラ番号:膣射中毒 * 10,200)
		CFLAG:被射精キャラ番号:溜まってる度 -= SQRT(NOWEX:被射精キャラ番号:膣内精液) * 欲求不満解消係数
		CFLAG:被射精キャラ番号:累計膣内精液 += NOWEX:被射精キャラ番号:膣内精液
		;SIF !TCVAR:被射精キャラ番号:口服避孕薬
		CALL SAMEN_SHOOT3(NOWEX:被射精キャラ番号:膣内精液 , 射精キャラ番号, 被射精キャラ番号)
		IF FLAG:時間停止 == 0 && TCVAR:被射精キャラ番号:泥酔 == 0 && !CFLAG:被射精キャラ番号:睡眠
			TCVAR:被射精キャラ番号:自覚 = 1
			CFLAG:被射精キャラ番号:自覚射精 = 1
		ENDIF
		IF TCVAR:被射精キャラ番号:無理矢理
			SELECTCASE ABL:被射精キャラ番号:膣射中毒
				CASE IS < 3
					無理矢理反感 = 100000
					好感度舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:50 + NOWEX:射精キャラ番号:射精 * RAND:50
					信頼舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:20 + NOWEX:射精キャラ番号:射精 * RAND:20
					無理矢理恐怖 = NOWEX:被射精キャラ番号:膣内精液 * 5
				CASE 3 TO 5
					無理矢理反感 = 50000
					好感度舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:30 + NOWEX:射精キャラ番号:射精 * RAND:30
					信頼舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:15 + NOWEX:射精キャラ番号:射精 * RAND:15
					無理矢理恐怖 = NOWEX:被射精キャラ番号:膣内精液 * 3
				CASEELSE
					無理矢理反感 = 10000
					好感度舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:10 + NOWEX:射精キャラ番号:射精 * RAND:10
					信頼舞茄子 = NOWEX:射精キャラ番号:射精 * RAND:10 + NOWEX:射精キャラ番号:射精 * RAND:10
					無理矢理恐怖 = NOWEX:被射精キャラ番号:膣内精液
			ENDSELECT
			IF CFLAG:被射精キャラ番号:生理周期 == 7
				無理矢理反感 *= 2
				無理矢理恐怖 *= 2
				好感度舞茄子 *= 2
				信頼舞茄子 *= 2
			ENDIF
			IF CFLAG:被射精キャラ番号:無理矢理膣射 > 1
				無理矢理反感 *= 2
				無理矢理恐怖 *= 2
				好感度舞茄子 *= 2
				信頼舞茄子 *= 2
			ENDIF
			SOURCE:被射精キャラ番号:反感 += 無理矢理反感
			IF CFLAG:被射精キャラ番号:無理矢理膣射 > 1
				CFLAG:被射精キャラ番号:無理矢理膣射 = 3
			ELSE
				CFLAG:被射精キャラ番号:無理矢理膣射 = 1
			ENDIF
			TFLAG:好感度管理 -= 好感度舞茄子
			CFLAG:被射精キャラ番号:信頼度 -= 信頼舞茄子
		ENDIF
		
		;ビデオ
		IF TEQUIP:ビデオ撮影
			LOCALS = SV/
			TSTR:1 += LOCALS
		ENDIF
	;アナル
	CASE 2
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精 * 2
		EXP:被射精キャラ番号:肛射経験 += NOWEX:射精キャラ番号:射精 * 2
		STAIN:被射精キャラ番号:Ａ |= 4
		STAIN:被射精キャラ番号:腸内 |= 4
		;中出し表示用
		TCVAR:被射精キャラ番号:Aに中出しされたフラグ = 2
		TCVAR:被射精キャラ番号:Aに中出ししたキャラの番号 = 射精キャラ番号
		IF FLAG:時間停止 == 1
			CFLAG:被射精キャラ番号:アナル射精 = 2
		ELSE
			CFLAG:被射精キャラ番号:アナル射精 = 1
		ENDIF
		NOWEX:被射精キャラ番号:アナル内精液 = NOWEX:射精キャラ番号:射精量
		EX:被射精キャラ番号:アナル内精液 += NOWEX:射精キャラ番号:射精量
		欲求不満解消係数 = 1000 / MIN(100 + ABL:被射精キャラ番号:肛射中毒 * 10,200)
		CFLAG:被射精キャラ番号:溜まってる度 -= SQRT(NOWEX:被射精キャラ番号:アナル内精液) * 欲求不満解消係数
		CFLAG:被射精キャラ番号:累計アナル精液 += NOWEX:射精キャラ番号:射精量
	;手淫
	CASE 3
		EXP:被射精キャラ番号:精液経験 += 1 + NOWEX:射精キャラ番号:射精
		STAIN:被射精キャラ番号:手 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:被射精キャラ番号:手に精液 = 2
		ELSE
			CFLAG:被射精キャラ番号:手に精液 = 1
		ENDIF
		IF ABL:被射精キャラ番号:精液中毒 > 2 && ABL:被射精キャラ番号:技巧 > 2
			EXP:被射精キャラ番号:精飲経験 ++
			CFLAG:被射精キャラ番号:累計精飲 += NOWEX:射精キャラ番号:射精量 / 2
			CFLAG:被射精キャラ番号:溜まってる度 -= NOWEX:射精キャラ番号:射精量 * ABL:被射精キャラ番号:精液中毒 / 10 * (TALENT:被射精キャラ番号:精愛味覚 + 1)
		ENDIF
		;接触のリセット
		TEQUIP:射精キャラ番号:指接触部位 = 0
	;口淫
	CASE 4
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精 * 3
		STAIN:被射精キャラ番号:口 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:被射精キャラ番号:口に精液 = 2
		ELSE
			CFLAG:被射精キャラ番号:口に精液 = 1
		ENDIF
		;強制口交
		欲求不満解消係数 = 1000 / MIN(100 + ABL:被射精キャラ番号:精液中毒 * 10,200)
		IF TALENT:被射精キャラ番号:精愛味覚
			SOURCE:被射精キャラ番号:快Ｍ += NOWEX:射精キャラ番号:射精 * 10
			欲求不満解消係数 += 1
		ENDIF
		IF SELECTCOM == 140
			EXP:被射精キャラ番号:精飲経験 += NOWEX:射精キャラ番号:射精 * 3
			CFLAG:被射精キャラ番号:累計精飲 += NOWEX:射精キャラ番号:射精量
		ELSEIF  RAND:(MIN(60, EXP:被射精キャラ番号:精飲経験 + 1)) > 10 + RAND:30
			EXP:被射精キャラ番号:精飲経験 += NOWEX:射精キャラ番号:射精 * 3
			CFLAG:被射精キャラ番号:累計精飲 += NOWEX:射精キャラ番号:射精量
		ELSE
			EXP:被射精キャラ番号:精飲経験 += NOWEX:射精キャラ番号:射精
			CFLAG:被射精キャラ番号:累計精飲 += NOWEX:射精キャラ番号:射精量 / 3
			欲求不満解消係数 /= 2
		ENDIF
		CFLAG:被射精キャラ番号:溜まってる度 -= SQRT(NOWEX:射精キャラ番号:射精量) * 欲求不満解消係数 / 4
		;接触のリセット
		TEQUIP:射精キャラ番号:口接触部位 = 0
	;乳交
	CASE 5
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精 * 3
		CFLAG:被射精キャラ番号:累計ぶっかけ += NOWEX:射精キャラ番号:射精量
		STAIN:被射精キャラ番号:5 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:被射精キャラ番号:顔に精液 = 2
		ELSE
			CFLAG:被射精キャラ番号:顔に精液 = 1
		ENDIF
		;TFLAG:特殊COM = 12 に該当するものがCSVに書いてない
		;(1=六九式 2=岩清水 3=Gスポット刺激 4=乱牡丹 5=手淫口交 6= 7= 8= 六九式乳交 13=交互挿入 )
		;TFLAGS.txtと書いてあることが違う（TFLAG:12 逆強姦継続フラグ（VA共通））
		;	→嫌な予感
		SIF TFLAG:特殊COM == 12
			STAIN:被射精キャラ番号:口 |= 4
		;接触のリセット
		TEQUIP:射精キャラ番号:Ｂ接触部位 = 0
	;素股
	CASE 6
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精
		CFLAG:被射精キャラ番号:累計ぶっかけ += NOWEX:射精キャラ番号:射精量
		STAIN:被射精キャラ番号:Ｖ |= 4
		;接触のリセット
		TEQUIP:射精キャラ番号:Ｃ接触部位 = 0
	;足交
	CASE 7
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精
		CFLAG:被射精キャラ番号:累計ぶっかけ += NOWEX:射精キャラ番号:射精量
	;体表
	CASE 8
		EXP:被射精キャラ番号:精液経験 += NOWEX:射精キャラ番号:射精 * 2
		CFLAG:被射精キャラ番号:累計ぶっかけ += NOWEX:射精キャラ番号:射精量
ENDSELECT
ENDIF
CFLAG:被射精キャラ番号:溜まってる度 = LIMIT(CFLAG:被射精キャラ番号:溜まってる度, 0, 1000)
CFLAG:射精キャラ番号:溜まってる度 = MAX(CFLAG:射精キャラ番号:溜まってる度 - NOWEX:射精キャラ番号:射精 * 50, 0)
;射精された精子を子宮内へ送る処理
@SAMEN_SHOOT3(射精した精液量, 射精したキャラ, 射精されたキャラ)
#DIM 射精した精液量
#DIM 射精されたキャラ
#DIM 減算補正値
#DIM 加算値

;使われてない……
#DIM 射精したキャラ

;73,挿入子宮口玩弄
;後ろから上書きされてるけどこれ意味有る？
IF SELECTCOM == 73
	減算補正値 = 100
ELSE
	SELECTCASE TEQUIP:射精されたキャラ:子宮
	CASE 0
		減算補正値 = 150
	CASE 1
		減算補正値 = 120
	CASE 2
		減算補正値 = 100
ENDSELECT
ENDIF

;だいしゅきホールド
SIF TCVAR:射精されたキャラ:カウンター行動の派生 == 2 && TCVAR:射精されたキャラ:カウンター行動 == 500
	減算補正値 -= 30
加算値 = 射精した精液量 - 減算補正値

SELECTCASE EXP:射精されたキャラ:子宮口開発経験
CASE IS >= 300
CASE IS > 100
	加算値 = 加算値 * 2 / 3
CASEELSE
	加算値 /= 2 
ENDSELECT

;計算結果が負なら加算しない
IF 加算値 < 0
	RETURN 0
ENDIF
EX:射精されたキャラ:子宮内精液 += 加算値