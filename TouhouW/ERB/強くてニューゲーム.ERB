@強くてニューゲーム
;パンツ保管庫。200人分30種類まで対応の優れもの
#DIM パンツ收藏,200,30

;使用LOCAL配列要素
;5：なりきりキャラの出禁状態
;6：今周回のキャラ人数（CHARANUM退避用）

;FLAGリセット
;残されるFLAG
;11：周回数
;560：祈願確認用。確認後リセット
;561：精力強化回数
;563：重婚可能人数（未使用）
;564-567：恋慕／恋人特典（未使用）
;700：パンツ收藏（種類数）
;701-702：実績解除関係
;850：清掃中毒治療回数
;1007：生理周期可視化のON/OFF
;1028：察知モード
;1030：なりきり

CALL BUFF_RESET

FOR LOCAL,0,1500
SELECTCASE LOCAL
	CASE 11,560,561,563 TO 567,700 TO 702,850,1007,1028,1030
		CONTINUE
	CASEELSE
		FLAG:LOCAL = 0
	ENDSELECT
NEXT

;なりきりキャラの出禁状態をチェック、出禁フラグはLOCAL:5に保存
SIF FLAG:なりきり
	LOCAL:5 = CFLAG:(FLAG:なりきり):出禁

CFLAG:MASTER:お招き = 0
CFLAG:MASTER:育児人数 = 0
CFLAG:MASTER:兒童人数 = 0
FLAG:周回数 ++
FOR LOCAL,1,CHARANUM
	IF TALENT:LOCAL:恋人 || GETBIT(CFLAG:LOCAL:引き継ぎ, 1)
		SETBIT TFLAG:(LOCAL + 500), 1
	ELSEIF TALENT:LOCAL:恋慕 || GETBIT(CFLAG:LOCAL:引き継ぎ, 0)
		SETBIT TFLAG:(LOCAL + 500), 0
	ENDIF
	SIF TALENT:LOCAL:自慰狂 || GETBIT(CFLAG:LOCAL:引き継ぎ, 2)
		SETBIT TFLAG:(LOCAL + 500), 2
	SIF TALENT:LOCAL:淫壺 || GETBIT(CFLAG:LOCAL:引き継ぎ, 3)
		SETBIT TFLAG:(LOCAL + 500), 3
	SIF TALENT:LOCAL:尻穴狂 || GETBIT(CFLAG:LOCAL:引き継ぎ, 4)
		SETBIT TFLAG:(LOCAL + 500), 4
	SIF TALENT:LOCAL:淫乳 || GETBIT(CFLAG:LOCAL:引き継ぎ, 5)
		SETBIT TFLAG:(LOCAL + 500), 5
	SIF TALENT:LOCAL:接吻魔 || GETBIT(CFLAG:LOCAL:引き継ぎ, 6)
		SETBIT TFLAG:(LOCAL + 500), 6
	FOR LOCAL:1,0,26
		パンツ收藏:LOCAL:(LOCAL:1) = CFLAG:LOCAL:(LOCAL:1 + 100)
	NEXT
NEXT
;リセット前一位角色人数をLOCAL:6に控えておく。
LOCAL:6 = CHARANUM

;主人公以外全員リセット。
;ここでCHARANUMの値が変わるので注意
PICKUPCHARA 0

;リセット前一位角色人数でFOR文を回す
FOR LOCAL, 1, LOCAL:6
	;CSVファイルの存在判定
	IF EXISTCSV(LOCAL)
		SIF GETCHARA (LOCAL) == -1
			ADDCHARA LOCAL
		;効率化追求のため引き継ぎフラグと同時に回す
		;ファイルチェックはすでにしてるんだからその中で回せば済む話でした（反省）
		FOR LOCAL:1,0,26
				;引き継ぎフラグのセット
				SIF LOCAL:1 < 7 && GETBIT (TFLAG:(LOCAL + 500), LOCAL:1)
					SETBIT CFLAG:LOCAL:引き継ぎ, LOCAL:1
				;パンツ收藏の引き継ぎ
				CFLAG:LOCAL:(LOCAL:1 + 100) = パンツ收藏:LOCAL:(LOCAL:1)
		NEXT
	ENDIF
NEXT
CALL 特典
;ターゲット・アシスタント初期化、貸本関係リセット
TARGET = 0
ASSI = 0
PBAND = 12
;パッチにて追加部分
	ITEM:101 = 0
	ITEM:102 = 0
	ITEM:103 = 0
	ITEM:104 = 0
;デフォルトオプション
CALL DEFAULT_OPTION
;初期地点設定
FOR LOCAL, 0, CHARANUM
	;開始位置
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	;服装
	CALL SELECT_CLOTHES(LOCAL, "今日の衣装")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_普段着")
	;これ多分問題なさそうだけど一応
	CFLAG:LOCAL:パジャマ = 0
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT

;MASTER起床時間セット（7時）
TIME:3 = 420

;MASTER睡眠中フラグON
CALL SET_SLEEP(1, MASTER, 0)

;日付１日目
DAY = 1
DAY:2 = 1
DAY:3 = 1

BASE:MASTER:体力 = MAXBASE:MASTER:体力
BASE:MASTER:気力 = MAXBASE:MASTER:気力
BASE:MASTER:精力 = MAXBASE:MASTER:精力


CFLAG:MASTER:就寝時間 = 0
CALL SET_NOUMIN
CALL SET_MAINHOME
STR:16 = 破旧小屋
FLAG:酒虫 = 1

;主人公の既成事実及び[恋人]をクリア
CLEARBIT CFLAG:MASTER:既成事実, 2
TALENT:MASTER:恋人 = 0

;なりきっているキャラの出禁をチェック
SIF FLAG:なりきり
	CFLAG:(FLAG:なりきり):出禁 = LOCAL:5

CALL VIRGIN_SET
CALL SET_CHARISMA("RESET")

PRINTFORML 要重置%CALLNAME:MASTER%的経験・能力麼？
CALL ASK_YN
IF !RESULT
	FOR LOCAL,0, 150
		EXP:MASTER:LOCAL = 0
		SIF LOCAL < 100
			ABL:MASTER:LOCAL = 0
	NEXT
	PRINTFORMW 已経重置。
ENDIF
;SYSTEMCALL to SHOW_SHOP
BEGIN SHOP

@特典
PRINTW ＊恋人特典＊
PRINTW 可以対曾経成為恋人的人物的素質进行変更
$LOOP
DRAWLINE
FOR LOCAL,1,CHARANUM
SIF GETBIT (CFLAG:LOCAL:引き継ぎ, 1)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 継续
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:引き継ぎ, 1)
	CALL CUSTOM_TERMINAL(RESULT)
	GOTO LOOP
ENDIF

PRINTW ＊恋慕特典＊
PRINTW 可以対曾経取得恋慕的人物的一部分素質进行変更
$LOOP2
DRAWLINE
FOR LOCAL,1,CHARANUM
SIF GETBIT (CFLAG:LOCAL:引き継ぎ, 0)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 継续
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:引き継ぎ, 0)
	CALL LIMITED_CUSTOM(RESULT)
	GOTO LOOP2
ENDIF
FOR LOCAL,1,CHARANUM
	SIF TCVAR:LOCAL:感度上昇部位
		TALENT:LOCAL:(TCVAR:LOCAL:感度上昇部位) ++
NEXT