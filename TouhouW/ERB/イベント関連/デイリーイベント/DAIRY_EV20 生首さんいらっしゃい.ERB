@DAIRY_CONDITION_EV20
SIF CFLAG:73:神社在住
	RETURN -1
SIF CFLAG:73:出禁
	RETURN -1
SIF !TALENT:73:思慕 && ! TALENT:73:恋慕
	RETURN -1
SIF RAND:15 > 0
	RETURN -1
CALL DAIRY_EV20
RETURN 1

@DAIRY_EV20
#DIM 来客数

PRINTFORML 半夜、從窗戶那傳来了響声
PRINTFORMW %CALLNAME:MASTER%睡眼惺忪打開了窗戶、外面漂着一個頭
PRINTFORML ……是赤蛮奇的頭
PRINTFORML 因為從這裡路過所以順便来看看
CALL ASK_YN("讓她進来","睡覚")
IF RESULT
	PRINTFORML 因為很困所以讓她回去了
	RETURN
ENDIF
PRINTFORML 讓她進来了

来客数 = 1
IF ABL:73:親密 >= 10 && !RAND:3
	PRINTFORML 腦袋從窗戶進来了……
	FOR LOCAL,2,ABL:73:親密 - 5
		SIF !RAND:2
			CONTINUE
		来客数 ++
		PRINTFORMW 但是、不止一個而是有{来客数}個腦袋進来了
		SIF 来客数 >= 9
			BREAK
	NEXT
	SIF 来客数 == 1
		PRINTFORML 想着或許不止一個、但只有一個
	SIF 来客数 >= 4
		PRINTFORMW 被同樣的臉包囲着、感覚不太舒坦……
ENDIF
IF TIME:5 >= 4
	PRINTFORML 因為被淋湿了所以把浴巾递过去了
	PRINTFORML 在毛巾上打着転的腦袋看起来是非常有趣的情景
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML 為了配合視線的高度、把坐墊放在了桌子之上
			SIF 来客数 > 1
				PRINTFORML 桌子上排列着腦袋的場景就像在確認斬首一樣
		CASE 1
			PRINTFORML 因為喝茶有些辛苦所以用了吸管
			PRINTFORML 喝下去的東西到底去了哪裡呢……
		CASE 2
			PRINTFORML 幇她從後頭部拿下了不小心沾上的葉子
	ENDSELECT
ENDIF
PRINTFORMW 
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

IF RAND:100 < ABL:73:親密 * 3 + TALENT:73:恋慕 * 15
	PRINTFORM 談笑了一段時候後、腦袋\@ 来客数 > 1 ? 們# \@説道明天身体会来
	IF !PRIVATEROOM:(CFLAG:MASTER:初期位置 % 100) && TALENT:73:恋慕
		PRINTFORML 打算回去的時候、説道既然明天身体要来的話就住在這裡吧
		PRINTFORML %CALLNAME:MASTER%把腦袋\@ 来客数 > 1 ? 排列# 放置\@在枕辺就這樣睡着了……
	ELSE
		PRINTFORML 後就回去了
	ENDIF
	CFLAG:73:翌日来訪 = 1
ELSE
	PRINTFORML 談笑了一段時候後、腦袋\@ 来客数 > 1 ? 們# \@從窗戶那回去了
	SIF !RAND:2
		CFLAG:73:翌日来訪 = 1
ENDIF
;システムメッセージつき好感度変化
;最後の引数だけ好感度が変化
CALL CHANGE_CFLAG(2,73,50)

;システムメッセージつき信頼度変化
;最後の引数だけ信頼度が変化
CALL CHANGE_CFLAG(4,73,20)

;睡眠時間減少
CFLAG:MASTER:就寝時間 += 60
CALL COLORMESSAGE("睡眠時間減少了1小時",C_YELLOW)