@DAIRY_CONDITION_EV13
#DIM 対象

SIF RAND:(10 - TALENT:MASTER:劣情察知)> 0
	RETURN -1
対象 = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10,1,CHARANUM
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	SIF !CFLAG:LOCAL:神社在住
		CONTINUE
	IF CFLAG:LOCAL:溜まってる度 >  700
		対象 = LOCAL
		BREAK
	ENDIF
NEXT
SIF !対象
	RETURN -1

CALL DAIRY_EV13(対象)
RETURN 1


@DAIRY_EV13(ARG)
PRINTFORMW 在半夜醒了過来
PRINTFORMW 想継續睡覚但是怎麼也睡不着了……
CALL ASK_YN("散步","待在房間")
IF RESULT
	PRINTFORM %CALLNAME:MASTER%直到重新発困為止
	PRINTDATA
		DATAFORM 整理・鑑賞着自己的收藏
		DATAFORM 看着美丽的胖次陷入了沈思
		DATAFORM 看着墙上的斑点発愣
	ENDDATA
	PRINTFORML ……
	CALL COLORMESSAGE("心霊受到了洗礼，气力和TSP暂时的増加了",C_YELLOW,,1)
	CALL BUFF_BASE(MASTER,BASE_気力,100)
	CALL BUFF_BASE(MASTER,BASE_TSP,100)
ELSEIF ITEM:兎符「開運大紋」
	ITEM:兎符「開運大紋」 --
	SETCOLOR COLOR ("YELLOW")
	PRINTFORMW 兎符「開運大紋」発動！
	RESETCOLOR
	CALL ONABARE(ARG)
	CFLAG:MASTER:徹夜 = 1
ELSEIF !RAND:5
	PRINTFORMW 隨意散步了一会、\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ? 在打算回自己房間的時候、聽到了什麼声音# 想着差不多該回去的時候、聽到從%CALLNAME:ARG%的房間傳来了什麼声音於是打算去看看\@
	CALL ONABARE(ARG)
	CFLAG:MASTER:徹夜 = 1
ELSE
	PRINTFORMW 出去闲逛后発现好像没有什么有趣的事，于是决定老老実実的回去睡覚了
	CFLAG:MASTER:就寝時間 += 60
	CALL COLORMESSAGE("睡眠時間減少了1小時",C_YELLOW,,1)
ENDIF
