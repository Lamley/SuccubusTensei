@DAIRY_CONDITION_EV12
VARSET LOCAL
IF RAND:15 > 0
	RETURN -1
ELSE
	CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
	FOR LOCAL:10,1,CHARANUM
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
		IF CFLAG:LOCAL:信頼度 >= 100 && CFLAG:LOCAL:神社在住
			LOCAL:1 = LOCAL
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		RETURN -1
	ELSE
		CALL DAIRY_EV12(LOCAL:1)
		RETURN 1
	ENDIF
ENDIF

@DAIRY_EV12(ARG)
#DIM 正解
#DIM 経験値
#DIM 制限時間
#DIM 倍率
#DIM FEINT
#DIM イカサマ
VARSET LOCAL
イカサマ = 0
制限時間 = 2000 - (ABL:ARG:戦闘能力 - ABL:MASTER:戦闘能力) * 100
PRINTFORML %CALLNAME:ARG%希望你陪伴完成新技能的訓練
SIF ABL:ARG:戦闘能力 >= 4
		PRINTFORML 以対方為対手、一個不好会受到重傷吧……
CALL ASK_YN("交手","不要")
IF RESULT
	PRINTFORML %CALLNAME:ARG%表示遺憾
ELSE
	正解 = RAND:4
	経験値 = MAX(ABL:ARG:戦闘能力 * 5, 5)
	SELECTCASE RAND:20
		CASE IS <= 11
		;普通に
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 100
		CASE 12 TO 15
		;ローマ字
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 160
		;英語
		CASE 16,17,18
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 120
		;スペイン語
		CASE 19
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 300
	ENDSELECT
	;戦闘能力が高いとフェイントかけてくる
	IF ABL:ARG:戦闘能力 > 3 && !RAND:3
		FEINT = 1
		制限時間 += 400
		倍率 += 80
	ELSE
		FEINT = 0
	ENDIF
	CALL FISHER_YATES_SHAFFLE(4)
	PRINTFORMW 回避%CALLNAME:ARG%的攻撃（点撃開始）
	PRINTFORM %CALLNAME:ARG%的
	SETCOLOR C_YELLOW
	IF FEINT
		PRINTFORM %LOCALS:(RAND:4)%
		RESETCOLOR
		PRINTFORM 虚晃一槍後從%LOCALS:(3 - 正解)%
	ELSE
		PRINTFORM %LOCALS:(3 - 正解)%
		RESETCOLOR
	ENDIF
	PRINTFORML 方発起了攻撃！
	$LOOP_IKASAMA
	FOR LOCAL:10,0,4
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10)
		PRINTFORML [{LOCAL}] - 避開%LOCALS:LOCAL%面
	NEXT
	SIF !イカサマ && BASE:MASTER:TSP > 500
		PRINTFORML [{4}] - 時間停止
	TINPUT 制限時間, -1, 1
	IF RESULT == 正解
		PRINTFORML %CALLNAME:MASTER%躲開了攻撃！
		IF !イカサマ
			PRINTFORMW 互相増加了経験
		ELSE
			PRINTFORMW 因爲太狡猾所有訓練沒有起到什麼效果……
		ENDIF
		経験値 = 経験値 * 倍率 / 100
		EXP:MASTER:戦闘経験 += 経験値
		EXP:ARG:戦闘経験 += 経験値 * 2
		CFLAG:ARG:好感度 += 経験値 * 2
		CFLAG:ARG:信頼度 += 経験値 * 2
		PRINTFORML 戦闘経験 ＋{経験値}（%CALLNAME:MASTER%）
		PRINTFORML 戦闘経験 ＋{経験値 * 2}（%CALLNAME:ARG%）
		PRINTFORML 好感度 ＋{経験値 * 2}（%CALLNAME:ARG%）
		PRINTFORML 信頼度 ＋{経験値 * 2}（%CALLNAME:ARG%）
	ELSEIF RESULT == 4 && !イカサマ && BASE:MASTER:TSP > 500
		イカサマ = 1
		倍率 /= 5
		PRINTFORMW %CALLNAME:MASTER%用時間停止能力看穿了攻撃！
		GOTO LOOP_IKASAMA
	ELSE
		PRINTFORML 避開了傷害！
		PRINTFORM %CALLNAME:MASTER%
		SELECTCASE ABL:ARG:戦闘能力
			CASE IS <= 1
				PRINTFORM 稍微
			CASE 2,3
				PRINTFORM 相当
			CASEELSE
				PRINTFORM 非常
		ENDSELECT
		PRINTFORMW 的疼
		BASE:MASTER:体力 -= 400 * (1 + ABL:ARG:戦闘能力)
		PRINTFORML 戦闘経験 ＋{経験値}（%CALLNAME:ARG%）
		EXP:ARG:戦闘経験 += 経験値
		CFLAG:ARG:好感度 += 経験値 / 2
		CFLAG:ARG:信頼度 += 経験値 / 2
		PRINTFORML 好感度 ＋{経験値}（%CALLNAME:ARG%）
		PRINTFORML 信頼度 ＋{経験値}（%CALLNAME:ARG%）
	ENDIF
ENDIF
