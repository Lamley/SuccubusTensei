@DAIRY_CONDITION_EV7

IF !RAND:15 && MAIN_MAP == 9
	CALL DAIRY_EV7
ELSE
	RETURN -1
ENDIF

@DAIRY_EV7
#DIM 人数
#DIM YUGI
VARSET LOCAL
YUGI = !RAND:4
IF YUGI
	LOCALS = 勇儀們
ELSE
	LOCALS = 鬼們
ENDIF
人数 = 1 + RAND:5
PRINTFORML %CALLNAME:MASTER%打算上床睡覚時、喝了酒的{人数}人的鬼滾出来了！
SIF YUGI
	PRINTFORML 而且還有勇儀……
PRINTFORMW 看来打算接着在這滯留下去喝酒……
PRINTFORML 
;他の選択肢は後で
;ASK_M("受け入れる","酒を渡して出て行ってもらう","ぶちのめす")
PRINTFORML %CALLNAME:MASTER%拼命接受了%LOCALS%
PRINTFORML %LOCALS%一点都不客気的帯着酒跟下酒菜開始狂歡了
LOCAL:1 = 1
FOR LOCAL,0,人数
	LOCAL:1 += RAND:3
NEXT
SIF YUGI
	LOCAL:1 += RAND:3 + RAND:3

FOR LOCAL,0,LOCAL:1
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORML %CALLNAME:MASTER%被強行灌着酒
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW 普通的酒
			LOCAL:2 = 200
		CASE 1
			PRINTFORMW 相当強力的酒
			LOCAL:2 = 400
		CASE 2
			PRINTFORMW 很吃力！
			LOCAL:2 = 700
	ENDSELECT
	PRINTFORMW 
	BASE:MASTER:酒気 += LOCAL:2
	IF YUGI
		CALL CHANGE_CFLAG(2,64,3)
		CALL CHANGE_CFLAG(4,64,1)
	ENDIF
	IF BASE:MASTER:酒気 > MAXBASE:MASTER:酒気
		PRINTFORML 「哇！」
		PRINTFORML %CALLNAME:MASTER%吐了！
		YOGORE:(CFLAG:MASTER:初期位置) += 1500
		BASE:MASTER:気力 -= 500
		MAXBASE:MASTER:酒気 += 3
		PRINTFORMW %LOCALS%哈哈大笑…
		IF BASE:MASTER:気力 < 0
			PRINTFORMW %CALLNAME:MASTER%直挺挺的倒了！
			BASE:MASTER:気力 -= 1000
			LOCAL:3 ++
			BREAK
		ELSE
			BASE:MASTER:酒気 /= 2
		ENDIF
	ELSEIF BASE:MASTER:酒気 > MAXBASE:MASTER:酒気 * 3 / 4
		PRINTFORMW 感到噁心了…
	ENDIF
NEXT

IF !LOCAL:3
	PRINTFORML %CALLNAME:MASTER%完全経得起無情的灌酒！
	IF YUGI
		CFLAG:64:好感度 += 150
		CFLAG:64:信頼度 += 100
	ENDIF
	MAXBASE:MASTER:酒気 += 20
ENDIF
CFLAG:MASTER:徹夜 = 1
YOGORE:(CFLAG:MASTER:初期位置) += 1000
MAXBASE:MASTER:酒気 = LIMIT(MAXBASE:MASTER:酒気,1500,3000)
PRINTFORML 黎明的時候%LOCALS%離開了
PRINTFORML 然後睡眠不足的%CALLNAME:MASTER%在有大量空瓶\@ LOCAL:3? 充満嘔吐物臭味的# \@房間留下了
RETURN 1