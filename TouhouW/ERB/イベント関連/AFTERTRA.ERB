;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND
#LATER
#DIM 変動値

PRINTL 一天結束了
PRINTFORML 
;SIF STRLENS(TSTR:1)
;	CALL SELL_VIDEO

;睡眠フラグ
CALL SET_SLEEP(1,MASTER,0)

CFLAG:MASTER:286 = 0
CFLAG:MASTER:287 = 0
CFLAG:MASTER:288 = 0
CFLAG:MASTER:289 = 0
CFLAG:MASTER:290 = 0
CFLAG:MASTER:ノーパン = 0

;神簽を引けるようにフラグリセット
CLEARBIT FLAG:999, 1

;中出しフラグリセット
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:286 = 0
	CFLAG:LOCAL:287 = 0
	CFLAG:LOCAL:288 = 0
	CFLAG:LOCAL:289 = 0
	CFLAG:LOCAL:290 = 0
	;祈願による感度上昇解除
	SIF GROUPMATCH(TCVAR:LOCAL:祈願補正,101,102,103,104,106)
		TALENT:LOCAL:(TCVAR:LOCAL:祈願補正) -= 1
	;好きな体位決定
	SIF CFLAG:LOCAL:好きな体位 > 10 
		CFLAG:LOCAL:好きな体位 = 0
	FOR COUNT,1,10
		SIF EXP:LOCAL:(COUNT + 100) > EXP:LOCAL:(CFLAG:LOCAL:好きな体位 + 100) + 50
			CFLAG:LOCAL:好きな体位 = COUNT
	NEXT
	;パンツフラグ
	CFLAG:LOCAL:ノーパン = 0
	SIF TCVAR:LOCAL:本来の服装
		CFLAG:LOCAL:基本服装セット = TCVAR:LOCAL:本来の服装
	SELECTCASE ABL:LOCAL:精液中毒
		CASE 0
			LOCAL:1 = 5
		CASE 1 TO 3
			LOCAL:1 = 7
		CASE 4 TO 7
			LOCAL:1 = 10
		CASEELSE
			LOCAL:1 = 20
	ENDSELECT
	SIF TALENT:MASTER:濃厚精液
		LOCAL:1 *= 2
	SETCOLOR 0xEE60EE
	IF EX:LOCAL:膣内精液 > 200 || EX:LOCAL:アナル内精液 > 200
		PRINTFORM 在%CALLNAME:MASTER%大量注入的精液影响下、%CALLNAME:LOCAL%的
		IF EX:LOCAL:膣内精液 > 200 && EX:LOCAL:アナル内精液 > 200
			PRINTL 陰道和肛门又热又痒…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
		ELSEIF EX:LOCAL:膣内精液 > 200
			PRINTL 陰道又热又痒…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
		ELSE
			PRINTL 肛门又热又痒…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
		ENDIF
	ENDIF
	IF (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) > 0
		PRINTFORM %CALLNAME:LOCAL%忍耐着
		SIF EX:LOCAL:20
			PRINT 陰蒂
		SIF EX:LOCAL:20 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:21
			PRINT 膣内
		SIF EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:22
			PRINT 肛門
		SIF EX:LOCAL:22 && (EX:LOCAL:23 || EX:LOCAL:24)
			PRINT 和
		SIF EX:LOCAL:23
			PRINT 胸部
		SIF EX:LOCAL:23 && EX:LOCAL:24
			PRINT 和
		SIF EX:LOCAL:24
			PRINT 嘴唇
		PRINTFORMW 傳来的痛楚…
		CFLAG:LOCAL:溜まってる度 += (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) * 100
	ENDIF
	IF (TALENT:LOCAL:150 || TALENT:LOCAL:151) && !TALENT:LOCAL:初潮 && EXP:LOCAL:Ｖ絶頂経験 > MAX(0, 200 - CFLAG:LOCAL:子宝補正) && !RAND:(10 - MIN(9, CFLAG:LOCAL:子宝補正 / 30)) && EX:LOCAL:膣内精液
		PRINTFORML 在対幼小的身体而言為时尚早的快楽冲撃下、%CALLNAME:LOCAL%変成了「女人」……
		PRINTFORMW %CALLNAME:LOCAL%迎来了初潮
		PRINTFORMW %CALLNAME:LOCAL%的身体変得可以怀孕了
		TALENT:LOCAL:初潮 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 < 3
		IF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 15000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10
			PRINTFORML 被无数次注入精液的%CALLNAME:LOCAL%的身体、已経记住了%CALLNAME:MASTER%精液的滋味…
			PRINTL 今后即使是無意識下也会変得主動想要了吧
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了3级
			ABL:LOCAL:精液中毒 = 3
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 7500 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && ABL:LOCAL:精液中毒 < 2
			PRINTFORML %CALLNAME:LOCAL%的身体、已経与精液十分熟稔了…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了2级
			ABL:LOCAL:精液中毒 = 2
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 3000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !ABL:LOCAL:精液中毒
			PRINTFORML %CALLNAME:LOCAL%的身体、已経记住了精液的味道…
			PRINTFORMW %CALLNAME:LOCAL%的精液中毒达到了1级
			ABL:LOCAL:精液中毒 = 1
		ENDIF
	ENDIF
	IF CFLAG:LOCAL:累計精飲 > 10000 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !TALENT:LOCAL:精愛味覚
		PRINTFORML 持續喝下精液的%CALLNAME:LOCAL%的身体、変得会因精液的味道而感到喜悦…
		PRINTFORMW %CALLNAME:LOCAL%得到了【精愛味覚】
		TALENT:LOCAL:精愛味覚 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 >= 3
		IF CFLAG:LOCAL:累計膣内精液 > 3000 * ABL:LOCAL:膣射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 &&  EX:LOCAL:膣内精液 > 200 && ABL:LOCAL:膣射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%的媚粘膜完全被精液玷汚…
			PRINTFORMW %CALLNAME:LOCAL%的膣道変得\@ ABL:LOCAL:膣射中毒 ? 強烈# \@渇望着%CALLNAME:MASTER%的精液
			ABL:LOCAL:膣射中毒 ++
		ELSEIF CFLAG:LOCAL:累計アナル精液 > 3000 * ABL:LOCAL:肛射中毒 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && EX:LOCAL:アナル内精液 > 200 && ABL:LOCAL:肛射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%的直腸完全被精液玷汚…
			PRINTFORMW %CALLNAME:LOCAL%的肛门変得\@ ABL:LOCAL:肛射中毒 ? 強烈# \@渇望着%CALLNAME:MASTER%的精液
			ABL:LOCAL:肛射中毒 ++
		ENDIF
	ENDIF
	RESETCOLOR
	IF FLAG:妊娠スイッチ == 1
		SIF CFLAG:LOCAL:妊娠経過日数 == 0 && !FLAG:避妊 && !TCVAR:LOCAL:口服避孕薬
			CALL GET_CHILD(LOCAL)
	ELSE
	;
	ENDIF
	CALL 出産経過(LOCAL)
	SIF CFLAG:LOCAL:600
		CFLAG:LOCAL:600 = 0
NEXT

FOR LOCAL,MINROOM(),MAXROOM
	YOGORE:LOCAL += RAND:50
NEXT
IF FLAG:加護
	FLAG:加護 = 0
	PRINTFORMW 神的加護的効果消失了
ENDIF
IF FLAG:異常気象
	FLAG:異常気象 = 0
	PRINTFORMW 異常気象消失了
ENDIF

IF TFLAG:出張お掃除
	PRINTFORML 応委托前去执行上门清扫服务
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%里面変得干浄了！
	FOR LOCAL,MINROOM(),MAXROOM
		YOGORE:LOCAL /= 4
	NEXT
ENDIF

[SKIPSTART]
;依頼の期限切れをチェック
FOR LOCAL,1,6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	SIF IRAI_DEADLINE:LOCAL > DAY
		CONTINUE
	PRINTFORML %CALLNAME:(IRAI_SLOT_TO_CLIENT(LOCAL))%的委托期限到了……
	CALL COLORMESSAGE("【依頼失敗】",C_YELLOW,2,1)
	CFLAG:(IRAI_CLIENT:LOCAL):依頼状況 = 4
NEXT
[SKIPEND]

;デイリー
;SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:MASTER:デート中 == MAIN_MAP
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	CALL DAIRYEVENT
;畑の管理
CALL YASAI

;貸本屋
CALL BOOKLEND

CALL 酒造

CALL MOENY_HENDOU



MONEY:1 += MAXBASE:MASTER:TSP - BASE:MASTER:TSP
IF MONEY:1 >= 30000 && MAXBASE:MASTER:TSP == 2500
	PRINTFORML 時間停止能力進化到了極限！
	MAXBASE:MASTER:TSP = 4000
ELSEIF MONEY:1 >= 10000 && MAXBASE:MASTER:TSP == 1500
	PRINTFORML 時間停止能力的使用已経炉火纯青了！
	MAXBASE:MASTER:TSP = 2500
ELSEIF MONEY:1 >= 5000 && MAXBASE:MASTER:TSP == 800
	PRINTFORML 時間停止能力的使用已経相当在行了！
	MAXBASE:MASTER:TSP = 1500
ELSEIF MONEY:1 >= 1800 && MAXBASE:MASTER:TSP == 500
	PRINTFORML 時間停止能力的使用已経很拿手了！
	MAXBASE:MASTER:TSP = 800
ELSEIF MONEY:1 >= 500 && MAXBASE:MASTER:TSP == 200
	PRINTFORML 時間停止能力的使用已経有点门道了！
	MAXBASE:MASTER:TSP = 500
ELSEIF MONEY:1 >= 150 && MAXBASE:MASTER:TSP == 100
	PRINTFORML 時間停止能力的使用已経有点入门了！
	MAXBASE:MASTER:TSP = 200
ENDIF

BASE:MASTER:TSP = MAXBASE:MASTER:TSP

IF TALENT:MASTER:2 & 2
	IF BASE:MASTER:精力 == MAXBASE:MASTER:精力
		FLAG:71 ++
		IF FLAG:71 >= 5 && !RAND:3
			PRINTFORML 因為最近没有做过，所以感到烦躁……
			PRINTFORML %CALLNAME:MASTER%在被子里滚来滚去
			CALL COLORMESSAGE("气力的最大值因為烦躁暂时下降了",C_YELLOW,2,1)
			変動値 = MIN(MAXBASE:MASTER:気力 * 20 / 100, 500)
			CALL BUFF_BASE(MASTER,BASE_気力,-変動値)
		ELSEIF FLAG:71 >= 3
			TALENT:MASTER:120 = 1
			PRINTL 话説这辺的小兄弟也蓄势已久了呢…
		ENDIF
	ELSE
		FLAG:71 = 0
		TALENT:MASTER:120 = 0
	ENDIF
	IF BASE:MASTER:精力 < MAXBASE:MASTER:精力 * 2 / 10 && BASE:MASTER:体力 > 500
		PRINTFORML 今天満満的射出来了……
		PRINTFORML %CALLNAME:MASTER%舒舒服服的睡着了
		CALL COLORMESSAGE("TSP和气力最大值暂时増加了",C_YELLOW,2,1)
		変動値 = MIN(MAXBASE:MASTER:気力 / 10, 300)
		CALL BUFF_BASE(MASTER,BASE_気力,変動値)
		変動値 = MIN(MAXBASE:MASTER:TSP / 2, 500)
		CALL BUFF_BASE(MASTER,BASE_TSP,変動値)
	ENDIF
ENDIF



CALL ONANISM

SIF FLAG:酒屋買取強化 > 0
	FLAG:酒屋買取強化 --
;何の珠を得られたか
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
	CALL JUEL_CHECK(LOCAL)
	CALL DENIAL_CHECK(LOCAL)
NEXT
BEGIN ABLUP



@JUEL_CHECK(ARG)
FOR LOCAL,0,100
	IF PALAM:ARG:LOCAL < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:1*3
		LOCAL:1 = 1
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2
		LOCAL:1 = 2
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2*3
		LOCAL:1 = 10
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3
		LOCAL:1 = 20
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3*2
		LOCAL:1 = 100
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:4
		LOCAL:1 = 200
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:5
		LOCAL:1 = 1000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:6
		LOCAL:1 = 2000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:7
		LOCAL:1 = 3000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:8
		LOCAL:1 = 5000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:9
		LOCAL:1 = 8000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:10
		LOCAL:1 = 12000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:11
		LOCAL:1 = 25000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:12
		LOCAL:1 = 40000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:13
		LOCAL:1 = 60000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:14
		LOCAL:1 = 100000
	ELSE
		LOCAL:1 = 150000
	ENDIF
	;快感系
	IF LOCAL >= 0 && LOCAL < 9
		GOTJUEL:ARG:LOCAL = LOCAL:1 + EX:ARG:LOCAL * 1000
	;其他
	ELSEIF LOCAL < 30
		GOTJUEL:ARG:LOCAL = LOCAL:1
	;否定の珠
	ELSEIF LOCAL == 30 || LOCAL == 31 || LOCAL == 32
		GOTJUEL:ARG:100 += LOCAL:1
	ENDIF
NEXT
JUEL:ARG:100 += GOTJUEL:ARG:100
FOR LOCAL,0,100
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF LOCAL != 9 &&LOCAL != 30 && LOCAL != 31 && LOCAL != 32
		JUEL:ARG:LOCAL += GOTJUEL:ARG:LOCAL
NEXT
@DENIAL_CHECK(ARG)
SIF JUEL:ARG:100 == 0
	RETURN 0
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恭順 && !JUEL:ARG:欲情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恥情 && !JUEL:ARG:苦痛 && !JUEL:ARG:恐怖 && !JUEL:ARG:好意 && !JUEL:ARG:優越)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:5
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 14
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
		CASE 4
			LOCAL:2 = 21
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

@ONANISM
#DIM 欲求不満度
#DIM 満足度
#DIM 嫉妬度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加Ｖ自慰
#DIM 追加Ａ自慰
#DIM 自慰タイプ
#DIM 自慰発生閾値
VARSET LOCAL

嫉妬度 = 0

;親密な人の数が多いほど嫉妬して欲求不満度が上がる
;FOR LOCALの中にFOR LOCALいれたらエラー出たので独立させる
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF TALENT:LOCAL:恋慕
		嫉妬度 += 3
	SIF TALENT:LOCAL:思慕
		嫉妬度 += 1
NEXT
DEBUGPRINTFORML 嫉妬度{嫉妬度}
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	部位指定 = 0
	自慰タイプ = 0
	追加Ｖ自慰 = 0
	追加Ａ自慰 = 0
	自慰発生閾値 = LIMIT(750 - 200 * TALENT:LOCAL:容易自慰 - 100 * TALENT:LOCAL:快感応答 - 100 * TALENT:LOCAL:自己愛 + 200 * TALENT:LOCAL:自制心,500,999)
	自慰発生閾値 -= 300 * TALENT:LOCAL:自慰狂
	IF CFLAG:LOCAL:オナバレ
		PRINTFORML %CALLNAME:LOCAL%把臉埋在了枕頭裡面…
	ELSEIF CFLAG:LOCAL:溜まってる度 > 自慰発生閾値
		IF CFLAG:LOCAL:既成事実 & 合意_うふふ && !RAND:(5 - TALENT:LOCAL:自制心)
			PRINTFORML %CALLNAME:LOCAL%忍耐着身体的蠢動、想到明天就能和%CALLNAME:MASTER%做了、強忍着睡着了……
			自慰タイプ = 7
			;CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,7)
			CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 100, 0 ,1000)
			SIF !CFLAG:LOCAL:神社在住 
				CFLAG:LOCAL:翌日来訪 = 1
		ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初期位置
			PRINTFORML 夜里、%CALLNAME:MASTER%听到了一些動静而睁開了眼睛
			PRINTFORML ……%CALLNAME:LOCAL%一辺抓着%CALLNAME:MASTER%的手一辺圧抑着呻吟声沈溺于自慰之中……
			自慰タイプ = 2
		ELSEIF ABL:LOCAL:精液中毒 > 2 && EX:LOCAL:膣内精液 && !RAND:4
			PRINTFORML 無法忍耐的%CALLNAME:LOCAL%、伴着膣内残留的%CALLNAME:MASTER%的精液開始了自慰……
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:無知
			LOCAL:1 = 0
			LOCAL:1 = EXP:自慰経験 - EXP:調教自慰経験
			IF LOCAL:1 >= 120 - (MIN(ABL:親密,6) * 10) && !FLAG:無知自動喪失禁止
				PRINTFORMW 終於%CALLNAME:LOCAL%和監護人商量了……
				PRINTFORMW %CALLNAME:LOCAL%失去了無知……
				TALENT:LOCAL:無知 = 0
			ELSEIF LOCAL:1 >= 80 - (MIN(ABL:親密,4) * 10)
				PRINTFORML 尚不明白自慰是自慰的%CALLNAME:LOCAL%埋頭自慰中……
			ELSEIF LOCAL:1 >= 50 - (MIN(ABL:親密,3) * 10)
				PRINTFORML %CALLNAME:LOCAL%到桌子角一辺按胯揮動腰肢、一辺刺激祕所……
			ELSEIF LOCAL:1 >= 30 - (MIN(ABL:親密,2) * 10)
				PRINTFORML %CALLNAME:LOCAL%把書夾在胯間刺激着幼小的祕裂……
			ELSEIF LOCAL:1 >= 15 - (MIN(ABL:親密,1) * 10)
				PRINTFORML %CALLNAME:LOCAL%在学習的時候把筆貼在了胯下、就這樣摩擦起秘縫……
			ELSE
				PRINTFORML %CALLNAME:LOCAL%躺在被窩裏一辺側躺使大腿互相摩擦一辺悶悶……
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && (TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人 || TALENT:LOCAL:愛欲 || TALENT:LOCAL:炮友) && !RAND:3
			PRINTFORML 怎么也忍耐不住了的%CALLNAME:LOCAL%、一辺嗅着沾在衣服上的%CALLNAME:MASTER%的气味一辺激烈的自慰着……
			LOCAL:2 = RAND:5
			IF (TALENT:LOCAL:Ｃ感度 == 1 || ABL:LOCAL:Ｃ感覚 >= 10) && LOCAL:2 == 4
				PRINTFORM 揉搓着陰核、即使潮吹了還一味地持續着動作
				部位指定 = 0
			ELSEIF (TALENT:LOCAL:Ｖ感度 == 1 || ABL:LOCAL:Ｖ感覚 >= 10) && LOCAL:2 == 3
				PRINTFORM 把手指深深地插入到蜜壺之中持續扣弄着愛液
				部位指定 = 1
				追加Ｖ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ａ感度 == 1 || ABL:LOCAL:Ａ感覚 >= 10) && LOCAL:2 == 2
				PRINTFORM 持續玩弄着蜜穴和菊穴
				部位指定 = 2
				追加Ａ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ｂ感度 == 1 || ABL:LOCAL:Ｂ感覚 >= 10) && LOCAL:2 == 1
				IF TALENT:LOCAL:胸囲 >= 1
					PRINTFORM 吸吮着自己那豊満的乳房、
				ELSEIF TALENT:LOCAL:胸囲 <= -1
					PRINTFORM 撫摸着貧瘠的胸部来回揉搓着乳頭、
				ELSE
					PRINTFORM 用下流的手法揉弄着柔軟的乳房、
				ENDIF
				PRINTFORM 高潮了好幾次
				部位指定 = 3
			ELSE
				;PRINTFORML しかし、どんなに快感を得ようとも%CALLNAME:MASTER%への想いは治まることはなく、%CALLNAME:LOCAL%は朝まで悶々とした時間を過ごした……
				PRINTFORM 因体内的快感余韻而顫抖着身体
			ENDIF
			IF TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人
				PRINTFORML 、対%CALLNAME:MASTER%的思念平息了……
			ELSE
				PRINTFORML 、燃燒了身体的熱量還沒有平息……
			ENDIF
			追加Ｖ自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && !RAND:2
			PRINTFORM 已経无法用手指満足的%CALLNAME:LOCAL%、用从河童那买来的玩具
			IF ABL:LOCAL:Ａ感覚 >= 5
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 尽可能深的插入陰道和肛门的深処
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私処和肛門
				ELSE
					PRINTFORM 玩弄着私処和尻穴
				ENDIF
				部位指定 = 2
				追加Ａ自慰 = 1
			ELSE
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM 插入陰道的深処
					部位指定 = 1
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM 抵住私処
					部位指定 = 0
				ELSE
					PRINTFORM 玩弄着私処
					部位指定 = 0
				ENDIF
			ENDIF
			PRINTFORML 沈溺于自慰之中…
			自慰タイプ = 6
		ELSE
			PRINTFORML 身体変的躁動难安的%CALLNAME:LOCAL%、自己用手指玩弄起了私処…
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,自慰タイプ,部位指定)
		SIF 自慰タイプ == 7
			CONTINUE
		EXP:LOCAL:自慰経験 += ABL:LOCAL:自慰中毒 * 2 + 1
		CALL COLORMESSAGE(@"自慰経験＋{ABL:LOCAL:自慰中毒 * 2 + 1}",C_PINK,1)
		SELECTCASE ABL:LOCAL:自慰中毒
			CASE 0 TO 2
				珠入手倍率 = 5
			CASE 3 TO 5
				珠入手倍率 = 3
			CASEELSE
				珠入手倍率 = 1
		ENDSELECT
		;快C
		CALL JUEL_UP_SPECIAL(LOCAL,"快Ｃ","Ｃ感覚",珠入手倍率,1)
		;恥情
		CALL JUEL_UP_SPECIAL(LOCAL,"恥情","自慰中毒",珠入手倍率 * 2,1)
		;欲情
		CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",珠入手倍率,1)
		SIF 追加Ｖ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｖ","Ｖ感覚",珠入手倍率,1)
		SIF 追加Ａ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ａ","Ａ感覚",珠入手倍率,1)
		CFLAG:LOCAL:溜まってる度 /= 4
		PRINTFORML 
	ENDIF
	;まず欲望で基礎値を決める
	SELECTCASE ABL:LOCAL:欲望
		CASE 0 TO 2
			欲求不満度 = 10
		CASE 3 TO 5
			欲求不満度 = 20
		CASE 6 TO 10
			欲求不満度 = 30
		CASE 10 TO 15
			欲求不満度 = 50		
		CASEELSE
			欲求不満度 = 80
	ENDSELECT
	IF TALENT:LOCAL:恋慕
		欲求不満度 += 嫉妬度 * 2
	ELSEIF TALENT:LOCAL:思慕
		欲求不満度 += 嫉妬度
	ENDIF
	欲求不満度 += ABL:LOCAL:自慰中毒 * 7 + (ABL:LOCAL:膣射中毒 + ABL:LOCAL:肛射中毒) * 3
	欲求不満度 = 欲求不満素質補正(LOCAL,欲求不満度)
	SELECTCASE 欲求不満度
		CASE IS > 400
			欲求不満度 = (欲求不満度 - 400) / 10 + 400
		CASE IS > 200
			欲求不満度 = (欲求不満度 - 200) / 4 + 200
		CASE IS > 100
			欲求不満度 = (欲求不満度 - 100) / 2 + 100
	ENDSELECT
	SIF 自慰タイプ
		欲求不満度 = 0
	;溜まってる度は0~1000の間に
	CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 欲求不満度, 0, 1000)
	DEBUGPRINTFORML %CALLNAME:LOCAL%的満足度：{満足度}　欲求不満上昇値：{欲求不満度}　积攒度：{CFLAG:LOCAL:溜まってる度}
NEXT

@DAIRYEVENT
;とりあえず30だけ。
#DIM EVENT_DAY,30

CALL FISHER_YATES_SHAFFLE(30)
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_DAY"

FOR LOCAL,0,30
	RESULT = 0
	TRYCALLFORM DAIRY_CONDITION_EV{EVENT_DAY:LOCAL}
	IF RESULT > 0
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}が発生したよ
		BREAK
	ENDIF
	SIF RESULT == -1
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}は発生条件を満たさなかったんだぜ
NEXT
PRINTFORMW 
DRAWLINE
PRINTFORML 


@欲求不満素質補正(ARG,ARG:1)
#FUNCTION
LOCAL = ARG:1
SIF TALENT:ARG:自制心
	LOCAL /= 2
SIF TALENT:ARG:自己愛 < 0
	LOCAL /= 2
SIF TALENT:ARG:自己愛 > 0
	LOCAL = LOCAL * 5 / 4
SIF TALENT:ARG:幼稚 && !TALENT:ARG:初潮
	LOCAL /= 4
SIF TALENT:ARG:妊娠
	LOCAL /= 4
SIF TALENT:ARG:育児中
	LOCAL /= 2
SIF CFLAG:ARG:オナバレ
	LOCAL /= 4
RETURNF LOCAL