@ENKAI_SETTING
;------------------------------------------------------------------------------------------------------
;宴会の開催判定
;------------------------------------------------------------------------------------------------------
;1ターンで飲む酒の量は経過時間 * 10 通常は30分なので300、宴会規模1200なら2時間程度の宴会となる
;主催者は宴会の準備と片づけの仕事をしなければならない
;一日に複数の宴会を開ける仕様ではないので注意（人が分散しちゃって寂しいし…）
;------------------------------------------------------------------------------------------------------
PRINTL  

;PRINTFORMW 宴会開催フラグ = {FLAG:宴会開催フラグ}

;開催判定は宴会フラグが立っていない状態でしか行われない（バグ防止）
IF FLAG:宴会開催フラグ == 0
	;メイン主催者が出禁になっているなら開催されない
	;宴会種別は98以上には設定しないこと
	;2 幻想郷年忘れ会　メイン主催者：なし　大晦日の一週間前に１００％発生する
	IF DAY:2 == 4 && DAY:3 >= 25 && DAY:3 <= 30
		;宴会名
		LOCALS = 幻想郷年会
		;宴会種別
		FLAG:宴会開催フラグ = 2
		;宴会規模
		FLAG:31 = 6000
		;宴会会場
		FLAG:32 = 9
		;開始時間
		FLAG:33 = 900
		;開始日
		FLAG:34 = 31 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 0
		PRINTL  
		PRINTFORMW 開年会的季节到来了
	;月見酒
	ELSEIF DAY:2 == 3 && DAY:3 >= 1 && DAY:3 < 15
		;宴会名
		LOCALS = 赏月酒
		;宴会種別
		FLAG:宴会開催フラグ = 3
		;宴会規模
		FLAG:31 = 2000 + RAND:1000
		;会場
		FLAG:32 = 409
		;開始時間
		FLAG:33 = 1140
		;開始日
		FLAG:34 = 15 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		IF MAIN_MAP == 4
			PRINTFORMW 被永遠亭的赏月酒诱惑了
		ELSE
			PRINTFORMW 被永遠亭邀請去喝赏月酒了
		ENDIF
	;収穫祭
	ELSEIF DAY:2 == 3 && DAY:3 >= 16 && DAY:3 < 27
		;宴会種別
		FLAG:宴会開催フラグ = 4
		;宴会規模
		FLAG:31 = 2000 + RAND:3000
		;会場
		FLAG:32 = 202
		;開始時間
		FLAG:33 = 540
		;開始日
		FLAG:34 = 27 - DAY:3
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 0
		PRINTL  
		PRINTFORMW 今年庆祝収穫的季節也到来了
	; 蕾米莉亜の気紛れなパーティ　メイン主催者：蕾米莉亜　自宅が紅魔館　土曜日に５０％で開催予告、実施日は日曜日
	ELSEIF CFLAG:16:出禁 == 0 && DAY%7 == 6 && MAIN_MAP == 3 && RAND:50 <= 49
		;宴会名
		LOCALS = 蕾米莉亜一时兴起的派対
		;宴会種別
		FLAG:宴会開催フラグ = 6
		;宴会規模
		FLAG:31 = 1500 + RAND:2500
		;宴会会場
		FLAG:32 = 324
		;開始時間
		FLAG:33 = 1140
		;開始日
		FLAG:34 = 1
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		PRINTFORMW %CALLNAME:16%似乎想召開派対的样子
	;SAMPLE 霊夢の気紛れ酒盛り　メイン主催者：霊夢　土曜日に５０％で開催予告、実施日は日曜日
	ELSEIF CFLAG:1:出禁 == 0 && DAY%7 == 6 && RAND:100 <= 49
		;宴会名
		LOCALS = 霊夢一时兴起的酒宴
		;宴会種別
		FLAG:宴会開催フラグ = 1
		;宴会規模
		FLAG:31 = 1000 + RAND:1500
		;宴会会場
		IF RAND:5 <= 3
			FLAG:32 = 11
		ELSEIF RAND:4 == 0
			FLAG:32 = 2
		ELSEIF RAND:4 == 0
			FLAG:32 = 20
		ELSEIF RAND:4 == 0
			FLAG:32 = 21
		ELSE
			FLAG:32 = 230
		ENDIF
		;開始時間
		FLAG:33 = 1360 - (FLAG:31 / 10)
		;開始日
		FLAG:34 = 1
		;現在の参加人数をリセット
		FLAG:35 = 0
		;雨天中止
		FLAG:36 = 1
		PRINTL  
		PRINTFORMW %CALLNAME:1%似乎想召開宴会的样子
;	ELSEIF DAY:3 >= 8 && DAY:3 <= 10
;		;宴会名
;		LOCALS = 彻夜诵経大会
;		;宴会種別
;		FLAG:宴会開催フラグ = 5
;		;宴会規模
;		FLAG:31 = 2000 + RAND:30 * 40
;		;宴会会場
;		FLAG:32 = 104
;		;開始時間
;		FLAG:33 = 1440 + 240 - (FLAG:31 /10) ;朝４時まで宴会する
;		;開始日
;		FLAG:34 = 10 - DAY:3
;		;現在の参加人数をリセット
;		FLAG:35 = 0
;		;雨天中止
;		FLAG:36 = 0
;		PRINTL  
;		PRINTFORMW 夜通し読経ライブの時期が近づいてきた
	ENDIF
ENDIF
SIF FLAG:宴会開催フラグ
	CALL 宴会概要
PRINTL  

@ENKAI_ENTRY
;---   宴会の参加者決定   -----------------------------------------------------------------------------
;CFLAG:宴会参加 0=不参加 1=参加 2=主催者 3=脱落者 4=飲まないけど参加
;------------------------------------------------------------------------------------------------------
;フラグ初期化
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:宴会参加 = 0
NEXT
SELECTCASE FLAG:宴会開催フラグ
	;2 幻想郷歳忘れ会
	CASE 2
		FOR LOCAL,1,CHARANUM
			;参加者　霊夢
			IF LOCAL == 1
				CFLAG:LOCAL:宴会参加 = 2
			;るーこと　霊夢の手伝いに駆り出される
			ELSEIF LOCAL == 2
				CFLAG:LOCAL:宴会参加 = 2
			;魔理沙　遊びに来てるだけだが手伝わされる
			ELSEIF LOCAL == 11
				CFLAG:LOCAL:宴会参加 = 2
			;萃香　まあ基本的に来るよね
			ELSEIF LOCAL == 10
				CFLAG:LOCAL:宴会参加 = 1
			;正邪は参加しない
			ELSEIF LOCAL == 97
				CFLAG:LOCAL:宴会参加 = 0
			;他のキャラはランダム参加
			ELSEIF RAND:5 == 0
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
		NEXT
	;1 霊夢の気紛れ酒盛り
	CASE 1
		SELECTCASE FLAG:宴会差し入れ
			CASE 51
				FLAG:31 += RAND:1000
			CASE 52
				FLAG:31 += RAND:2000
		ENDSELECT
		FOR LOCAL,1,CHARANUM
			;参加者　霊夢(主催者)
			IF LOCAL == 1
				CFLAG:LOCAL:宴会参加 = 2
			;るーこと　霊夢の手伝いに駆り出される
			ELSEIF LOCAL == 2
				CFLAG:LOCAL:宴会参加 = 2
			;魔理沙　遊びに来てるだけだが手伝わされる
			ELSEIF LOCAL == 11
				CFLAG:LOCAL:宴会参加 = 2
			;萃香　まあ基本的に来るよね
			ELSEIF LOCAL == 10
				CFLAG:LOCAL:宴会参加 = 1
			;正邪は参加しない
			ELSEIF LOCAL == 97
				CFLAG:LOCAL:宴会参加 = 0
			;他のキャラはランダム参加
			ELSEIF RAND:14 == 0
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
			;宴会規模が一定以上なら参加確率２倍に
			IF FLAG:31 >= 2500 && RAND:14 == 0 && CFLAG:LOCAL:宴会参加 == 0
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
		NEXT
	;月見酒
	CASE 3
		FOR LOCAL,1,CHARANUM
			;参加者　
			SELECTCASE LOCAL
				;ぐや　主催者だけど働かない、姫だから 
				CASE 62
					CFLAG:LOCAL:宴会参加 = 1
				;えーりん烏冬面帝は働く　梅蒂欣も手伝う
				CASE 52,53,72,75
					CFLAG:LOCAL:宴会参加 = 2
				CASE 97
				CFLAG:LOCAL:宴会参加 = 0
				;他のキャラはランダム参加だが、竹林方面に住んでると確率が上がる
				CASEELSE
					IF CFLAG:LOCAL:自宅位置 / 100 == 4 && !RAND:3
						CFLAG:LOCAL:宴会参加 = 1
					ELSEIF !RAND:11
						CFLAG:LOCAL:宴会参加 = 1
					ENDIF
			ENDSELECT
		NEXT
	;収穫祭
	CASE 4
		FOR LOCAL,1,CHARANUM
			;参加者　
			SELECTCASE LOCAL
				;秋姉妹は必ず来る
				CASE 77,78
					CFLAG:LOCAL:宴会参加 = 1
				CASE 97
				CFLAG:LOCAL:宴会参加 = 0
				;人里在住のキャラは働く
				CASEELSE
					IF CFLAG:LOCAL:自宅位置 / 100 == 2
						CFLAG:LOCAL:宴会参加 = 2
					ELSEIF !RAND:12
						CFLAG:LOCAL:宴会参加 = 1
					ENDIF
			ENDSELECT
		NEXT
	;読経ライブ
;	CASE 5
;		FOR LOCAL,1,CHARANUM
;			;参加者　
;			SELECTCASE LOCAL
;				;命蓮寺メンバーは働く
;				CASE 39,40,41,55,85,86,87,88
;					CFLAG:LOCAL:宴会参加 = 2
;				;博麗神社組はこない
;				CASE 1 TO 11
;					CFLAG:LOCAL:宴会参加 = 0
;				;正邪もこない
;				CASE 97
;					CFLAG:LOCAL:宴会参加 = 0
;				;他は酒を飲まない
;				CASEELSE
;					SIF RAND:15 == 1
;						CFLAG:LOCAL:宴会参加 = 9
;			ENDSELECT
;		NEXT
	;6 蕾米莉亜の気紛れなパーティ
	CASE 6
		FOR LOCAL,1,CHARANUM
			;参加者　
			SELECTCASE LOCAL
				;蕾米莉亜、フランは働かない
				CASE 16,50
					CFLAG:LOCAL:宴会参加 = 1
				;咲夜、美鈴、小悪魔は働く
				CASE 15,58,59
					CFLAG:LOCAL:宴会参加 = 2
				;琶秋莉と正邪は参加しない
				CASE 54,97
				CFLAG:LOCAL:宴会参加 = 0
				;他のキャラはランダム参加だが、霧之湖方面に住んでると確率が上がる
				CASEELSE
					IF CFLAG:LOCAL:自宅位置 / 100 == 3 && !RAND:3
						CFLAG:LOCAL:宴会参加 = 1
					ELSEIF !RAND:11
						CFLAG:LOCAL:宴会参加 = 1
					ENDIF

			ENDSELECT
			;宴会規模が一定以上なら参加確率２倍に
			IF FLAG:31 >= 2500 && RAND:14 == 0 && CFLAG:LOCAL:宴会参加 == 0
				CFLAG:LOCAL:宴会参加 = 1
			ENDIF
		NEXT
ENDSELECT

@ENKAI_WORK(ARG)
;宴会の行動内容を入力する
;NOSAMES:すべての引数が異なると1, 1つでも一致していると0を返す
IF FLAG:宴会開催フラグ > 0 && (NOSAMES(CFLAG:ARG:職種, 48, 49, 50, 51) || CFLAG:ARG:行動 == 0)
	;宴会の時間になったら準備仕事の量に応じて主催者が動き始める
	IF TIME >= FLAG:開始時間 - (FLAG:宴会規模 / 30) - 15 && TIME < FLAG:33 && CFLAG:ARG:宴会参加 == 2
		CALL ENKAI_PREPARE(ARG)
		RETURN 1
	;遊びに来るだけの奴は時間になったら遊びに来る
	ELSEIF DAY == FLAG:34 && TIME >= FLAG:33 && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	;とっくに始まってる場合
	ELSEIF FLAG:宴会開催フラグ >= 98 && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	ENDIF
ELSEIF FLAG:宴会開催フラグ > 0
	RETURN 1
ENDIF
RETURN 0


@ENKAI_PREPARE(ARG)
;主催者の準備仕事
IF CFLAG:ARG:職種 != 宴会準備
	MAXBASE:ARG:工作量 = FLAG:31 / 3
	;最大値、2400
	SIF MAXBASE:ARG:工作量 >= 2400
		MAXBASE:ARG:工作量 = 2400
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	CFLAG:ARG:職種 = 宴会準備
	CFLAG:ARG:職場 = FLAG:32
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行動 = 5
ENDIF
RETURN 1

@ENKAI_START(ARG)
;それぞれが飲むペースを変えるので、宴の規模に応じて工作量が変化
IF CFLAG:ARG:職種 != ENKAI_SANKA && TCVAR:ARG:泥酔 == 0
	MAXBASE:ARG:工作量 = FLAG:31
	;最大値、通常7200(半日)・酒呑み神なら制限なし
	SIF MAXBASE:ARG:工作量 >= 7200 && TALENT:ARG:酒耐性 != 3
		MAXBASE:ARG:工作量 = 7200
	MAXBASE:ARG:工作量 += RAND:500
	IF TALENT:ARG:酒耐性 == -1
		TIMES MAXBASE:ARG:工作量, 0.75
	ELSEIF TALENT:ARG:酒耐性 == 1
		TIMES MAXBASE:ARG:工作量, 1.25
	ELSEIF TALENT:ARG:酒耐性 == 2
		TIMES MAXBASE:ARG:工作量, 1.5
	ELSEIF TALENT:ARG:酒耐性 == 3
		TIMES MAXBASE:ARG:工作量, 2.0
	ENDIF
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	;仕事内容・宴会に参加
	CFLAG:ARG:職種 = ENKAI_SANKA
	CFLAG:ARG:職場 = FLAG:32
	CFLAG:ARG:現在位置 = FLAG:32
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行動 = 5
	IF CFLAG:ARG:宴会参加 == 2
		;主催者は片付け予定をセット(工作量は最大1200)
		CFLAG:ARG:片付け工作量 = MIN(FLAG:宴会規模 / 6, 1200)
		CFLAG:ARG:片付け場所 = FLAG:宴会会場
	ENDIF
ENDIF

RETURN 1


@ENKAI_KATAZUKE(ARG)
;主催者の片づけ仕事
MAXBASE:ARG:工作量 = CFLAG:ARG:片付け工作量
BASE:ARG:工作量 = MAXBASE:ARG:工作量
;仕事内容・宴会の片づけ
CFLAG:ARG:職種 = 50
CFLAG:ARG:職場 = CFLAG:ARG:片付け場所
TCVAR:ARG:手伝えない = 0
CFLAG:ARG:行動 = 5
RETURN 1


@ENKAI_DRUNK(ARG)
;酔いつぶれるラインを超えていたら戻る
IF BASE:ARG:酒気 > (MAXBASE:ARG:酒気 / 10) * 8
	RETURN 1
ENDIF

;酒を飲まないなら酔わない 
IF TALENT:ARG:酒耐性 == -2 || CFLAG:ARG:妊娠自覚状態 == 1
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:宴会参加 = 4
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG%好像过得十分開心
	ENDIF
ELSE
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:宴会参加 = 3
;	PRINTFORMW %CALLNAME:ARG%はほろ酔い気分のようです
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
		PRINTFORMW %CALLNAME:ARG%好像过得十分開心
	ENDIF
ENDIF
RETURN 1


@ENKAI_STOP
;悪天候などによる中止
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:職種 = 0
	MAXBASE:LOCAL:工作量 = 0
	BASE:LOCAL:工作量 = 0
	CFLAG:LOCAL:宴会参加 = 0
	CFLAG:LOCAL:行動 = 0
NEXT
FLAG:宴会開催フラグ = 0
FLAG:31 = 0
FLAG:32 = 0
FLAG:33 = 0
FLAG:34 = 0
FLAG:35 = 0
FLAG:36 = 0
FLAG:宴会差し入れ = 0
RETURN 0

@宴会予定表示
VARSET LOCALS
SELECTCASE FLAG:宴会開催フラグ
	CASE 1
		LOCALS = 「霊夢一时兴起的酒宴」
	CASE 2
		LOCALS = 「幻想郷年会」
	CASE 3
		LOCALS = 「赏月酒」
	CASE 4
		LOCALS = 「収穫祭」
	CASE 5
		LOCALS = 「彻夜诵経大会」
	CASE 6
		LOCALS = 蕾米莉亜一时兴起的派対
ENDSELECT
IF IN_HOME(FLAG:32)
	PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}開始在%NAME_FROM_PLACE(FLAG:32)%会召開%LOCALS%
ELSE
	PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}開始在%STR:(5000+(GET_JOBPLACE("宴会")/100))%的%STR:(6000+(GET_JOBPLACE("宴会")/10))%会召開%LOCALS%
ENDIF

@宴会概要
SELECTCASE FLAG:宴会開催フラグ
	CASE 1
		LOCALS = 霊夢一时兴起的酒宴
	CASE 2
		LOCALS = 幻想郷年会
	CASE 3
		LOCALS = 赏月酒
	CASE 4
		LOCALS = 収穫祭
	CASE 5
		LOCALS = 彻夜诵経大会
	CASE 6
		LOCALS = 蕾米莉亜一时兴起的派対
ENDSELECT
IF FLAG:34 == 1
	PRINT 明日、
ELSE
	PRINTFORM {FLAG:34}日後、
ENDIF
IF IN_HOME(FLAG:32)
	PRINTFORML {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}開始在%NAME_FROM_PLACE(FLAG:32)%、会召開「%LOCALS%」
ELSE
	PRINTFORML {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}開始在%STR:(5000+(GET_JOBPLACE("宴会")/100))%的%STR:(6000+(GET_JOBPLACE("宴会")/10))%、会召開「%LOCALS%」
ENDIF
PRINT 这样的话
IF FLAG:31 >= 5000
	PRINTW ……看起来这将是持久戦啊
ELSEIF FLAG:31 >= 2500
	PRINTW 、这辺也得充分准备呢
ELSEIF FLAG:31 >= 1500
	PRINTW 、正好可以用来联络感情
ELSE
	PRINTW 、応该将会是个不错的聚会
ENDIF
IF FLAG:34 == 1
	PRINTFORMW 时间上有一点仓促呢、要不要参加呢……
ELSE
	PRINTFORMW 要参加就得把预定空出来呢……
ENDIF
SIF FLAG:36 > 0
	PRINTFORMW 也説不准、碰上雨天就中止了……
PRINTL  
FLAG:34 += DAY


@宴会参加人数再計算
;宴会の参加人数をセット
FLAG:参加人数 = 0
FOR LOCAL,0,CHARANUM
	IF (CFLAG:LOCAL:宴会参加 == 1 || CFLAG:LOCAL:宴会参加 == 2) && !TCVAR:LOCAL:泥酔
		SIF CFLAG:LOCAL:現在位置 == FLAG:宴会会場
			FLAG:参加人数 ++
	ENDIF
NEXT
