;守矢神社謹製一番くじ、ついに販売開始です！
;
;ルール
;1.くじは全部で300枚、引ききるか特賞を引くと箱が入れ替わる
;2.最初に引くとき、もしくは箱の入れ替え時にどの順番で何が出るかは確定している（シャッフル配列を専用のくじ配列にコピー）
;3.くじの結果を確認したら引いたくじを廃棄して前詰めする
;4.特賞は1本、引いたらSETBIT FLAG:999, 3 で箱交換フラグを立てる
;特賞の薬がどちらなのかはFLAG:999の第４ビットで判定（判定後INVERTBITでフラグを入れ替える）
;ユーザー変数がセーブ出来ないのでSAVESTRを一つ拝借してそこに結果を文字列で残しておく
;(16/07/02)仕様変更によりユーザー変数のセーブが可能になったのでSAVESTRの利用を停止
@FOTUNE_MORIYA
#DIM RANK_FORTUNE
#DIM お相手
#DIM SKIP_FLAG
#DIMS くじ枚数,300

IF MATCH(MORIYA_BOX, 0) >= 2 && STRLENS(SAVESTR:9)
	SPLIT SAVESTR:9, ",", くじ枚数
	FOR LOCAL:0, 0, 300
		MORIYA_BOX:(LOCAL:0) = TOINT(くじ枚数:(LOCAL:0))
	NEXT
	SAVESTR:9 = ""
ENDIF

;兎符「開運大紋」
WHILE TFLAG:194 && MORIYA_BOX:0 >= 60
	SETCOLOR COLOR ("YELLOW")
	PRINTFORMW 兎符「開運大紋」発動！
	RESETCOLOR
	ARRAYSHIFT MORIYA_BOX, -1, -1
	TFLAG:194 --
WEND

;博麗神簽による幸運補正、結果が悪ければそれを消滅して引き直す
DO
	SKIP_FLAG = 0
	SELECTCASE TFLAG:幸運補正
		CASE IS <= 0
		;たわし回避
		CASE 1 TO 2
			SIF MORIYA_BOX:0 >= 130
					SKIP_FLAG = 1
		;７等までを回避
		CASE 3 TO 4
			SIF MORIYA_BOX:0 >= 60
					SKIP_FLAG = 1
		;５等までを回避
		CASE 5
			SIF MORIYA_BOX:0 >= 25
					SKIP_FLAG = 1
		CASEELSE
	ENDSELECT
	IF SKIP_FLAG
		ARRAYSHIFT MORIYA_BOX, -1, -1
		PRINTFORML 比起博麗神社那抽的神簽、也需可以抽出相当好的東西
		TFLAG:幸運補正 --
	ENDIF

LOOP SKIP_FLAG

;くじの結果は0～299。これを各賞に振り分けておく
;内訳 特賞:1本, １等:3本, ２等:5本, ３等:6本, ４等:10本, ５等:15本, ６等:20本, ７等:30本, ８等:40本, ９等(たわし):70本, 10等(スカ):100本
;RANK_FORTUNEをMORIYA_BOX:0へ依存させないように(やろうと思えば既存のものを弄らず下に追加できるよ。RANK_FORTUNEが混乱するからやらないと思うけど)
SELECTCASE MORIYA_BOX:0
;特賞：奇数回目であれば体力、偶数回であれば気力が上がる丸薬（最大値＋50）
CASE 0
	SETBIT FLAG:999, 3
	RANK_FORTUNE = 0
;１等：守矢神社の２柱＋風祝とのデートチケット。各１枚
CASE 1 TO 3
	お相手 = MORIYA_BOX:0 + 30
	RANK_FORTUNE = 1
;２等：東風谷早苗様による幸せの奇跡（全員心情愉快＋自分のステータス回復）
CASE 4 TO 8
	RANK_FORTUNE = 2
;３等：MORIYAブートキャンプ招待券、わずか1時間で見違えるような効果が！
CASE 9 TO 14
	RANK_FORTUNE = 3
;４等：楽しく呑もう、幻想郷呑み比べセット
CASE 15 TO 24
	RANK_FORTUNE = 4
;５等：健康に興味ない主人公に、元気ハツラツぅ～？セット
CASE 25 TO 39
	RANK_FORTUNE = 5
;６等：これであの子とイイ仲に！？恋のハプニングセット
CASE 40 TO 59
	RANK_FORTUNE = 6
;７等：実りの喜びを主人公に、野菜苗セット（野菜ができるとは言っていない）
CASE 60 TO 89
	RANK_FORTUNE = 7
;８等：一度食べると止まらない、義理チョコの王様のエントリーだ！
CASE 90 TO 129
	RANK_FORTUNE = 8
;残念…９等：たわし、10個集めると金のたわしと交換です！
CASE 130 TO 199
	RANK_FORTUNE = 9
;スカ：ハズレくじ、5枚集めるともう一度引けます
CASE 200 TO 299
	RANK_FORTUNE = 10
CASEELSE
	RANK_FORTUNE = 99
ENDSELECT
;引いたくじは廃棄処分して前詰め、空いたところは-1で詰める
ARRAYSHIFT MORIYA_BOX, -1, -1
;SAVESTR:9 = {MORIYA_BOX:0}
;FOR LOCAL:0, 1, 300
;	SAVESTR:9 = %SAVESTR:9%,{MORIYA_BOX:(LOCAL:0)}
;NEXT

PRINTDATAW
	DATAFORM 「那么、結果是…」
	DATAFORM 「唔唔、这个是……」
	DATAFORM 「啊ー…啊ー？啊ー、啊ー！」
	DATAFORM 「那個、這是…」
	DATAFORM 「这就是結果吗……」
	DATAFORM 「会是什么呢、会是什么呢？」
	DATAFORM 「哎呀呀、抽到这个了吗」
	DATAFORM 「那个、这还真是…」
	DATAFORM 「奇怪的文字呢…啊、上下拿反了」
	DATAFORM 「呵～呵～、这还真是不得了…」
	DATAFORM 「沒想到会是這樣～」
	DATAFORM 「……？……！………！？」
ENDDATA
PRINTW 


;結果処理。地の文まで書き込んでいます
SELECTCASE RANK_FORTUNE
;特賞：奇数回目であれば体力、偶数回であれば気力が上がる丸薬（最大値＋50）
CASE 0
	PRINTW 虽然面有难色但还是急忙摆出了満脸笑容
	PRINTL 「恭喜恭喜ー！中了特等奨呢！」
	PRINTFORML 「請你收下作為特等奨的、\@GETBIT(FLAG:999, 4) ? 活力的丸薬 # 生命的秘粉 \@」
	PRINTL 「来、請放心饮用吧。啊、这里有水」
	PRINTW 一将接过的薬混着水一起喝下、便感到力量从体内不断地涌了上来……
	IF GETBIT(FLAG:999, 4)
		MAXBASE:MASTER:気力 += 50
	ELSE
		MAXBASE:MASTER:体力 += 50
	ENDIF
	PRINTFORML 当力量涌上来的感覚渐渐消失后、発覚自己的\@GETBIT(FLAG:999, 4) ? 気力 # 体力 \@上限稍微増加了
	INVERTBIT FLAG:999, 4
	
	PRINTFORMW 「哦対了、差点忘记説了」
	PRINTFORML 「因為这个箱子里已経開出了特等奨、所以希望你能允许我更换下一个签箱」
	PRINTFORML 「如果不想换新的箱子的话可以先提出来」
	PRINTFORMW 「不过、請注意换了新箱子后刚才的箱子就会被廃弃掉了哦」
;１等：守矢神社の２柱＋風祝とのデートチケット。各１枚
CASE 1
	PRINTW 「这个是……」
	PRINTL 苦笑着从里面発出了声音
	SELECTCASE お相手
		CASE 31
			PRINTL 「恭喜你、得到了作為１等奨的与東風谷早苗大人的约会权！」
		CASE 32
			PRINTL 「恭喜你、得到了作為１等奨的与八坂神奈子大人的约会权！」
		CASE 33
			PRINTL 「恭喜你、得到了作為１等奨的与洩矢諏訪子大人的约会权！」
		CASEELSE
	ENDSELECT
	PRINTW 「那么、請立即行使这一権利尽情享受吧♪」
	IF FLAG:デート相手
		CFLAG:(FLAG:デート相手):デート中 = MAIN_MAP
		CFLAG:(FLAG:デート相手):同行 = 0
		;連続で引き当てた豪運の兵のために…
		CALL 守矢神簽デート相手のリセット()
	ENDIF
	;お相手のキャラの同行(MASTERも)，現在位置，デート中のCFLAGをセット
	CFLAG:MASTER:同行 = 60
	CFLAG:お相手:同行 = 60
	CFLAG:お相手:現在位置 = CFLAG:MASTER:現在位置
	CFLAG:お相手:デート中 = CFLAG:MASTER:現在位置 / 100
	FLAG:デート相手 = お相手
	
	;守矢神簽デート相手にデートの相手を格納
	FLAG:守矢神簽デート相手 = FLAG:デート相手
	CFLAG:(FLAG:デート相手):来訪時間 = 0
	CFLAG:(FLAG:デート相手):帰宅時間 = 0
	CFLAG:(FLAG:デート相手):就寝時間 = 0
	CFLAG:(FLAG:デート相手):起床時間 = 0
	SIF !CFLAG:(FLAG:守矢神簽デート相手):面識
		CALL KOJO_MESSAGE_SEND("ENCOUNTER",MASTER,FLAG:守矢神簽デート相手,1,0)
;２等：幸せの奇跡が幻想郷を包みます（全員心情愉快＋自分のステータス回復）
CASE 2
	PRINTL 「恭喜你、２等奨是東風谷早苗大人所引発的幸福之奇跡的表演」
	PRINTW 「一直以来总是傲的要命的女孩子或许今天会変的娇起来哦？」
	FOR LOCAL:0, 1, CHARANUM
		TALENT:(LOCAL:0):心情 = 1
		BASE:(LOCAL:0):憤怒 = 0
	NEXT
	PRINTL 感覚情绪変得格外的平稳
	PRINTL 「啊、虽然変得娇了不过要是做了过分的事情还是会被惹怒的、請注意哦」
	PRINTL 奇跡看来也不是万能的呢
	PRINTW 另外感覚自己的身体也変軽了呢
	BASE:MASTER:体力 = MAXBASE:MASTER:体力
	BASE:MASTER:気力 = MAXBASE:MASTER:気力
	BASE:MASTER:精力 = MAXBASE:MASTER:精力
	BASE:MASTER:TSP  = MAXBASE:MASTER:TSP
	BASE:MASTER:酒気 = 0

;３等：MORIYAブートキャンプ招待券、わずか1時間で見違えるような効果が！
CASE 3
	PRINTL 「恭喜获得３等奨？」
	PRINTL 「３等奨是守矢新人训练営的優待券哦」
	PRINTW 「刚好使用期限就在今天那么就从现在開始吧」
	PRINTL 不了、接下来还有别的预定……
	PRINTW 「你可没有拒絶的权力哦。来来、請往这辺……」
	PRINTL ………
	PRINTL ……
	PRINTL …
	;会話経験、戦闘経験、学習経験のうち最も低いものを底上げする
	;なお講師は会話／戦闘／学習で早苗／神奈子／諏訪子が担当
	;……ケロちゃんのほうが戦闘高くて神奈子と教養が同じのは秘密だよ
	;あ、ついでに講師にも経験を追加。教えるのもいい経験だし
	IF EXP:MASTER:会話経験 < EXP:MASTER:戦闘経験 &&  EXP:MASTER:会話経験 < EXP:MASTER:学習経験
		PRINTW 通过早苗的演讲講座掌握了会話的诀窍！
		EXP:MASTER:会話経験 += 10
		EXP:31:会話経験 += 5
	ELSEIF EXP:MASTER:戦闘経験 < EXP:MASTER:学習経験
		PRINTL 通过神奈子的直接指導対戦闘技術略窥门径了！
		PRINTW 另外訓練的疲労也被治愈了
		EXP:MASTER:戦闘経験 += 10
		EXP:32:戦闘経験 += 5
	ELSE
		PRINTW 諏訪子厳谨的填鸭教育以后一定会派的上用场吧
		EXP:MASTER:学習経験 += 10
		EXP:33:学習経験 += 5
	ENDIF
	;あれだけ過酷な訓練でもわずか一時間、MORIYAブートキャンプ、恐るべし……
	TIME += 60

;４等：楽しく呑もう、幻想郷呑み比べセット
CASE 4
	PRINTL 「中４等奨了呢」
	PRINTL 「４等的奨品是幻想郷拼酒礼包」
	PRINTL 「為你准备了神奈子大人秘蔵的甜酒浊酒和清酒、还有鬼殺酒哦」
	PRINTW 「請仔细品尝吧！」
	ITEM:甜酒 = MIN(ITEM:甜酒 + 1, 99)
	ITEM:濁酒 = MIN(ITEM:濁酒 + 1, 99)
	ITEM:清酒 = MIN(ITEM:清酒 + 1, 99)
	ITEM:鬼殺酒 = MIN(ITEM:鬼殺酒 + 1, 99)
	PRINTW 「啊、拿不下的份可得还给神奈子大人、請放在这里吧」

;５等：健康に興味ない主人公に、元気ハツラツぅ～？セット
CASE 5
	PRINTL 「恭喜、是５等奨哦」
	PRINTL 「為体力不足而烦悩的你、想変得精力充沛吗～？特為你送上这一礼包」
	PRINTW 「精疲力竭的时候請用这个来熬过去吧」
	PRINTL 里面是精力剤、大回复薬、薬水、姜黄饮料5份
	PRINTL 还有青汁顆粒10杯份（守矢的美味水2L瓶装）、运動饮料10份
	PRINTW 虽然光是拿着这些走路就已経挺累的説……
	ITEM:精力剤 = MIN(ITEM:精力剤 + 1, 99)
	ITEM:大回復薬 = MIN(ITEM:大回復薬 + 1, 99)
	ITEM:薬水 = MIN(ITEM:薬水 + 1, 99)
	ITEM:姜黄飲料 = MIN(ITEM:姜黄飲料 + 5, 99)
	ITEM:青汁 = MIN(ITEM:青汁 + 10, 99)
	ITEM:運動飲料 = MIN(ITEM:運動飲料 + 10, 99)
	PRINTW 「啊、拿不下的份还請做為対守矢神社的赠品来帰还」

;６等：これであの子とイイ仲に！？恋のハプニングセット
CASE 6
	PRINTL 「是６等奨呢」
	PRINTL 「奨品是…恋之惊喜礼包、呢」
	PRINTW 红着脸送出了奨品。
	ITEM:潤滑液 = MIN(ITEM:潤滑液 + 1, 99)
	ITEM:媚薬 = MIN(ITEM:媚薬 + 1, 99)
	ITEM:催情薬 = MIN(ITEM:催情薬 + 1, 99)
	ITEM:避孕套 = MIN(ITEM:避孕套 + 1, 99)
	ITEM:口服避孕薬 = MIN(ITEM:口服避孕薬 + 1, 99)

;７等：実りの喜びを主人公に、野菜苗セット（野菜ができるとは言っていない）
CASE 7
	PRINTL 「是７等奨」
	PRINTL 「奨品是蔬菜苗礼包」
	PRINTL 「請努力地以豊收為目标吧」
	PRINTW 「順便一提、我们守矢神社也受理进行豊收祈願的業务哦」
	ITEM:捲心菜苗 = MIN(ITEM:捲心菜苗 + 1, 99)
	ITEM:草莓苗 = MIN(ITEM:草莓苗 + 1, 99)
	ITEM:芜菁苗 = MIN(ITEM:芜菁苗 + 1, 99)
	ITEM:黄瓜苗 = MIN(ITEM:黄瓜苗 + 1, 99)
	ITEM:番茄苗 = MIN(ITEM:番茄苗 + 1, 99)
	ITEM:西瓜苗 = MIN(ITEM:西瓜苗 + 1, 99)
	ITEM:茄子苗 = MIN(ITEM:茄子苗 + 1, 99)
	ITEM:紅薯苗 = MIN(ITEM:紅薯苗 + 1, 99)
	ITEM:百合苗 = MIN(ITEM:百合苗 + 1, 99)
	ITEM:胡蘿卜苗 = MIN(ITEM:胡蘿卜苗 + 1, 99)
	ITEM:葱苗 = MIN(ITEM:葱苗 + 1, 99)
	ITEM:跳蛋苗 = MIN(ITEM:跳蛋苗 + 1, 99)
	
	PRINTW 「啊、拿不下的份还請做為対守矢神社的赠品来帰还哦」

;８等：一度食べると止まらない、義理チョコの王様のエントリーだ！
CASE 8
	PRINTL 「是８等奨」
	PRINTL 「奨品是吃过一次就停不下来的、作為義理巧克力代名詞的标识性品牌哦！」
	PRINTL 「总而言之請收下吧」
	PRINTW 听完后得到了一个巧克力点心盒
	ITEM:黑雷神巧克力 = MIN(ITEM:黑雷神巧克力 + 20, 99)

;残念…９等：たわし、10個集めると金のたわしと交換です！
CASE 9
	PRINTL 「这个是特別奨呢」
	PRINTL 「特別奨就是刷帚」
	PRINTL 「这个刷帚、并不是普通的刷帚哦」
	PRINTW 「收集10個前来奉納的话、就能从我们这换到一个金刷帚哦」
	FLAG:999 += 1 << 5
	
	IF (FLAG:999 >> 5 & 15) == 10
		PRINTL 「恭喜你、因為收集了10個刷帚所以能够交换金刷帚了哦」
		PRINTL 得到了金刷帚！
		PRINTFORMW 感覚\@GETBIT(FLAG:999, 9)? 掃除 # 料理 \@稍微変好了一点
		IF GETBIT(FLAG:999, 9)
			EXP:MASTER:清掃経験 += 1
		ELSE
			EXP:MASTER:料理経験 += 1
		ENDIF
		;金のたわしの効果を変換、たわし収集個数をリセット
		;15(0b1111)を5ビットシフトし、ビット反転したもの(0b000011111)をFLAG:999とビットごとの論理和させています、多分
		INVERTBIT FLAG:999, 9
		FLAG:999 &= ~(15 << 5)
	ELSE
		PRINTFORMW 「現在共收集了{(FLAG:999 >> 5) & 15}個刷帚、請以金刷帚為目标而努力吧」
	ENDIF
;スカ：守矢之守札（はずれくじ）、5枚でもう一回引けるドン
CASE 10
	PRINTW 満臉笑容把簽递了过来
	PRINTW ……
	PRINTW …
	PRINTL ┌─────────────┐
	PRINTL ｜　　　　　　　　　　　　　｜
	PRINTL ｜　　守矢之守札　　　　　　｜
	PRINTL ｜　　　　　　　　　　　　　｜
	PRINTL ｜　　    積攢五枚　　　　　｜
	PRINTL ｜　　　      額外贈送一回　｜
	PRINTL ｜　　　　　　　　　　　　　｜
	PRINTL ｜　            不保證利益　｜
	PRINTL ｜　　　　　　　　　　　　　｜
	PRINTL ｜　　　　　　　　守矢神社　｜
	PRINTL └─────────────┘
	PRINTW 雖然説得拐彎抹角、但這不就是落空了麼？
	PRINTFORMW 「下次一定会抽到更好的、加油%UNICODE(0x2665)%加油%UNICODE(0x2665)%」
	ITEM:131 ++

CASEELSE
	PRINTFORML 「哎呀、好奇怪呢」
	PRINTFORML 看着神签的内容歪了歪腦袋
	PRINTFORMW 「明明神签上不応该写着这些东西啊」
	PRINTFORML 「真没办法、这次就当没抽过把代金退给你吧」
	PRINTFORMW 好像是把无関的神签给混进去了呢
	MONEY += 3000
ENDSELECT
RETURN 0

@守矢神簽デート相手のリセット()
;守矢神簽のデートのときに該当キャラの起床時間etcを元に戻す
IF FLAG:守矢神簽デート相手
	CSVCFLAG (FLAG:守矢神簽デート相手),352
	CFLAG:(FLAG:守矢神簽デート相手):来訪時間 = RESULT
	CSVCFLAG (FLAG:守矢神簽デート相手),353
	CFLAG:(FLAG:守矢神簽デート相手):帰宅時間 = RESULT
	CSVCFLAG (FLAG:守矢神簽デート相手),354
	CFLAG:(FLAG:守矢神簽デート相手):就寝時間 = RESULT
	CSVCFLAG (FLAG:守矢神簽デート相手),355
	CFLAG:(FLAG:守矢神簽デート相手):起床時間 = RESULT
	FLAG:守矢神簽デート相手 = 0
ENDIF
