@デート終了タイムアップ処理
VARSET LOCAL
;気力ゼロ,衰弱時デート中断
FOR LOCAL,0,CHARANUM
	SIF CHK_DATENOW(CFLAG:LOCAL:デート中) && (CFLAG:LOCAL:衰弱 || (BASE:LOCAL:気力 == 0 && CFLAG:LOCAL:現在位置 != 情人用宿屋 && CFLAG:LOCAL:現在位置 != 情人旅館 && CFLAG:LOCAL:現在位置 != OMANEKIBEYA()))
		LOCAL:1 ++
	SIF CHK_DATENOW(CFLAG:LOCAL:デート中) && CFLAG:LOCAL:衰弱
		CALL ENDUFUFU(LOCAL)
NEXT
SIF NEMUKE() >= 720 || BASE:MASTER:酒気 >= MAXBASE:MASTER:酒気
	LOCAL:2 ++
;お招きしていて連泊でなければ泊めて貰える
IF CFLAG:MASTER:お招き && (LOCAL:1 || LOCAL:2) && !FLAG:70
	IF LOCAL:1
		PRINT 気力快用完了
	ELSEIF LOCAL:2
		PRINT 差不多到了回去的時間了
	ENDIF
	SIF !TCVAR:(CFLAG:MASTER:お招き):連泊禁止
		PRINTFORMW 今天%CALLNAME:(CFLAG:MASTER:お招き)%提供了借宿的样子
	SIF TCVAR:(CFLAG:MASTER:お招き):連泊禁止
		PRINTFORMW 今天也住在%CALLNAME:(CFLAG:MASTER:お招き)%这吗？
	PRINTL [0]住下来 [1]回去
	WHILE 1
	INPUT
	SIF RESULT == 0 || RESULT == 1
		BREAK
	WEND
	IF !RESULT && !TCVAR:(CFLAG:MASTER:お招き):連泊禁止
		PRINTFORMW 恭敬不如从命那就住下来吧
		;デートは終了
		CFLAG:MASTER:デート中 = MAIN_MAP
		CFLAG:TARGET:デート中 = MAIN_MAP
		FLAG:デート相手 = 0
		TFLAG:デート前好感度 = 0
		CALL 守矢神簽デート相手のリセット()
		;以後の処理をカット
		GOTO OTOMARI
	ELSEIF !RESULT && TCVAR:(CFLAG:MASTER:お招き):連泊禁止
		PRINTFORMW 果然还是婉拒接待吧…
	ENDIF
ENDIF
IF LOCAL:1 && CFLAG:MASTER:現在位置
	PRINTL 
	PRINTW ***** 因為気力帰０了、踏上了帰途 *****
	CALL 帰宅の時間経過
	FLAG:70 = 0
	RESETBGCOLOR
ELSEIF LOCAL:2
	PRINTL
	IF BASE:MASTER:酒気 >= MAXBASE:MASTER:酒気
		PRINTW ***** 酩酊大酔、踉蹌着踏上了帰途 *****
		PRINTFORMW 回到了%GET_MAPNAME(MAIN_MAP,0)%的%CALLNAME:MASTER%直接当場倒下了
	ELSE
		PRINTW ***** 因為倦意袭来了、踏上了帰途 *****
	ENDIF
	CALL 帰宅の時間経過
ENDIF
IF LOCAL:1 || LOCAL:2
	FOR LOCAL,0,CHARANUM
		IF LOCAL && CFLAG:LOCAL:衰弱
			IF !CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初期位置 = SUKIMA()
				CFLAG:LOCAL:現在位置 = SUKIMA()
				CFLAG:LOCAL:拠点来訪 = 0
				CALL KOJO_MESSAGE_SEND("EVENT",20,LOCAL,2,0)
				PRINTFORMW 筋疲力尽的%CALLNAME:LOCAL%回去了
			ELSE
				PRINTFORM %CALLNAME:LOCAL%往%NAME_FROM_PLACE(CFLAG:LOCAL:初期位置)%
				PRINTW 返回
				SIF CFLAG:MASTER:初期位置 != CFLAG:LOCAL:初期位置
					LOCK:(CFLAG:LOCAL:初期位置) = 1
				CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
				CALL KOJO_MESSAGE_SEND("EVENT",3,1,LOCAL,0)
			ENDIF
		ENDIF
		CALL SET_DATE(99,LOCAL)
		IF CFLAG:LOCAL:お招き
			CFLAG:LOCAL:お招き = 0
			IF LOCAL == 0
			ELSEIF CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初期位置 = CFLAG:LOCAL:神社在住
			ELSE
				CFLAG:LOCAL:初期位置 = SUKIMA()
			ENDIF
		ENDIF
		SIF CFLAG:LOCAL:添い寝中 > 0
			CFLAG:LOCAL:添い寝中 = 0
		SIF CFLAG:LOCAL:うふふ
			CALL ENDUFUFU(LOCAL)
	NEXT
	CALL 守矢神簽デート相手のリセット()
ENDIF
IF ((CFLAG:MASTER:現在位置 % 100) % 10) == 0 && (CFLAG:MASTER:現在位置 % 100) == 0
	IF CFLAG:MASTER:現在位置 == 1100
		FOR LOCAL,0,CHARANUM
			SIF CHK_DATENOW(CFLAG:LOCAL:デート中)
				CFLAG:LOCAL:現在位置 -= 1100
		NEXT
	ENDIF
	SIF CHK_DATENOW(CFLAG:MASTER:デート中)
		PRINTFORMW 到达了%DATENAME_PLACE()%
	FOR LOCAL,0,CHARANUM
		SIF CHK_DATENOW(CFLAG:LOCAL:デート中)
			CFLAG:LOCAL:現在位置 += 10
	NEXT
ENDIF
$OTOMARI
