@EVENTTURNEND
#DIM 養育費
SIF TFLAG:妄想中
	DELCHARA (CHARANUM - 1)
;宴会当日に一日が終わると宴会フラグOFF
;IF DAY >= FLAG:34 && FLAG:宴会開催フラグ
IF TIME + 1440 * DAY >= FLAG:開始時間 + 1440 * FLAG:開始日 && FLAG:宴会開催フラグ
	;会場となった場所は荒れ果てる
	SIF YOGORE:(FLAG:32) < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:宴会開催フラグ = 0
	FLAG:31 = 0
	FLAG:32 = 0
	FLAG:33 = 0
	FLAG:34 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	FLAG:宴会差し入れ = 0
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:工作量 = 0
		BASE:LOCAL:工作量 = 0
		CFLAG:LOCAL:宴会参加 = 0
		CFLAG:LOCAL:行動 = 0
	NEXT
ENDIF
;霊廟フラグOFF
FLAG:17 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL,1,CHARANUM
	;恋慕取得
	IF 恋慕取得条件(LOCAL)
		;面霊気は特別あつかい
		IF LOCAL == 57
			PRINTFORMW %CALLNAME:LOCAL%在察覚了自己対%CALLNAME:MASTER%的思慕的同時、嘴角翘了起来
			PRINTFORMW 浮现出自然笑容的%CALLNAME:LOCAL%、好像拥有了表情的样子
			PRINTFORMW %CALLNAME:LOCAL%的\@ TALENT:LOCAL:思慕 ? [思慕] # [愛欲] \@変成了[恋慕]
			FLAG:10 ++
		;愛欲の有無で分岐
		ELSEIF TALENT:LOCAL:愛欲
			PRINTFORMW %CALLNAME:LOCAL%因為身体与心霊都被%CALLNAME:MASTER%填満而感到了喜悦…
			PRINTL 即便是从糜烂的関係中催生出的爱意、不也还是一种恋的形式么…
			PRINTFORMW %CALLNAME:LOCAL%的[愛欲]変成了[恋慕]
		ELSE
			PRINTFORML %CALLNAME:LOCAL%因為与%CALLNAME:MASTER%共同渡过的時光而感到很幸福的样子…
			PRINTFORMW %CALLNAME:LOCAL%的[思慕]変成了[恋慕]
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:時姦刻印 && !TALENT:LOCAL:愛欲
			CFLAG:LOCAL:清い交際 = 1
		TALENT:LOCAL:恋慕 = 1
		TALENT:LOCAL:思慕 = 0
		TALENT:LOCAL:愛欲 = 0
		TALENT:LOCAL:炮友 = 0
		CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,1,0)
	;思慕取得
	;炮友／愛人の取得で分岐
	ELSEIF 思慕取得条件(LOCAL)
		IF TALENT:LOCAL:炮友
			PRINTFORML %CALLNAME:LOCAL%対%CALLNAME:MASTER%産生了身体之外的兴趣
			PRINTFORMW %CALLNAME:LOCAL%获得了[思慕]
		ELSE
			PRINTFORML %CALLNAME:LOCAL%変得対%CALLNAME:MASTER%很在意的样子…
			PRINTFORMW %CALLNAME:LOCAL%获得了[思慕]
		ENDIF
		TALENT:LOCAL:思慕 = 1
		CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,2,0)
	;愛欲取得
	;奉仕快楽の分岐がまだないので、取得条件に併合
	ELSEIF 愛欲取得条件(LOCAL)
		;思慕持ち、心より身体を選んだ的な
		IF TALENT:LOCAL:思慕
			PRINTFORML %CALLNAME:LOCAL%与%CALLNAME:MASTER%不仅在心霊上也在肉体的羁绊上获得了満足的样子…
			PRINTFORML 比起肉眼看不见的羁绊更愿意為了看得见的证明而相拥在一起
			PRINTFORMW %CALLNAME:LOCAL%获得了[愛欲]
		ELSE
			PRINTFORML %CALLNAME:LOCAL%好像沈溺于対%CALLNAME:MASTER%的情欲之中了…
			PRINTFORMW %CALLNAME:LOCAL%获得了[愛欲]
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:愛欲 = 1
			TALENT:LOCAL:思慕 = 0
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,3,0)
	;炮友／愛人取得
	;炮友と愛人の違いは思慕の有無
	ELSEIF 炮友取得条件(LOCAL)
		;思慕取得済＋炮友未取得
		IF TALENT:LOCAL:思慕 && !TALENT:LOCAL:炮友
			PRINTFORML %CALLNAME:LOCAL%似乎開始覚得就算这样沈溺在与%CALLNAME:MASTER%的快楽之中也没有什么不好的了…
			PRINTFORMW %CALLNAME:LOCAL%成為了%CALLNAME:MASTER%的[愛人]
			TALENT:LOCAL:炮友 = 2
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,4,0)
		;思慕取得済＋炮友取得済
		ELSEIF TALENT:LOCAL:思慕 && TALENT:LOCAL:炮友 == 1
			PRINTFORML %CALLNAME:LOCAL%好像渇望着与%CALLNAME:MASTER%産生超越単纯的肉体関系之上的羁绊的样子…
			PRINTFORMW %CALLNAME:LOCAL%成為了[愛人]
			TALENT:LOCAL:炮友 = 2
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,4,0)
		;思慕未取得＋炮友未取得ならまず炮友取得
		ELSEIF !TALENT:LOCAL:炮友
			PRINTFORML %CALLNAME:LOCAL%沈溺在%CALLNAME:MASTER%給予的快感之中…
			PRINTFORMW %CALLNAME:LOCAL%成為了%CALLNAME:MASTER%的[炮友]
			TALENT:LOCAL:炮友 = 1
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,5,0)
		ENDIF
	ENDIF

	IF EXP:LOCAL:接吻経験 > 300 && ABL:LOCAL:Ｍ感覚 > 5 && !TALENT:LOCAL:接吻魔
		PRINTFORMW %CALLNAME:LOCAL%獲得了[接吻魔]
		TALENT:LOCAL:接吻魔 = 1
	ENDIF
	IF EXP:LOCAL:調教自慰経験 > 200 && EXP:LOCAL:自慰経験 > 500 && !TALENT:LOCAL:自慰狂
		PRINTFORMW %CALLNAME:LOCAL%獲得了[自慰狂]
		TALENT:LOCAL:自慰狂 = 1
	ENDIF
	IF EXP:LOCAL:子宮口開発経験 > 50 && !TALENT:LOCAL:子宮口性感
		PRINTFORML 被反復進攻着子宮口的%CALLNAME:LOCAL%、比起痛苦来能感覚到更加強烈的快感…
		PRINTFORMW %CALLNAME:LOCAL%獲得了[子宮口性感]
		TALENT:LOCAL:子宮口性感 = 1
	ENDIF
	IF EXP:LOCAL:演奏経験 >= 1000 && ABL:LOCAL:技巧 > 3 && !TALENT:LOCAL:音楽知識
		PRINTFORMW %CALLNAME:LOCAL%演奏楽器造詣精進了
		PRINTFORMW %CALLNAME:LOCAL%獲得了[楽器知識]
		;PRINTFORMW %CALLNAME:LOCAL%は音楽に造詣深くなった
		;PRINTFORMW %CALLNAME:LOCAL%は[音楽知識]を得た
		TALENT:LOCAL:音楽知識 = 1
	ELSEIF EXP:LOCAL:演奏経験 >=5000 && !TALENT:LOCAL:音感
		PRINTFORMW %CALLNAME:LOCAL%的音感得到了重複鍛煉
		PRINTFORMW %CALLNAME:LOCAL%獲得了[相対音感]
		TALENT:LOCAL:音感 = 1
	ENDIF
	IF CFLAG:LOCAL:心情不快
		CFLAG:LOCAL:心情不快 --
		IF !CFLAG:LOCAL:心情不快
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:心情不快 ++
		ENDIF
	ENDIF
	;EXキャラの恋慕時はメンバー追加イベントフラグを立てる
	IF LOCAL >= 11 && LOCAL <= CHARANUM && LOCAL != 57 && LOCAL != 71
		SIF TALENT:LOCAL:恋慕 && EXP:LOCAL:愛情経験 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:異常清掃経験 > (500 + 100 * FLAG:850) && ABL:MASTER:清掃技能 > 3 && !TALENT:MASTER:清掃中毒
	PRINTFORMW %CALLNAME:MASTER%注意到了可怕的事情
	PRINTFORMW 不管是睡着还是醒着、就连在停止时间时、与少女们肌肤相亲时、
	PRINTFORMW 都会情不自禁地去留意房间是否整理干浄了
	PRINTFORMW 而且%CALLNAME:MASTER%只要在掃除的時候心情就能平静下来
	PRINTFORMW %CALLNAME:MASTER%対於掃除的講究、已経到達了無可挽回的地步
	PRINTFORMW %CALLNAME:MASTER%獲得了[清掃中毒]
	TALENT:MASTER:清掃中毒 = 1
ENDIF
;MASTERを除く全員のプリセット処理
FOR LOCAL,1,CHARANUM
	;気力の回復
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;回復快、回復慢
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)
	
	;添い寝してたら回復速度向上
	SIF CFLAG:LOCAL:添い寝中
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:徹夜
		LOCAL:1 /= 2
	CFLAG:LOCAL:徹夜 = 0
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;南無三カード発行者が居たら発行フラグOFF
	CFLAG:LOCAL:328 = 0
NEXT
;全員のプリセット処理
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:精力 = MIN(MAXBASE:LOCAL:精力, BASE:LOCAL:精力 + MAXBASE:LOCAL:精力 * BASE:LOCAL:体力 / MAXBASE:LOCAL:体力)
	
	;隠密中のキャラが居たらフラグOFF
	CALL SET_SNEAK(0,LOCAL,1)
	;CFLAG:LOCAL:隠密中 = 0
	
	;イタズラ中のキャラが居たらフラグOFF
	CALL SET_PRANK(0,LOCAL,1)
	;CFLAG:LOCAL:イタズラ = 0
	
NEXT

;宴会の開催判定
SIF FLAG:3 && FLAG:宴会開催フラグ == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:育児人数
	養育費 = CFLAG:MASTER:育児人数 * 1000 / (1 + FLAG:甲斐性無)
	PRINTFORML %CALLNAME:MASTER%支付了\\{養育費}的子女養育費。
	MONEY -= 養育費
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0


BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;清潔度
LOCAL:2 = EX:MASTER:今日の掃除回数 / 10
PRINTFORML 清掃得到的额外奨励 \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【大掃除额外奨励】
	LOCAL:2 *= 2
ENDIF
;拠点の主の好感度と信頼度が上昇
LOCAL:3 = MIN(10, EX:MASTER:今日の掃除回数 / 2000)
IF LOCAL:3
	PRINTFORML 通过努力的掃除、提高了%CALLNAME:拠点_大家%対你的評価
	CFLAG:拠点_大家:好感度 += LOCAL:3 * 2
	CFLAG:拠点_大家:信頼度 += LOCAL:3
ENDIF
PRINTL  
;手伝い報酬
LOCAL:4 = TCVAR:MASTER:手伝った量

SIF LOCAL:4 > 0
	PRINTFORML 幇忙干活得到的额外奨励 \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;好感度上昇
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 好感度上昇额外奨励 ＋{LOCAL:5}籌碼
	MONEY:2 += LOCAL:5
	PRINTFORML 現在的籌碼：{MONEY:2}　籌碼保有上限：{CM_LIMIT}
ENDIF
FLAG:12 = 0

PRINTW  

@恋慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < 3000
	RETURNF 0
SIF CFLAG:ARG:信頼度 < 500
	RETURNF 0
SIF ABL:ARG:親密 < 10
	RETURNF 0
SIF TALENT:ARG:愛欲 && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@思慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:思慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < 1500
	RETURNF 0
SIF CFLAG:ARG:信頼度 < 200
	RETURNF 0
SIF ABL:ARG:親密 < 5
	RETURNF 0
SIF TALENT:ARG:炮友 && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@愛欲取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF !TALENT:ARG:炮友
	RETURNF 0
SIF CFLAG:ARG:好感度 < 3000
	RETURNF 0
SIF ABL:ARG:欲望 < 10
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < 500
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 1
	RETURNF 0
RETURNF 1

@炮友取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:炮友 == 2
	RETURNF 0
SIF CFLAG:ARG:好感度 <= 1500
	RETURNF 0
SIF ABL:ARG:欲望 < 5
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < 200
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 0
	RETURNF 0
RETURNF 1
