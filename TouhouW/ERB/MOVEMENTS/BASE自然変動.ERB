@BASE自然変動(ARG)
#DIM 体力回復量
#DIM 気力回復量
#DIM 欲求不満
#DIM 傘ダメージ
;MASTERと同じ場所にいないとき
IF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	BASE:ARG:情緒 = BASE:ARG:情緒 - TIME_PROGRESS(TIME:1) * 4 * (BASE:ARG:情緒 + 500) / 500
	BASE:ARG:情緒 = MAX(0,BASE:ARG:情緒)
	BASE:ARG:理性 = BASE:ARG:理性 + TIME_PROGRESS(TIME:1) * (MAXBASE:ARG:理性 + 500) / (BASE:ARG:理性 + 500)
	BASE:ARG:理性 = MIN(MAXBASE:ARG:理性,BASE:ARG:理性)
ENDIF
;仕事中の場合、工作量が減少する
IF CFLAG:ARG:行動 == 5
	BASE:ARG:工作量 = BASE:ARG:工作量 - TIME_PROGRESS(TIME:1) * 10
	SIF BASE:ARG:工作量 < 0
		BASE:ARG:工作量 = 0
	;宴会で呑んでる場合酒気が上昇
	SIF CFLAG:ARG:職種 == 49 && TALENT:ARG:酒耐性 != -2 && CFLAG:ARG:妊娠自覚状態 != 1
		BASE:ARG:酒気 += TIME_PROGRESS(TIME:1) * (RAND:3 * (4 - TALENT:ARG:酒耐性))
ENDIF
;憤怒は常に減少する
BASE:ARG:憤怒 = BASE:ARG:憤怒 - TIME_PROGRESS(TIME:1) * 5 / (CFLAG:ARG:ブチギレ + 1)
;宴会の席ならさらに減少する
SIF CFLAG:ARG:職種 == 49 && CFLAG:ARG:ブチギレ > 0
	BASE:ARG:憤怒 -= TIME_PROGRESS(TIME:1) * 5 / (CFLAG:ARG:ブチギレ + 1)
SIF BASE:ARG:憤怒 <= 0
	CFLAG:ARG:ブチギレ = 0
BASE:ARG:憤怒 = MAX(0,BASE:ARG:憤怒)
;酒気は常に減少する
BASE:ARG:酒気 = BASE:ARG:酒気 - TIME_PROGRESS(TIME:1)
;泥酔時は減少量が増える
SIF TCVAR:ARG:泥酔
	BASE:ARG:酒気 -= TIME_PROGRESS(TIME:1) * (RAND:4) 
BASE:ARG:酒気 = MAX(0,BASE:ARG:酒気)
;睡眠深度の変動
IF CFLAG:ARG:睡眠
	;寝てる場合
	IF !TCVAR:ARG:睡眠薬強度
		TCVAR:ARG:睡眠深度 += TIME_PROGRESS(TIME:1) * 10
	ELSE
		TCVAR:ARG:睡眠深度 += TIME_PROGRESS(TIME:1) * 10 * TCVAR:ARG:睡眠薬強度
	ENDIF
ELSE
	;寝てない
	IF TCVAR:ARG:睡眠深度 > 0
		TCVAR:ARG:睡眠深度 -= TIME_PROGRESS(TIME:1) * 10
	ELSE
		TCVAR:ARG:睡眠深度 = 0
	ENDIF
ENDIF
;心情不快
SIF CFLAG:ARG:心情不快
	BASE:ARG:憤怒 = MAXBASE:ARG:憤怒
;勃起度の自然減少
SIF CFLAG:ARG:勃起度２ <= CFLAG:ARG:勃起度 && !TCVAR:ARG:イきそう
	BASE:ARG:勃起 = MAX(0,BASE:ARG:勃起 - TIME_PROGRESS(TIME:1) * 20)
;媚薬フラグ
SIF TCVAR:ARG:媚薬
	TCVAR:ARG:媚薬 = MAX(TCVAR:ARG:媚薬 - TIME_PROGRESS(TIME:1),0)
;利尿剤
SIF TCVAR:ARG:利尿剤
	TCVAR:ARG:利尿剤 = MAX(TCVAR:ARG:利尿剤 - TIME_PROGRESS(TIME:1),0)
;催情薬フラグ
SIF TCVAR:ARG:催情薬
	TCVAR:ARG:催情薬 = MAX(TCVAR:ARG:催情薬 - TIME_PROGRESS(TIME:1),0)

欲求不満 = 0
IF TCVAR:ARG:媚薬 || TCVAR:ARG:発情
	欲求不満 = 欲求不満素質補正(ARG,TIME_PROGRESS(TIME:1))
	CFLAG:ARG:溜まってる度 = CFLAG:ARG:溜まってる度 + 欲求不満
ENDIF
;ディレイ減算処理
SIF CFLAG:ARG:ディレイ > 0
	CFLAG:ARG:ディレイ = MAX(CFLAG:ARG:ディレイ - TIME_PROGRESS(TIME:1),0)
;汚れを付ける
TCVAR:ARG:汚す量 = 0
IF AT_HOME(ARG) || CFLAG:ARG:現在位置 == OMANEKIBEYA()
	TCVAR:ARG:汚す量 = CFLAG:ARG:睡眠 ? 0 # 3 - ABL:ARG:清掃技能 / (1 + TALENT:ARG:懒散)
	SIF TALENT:ARG:幼稚
		TCVAR:ARG:汚す量 += 2
	SIF CFLAG:ARG:初期位置 == CFLAG:ARG:現在位置 && TALENT:ARG:懒散
		TCVAR:ARG:汚す量 += 3
	SIF CFLAG:ARG:大掃除 > 0
		TCVAR:ARG:汚す量 = CFLAG:ARG:睡眠 ? 0 # 1 + TALENT:ARG:幼稚 - 3 * (ABL:ARG:清掃技能 + 1)
	;掃除をしてる場合、汚れが減少
	IF CFLAG:ARG:職種 == 40 && CFLAG:ARG:行動 == 5
		TCVAR:ARG:汚す量 = -2 * (ABL:ARG:清掃技能 * 20 + 100)
	;掃除係は12～18時の間掃除,主人公の技能が高いと掃除させてもらえない
	ELSEIF TALENT:ARG:掃除係 && CFLAG:ARG:神社在住 && BETWEENTIME(720,1080)
		TCVAR:ARG:汚す量 = -1 * MAX(ABL:ARG:清掃技能 + 2 - ABL:MASTER:清掃技能, 1)
	ENDIF
	YOGORE:(CFLAG:ARG:現在位置) += TCVAR:ARG:汚す量 * TIME_PROGRESS(TIME:1) / 5
	YOGORE:(CFLAG:ARG:現在位置) = LIMIT(YOGORE:(CFLAG:ARG:現在位置), 0, 6000)
ENDIF
;衰弱の処理
IF CFLAG:ARG:衰弱
	体力回復量 = (3 + TALENT:ARG:回復速度) * MAXBASE:ARG:体力/2000
	気力回復量 = (3 + TALENT:ARG:回復速度) * MAXBASE:ARG:気力/2000
	;添い寝状態だと相手の回復量が上昇
	IF CFLAG:ARG:添い寝中
		体力回復量 *= 2
		気力回復量 *= 2
	ENDIF
	CALL RECOVER(ARG,TIME_PROGRESS(TIME:1) * 体力回復量,"体力")
	CALL RECOVER(ARG,TIME_PROGRESS(TIME:1) * 気力回復量,"気力")
	CFLAG:ARG:衰弱 = MAX(CFLAG:ARG:衰弱 - TIME_PROGRESS(TIME:1),0)
	IF BASE:ARG:体力 > MAXBASE:ARG:体力 / 2
		CFLAG:ARG:衰弱 = 0
		TCVAR:ARG:休憩中 = 0
	ENDIF
ENDIF
;雨、雪が降っている場合、屋根がないなら体力がちょっとずつ減る
;神社外にいるキャラは減らないが主人公とデート中の相手は減る
;雨で衰弱状態にはならない
IF OUTROOF(CFLAG:ARG:現在位置) && (AT_HOME(ARG) || ARG == MASTER || FLAG:デート相手 == ARG)
	;主人公が折りたたみ傘を持っていない場合
	IF ARG == MASTER && !ITEM:折畳傘
		SELECTCASE TIME:5
			;雨・霧雨
			CASE 4, 6
				;余計なタイミングでメッセージが出ないように
				SIF TIME_PROGRESS(TIME:1) && !TFLAG:195 && !CFLAG:ARG:うふふ && !CFLAG:ARG:睡眠
					PRINTW 被雨淋的皮肤発寒
			;大雨
			CASE 5
				SIF TIME_PROGRESS(TIME:1) && !TFLAG:195 && !CFLAG:ARG:うふふ && !CFLAG:ARG:睡眠
					PRINTW 冰冷的雨刺激着皮肤
			;雪・細雪・霧雪・雨雪交加
			CASE 8, 10, 12, 13
				SIF TIME_PROGRESS(TIME:1) && !TFLAG:195 && !CFLAG:ARG:うふふ && !CFLAG:ARG:睡眠
					PRINTW 被雪弄得皮肤発寒
			;吹雪
			CASE 9
				SIF TIME_PROGRESS(TIME:1) && !TFLAG:195 && !CFLAG:ARG:うふふ && !CFLAG:ARG:睡眠
					PRINTW 暴风雪快把身体凍僵了
		ENDSELECT
	;主人公が折りたたみ傘を持っていると体力の代わりに傘耐久度が減る
	ELSEIF ARG == MASTER
		SELECTCASE TIME:5
			;霧雨、霧雪
			CASE 6, 12
				傘ダメージ = 1
			;雨雪交加
			CASE 13
				傘ダメージ = 5
			;雨・雪・細雪
			CASE 4, 6, 8, 10, 12
				傘ダメージ = 3
			;大雨・吹雪
			CASE 5, 9
				傘ダメージ = 50
			CASEELSE
				傘ダメージ = 0
		ENDSELECT
			FLAG:傘耐久度 += 傘ダメージ * TIME_PROGRESS(TIME:1)
			IF FLAG:傘耐久度 > 10000
				ITEM:折畳傘 = 0
				ITEMSALES:36 ++
				FLAG:傘耐久度 = 0
				SIF EQUIP:TARGET:飾品 != 6
					TEQUIP:TARGET:相合い傘 = 0
				PRINTW 傘壊掉了！
			ELSEIF FLAG:傘耐久度 > 8000
				SIF TIME_PROGRESS(TIME:1) && !TFLAG:195 && !CFLAG:ARG:うふふ && !CFLAG:ARG:睡眠
					PRINTW 傘好像壊掉了…
			ENDIF
	ENDIF
	BASE:ARG:体力 = MAX(BASE:ARG:体力 + 天候による影響(ARG, CFLAG:ARG:現在位置) * TIME_PROGRESS(TIME:1), 500)
ENDIF

;------------------------------------------
;キャラ(ARG)が地点(ARG:1)に居る時に受ける体力への増減値
;移動傾向でも使うので関数に分離
;------------------------------------------
@天候による影響(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
;屋外、傘装備なし、相合い傘してない
IF OUTROOF(ARG:1) && EQUIP:ARG:飾品 != 6 && !TEQUIP:ARG:相合い傘
	;主人公は傘を持っていれば除外
	SIF ARG == MASTER && ITEM:折畳傘
		RETURNF 0
	SELECTCASE TIME:5
		;夏の晴天
		CASE 0
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:氷精 == 2)
				LOCAL = -2
		;夏の晴天
		CASE 1
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:氷精 == 2)
				LOCAL = -3
		;雨・霧雨
		CASE 4, 6
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;雪・細雪・霧雪・雨雪交加
		CASE 8, 10, 12, 13
			IF TALENT:ARG:氷精
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;大雨
		CASE 5
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;吹雪
		CASE 9
			IF TALENT:ARG:氷精
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
	ENDSELECT
ENDIF
;吸血鬼と氷精はダメージ2倍,主人公も防衛できるので影響を大きく
SIF LOCAL < 0 && (TALENT:ARG:吸血鬼 || TALENT:ARG:氷精 || ARG == MASTER)
	TIMES LOCAL, 2.0
RETURNF LOCAL
