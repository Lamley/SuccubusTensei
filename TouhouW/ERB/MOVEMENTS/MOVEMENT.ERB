;FileName_MOVEMENT.ERB ----------------------------- Rev1.01
;キャラ移動処理
;CALL		USER
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_MOVEMENT()
LOCAL:99 = LINECOUNT
PRINTL 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　（少女移動中…）


;時間経過によるBASE等の自然変動、会話累積度、浴室の処理
CALL MOVEMENT共通処理(MASTER)

;各キャラごとのその日の工作情報を更新
FOR LOCAL, 1, CHARANUM
	;出禁キャラをスキマへ飛ばす
	IF CFLAG:LOCAL:出禁
		CFLAG:LOCAL:現在位置 = SUKIMA()
	ELSE
		CALL 仕事内容設定(LOCAL)
	ENDIF
NEXT

;行動順をシャッフル
;CALL FISHER_YATES_SHAFFLE(CHARANUM)
;MASTERを0番に移動
;SWAP SHAFFLE_ARRAY:0 , SHAFFLE_ARRAY:(FINDELEMENT(SHAFFLE_ARRAY , 0))

FOR LOCAL, 1, CHARANUM
	;LOCAL = SHAFFLE_ARRAY:(LOCAL:100)

	SIF MOVEMENTスキップ判定(LOCAL)
		CONTINUE

	CALL 寝室から追い出し(LOCAL)

	;時間経過によるBASE等の自然変動、会話累積度、浴室の処理
	CALL MOVEMENT共通処理(LOCAL)

	;不適切な位置にいるキャラを別の場所へ移動させる
	CALL 強制移動(LOCAL)
NEXT

CALL 宴会参加人数再計算

FOR LOCAL, 1, CHARANUM
	;LOCAL = SHAFFLE_ARRAY:(LOCAL:100)

	SIF MOVEMENTスキップ判定(LOCAL)
		CONTINUE

	;時間に合わせてキャラを起こす／寝かす
	CALL 起床就寝処理(LOCAL)

	;訪問フラグが立っているキャラを主人公の拠点物件に移動させる
	;キャラを自分的家に返す／在宅中の処理を行う
	CALL 訪問帰宅処理(LOCAL)

	;仕事がないキャラを外出させる
	CALL 休日移動処理(LOCAL)

	;待客室にいるキャラの処理
	CALL 待客室処理(LOCAL)

	;同行中のキャラを主人公の移動に合わせて移動させる
	CALL 同行処理(LOCAL)

	;仕事や宴会の開始／経過／終了やメッセージの表示を行う
	CALL 仕事宴会処理(LOCAL)

	;主人公の拠点物件にいるキャラを移動させる
	CALL キャラ移動処理(LOCAL)

	;泥酔になる／泥酔からの復活
	CALL CHARA_ACTION_DRUNK(LOCAL)

	SIF CFLAG:LOCAL:現在位置 == 0
		PRINTFORMW ### MOVEMENT LOCATION ERROR %CALLNAME:LOCAL% 的現在位置異常 ### CHARA_NO:{LOCAL}
NEXT

CALL 宴会参加人数再計算

SIF LINECOUNT == LOCAL:99 + 1
	CLEARLINE 1
