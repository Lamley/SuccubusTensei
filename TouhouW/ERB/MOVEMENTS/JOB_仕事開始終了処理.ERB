;FileName_JOB.ERB ----------------------------- Rev1.00
;仕事開始処理
;CALL		USER
;ARG		キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_JOBSTR(ARG)
#DIM 移動前
#DIM 移動後
#DIMS HOLD_SAMEN

;FOR LOCAL,0,CHARANUM
;	SIF CFLAG:LOCAL:職場 == 0 && CFLAG:LOCAL:行動 == 5
;		PRINTFORMW ### JOB 326ERROR %CALLNAME:LOCAL% ###
;NEXT
VARSET LOCAL
移動前 = 0
移動後 = 0
HOLD_SAMEN =

;泣く泣く仕事へ 真面目だからね
IF CFLAG:ARG:うふふ && !GROUPMATCH(CFLAG:ARG:職種, 49, 51)
	PRINTFORMW %CALLNAME:ARG%必須要\@ CFLAG:ARG:職場 == CFLAG:ARG:現在位置 ? 開始 # 前去 \@工作了…
	CALL ENDUFUFU(ARG)
	CFLAG:MASTER:うふふ = 0
	TFLAG:106 = 0
ENDIF

SIF CFLAG:ARG:行動 == 5
	CALL JOBMOVE(ARG)

IF (CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:ARG:現在位置) == 2 || CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置) && !TFLAG:遠距離移動
	IF CFLAG:ARG:現在位置 == FLAG:32 && TIME_PROGRESS(TIME:1) && (CFLAG:ARG:職種 == 49 || CFLAG:ARG:職種 == 51)
		CALL 宴会描写(ARG)
	ELSEIF CFLAG:ARG:現在位置 == CFLAG:ARG:職場 && TIME_PROGRESS(TIME:1)
		CALL 仕事情景描写(ARG)
		IF BASE:ARG:工作量
			;仕事がまだ3分の2以上残っている
			IF BASE:ARG:工作量 >= (MAXBASE:ARG:工作量 / 3) * 2
				IF CFLAG:ARG:職種 == 48
					PRINTW 　宴会的準備工作还剩很多没完成的样子…
				ELSEIF CFLAG:ARG:職種 == 50
					PRINTW 　宴会的善后工作还剩很多没完成的样子…
				ELSEIF CFLAG:ARG:職種 == 46
					PRINTW 埋头于玩耍之中…
				ELSE
					PRINTFORMW %GET_JOBNAME(ARG)%还剩很多没完成的样子…
				ENDIF
				LOCAL = 3
			;仕事がまだ3分の1以上残っている
			ELSEIF BASE:ARG:工作量 >= (MAXBASE:ARG:工作量 / 3)
				IF CFLAG:ARG:職種 == 48
					PRINTW 　距离宴会的準備工作完成还需要一段时间…
				ELSEIF CFLAG:ARG:職種 == 50
					PRINTW 　距离宴会的善后工作完成还需要一段时间…
				ELSEIF CFLAG:ARG:職種 == 46
					PRINTW 好像还没玩够的样子…
				ELSE
					PRINTFORMW 距离%GET_JOBNAME(ARG)%完成还需要一段时间…
				ENDIF
				LOCAL = 2
			ELSE
				IF CFLAG:ARG:職種 == 48
					PRINTW 　宴会的準備工作还差一点就可以完成了…
				ELSEIF CFLAG:ARG:職種 == 50
					PRINTW 　宴会的善后工作还差一点就可以完成了…
				ELSEIF CFLAG:ARG:職種 == 46
					PRINTW 玩累了的样子…
				ELSE
					PRINTFORMW %GET_JOBNAME(ARG)%还差一点就可以完成了…
				ENDIF
				LOCAL = 1
			ENDIF
			;通常のお仕事口上　お仕事中かつ宴会フラグが立ってなければ表示する
			SIF CFLAG:ARG:宴会参加 == 0
				CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,2,LOCAL)
		ENDIF
		;宴会中の場合のみ、スムーズに次の動作に繋がる
		;酒を飲み終わった時に１ターン何もせずボーっとするのを防止する処理
;		SIF CFLAG:ARG:宴会参加 > 0 && BASE:ARG:工作量 <= 0
;			CALL CHARA_JOBEND(ARG)
	ELSEIF CFLAG:ARG:職種 != 51
		IF EX:ARG:膣内精液 + EX:ARG:アナル内精液 >= 500
			HOLD_SAMEN = "お腹にたっぷり精液を溜めたまま"
		ELSEIF EX:ARG:膣内精液 + EX:ARG:アナル内精液 >= 100
			HOLD_SAMEN = "お腹に精液を溜めたまま"
		ENDIF
		IF !AT_HOME(ARG) && GET_MAPID(CFLAG:ARG:現在位置) != MAIN_MAP
			移動前 = 6000 + (CFLAG:ARG:現在位置 / 10)
		ELSE
			移動前 = CFLAG:ARG:現在位置
		ENDIF
		IF IN_HOME(CFLAG:ARG:職場) == 0 && GET_MAPID(CFLAG:ARG:職場) != MAIN_MAP
			移動後 = 6000 + (CFLAG:ARG:職場 / 10)
		ELSE
			移動後 = CFLAG:ARG:職場
		ENDIF
		IF CFLAG:ARG:現在位置 == OMANEKIBEYA() && TIME_PROGRESS(TIME:1)
			;部屋に上がってたら出る,すいません面倒なので地の文まとめちゃいます；
			CFLAG:MASTER:現在位置 = CFLAG:ARG:自宅位置
			PRINTFORMW %CALLNAME:ARG%好像有事外出了
		ELSEIF CFLAG:ARG:職種 == 48 && TIME_PROGRESS(TIME:1)
			PRINTFORM %CALLNAME:ARG%為了準備宴会从、%HOLD_SAMEN%
			PRINTFORMS NAME_FROM_PLACE(移動前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移動後)
			PRINTW 出発了。
		ELSEIF CFLAG:ARG:職種 == 49 && TIME_PROGRESS(TIME:1)
			PRINTFORM %CALLNAME:ARG%%HOLD_SAMEN%
			PRINTFORMS NAME_FROM_PLACE(移動前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移動後)
			PRINTW 、為参加宴会而出発了。
		ELSEIF CFLAG:ARG:職種 == 46 && TIME_PROGRESS(TIME:1)
			PRINTFORM %CALLNAME:ARG%%HOLD_SAMEN%
			PRINTFORMS NAME_FROM_PLACE(移動前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移動後)
			PRINTW 、為玩耍而出発了。
		ELSEIF TIME_PROGRESS(TIME:1)
			PRINTFORM %CALLNAME:ARG%為了%GET_JOBNAME(ARG)%、%HOLD_SAMEN%
			PRINTFORMS NAME_FROM_PLACE(移動前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移動後)
			PRINTW 出発了。
		ENDIF
		
		;通常の職場に移動口上　宴会フラグが立ってなければ表示する
		SIF CFLAG:ARG:宴会参加 == 0
			CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,1,LOCAL)
	ENDIF
ENDIF
SIF CFLAG:ARG:添い寝中
	CALL SET_TOGETHER(0,ARG,1)
SIF CFLAG:ARG:同行 && CFLAG:ARG:職種 != 51
	CFLAG:ARG:同行 = 0
IF FLAG:デート相手 == ARG
	FLAG:デート相手 = 0
	TFLAG:デート前好感度 = 0
	CFLAG:ARG:デート中 = MAIN_MAP
ENDIF
;職場へ移動
SIF CFLAG:ARG:職場 && SHIRAHU(ARG)
	CFLAG:ARG:現在位置 = CFLAG:ARG:職場
SIF CFLAG:ARG:職種 == 49 && !TCVAR:ARG:泥酔 && (BASE:ARG:酒気 > (MAXBASE:ARG:酒気 / 10) * 8)
	CALL ENKAI_DRUNK(ARG)
CFLAG:ARG:行動 = 5
IF TCVAR:ARG:着替え
	CALL OKIGAE(ARG,TCVAR:ARG:着替え)
ENDIF
;FileName_JOB.ERB ----------------------------- Rev1.00
;仕事終了処理
;CALL		USER
;ARG		キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_JOBEND(ARG)
;仕事後ディレイSET
SIF CFLAG:ARG:行動 == 5
	CALL SET_DERAY(3,ARG)

;工作量を0にする
SIF CFLAG:ARG:行動 == 5
	BASE:ARG:工作量 = 0

;仕事中フラグOFF
CFLAG:ARG:行動 = 0

IF CFLAG:ARG:現在位置 == CFLAG:ARG:職場 && CFLAG:ARG:職種 != 51
	IF CFLAG:ARG:職種 == 48
		IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %CALLNAME:ARG%的宴会準備已経結束了。
			SIF ARG == 1
				PRINTL 　真是辛苦了
		ENDIF
		IF DAY == FLAG:開始日 && TIME >= FLAG:33
			CALL ENKAI_START(ARG)
		ELSEIF FLAG:宴会開催フラグ >= 98
			CALL ENKAI_START(ARG)
		ENDIF

	ELSEIF CFLAG:ARG:職種 == 49 
		CALL ENKAI_DRUNK(ARG)

	ELSEIF CFLAG:ARG:職種 == 50
		IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %CALLNAME:ARG%的宴会善后已経結束了。
			SIF ARG == 1
				PRINTL 　真是辛苦了
		ENDIF
		CFLAG:ARG:職種 = 0
		CFLAG:ARG:宴会参加 = 3

	ELSEIF CFLAG:ARG:職種 == 46
		IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %CALLNAME:ARG%結束了玩耍。
			;お疲れ様口上
			SIF CFLAG:ARG:宴会参加 == 0
				CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,3,0)
		ENDIF

	ELSE
		IF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %CALLNAME:ARG%的%GET_JOBNAME(ARG)%結束了。
			;お疲れ様口上
			CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,3,0)
			IF MAXBASE:ARG:工作量 < (TCVAR:ARG:手伝った量 * 3) && TCVAR:ARG:お礼
				PRINTFORMW 從%CALLNAME:ARG%得到了%ITEMNAME:(TCVAR:ARG:お礼)%
				ITEM:(TCVAR:ARG:お礼) ++
			ENDIF
		ENDIF
		IF CFLAG:ARG:現在位置 / 100 != MAIN_MAP && !CFLAG:ARG:神社在住 && VISIT(ARG)
			IF !RAND:15
				CALL 主人公物件へ向かう(ARG)
			ELSEIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && CAN_DATE(ARG)
				PRINTFORML 要邀請%CALLNAME:ARG%約会麼？
				CALL ASK_YN
				IF !RESULT
					CALL 外出先から誘えるか(ARG)
					IF RESULT
						CFLAG:MASTER:同行 = 60
						CFLAG:ARG:同行 = 60
						CFLAG:ARG:デート中 = CFLAG:MASTER:現在位置 / 100
						FLAG:デート相手 = ARG
						TFLAG:デート前好感度 = CFLAG:ARG:好感度
						TIME += 1
						PRINTFORMW 決定工作結束後和%CALLNAME:ARG%去約会
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	IF CFLAG:ARG:神社在住 && !AT_HOME(ARG)
		SIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %CALLNAME:ARG%回去%GET_MAPNAME(MAIN_MAP)%了
		CFLAG:ARG:現在位置 = MAXROOM
	ENDIF
ENDIF
