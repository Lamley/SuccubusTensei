@KICKOUT_9()
#FUNCTION
SELECTCASE CHK_INHOME(CFLAG:MASTER:現在位置)
	CASE 21
		RETURNF 922
	CASE 34
		RETURNF 932
ENDSELECT
RETURNF 0


@MAP_UNIQUEMESSAGE_9
CALL DRAW_INFORMATIONLINE("Info",1)
IF WITH_MOB()
	CALL MOB_MESSAGE_9
	RETURN 1
ELSE
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 944,946
			PRINTFORML 聽到了繁華街的喧鬧声　在那対面能看到地霊殿
		CASE 948
			PRINTFORML 在道路的一頭看見了小小的地藏
	ENDSELECT
ENDIF
IF CHK_FOCUS(944,CFLAG:MASTER:現在位置,948)
	SIF CFLAG:60:行動 == 5
		PRINTFORML 帕露西在橋上監視着通行者
ENDIF
RETURN 1
@MOB_MESSAGE_9
IF CHK_FOCUS(927,CFLAG:MASTER:現在位置,930)
	IF FLAG:70
		PRINTL 怨霊們停下了動作
	ELSE
		IF !RAND:2
			PRINTL 怨霊在彷徨
		ELSE
			PRINTL 怨霊飄蕩着
		ENDIF
	ENDIF
ELSEIF CHK_FOCUS(931,CFLAG:MASTER:現在位置,939)
	IF FLAG:70
		PRINTL 鬼們停下了動作
	ELSEIF GROUPMATCH(TIME:2,1,6,7)
		PRINTL 鬼們在睡覚…
	ELSE
		SELECTCASE RAND:6
			CASE 0
				PRINTL 鬼們舉行着酒宴
			CASE 1
				PRINTL 鬼們聚集在一起
			CASE 2
				PRINTL 鬼們在打着架
			CASE 3
				PRINTL 鬼們徘徊着
			CASE 4
				PRINTL 鬼們吵鬧着
			CASE 5
				PRINTL 鬼躺在地上…真礙事
		ENDSELECT
	ENDIF
ELSE
	IF FLAG:70
		PRINTFORML %ANIMALNAME()%停下了動作
	ELSEIF GROUPMATCH(TIME:2,1,6,7)
		PRINTFORML %ANIMALNAME()%在睡覚…
	ELSEIF CFLAG:MASTER:現在位置 == 923
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%在洗澡
		ELSE
			PRINTFORML %ANIMALNAME()%在游泳
		ENDIF
	ELSE
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%徘徊着
		ELSE
			PRINTFORML %ANIMALNAME()%躺在地上…真礙事
		ENDIF
	ENDIF
ENDIF
;-----------------------------------------
;非自宅時に差し替える場所IDを返す関数
;ARG,対象ID
;RETURNF,差し替え済み対象ID
;-----------------------------------------

@MAP_SEARCH_REPLACEMENT_9(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 948
	;幻想風穴
		RETURNF 910
	CASE 940 TO 946
	;地獄的深道
		RETURNF 920
	CASE 934
	;旧地獄温泉
		RETURNF 940
	CASE 935
	;情人旅館
		RETURNF 950
	CASE 931 TO 939
	;旧地獄街道
		RETURNF 930
	CASE 901 TO 926
	;地霊殿
		RETURNF 960
	CASE 927 TO 930
	;灼熱地獄跡
		RETURNF 970
ENDSELECT
RETURNF 0