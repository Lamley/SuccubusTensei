;FileName_SLEEP.ERB ----------------------------- Rev1.00
;キャラ睡眠処理
;CALL		USER
;ARG		0:キャラNO, 1:MESSAGE出力要否
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_SLEEP, ARG ,ARG:1
SIF TALENT:ARG:妄想的産物
	RETURN 0
SIF TCVAR:ARG:泥酔
	RETURN 0

VARSET LOCAL
IF (CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:ARG:現在位置) == 2 || CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置) && !CFLAG:MASTER:睡眠 && !CFLAG:ARG:ブチギレ
	IF (IN_HOME(CFLAG:MASTER:現在位置) && CFLAG:ARG:現在位置 == CFLAG:ARG:初期位置 && CFLAG:ARG:現在位置 != MAXROOM) || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
		IF TALENT:ARG:恋慕
			IF CFLAG:ARG:添い寝中
				IF CFLAG:ARG:睡眠
					IF ARG:1
						PRINTDATA
							DATAFORM %CALLNAME:ARG%幸福的微笑着
							DATAFORM %CALLNAME:ARG%小声的念着%CALLNAME:MASTER%的名字
							DATAFORM %CALLNAME:ARG%香甜的睡着
							DATAFORM %CALLNAME:ARG%無意識地抱住了%CALLNAME:MASTER%
						ENDDATA
					ENDIF
					LOCAL = 3
					PRINTW
				ELSE
					IF ARG:1
						PRINTDATA
							DATAFORM %CALLNAME:ARG%幸福的微笑着
							DATAFORM %CALLNAME:ARG%打了个小哈欠
							DATAFORM %CALLNAME:ARG%红着脸看着%CALLNAME:MASTER%
							DATAFORM %CALLNAME:ARG%悄悄地偎依到%CALLNAME:MASTER%身辺
						ENDDATA
					ENDIF
					LOCAL = 2
					PRINTW
				ENDIF
			ELSE
				IF CFLAG:ARG:睡眠 
					SIF ARG:1
						PRINTFORMW %CALLNAME:ARG%在睡着
					LOCAL = 3
				ELSEIF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
					IF ARG:1
						IF CFLAG:ARG:ディレイ
							PRINTFORMW %CALLNAME:ARG%好像在睡着
						ELSE
							PRINTFORMW %CALLNAME:ARG%败给了睡魔、进入了梦乡…
							IF CFLAG:ARG:うふふ
								LOCAL:3 = -1
								WHILE 1
									SIF LOCAL:3 >= CHARANUM - 1
										BREAK
									LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, LOCAL:3 + 1)
									SIF LOCAL:3 < 0
										BREAK
									SIF LOCAL:3 != ARG
										CALL ENDUFUFU(LOCAL:3)
								WEND
								CALL ENDUFUFU(ARG)
								TFLAG:106 = 0
								CFLAG:MASTER:うふふ = 0
							ENDIF
						ENDIF
					ENDIF
					SIF !CFLAG:ARG:ディレイ
						LOCAL = 9
				ENDIF
			ENDIF
		ELSEIF CFLAG:ARG:睡眠 == 1
			SIF ARG:1
				PRINTFORMW %CALLNAME:ARG%在睡着
			LOCAL = 3
		ELSE
			SIF ARG:1
				PRINTFORMW %CALLNAME:ARG%好像在睡着
			CALL 寝室から追い出し(ARG)
			LOCAL = 2
		ENDIF
		;隠密中セット
		SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:前ターン位置
			CALL SET_SNEAK(1,ARG,1)
	;寝てない場合
	ELSEIF 睡眠時間(ARG) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置 && !TCVAR:ARG:休憩中
		IF CFLAG:ARG:神社在住
			CALL SLEEP_RESIDENT(ARG)
		ELSE
			CALL SLEEP_VISITOR(ARG)
		ENDIF
		LOCAL = RESULT
		IF CFLAG:ARG:うふふ
			LOCAL:3 = -1
			WHILE 1
				SIF LOCAL:3 >= CHARANUM - 1
					BREAK
				LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, LOCAL:3 + 1)
				SIF LOCAL:3 < 0
					BREAK
				SIF LOCAL:3 != ARG
					CALL ENDUFUFU(LOCAL:3)
			WEND
			CALL ENDUFUFU(ARG)
			TFLAG:106 = 0
			CFLAG:MASTER:うふふ = 0
		ENDIF
		LOCAL:6 ++
	;相手が自室に帰っていく
	ELSE
		LOCAL = 1
	ENDIF
	;おやすみ口上or帰宅口上
	IF (CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 || (睡眠時間(ARG) && CFLAG:ARG:現在位置 == CFLAG:ARG:神社在住)) && !LOCAL:6
		;CALL KOJO_EVENT(7, ARG, LOCAL)
		IF CFLAG:ARG:神社在住 || CFLAG:MASTER:現在位置 == OMANEKIBEYA() || TCVAR:ARG:休憩中
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,LOCAL,0)
		ELSE
			LOCAL:2 = 1
			SIF CHK_DATENOW(CFLAG:デート中)
				LOCAL:2 = 2
			CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,LOCAL:2,0)
		ENDIF
	ENDIF
ENDIF
;宴会中に寝たキャラは宴会フラグが外れる
IF GROUPMATCH(CFLAG:ARG:職種, 48, 49, 50, 51)
	CFLAG:ARG:宴会参加 = 0
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:行動 = 0
	BASE:ARG:工作量 = 0
ENDIF
SIF BASE:ARG:工作量 <= 0 && CFLAG:ARG:行動 == 5
	CALL CHARA_JOBEND(ARG)
;仕事中にぶっ倒れたらお仕事終了
IF CFLAG:ARG:衰弱 && CFLAG:ARG:行動 == 5
	CFLAG:ARG:行動 = 0
	BASE:ARG:工作量 = 0
ENDIF


;自室で寝るべく戻ってくる
IF CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置 && !CFLAG:ARG:睡眠 && !CFLAG:ARG:出禁
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:初期位置
		PRINTFORML %CALLNAME:ARG%回到了房间
		IF TALENT:ARG:恋慕 || (CFLAG:ARG:初期位置 == 15 && (TALENT:1:恋慕 || TALENT:57:恋慕 || TALENT:71:恋慕))
			LOCAL:4 = 0
			LOCAL:5 = 0
			IF CFLAG:MASTER:うふふ
				LOCAL:3 = -1
				WHILE 1
					SIF LOCAL:3 >= CHARANUM - 1
						BREAK
					LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:MASTER:うふふ, LOCAL:3 + 1)
					SIF LOCAL:3 < 0
						BREAK
					IF LOCAL:3
						CALL ENDUFUFU(LOCAL:3)
						LOCAL:4 ++
					ENDIF
				WEND
				TFLAG:106 = 0
				CFLAG:MASTER:うふふ = 0
				IF !RAND:2
					LOCAL:3 = FINDCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
					SIF LOCAL:3 == ARG
						LOCAL:3 = FINDLASTCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
				ELSE
					LOCAL:3 = FINDLASTCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
					SIF LOCAL:3 == ARG
						LOCAL:3 = FINDCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
				ENDIF
				PRINTFORML %CALLNAME:MASTER%因為遠処的%CALLNAME:(LOCAL:3)%\@ LOCAL:4 > 1 ? 們 #  \@、不情願地和%CALLNAME:ARG%嘴唇分離
				LOCAL:5 = 2
			ENDIF
			PRINTL 
			CALL 寝起き着替え(ARG)
			;PRINTFORMW %CALLNAME:ARG%は更衣と布団に潜った…
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,7,LOCAL:5)
		ELSE
			PRINTFORMW 被犯困的%CALLNAME:ARG%赶出来了…
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,7,1)
			;忍び込みOFF
			CALL SET_SNEAK(0,LOCAL,2)
			;イタズラOFF
			CALL SET_PRANK(0,LOCAL,2)
			FOR LOCAL:1,0,CHARANUM
				CALL ENDUFUFU(LOCAL:1)
			NEXT
			TFLAG:106 = 0
			;移動可能な場所へ移動
			CALL GETOUT(MASTER)
			;遠距離移動フラグOFF
			TFLAG:195 = 0
		ENDIF
	ENDIF
	;寝間着にお着替え

	CALL CTRL_CLOTHES_SET(ARG, "現在衣装の変更_パジャマ")
	CALL CLOTHES_SETTING_TRAIN(ARG)
ENDIF

;自室でねる
IF !TCVAR:ARG:休憩中
	CFLAG:ARG:現在位置 = CFLAG:ARG:初期位置
	DEBUGPRINTFORML %CALLNAME:ARG%は{TIME}に{CFLAG:ARG:初期位置}で寝たよ
ENDIF
SIF LOCAL == 5 && CFLAG:ARG:初期位置 == 54 && TALENT:MASTER:100 >= -4
	CFLAG:MASTER:現在位置 = 9
SIF LOCAL == 5
	CFLAG:MASTER:現在位置 = CFLAG:ARG:初期位置
;施錠
SIF GET_MAPID(CFLAG:ARG:初期位置) == MAIN_MAP && CFLAG:MASTER:初期位置 != CFLAG:ARG:初期位置
	LOCK:(CFLAG:ARG:初期位置) = 1
CFLAG:ARG:同行 = 0
CALL SET_SLEEP(1,ARG,0)
SIF CFLAG:ARG:睡眠
	CALL SET_DERAY(99,ARG)
;FileName_SLEEP.ERB ----------------------------- Rev1.00
;キャラ起床処理
;CALL		USER
;ARG		0:キャラNO, 1:施錠解除有無, 2:睡眠姦からの起床かどうか
;起床場所	数値1なら同棲、数値0なら非同棲
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_AWAKE, ARG,ARG:1,ARG:2
#DIM 起床場所
起床場所 = 0
IF CFLAG:ARG:睡眠 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:ARG:起床時間 <= TIME
	SIF ARG > 0 && !CFLAG:MASTER:睡眠 && !ARG:2
		PRINTFORMW %CALLNAME:ARG%醒来了
	;睡眠姦からの起床ならスルー
	IF !ARG:2
		IF !TALENT:ARG:恋慕
			PRINTFORML %CALLNAME:MASTER%、在%CALLNAME:ARG%要換衣服的時候、暫時被趕出去了
			;おはよう口上
			IF !CFLAG:ARG:面識
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,0)
			ENDIF
		ELSE
			SIF CFLAG:MASTER:初期位置 == CFLAG:ARG:初期位置
				起床場所 = 1
			;おはよう口上
			IF !CFLAG:ARG:面識
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,起床場所)
			ENDIF
			IF GETBIT(CFLAG:ARG:着替え実行, 1) && !CFLAG:MASTER:睡眠
				CALL 寝起き着替え(ARG)
			ENDIF
		ENDIF
	ENDIF
ENDIF
CALL CTRL_CLOTHES_SET(ARG, "現在衣装の変更_普段着")
CALL CLOTHES_SETTING_TRAIN(ARG)

;ディレイSET
IF CFLAG:ARG:睡眠
	CALL SET_DERAY(2,ARG)
ENDIF

CALL SET_SLEEP(0,ARG,0)

;各フラグOFF
CALL SET_PRANK(0,ARG,2)
CALL SET_SNEAK(0,ARG,2)
;CALL SET_TOGETHER(0,ARG,0)
;施錠
SIF ARG:1 == 0 && GET_MAPID(CFLAG:ARG:初期位置) == MAIN_MAP
	LOCK:(CFLAG:ARG:初期位置) = 0
CFLAG:ARG:休日移動済 = 0


@SLEEP_RESIDENT(ARG)
IF CHK_DATENOW(CFLAG:ARG:デート中)
	PRINTFORML %CALLNAME:ARG%因為困了回家去了
	CALL SET_DATE(99,ARG)
	CALL 帰宅の時間経過
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	IF TALENT:ARG:恋慕
		CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	ELSE
		CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
	ENDIF
ELSEIF (TALENT:ARG:恋慕 && AT_HOME(MASTER) && CFLAG:ARG:衰弱) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
	PRINTFORM 把%CALLNAME:ARG%從
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
	PRINT 運到了
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初期位置)
	PRINTW 
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	RESULT = 5
	RETURN RESULT
ELSEIF CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置
	PRINTFORM %CALLNAME:ARG%從
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
	PRINT 返回了
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初期位置)
	PRINTW 
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	;MASTERついてく必要ないよね？
	;CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	RESULT = 1
	RETURN RESULT
ENDIF

@SLEEP_VISITOR(ARG)
IF CHK_DATENOW(CFLAG:ARG:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,2,0)
	PRINTFORMW %CALLNAME:ARG%説\@ CFLAG:ARG:衰弱 ? 累了 # 困了\@然后就回去了
	CFLAG:MASTER:同行 = 0
	CFLAG:ARG:同行 = 0
	CFLAG:ARG:デート中 = MAIN_MAP
	FLAG:デート相手 = 0
	TFLAG:デート前好感度 = 0
	;日常ON
	TFLAG:102 = 1
	;挿入解除
	TFLAG:106 = 0
ELSEIF TALENT:ARG:恋慕 && AT_HOME(MASTER) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
	PRINTFORML %CALLNAME:ARG%精疲力尽了…
	CALL ASK_YN("送到参道為止吧","稍微歇会再走吧")
	IF RESULT == 0
		CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)
		;CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,1,0)
		GOTO 神社から帰る
	ELSE
		PRINTFORM %CALLNAME:ARG%を
		PRINTFORMW 往%NAME_FROM_PLACE(デフォルト初期位置)%送去
		CFLAG:ARG:同行 = 0
		FLAG:デート相手 = 0
		TFLAG:デート前好感度 = 0
		CFLAG:ARG:デート中 = MAIN_MAP
		CFLAG:ARG:現在位置 = デフォルト初期位置
		TCVAR:ARG:休憩中 = 1
		CFLAG:MASTER:現在位置 = デフォルト初期位置
		SIF デフォルト初期位置 == 小人的虫籠 && TALENT:MASTER:体型 > -5
			CFLAG:ARG:現在位置 = 走廊
		RESULT = 8
		RETURN RESULT
	ENDIF
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	IF CFLAG:MASTER:イタズラ == 2
		PRINTFORMW 好像対%CALLNAME:ARG%做得太過分了、在醒来之前撤退吧
	ELSE
		PRINTFORMW %CALLNAME:ARG%説累了、便躺下来了
	ENDIF
	TCVAR:ARG:休憩中 = 1
	RESULT = 5
	RETURN RESULT
ELSE
	$神社から帰る
	CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,1,0)
	PRINTFORMW 疲惫的%CALLNAME:ARG%回去了
	IF CHK_DATENOW(CFLAG:ARG:デート中)
		CFLAG:ARG:同行 = 0
		FLAG:デート相手 = 0
		TFLAG:デート前好感度 = 0
		CFLAG:ARG:デート中 = MAIN_MAP
		CFLAG:ARG:拠点来訪 = 0
	ENDIF
	CFLAG:ARG:現在位置 = CFLAG:ARG:初期位置
ENDIF