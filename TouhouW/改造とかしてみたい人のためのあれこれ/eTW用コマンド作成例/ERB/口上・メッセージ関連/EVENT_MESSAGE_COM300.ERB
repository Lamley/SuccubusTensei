;-------------------------------------------------
;300,会話
;-------------------------------------------------
;会話
@MESSAGE_COM300
IF TCVAR:会話不能
	RETURN
ENDIF

IF TCVAR:泥酔
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に話し掛けてみたが、%CALLNAME:TARGET%は呂律が回っておらず何を言っているかも聴き取れない
	PRINTFORML ほとんど会話にならなかった…
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%と
	IF CFLAG:怒り > 0
		IF TALENT:恋人
			PRINTDATA
				DATAFORM 素直になれない調子で
				DATAFORM やや意地を張って見せながら
				DATAFORM あえて謝罪を後回しにして
				DATAFORM 機嫌を伺いつつ
				DATAFORM 心の底では謝りつつ
			ENDDATA
		ELSE
			PRINTDATA
				DATAFORM 機嫌を伺いつつ
				DATAFORM ぎこちない雰囲気で
				DATAFORM 後ろめたい気持ちで
				DATAFORM あえて悪びれずに
				DATAFORM 一方的に
			ENDDATA
		ENDIF
	ELSEIF CFLAG:TARGET:20 > 0 && CFLAG:TARGET:350 == 49
		IF TALENT:恋慕
			PRINTDATA
				DATAFORM お酌し合いながら
				DATAFORM 笑顔を交わしながら
				DATAFORM 大笑いしながら
				DATAFORM リラックスした雰囲気で
				DATAFORM やや大胆な話題について
			ENDDATA
		ELSEIF ABL:親密 >= 5
			PRINTDATA
				DATAFORM お酌し合いながら
				DATAFORM お互いを気にしながら
				DATAFORM 冗談を飛ばして笑いあいつつ
				DATAFORM リラックスした雰囲気で
				DATAFORM あえて距離を保ちつつ
			ENDDATA
		ELSE
			PRINTDATA
				DATAFORM ちびちびと飲みながら
				DATAFORM 少しだけ親しげに
				DATAFORM の交友を深めるべく
				DATAFORM 場の雰囲気の勢いに乗って
				DATAFORM 普段とは違う雰囲気で
			ENDDATA
		ENDIF
	ELSEIF CFLAG:TARGET:添い寝中
		PRINTDATA
			DATAFORM 指先を絡ませながら
			DATAFORM 寄り添って
			DATAFORM 少し恥ずかしそうに
			DATAFORM 抱き合いながら
			DATAFORM じゃれ合いながら
			DATAFORM 頬に手を添えて
			DATAFORM 布団の中で
		ENDDATA
	ELSEIF CHK_DATENOW(CFLAG:TARGET:デート中)
		IF TALENT:恋慕
			PRINTDATA
				DATAFORM デート先について
				DATAFORM 隣りを歩きながら
				DATAFORM 少し照れながら
				DATAFORM じゃれ合いながら
				DATAFORM 微笑みを交わしながら
			ENDDATA
		ELSE
			PRINTDATA
				DATAFORM 世間
				DATAFORM 出掛け先について
				DATAFORM たわいない
				DATAFORM 取るに足らない
				DATAFORM 歩きながら
			ENDDATA
		ENDIF
	ELSEIF TALENT:恋慕
		PRINTDATA
			DATAFORM 指先を絡ませながら
			DATAFORM 隣りあって
			DATAFORM 少し照れながら
			DATAFORM \@ TIME >= 360 && TIME <= 1080 ? 昼の光の中で # 夜の静けさの中で \@
			DATAFORM じゃれ合いながら
			DATAFORM 微笑みを交わしながら
		ENDDATA
	ELSE
		PRINTDATA
			DATAFORM 世間
			DATAFORM とりとめのない
			DATAFORM 下らない
			DATAFORM たわいない
			DATAFORM 取るに足らない
			DATAFORM 少し
			DATAFORM 上を向いて
		ENDDATA
	ENDIF
	PRINTFORML 話をした
	IF TFLAG:193 == 1
		PRINTFORML 楽しい時間が流れた…
	ELSEIF TFLAG:193 == 2
		PRINTDATA
		;		DATAFORM 新たな異変の
			DATAFORM 大げさな
			DATAFORM 取って置きの
			DATAFORM 思いがけない
			DATAFORM 気になっていた
		ENDDATA
		PRINTFORML 話に%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@は大いに盛り上がった…
	ELSEIF TFLAG:193 == -1
		PRINTFORML 会話が途切れると気まずい空気が流れた…
	ELSEIF TFLAG:193 == -2
		PRINTFORML 話が噛み合わない…
	ENDIF
ENDIF

;-------------------------------------------------
;301,お茶を淹れる
;-------------------------------------------------
@MESSAGE_COM301
IF CFLAG:TARGET:行動 == 5
	PRINTFORML %CALLNAME:PLAYER%は休憩がてらお茶を淹れて%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@に差し出した…
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:PLAYER%はお茶を飲みながら休憩時間を楽しんだ…
	ELSEIF TFLAG:193 == -1
		PRINTFORML …どうやら%CALLNAME:TARGET%の口に合わなかったようだ
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%はお茶を淹れて%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@に差し出した…
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:PLAYER%はお茶を飲みながら楽しいひとときを過ごした…
	ELSEIF TFLAG:193 == -1
		PRINTFORML …どうやら%CALLNAME:TARGET%の口に合わなかったようだ
	ENDIF
ENDIF

;-------------------------------------------------
;302,スキンシップ
;-------------------------------------------------
@MESSAGE_COM302
;IF CFLAG:MASTER:現在位置 == 13 || CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 30 || CFLAG:MASTER:現在位置 == 39
IF BATHCHECK(CFLAG:MASTER:現在位置) && IN_HOME(CFLAG:MASTER:現在位置)
	IF TALENT:恋人 && !RAND:2
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に背中を流してもらった
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の背中を流した
	ENDIF
ELSEIF CHK_DATENOW(CFLAG:TARGET:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と\@ (CFLAG:TARGET:現在位置 % 100) % 10 ? 歩きながら # 軽く \@スキンシップを図った
ELSEIF CFLAG:TARGET:350 == 49
	PRINTFORML %CALLNAME:PLAYER%は普段より大胆に%CALLNAME:TARGET%とのスキンシップを図った
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%とのスキンシップを図った
ENDIF
IF TFLAG:193 == 1
	PRINTFORML %CALLNAME:TARGET%は何だか嬉しそうにしている…
ELSEIF TFLAG:193 == -1
	PRINTFORML …どうやら、少し馴れ馴れしくしすぎたようだ…
ENDIF
TFLAG:400 = RAND:2
SIF TFLAG:400 && (DATE_HITOGOMI() || WITH_MOB() == 9)
	CALL DATE_HITOGOMI_MESSAGE(CFLAG:PLAYER:現在位置)

;-------------------------------------------------
;303,謝る
;-------------------------------------------------
@MESSAGE_COM303
IF TFLAG:193 == -2 || TFLAG:193 == 2
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に非礼を詫びて菓子折りを渡した
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に非礼を詫びた
ENDIF
IF TFLAG:193 == 1
	PRINTFORML %CALLNAME:TARGET%は笑って許してくれた…
ELSEIF TFLAG:193 == 2
	PRINTFORML %CALLNAME:TARGET%は過去のことは水に流してくれた…
	PRINTFORMW %CALLNAME:TARGET%は反発刻印LV1を失った
ELSEIF TFLAG:193 == 0
	PRINTFORML %CALLNAME:TARGET%は仕方なく%CALLNAME:PLAYER%を許した…
ELSEIF TFLAG:193 == -2
	PRINTFORML %CALLNAME:TARGET%は菓子折りを受け取ってはくれなかった…
	PRINTFORML %CALLNAME:TARGET%はまだ許してはくれないようだ…
ELSE
	IF TALENT:度胸 < 0
		PRINTFORML しかし%CALLNAME:TARGET%はまだ泣いている…
	ELSE
		PRINTFORML しかし%CALLNAME:TARGET%はまだ怒っている…
	ENDIF
ENDIF

;-------------------------------------------------
;304,仕事手伝い
;-------------------------------------------------
@MESSAGE_COM304
IF FLAG:70
	IF CFLAG:TARGET:350 == 46
		PRINTFORML %CALLNAME:PLAYER%はぴくりとも動かない%CALLNAME:TARGET%と遊んでみた
		PRINTL ……自分は一体何をやっているのだろう
	ELSEIF CFLAG:TARGET:350 == 41 || TCVAR:手伝えない == 2
		PRINTFORML %CALLNAME:PLAYER%は時間を止めているうちに%CALLNAME:TARGET%の仕事を進めておこうとしたが、
		PRINTFORML よく考えたら時間停止中はできない仕事だった
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は時間を止めているうちに%CALLNAME:TARGET%の仕事を進めておいた
		IF TFLAG:193 == 1
			PRINTFORML 結構仕事を進められた気がする…
		ELSEIF TFLAG:193 == -1
			PRINTFORML あまり役に立てている気がしない…
		ENDIF
	ENDIF
ELSE
	IF CFLAG:TARGET:350 == 46
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の遊びに加わった
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の仕事を手伝った
		IF TFLAG:193 == 1
			PRINTFORML とても仕事がはかどったようだ…
		ELSEIF TFLAG:193 == -1
			PRINTFORML あまり手伝いにならなかったようだ…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;305,膝枕してもらう
;-------------------------------------------------
@MESSAGE_COM305
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に膝枕してもらった
IF TFLAG:193 == -5
	PRINTFORML %CALLNAME:MASTER%の頭を載せられた%CALLNAME:TARGET%は蛙が踏みつぶされたような声を上げた
	PRINTFORML %CALLNAME:MASTER%は慌てて頭をどけた
ELSEIF ABL:TARGET:親密 > 5
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の顔を覗き込みながら頭を優しく撫でている…
ELSEIF ABL:TARGET:親密 > 2
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の頭を優しく撫でている…
ELSE
	PRINTFORML %CALLNAME:TARGET%は身を縮めてじっとしている…
ENDIF
TFLAG:400 = RAND:2
SIF TFLAG:400 && (DATE_HITOGOMI() || WITH_MOB() == 9)
	CALL DATE_HITOGOMI_MESSAGE(CFLAG:PLAYER:現在位置)

;-------------------------------------------------
;306,お腹を撫でる
;-------------------------------------------------
@MESSAGE_COM306
PRINTFORM %CALLNAME:PLAYER%は
IF TALENT:TARGET:妊娠
	PRINTFORM %CALLNAME:PLAYER%の子を宿している
ELSEIF 充填率(TARGET,1) > 30
	PRINT たっぷり子種を植え付けた
ENDIF
PRINTFORML %CALLNAME:TARGET%のお腹をなでた
IF !FLAG:70
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の手に自分の手を愛おしげに重ねた
	ELSEIF TFLAG:193 == -1
		PRINTFORML %CALLNAME:TARGET%は露骨に不快そうだ
	ELSE
		PRINTFORML %CALLNAME:TARGET%は怪訝そうにしている…
	ENDIF
	TFLAG:400 = RAND:2
	SIF TFLAG:400 && (DATE_HITOGOMI() || WITH_MOB() == 9)
		CALL DATE_HITOGOMI_MESSAGE(CFLAG:PLAYER:現在位置)
ENDIF

;-------------------------------------------------
;307,頬をつまむ
;-------------------------------------------------
@MESSAGE_COM307
IF FLAG:70 == 1
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の頬に手を伸ばした…
	PRINTFORML 時間を止められた%CALLNAME:TARGET%は無抵抗のまま頬をつままれている…
ELSEIF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:PLAYER%は眠っている%CALLNAME:TARGET%の頬を指でつまんだ…
	PRINTFORML %CALLNAME:TARGET%は嫌そうに顔を背けた…
ELSE
	PRINTFORM %CALLNAME:PLAYER%は\@ CFLAG:TARGET:添い寝中 ? 添い寝する # \@%CALLNAME:TARGET%の頬に手を伸ばし
	IF TARGET == (NO:14)
		PRINTDATA
			DATAFORM ふにふにした…
			DATAFORM ぷにぷにした…
			DATAFORM むにむにした…
			DATAFORM ぐにぐにした…
			DATAFORM ぐいぐいした…
		ENDDATA
	ELSE
		PRINTL 指でつまんだ…
	ENDIF
	IF TALENT:恋慕
		PRINTFORML %CALLNAME:TARGET%は照れくさそうな表情を浮かべながらやり返してきた…
	ELSEIF ABL:親密 > 5
		PRINTFORML %CALLNAME:TARGET%もおずおずと%CALLNAME:PLAYER%の頬に手を伸ばした…
	ELSEIF ABL:従順 >= 5
		PRINTFORML %CALLNAME:TARGET%は照れくさそうな表情を浮かべながらも%CALLNAME:PLAYER%にされるがままになっている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は頬を膨らますと%CALLNAME:PLAYER%の手を払った…
	ENDIF
ENDIF

;-------------------------------------------------
;308,頬をつつく
;-------------------------------------------------
@MESSAGE_COM308
IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:PLAYER%は眠っている%CALLNAME:TARGET%の頬を指でつついた
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%は嬉しそうに微笑んでいる…
	ELSEIF TFLAG:193 == -1
		PRINTFORML %CALLNAME:TARGET%は嫌そうに顔を背けた…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%は添い寝する%CALLNAME:TARGET%の頬を指でつついた
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%はにやにやしながら仕返ししてきた…
	ELSEIF TFLAG:193 == -1
		PRINTFORML %CALLNAME:TARGET%は頬を膨らますと%CALLNAME:PLAYER%の手を払った…
	ENDIF
ENDIF

;-------------------------------------------------
;309,頭を撫でる
;-------------------------------------------------
@MESSAGE_COM309
IF FLAG:70 == 1
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の頭に手を伸ばした
	PRINTFORML 時間を止められた%CALLNAME:TARGET%は無抵抗のまま頭を撫でられている…
ELSEIF CHK_DATENOW(CFLAG:TARGET:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	PRINTFORML %CALLNAME:PLAYER%は\@ DATE_REMAINTIME() ? 隣を歩く # 隣に寄り添う \@%CALLNAME:TARGET%の頭を撫でた
	SELECTCASE TFLAG:193
		CASE 1
			PRINTFORML %CALLNAME:TARGET%はリラックスしたような表情を浮かべて目を細めた…
		CASE 0
			IF ABL:親密 > 5
				PRINTFORML %CALLNAME:TARGET%は照れくさそうな表情を浮かべながらもじっと%CALLNAME:PLAYER%に撫でられている…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は顔を赤らめて%CALLNAME:PLAYER%の手を払いのけた…
			ENDIF
		CASE -1
			PRINTFORML …少々馴れ馴れしくしすぎたようだ
	ENDSELECT
ELSEIF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:PLAYER%は眠っている%CALLNAME:TARGET%の頭を優しく撫でた
	IF ABL:親密 > 5
		PRINTFORML %CALLNAME:TARGET%は気持よさそうに笑っている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は嫌がる素振りもなく%CALLNAME:PLAYER%に撫でられている…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%は\@ CFLAG:TARGET:添い寝中 ? 添い寝する # 優しく \@%CALLNAME:TARGET%の頭を撫でた
	SELECTCASE TFLAG:193
		CASE 1
			PRINTFORML %CALLNAME:TARGET%はリラックスしたような表情を浮かべて目を細めた…
		CASE 0
			IF ABL:親密 > 5
				PRINTFORML %CALLNAME:TARGET%は照れくさそうな表情を浮かべながらもじっと%CALLNAME:PLAYER%に撫でられている…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は顔を赤らめて%CALLNAME:PLAYER%の手を払いのけた…
			ENDIF
		CASE -1
			PRINTFORML …少々馴れ馴れしくしすぎたようだ
	ENDSELECT
ENDIF
;-------------------------------------------------
;331,お茶を淹れる（薬入り）
;-------------------------------------------------
@MESSAGE_COM331
#DIM DYNAMIC 人数Ａ
#DIM DYNAMIC 人数Ｂ
#DIM DYNAMIC 加減
VARSET LOCAL
VARSET LOCALS
IF TCVAR:TARGET:媚薬 == 210
	LOCALS = 媚薬
ELSEIF TCVAR:TARGET:利尿剤 == 210
	LOCALS = 利尿剤
ELSEIF TCVAR:TARGET:惚れ薬 == 1
	LOCALS = 惚れ薬
ELSE
	LOCALS = 薬
ENDIF
IF !RAND:2
	人数Ａ = CHARANUM - 1
	人数Ｂ = 0
	加減 = -1
ELSE
	人数Ａ = 1
	人数Ｂ = CHARANUM
	加減 = 1
ENDIF

IF CFLAG:TARGET:行動 == 5
	PRINTFORML %CALLNAME:PLAYER%は休憩がてら%LOCALS%入りお茶を淹れて%CALLNAME:TARGET%\@ GET_TARGETNUM() ? 達 # \@に差し出した…
	FOR LOCAL,人数Ａ,人数Ｂ,加減
		SIF LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && (TCVAR:LOCAL:媚薬 == 210 || TCVAR:LOCAL:利尿剤 == 210 || TCVAR:LOCAL:惚れ薬 == 1)
			PRINTFORML %CALLNAME:LOCAL%は美味しそうにお茶を飲んでいる
			LOCAL:1 ++
			IF LOCAL:1 > 4 && GET_TARGETNUM() > 5
				PRINTFORML 配るのに手一杯で他の者達が飲んでるかまでは見て取れない
				BREAK
			ENDIF
		ENDIF
		IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && (TCVAR:LOCAL:媚薬 != 210 && TCVAR:LOCAL:利尿剤 != 210 && TCVAR:LOCAL:惚れ薬 != 1)
			PRINTFORML %CALLNAME:LOCAL%は首をかしげ、お茶に口をつけなかった
			LOCAL:2 ++
			IF LOCAL:2 > 4 && GET_TARGETNUM() > 5
				PRINTFORML 配るのに手一杯で他の者達が飲んでるかまでは見て取れない
				BREAK
			ENDIF
		ENDIF
	NEXT
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:PLAYER%はお茶を飲みながら休憩時間を楽しんだ…
	PRINTL
	ELSEIF TFLAG:193 == -1
		PRINTFORML %CALLNAME:TARGET%は物問いたげな視線をこちらに向け、お茶に口をつけなかった
	ELSE
		PRINTFORML %CALLNAME:TARGET%は美味しそうにお茶を飲んでいる
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%LOCALS%入りお茶を淹れて%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@に差し出した…
	FOR LOCAL,人数Ａ,人数Ｂ,加減
		SIF LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && (TCVAR:LOCAL:媚薬 == 210 || TCVAR:LOCAL:利尿剤 == 210 || TCVAR:LOCAL:惚れ薬 == 1)
			PRINTFORML %CALLNAME:LOCAL%は美味しそうにお茶を飲んでいる
			LOCAL:1 ++
			IF LOCAL:1 > 4 && GET_TARGETNUM() > 5
				PRINTFORML 配るのに手一杯で他の者達が飲んでるかまでは見て取れない
				BREAK
			ENDIF
		ENDIF
		IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && (TCVAR:LOCAL:媚薬 != 210 && TCVAR:LOCAL:利尿剤 != 210 && TCVAR:LOCAL:惚れ薬 != 1)
			PRINTFORML %CALLNAME:LOCAL%は首をかしげ、お茶に口をつけなかった
			LOCAL:2 ++
			IF LOCAL:2 > 4 && GET_TARGETNUM() > 5
				PRINTFORML 配るのに手一杯で他の者達が飲んでるかまでは見て取れない
				BREAK
			ENDIF
		ENDIF
	NEXT
	PRINTL
	IF TFLAG:193 == 1
		PRINTFORML %CALLNAME:TARGET%と%CALLNAME:PLAYER%はお茶を飲みながら楽しいひとときを過ごした…
	ELSEIF TFLAG:193 == -1
		PRINTFORML %CALLNAME:TARGET%は物問いたげな視線をこちらに向け、お茶に口をつけなかった
	ELSE
		PRINTFORML %CALLNAME:TARGET%は美味しそうにお茶を飲んでいる
	ENDIF
ENDIF
LOCAL:1 = 0
PRINTL 
FOR LOCAL,人数Ａ,人数Ｂ,加減
	IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && (TCVAR:LOCAL:媚薬 == 210 || TCVAR:LOCAL:利尿剤 == 210 || TCVAR:LOCAL:惚れ薬 == 1)
		LOCAL:1 ++
		IF LOCAL:1 > 4 && GET_TARGETNUM() > 5
			PRINTFORML 他の者達の様子までは見て取れない
			BREAK
		ENDIF
		PRINTFORM %CALLNAME:LOCAL%は
		IF TCVAR:TARGET:媚薬 == 210
			PRINTFORML 頬を紅潮させ妙に熱っぽい吐息を漏らしている
		ELSEIF TCVAR:TARGET:利尿剤 == 210
			PRINTFORML 僅かに内股になり、もじもじと腰を揺らしている
		ELSEIF TCVAR:TARGET:惚れ薬 == 1
			PRINTFORML うっとりとした表情でじっとこちらを見つめている
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;333,愚痴を聞く
;-------------------------------------------------
@MESSAGE_COM333
PRINTFORML %CALLNAME:TARGET%の愚痴を聞いてやった
SELECTCASE TFLAG:193
	CASE 1
		PRINTFORML しゃべっているうちにまた怒りがぶり返してきたようだ…
	CASE 2
		PRINTFORML 言いたいことを言って落ち着いたようだ
	CASE 3
		PRINTFORML 一通り吐き出した%CALLNAME:TARGET%は、大人しく聞いてくれた%CALLNAME:MASTER%に礼を言った
	CASE 4
		PRINTFORML %CALLNAME:TARGET%はますます理不尽な絡み方をしてきた…
	CASE 5
		PRINTFORML 自分で何を言っているかわかっているのかも怪しい…
		PRINTFORML 酔っ払いの話を真面目に聞いても無駄のようだ
	CASE 6
		PRINTFORML %CALLNAME:MASTER%が適当に相槌を打っていると、なにかツボにはまったのか%CALLNAME:TARGET%は急に笑い出した
		PRINTFORML よく分からないが楽しそうなので良しとしよう
ENDSELECT

;-------------------------------------------------
;334,杯を交わす
;-------------------------------------------------
@MESSAGE_COM334
IF CFLAG:TARGET:妊娠自覚状態 == 1
	PRINTFORML %CALLNAME:PLAYER%は妊娠中で酒を控えている%CALLNAME:TARGET%の皿に料理を盛りながら、一緒に食事を楽しんだ
ELSEIF TALENT:TARGET:酒耐性 == -2
	PRINTFORML %CALLNAME:PLAYER%は酒が苦手な%CALLNAME:TARGET%の皿に料理を盛りながら、一緒に食事を楽しんだ
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の杯に酒を注ぎながら、一緒に酒を飲んだ
ENDIF
SELECTCASE TFLAG:193
	CASE 1
		PRINTFORML %CALLNAME:TARGET%はとても楽しそうだ…
	CASE -1
		PRINTFORML ほとんど会話が続かなかった…
ENDSELECT

;-------------------------------------------------
;335,介抱する
;-------------------------------------------------
@MESSAGE_COM335
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を介抱した
SELECTCASE TFLAG:193
	CASE 1
		PRINTFORML %CALLNAME:TARGET%の酔いをいい感じに醒ますことができたようだ…
	CASE -1
		PRINTFORML あまり%CALLNAME:TARGET%の助けにはならなかったようだ…
ENDSELECT

;-------------------------------------------------
;352,告白する
;-------------------------------------------------
@MESSAGE_COM352
IF TFLAG:193
	PRINTFORML %CALLNAME:PLAYER%は意を決して%CALLNAME:TARGET%に恋情を伝えた…
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に告白しようとしたが言葉が口から出てこない…
ENDIF

;-------------------------------------------------
;353,イタズラする
;-------------------------------------------------
@MESSAGE_COM353
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を起こさぬようにゆっくりと近づいた…

;-------------------------------------------------
;354,添い寝する
;-------------------------------------------------
@MESSAGE_COM354
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の隣に横になった…
IF CFLAG:TARGET:衰弱 || CFLAG:TARGET:お招き
	PRINTFORML %CALLNAME:TARGET%は小さく身をよじった…
ELSE
	PRINTFORML %CALLNAME:TARGET%は目を覚ますと嬉しそうに%CALLNAME:PLAYER%を迎え入れた
ENDIF

;-------------------------------------------------
;355,時間停止
;-------------------------------------------------
@MESSAGE_COM355
PRINTFORML ザ・ワールド！！　時よ止まれ！

;-------------------------------------------------
;360,身を任せる
;-------------------------------------------------
@MESSAGE_COM360
SELECTCASE TCVAR:20
CASE 27
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の谷間をじっくりと視姦した……
CASE 32
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の谷間に顔を埋め、甘い香りを吸い込んだ……
CASE 35
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の形の良い尻をじっくりと観賞した……
CASE 53
	PRINTFORML %CALLNAME:PLAYER%はパイズリしている%CALLNAME:TARGET%の頭を撫でてあげた
	IF TALENT:TARGET:恋慕 || TALENT:TARGET:思慕
		PRINTFORML %CALLNAME:TARGET%は嬉しそうに目を細め、より一層激しく胸で奉仕してきた……
	ELSEIF TALENT:TARGET:愛欲 || TALENT:TARGET:セフレ
		PRINTFORML %CALLNAME:TARGET%は苦笑して、より一層激しく胸で責め立てて来た……
	ELSE
		PRINTFORML %CALLNAME:TARGET%は照れくさそうに笑いながらペニスを扱いている……
	ENDIF
CASE 73
	SELECTCASE RAND:3
	CASE 0
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の乳房に包まれて幸せな一時を過ごした……
	CASE 1
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の谷間に挟まれ、甘い香りを堪能した……
	CASEELSE
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に乳房に顔を擦り付けて甘えた……
	ENDSELECT
CASE 77
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の%NIPPLE_DESCRIPTION(TARGET)%のコリコリした感触を味わった……
CASE 85,87 TO 89
	PRINTFORML %CALLNAME:TARGET%は必死に腰を振って%CALLNAME:PLAYER%に奉仕している……
CASE 86
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に抱きつかれたまま膣肉の感触を楽しむ事にした
	PRINTFORML 辺りに水音と肉がぶつかり合う音と%CALLNAME:TARGET%の喘ぎ声が響いている……
CASEELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に身を任せた…
ENDSELECT

;-------------------------------------------------
;363,読み聞かせ
;-------------------------------------------------
@MESSAGE_COM363
IF CFLAG:TARGET:添い寝中
	PRINTFORML %CALLNAME:PLAYER%はふとんの中で本を読んでやった…
		SIF CFLAG:TARGET:睡眠
			PRINTFORML %CALLNAME:TARGET%は寝息をたてている…
ELSEIF CFLAG:MASTER:現在位置 == 9
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を膝の上に乗せながら本を読んでやった…
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に本を読んでやった
ENDIF

;-------------------------------------------------
;364,豆まき
;-------------------------------------------------
@MESSAGE_COM364
SIF TFLAG:193 > 1
	PRINT みんなで
PRINTFORML %CALLNAME:TARGET%に豆をぶつけた
PRINTW …
PRINTW ……
PRINTW ………福が来た気がする！

;-------------------------------------------------
;380,腹筋させる
;-------------------------------------------------
@MESSAGE_COM380
PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の両足を押さえ腹筋運動をさせた
SIF TALENT:バストサイズ >= 1
	PRINTFORML 眼前に迫るおっぱいは圧巻の一言だ……

;-------------------------------------------------
;381,腕立て伏せさせる
;-------------------------------------------------
@MESSAGE_COM381
IF TFLAG:193 > 0
	PRINTFORML %CALLNAME:TARGET%に腕立て伏せをさせた
	PRINTFORML %CALLNAME:TARGET%は胸が床につかないよう慎重に腕立て伏せをしている……
ELSEIF TFLAG:193 == 0
	PRINTFORML %CALLNAME:TARGET%に腕立て伏せをさせた
	PRINTFORML 首筋を伝う汗が艶めかしい……
ELSE
	PRINTFORML %CALLNAME:TARGET%に腕立て伏せをさせた
	PRINTFORML %CALLNAME:TARGET%は深く深く腕を曲げ腕立て伏せをしている……
ENDIF
