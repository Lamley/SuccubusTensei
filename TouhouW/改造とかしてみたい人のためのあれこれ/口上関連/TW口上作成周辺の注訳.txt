;------------------------------------------------------------
;【TW口上作成周辺の注訳】
;目的 口上作成におけるエロ文章書く能力以外の障壁を撤廃する
;------------------------------------------------------------
@口上書くにはどうしたらいいの？_212_777
;地の文を表示せよ。頭に;を打つとコメントアウトつまり無効化できる
;CALL TRAIN_MESSAGE

;下記の@口上書くにはどうしたらいいの？_212_777を実行する事。見つからなかったらエラー落ち。
CALL 口上書くにはどうしたらいいの？_212_777_1
;以上。上記を以って真とし、以下を実行せず切り上げる事。
RETURN 1

;↓この箇所は見やすいように付け加えられてるだけです。
;------------------------------------------------------------
;概要 CALL 口上書くにはどうしたらいいの？_212_777_1の実際の中身
;------------------------------------------------------------
;				名称が被らない工夫↓実際にはキャラ番号_コマンド番号_派生番号のようになっている
@口上書くにはどうしたらいいの？_212_K777_1

;条件を通らずに表示される箇所
PRINTFORML 口上書くに難になりそうな箇所にコメントを書いてみる。

;装備を見る事のエディタ偽。これ真の場合、下記を表示し、ELSEIF ELSEの箇所は飛ばしENDIFまで進む事。
IF !EQUIP:エディタ
	PRINTFORML 【１　ツール関連】
	PRINTFORML メモ帳で書こうとするのは不可能と思っていいです
	PRINTFORML メモ帳とエディタでは作業時間に徒歩と自動車ほどの差が出ます
	PRINTFORMW サクラエディタよりもいいテキストエディタはあるものの初期設定方法が用意されていて楽なのでサクラエディタをお勧めします
	
	;ここで数値を上昇させても今回は反映されず、下のELSEIF ABL:読み手:構文 < 1の箇所は通ってくれない
	;!EQUIP:エディタという条件に影響しない。上限設定が無いので青天井に上がってしまう。不具合が出ます。
	
	;構文技能に加える事の１
	ABL:読み手:構文 ++
	
;読み手の構文技能が1未満。この条件に照らし、これ真なら（真にはマイナスも含まれる）下記を表示しENDIFまで進む事。
ELSEIF ABL:読み手:構文 < 1
	PRINTFORML 【２　テンプレのどこに書けばいいかわからない】
	PRINTFORMW やってみるのが一番！熟読するより１０倍速い
	PRINTFORML \改造とかしてみたい人のためのあれこれ\口上関連\口上テンプレ\M_KOJO_KX_日常系コマンド.ERB
	PRINTFORMW これをコピーしてERBフォルダに入れる
	PRINTFORML 7行目12行目15行目のKXをK11に書きかえる（キャラ番号について詳しくはCVS参照の事）
	PRINTFORMW 18行目のLOCAL = 0の0を真（0以外の数値、基本的に1）に書きかえる
	PRINTFORML 22行目の空欄に下記をコピペ

	PRINTFORMW 「だぜだぜ」
 
	PRINTFORML 魔理沙と会話すると喋るようになるよ！
	EXP:読み手:口上作成経験 ++
	
;上記以外で２の場合。　イコールなら == 数値を使う。 = 数値では数値を上書きせよの意。
ELSEIF ABL:読み手:構文 == 2
	PRINTFORML 【３プログラム的な何かがわからない】
	PRINTFORMW これだけでok
	IF 条件もしもだぜならば
		PRINTFORML 「だぜっだぜっ」
		;以上
		RETURN 1
	;IFとENDIFはワンセット
	ENDIF
	PRINTFORML SIF PRINTDATA SELECTCASE \@ 条件文 ? 真 # 偽 \@
	PRINTFORML よく使うこれらは全部IF文やIF RAND:値 の変形だと思えば大体合ってるので無理に使わなくていいです
	PRINTFORML こだわらなければ簡単　こだわればキリがない

	;IF文の例 上流の条件満たすと下流は通らないので上流に困難な条件を置く事
	;もしも空腹　かつ　うどん欠乏フラグ或いはうどん中毒のどれか一つでも真ならば
	IF CFLAG:空腹 && (CFLAG:うどん欠乏フラグ || TALENT:うどん中毒)
		PRINTFORML そんなことよりおうどんたべたい
	;うどん欠乏フラグとうどん中毒どちらも満たしていない状態での空腹
	ELSEIF 空腹
		PRINTFORML 「ごはん」
		PRINTFORMW %CALLNAME:TARGET%は雛鳥の様にちょこんと座り%CALLNAME:MASTER%のご飯を待っている
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%に海苔の佃煮を塗りたくった
	;それ以外 ここではお腹すいてない
	ELSE
		;何も書かずにスルーして打ち切る
		;RETURN 0は口上なんてなかった。　RETURN 1は口上はあった。（けど空欄だった）の意
		RETURN 1
	ENDIF

;上記以外で３以上かつ3以下の場合
ELSEIF ABL:読み手:構文 >= 3 && ABL:読み手:構文 <= 3
	PRINTFORML 【４　略語が判らない】
	PRINTFORML LOCALはLOCAL:0 IF LOCAL LOCAL:0が真ならばの略　CFLAG:うどん欠乏フラグはCFLAG:TARGET:うどん欠乏フラグの略
	PRINTFORML 口上はそのキャラの場合のみ表示されるので相手が「魔理沙の場合」などの条件は不要
	PRINTFORMW 引数の省略や()空の括弧などは更に複雑になるのでここでは触れません

;上記以外で４以下の場合。 構文技能を見る。上流で１を見落としているので１と４が該当
ELSEIF ABL:読み手:構文 <= 4
	PRINTFORML 【５　条件設定がわからない どれを指定すればいいかわからない どんな条件が存在するかわからない】
	PRINTFORMW \ERB\口上・メッセージ関連にある地の文の物を見るのが一番楽です 楽な事は良い事です
	PRINTFORML 
	PRINTFORML 他の手段としては、他の口上を覗いて参考にする＞grepする＞ERB\コマンド関連\COMABLEのコマンド実行条件を参考にする＞CVSフォルダを見る＞資料フォルダ\変数一覧を見る等もあります。
	PRINTFORML 
	PRINTFORML TALENT:処女　ABL:Ｖ感覚　EXP:Ｖ経験
	PRINTFORMW などなど、新品の穴　にも色々ありますが表記は統一しておいた方が収拾がつきやすいです
	PRINTFORMW ＴＷにおいてはTALENT:処女には-1無自覚非処女が含まれます
	
;上記以外で５以上 レベル上限が５でないバリアントでは範囲が広すぎて困った事に
ELSEIF ABL:読み手:構文 >= 5
	PRINTFORML 【６　LOCAL？ARG？CALL？CFLAG？】
	PRINTFORML 口上を書く分には関わりがあるものの長くなるのでCFLAGだけ
	CFLAG キャラクターフラグ 自力で設定し自力で設置して使います
	フラグ はチェックポイントを通過した印だと思って下さい
	;CFLAG:1234を妖夢口上うどん欠乏フラグに使うよ
	;１口上用の ２３二十三番 魂魄妖夢ちゃん用の ４五番目のフラグ（ゼロも使えるのでカウントする）
	;うどん欠乏イベント これが偽ならここを通るよ
	IF CFLAG:TARGET:1234 == 0
		PRINTFORML そんな事よりおうどん食べに行きませんか？
		PRINTFORML %CALLNAME:TARGET%の目が赤い……
		PRINTFORMW %CALLNAME:TARGET%のおうどんスイッチが入った！
		;フラグを立てるよ
		CFLAG:TARGET:1234 = 1
	ENDIF
	
ELSE
	PRINTFORML 条件にも穴はある\@ 魔理沙 ? んだぜ # のぜ \@……
ENDIF

	PRINTFORML ここまでにRETURNされてないならここも表示されるよ 次の@まで実行されるよ
	;あなたが素質清掃中毒を所持　SIFはENDIFを省略できるIF文
	SIF TALENT:MASTER:清掃中毒
		PRINTFORML そんなことよりおそうじしたい
RETURN 1
;-----------------------------------------------------------
@サンプル会話口上

CALL TRAIN_MESSAGE
CALL サンプル会話口上_1
RETURN 1

@サンプル会話口上_1
;時間停止中などは喋らせず打ち切る
SIF !SHIRAHU(TARGET)
	RETURN 1
IF MARK:反発刻印
	PRINTFORMW 「%CALLNAME:MASTER%って本当に最低の屑ね」
	RETURN 1
ENDIF
IF TALENT:恋慕
	IF TALENT:処女 == -1
		PRINTFORMW 「そろそろ普通にし…」
		PRINTFORMDW %CALLNAME:MASTER%はその言葉を遮り却下した
		PRINTFORMDW 非合意セックスに拘りのある%CALLNAME:MASTER%であった……
	ELSE
		PRINTFORMDW 甘過ぎずべとつかずそれでいてコクのあるちゅっちゅ！
	ENDIF
ELSE
	IF TALENT:体型 < 0
		PRINTFORMDW %CALLNAME:MASTER%が幼女に話しかける事案が発生
		PRINTFORML 
	ENDIF
	;五種均等の確率で分岐
	;		↓PRINTDATA後の改行の指示は忘れがち
	PRINTDATAL
		DATAFORM １　フランちゃんウフフ
		DATAFORM ２　ふらんちゃんうふふ
		DATALIST
			DATAFORM ３－１　フ
			DATAFORM ３－２　ラ
			DATAFORM ３－３　ンちゃんウフフ
		ENDLIST
		DATALIST
			DATAFORM ４－１　フランちゃんウ
			DATAFORM ４－２　　　　　　　フ
			DATAFORM ４－３　　　　　　　フ
		ENDLIST
		DATALIST
			DATAFORM ５－１　フランち
			DATAFORM ５－２　　　　ゃ
			DATAFORM ５－３　フフウん
		ENDLIST
	ENDDATA　
ENDIF
RETURN 1