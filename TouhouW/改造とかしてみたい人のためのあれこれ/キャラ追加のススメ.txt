関数を書き直してキャラ追加の手間をかなり減らしたので、キャラ番号58の美鈴を例に、簡単なキャラ追加方法を説明します。

------------------------------------------
①CSVを作ろう
-------------------------------------------
まずはこれがなければ始まらない、CSVファイルです。CSVフォルダに入ってるCharaXX.csvって奴ですね。

番号,58,　　　　　　　←キャラ番号です。現在62までいるので、新たに追加するなら63からになります。
名前,紅美鈴,　　　　　←フルネームです。
呼び名,美鈴,　　　　　←呼び名です。普段はこっちで呼ばれます。
基礎,0,2500
基礎,1,2300　　　　　←基礎0と1はそれぞれ体力と気力です。
基礎,5,1500
基礎,10,1500
基礎,11,1000
基礎,12,1000
基礎,14,3000　　　　　←5、10~14はそのままコピペしてください。

               素質・経験は基本的に既存の紅魔系バリアントからコピペして大丈夫です。

素質,2,1 　　　　　　　←この素質,2,1を忘れると女じゃなくなるので注意
素質,20,
素質,25,
素質,40,-1
素質,62,
素質,105,1
素質,130,1
素質,135,
能力,0,1
能力,1,1
能力,3,1
能力,10,1
能力,13,1
能力,17,1
能力,42,3
経験,1,30
経験,10,5
経験,22,20
経験,51,50
経験,53,1
経験,74,30
フラグ,7,158;服装　　　←服装はCLOTHESファイルで決定。とりあえずキャラ番号+100にしておきましょう。
フラグ,310,100;地位　　　←大して意味のあるステじゃないので適当でいいです。
フラグ,311,99;開始位置　　←神社の住人でなければ99で。
フラグ,来訪時間,540
フラグ,帰宅時間,1080　　　←テクノロジーの進歩によりもはやCHARAMOVEにややこしい条件を書き込む必要はなくなった。来訪・帰宅時間を分で設定。
フラグ,就寝時間,0
フラグ,起床時間,360　　　←こっちは離れに引っ越してきた時用の就寝・起床時間。
フラグ,よく行く地域,3　　←（1=寺2=里3=館4=竹5=森6=湖7=山8=守9=地）。めーりんは当然3。
フラグ,自宅位置,340　　　←紅魔館正門を指定。場所の番号はDIM.ERHに地名一覧があるので、場所に対応した番号を入力。この場合だと#DIM CONST 正門 = 340。
相性,15,200;咲夜　　　　←相性が良い/悪いキャラを設定,デフォルトは100（高いほど良い）
CSTR,仕事情報,門番　　　←仕事内容や勤務時間
CSTR,職場,正門　　　　　←文字は自由に決めて良い
CSTR,10,～華人小娘～　●種族：妖怪　●能力：気を使う程度の能力　　　←CSTR,XX,で(XX-9)行の紹介が書ける(XXは20まで)
-------------------------------------------
②お次はCHARAMOVE
-------------------------------------------
worldにキャラを追加する上で最も面倒くさいのがMOVEMENTSフォルダにある@CHARAMOVE_DATA_Xの設定。
既存キャラのを改変しようにも元のがゴチャゴチャしてて分かりにくい、分かってはいても書くのが面倒くさい！
しかし文明の進歩はこの負担を大幅に軽減した。文明は日進月歩なのだ。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
新CHARAMOVEテンプレ　XXにキャラ番号を代入してちょっと手を加えれば使えます。

@CHARAMOVE_DATA_XX(ARG,ARG:2)
SELECTCASE ARG
	;移動対象メンバーチェック
	CASE 1
		;休日中遭遇チェック
		IF !FLAG:(XX + 590)
			CFLAG:XX:現在位置 = 99
		ELSE
			CFLAG:XX:現在位置 = FLAG:(XX + 590)
		ENDIF
		CFLAG:(NO:XX):同行 = 0
		RETURN 0
	;移動確率
	CASE 2
		;悪天候時、外なら移動しやすく
		SIF OUTROOF(CFLAG:(NO:XX):現在位置) && ((TIME:5 & 4) || (TIME:5 & 8))
			RETURN 10
		RETURN 90
	;起床時間帯
	CASE 3
		RETURN AWAKE(XX)
	;移動傾向
	CASE 4
		SELECTCASE ARG:2
			;風呂（入らない風呂は消す）
			CASE 13,24,30,39
				IF CFLAG:(NO:XX):風呂 < 1000
					RETURN 0
				ELSE
					RETURN 300
				ENDIF
			;虫かご
			CASE 54
				RETURN 0
			;まんべんなく移動する
			CASEELSE
				RETURN 80
		ENDSELECT
	;仕事時間帯
	CASE 5
		RETURN 0
	CASE 6
		;行く地域（1=寺2=里3=館4=竹5=森6=湖7=山8=守9=地）
		SELECTCASE ARG:2
			CASE Y
				RETURN 1
			CASEELSE
				RETURN 0
		ENDSELECT
	CASE 7
		;出現場所
		SELECTCASE ARG:2
			CASE ZZ
				RETURN 1
			CASEELSE
				RETURN 0
		ENDSELECT
ENDSELECT
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
めーりんを例に解説。
;めーりん
@CHARAMOVE_DATA_58(ARG,ARG:2)
SELECTCASE ARG
	;移動対象メンバーチェック
	CASE 1　　　　　　　　　　　　　　　　　　　　　←MOVEMENTファイルを書き換えたのでCASE 1では休日中遭遇設定だけでおｋ。
		;休日中遭遇チェック　　　　　　　　　　　テンプレにキャラ番号を代入するだけで、Ⅰ来訪フラグが立ってない時40%～60%の確率で遭遇フラグFLAG:(XX + 590)が立ち、
		IF !FLAG:648　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　 Ⅱ25%で自宅位置に出現、自宅にいない場合さらに33%でよく行く地域のどこかに出現、
			CFLAG:58:現在位置 = 99　　　　　　　　　　　　　　　　　　　　　　　　　　 Ⅲそうでない場合行く地域のどこかにランダムで出現
		ELSE
			CFLAG:58:現在位置 = FLAG:648　　　　　　　　　　　　　　　　　　　　　という設定が済むようになりました。違う挙動をさせたいなら自力で式書いて下さい。
		ENDIF
		CFLAG:(NO:58):同行 = 0
		RETURN 0
	;移動確率
	CASE 2
		;悪天候時、外なら移動しやすく
		SIF OUTROOF(CFLAG:(NO:58):現在位置) && ((TIME:5 & 4) || (TIME:5 & 8))
			RETURN 10
		RETURN 90　　　　　　　　　　　　　　←神社に滞在中の移動頻度の設定。数字が小さい程頻繁に動きます。
	;起床時間帯
	CASE 3
		RETURN AWAKE(58)　　　　　　　　←CFLAGで起床・就寝時間を設定しておけばCALL AWAKE(XX)だけでおｋ。
	;移動傾向
	CASE 4
		SELECTCASE ARG:2　　　　　　　　　　　　　　　　　　　　←CSVフォルダにあるStrファイルを参考に、場所ごとの移動傾向を変えられる。
			;風呂
			CASE 24　　　　　　　　　　　　　　　　　　　　←13=風呂,24=温泉,30=三月精宅の風呂,39=夢幻遺跡の風呂,下で入らない設定にするなら該当番号を消す。
				IF CFLAG:(NO:58):風呂 < 1000　　　　　←移動傾向に拘りがないならこれで十分。あるなら他のキャラを参考に改変。
					RETURN 0　　　　↑どれくらい頻繁に風呂に入るか、小さいほど頻繁に入る
				ELSE
					RETURN 300　　　←入る頃合いになった時どれくらいの確率で入るか、大きい方が入りやすい（300でも十分）
				ENDIF
			;ひとんちの風呂とかトイレにはいかない　　　　←移動傾向は他のキャラを参考にコピペでおｋ。ただし書く順番には気をつけること。
			CASE 12,13,14,18,30,35,39,40,51　　　　　　　←Strファイルを参考に。13,30,39はこっちで扱うので風呂の方は消す。
				RETURN 0
			;私室には入らない
			CASE 15,17,19,53,55
				SIF FLAG:15 == 58　　　　　　　　　←SIF FLAG:15 == XXは住み込んだ時の挙動。
					RETURN 40　　　　　　　　←この場合普段は入らないが住み込むと入るようになる。
				RETURN 0
			CASE 18,43,44,51,52　　　　　　　　　　　　　←Strファイルを参考に。18は「ひとんちの～」で書かれているが、結果が同じなのでこれで良い。
				RETURN 0　　　　　　　　　　　　　←こっちは住み込んでも入らないよってこと。
			;物置等にはあまり行かない
			CASE 6,17,19,51,53　　　　　　　　　　　　　←Strファイルを参考に。17,19,51,53は「私室には～」で書かれているが、先に書いてある方が優先される。
				RETURN 30　　　　　　　　　　　　　つまり6以外は実質意味が無い（これはコピペなので被っているだけ）
			;ボロ小屋の時は行かない
			CASE 16
				SIF FLAG:15 == 58　　　　　　　　　←住み込んだ時は自分の小屋なので入る。
					RETURN 80
				RETURN 0
			;三月精の家と遺跡には行かない
			CASE 26 TO 51　　　　　　　　　　　　　　　　← Y TO Z でY～Zまでの範囲をまとめられる。
				RETURN 0　　　　　　　　　　　　　　　Y～Z間に風呂や物置など、先に書いたものがあるとそちらが優先されるので、そういうのは先に書いておく。
			;虫かご
			CASE 54　　　　　　　　　　　　　　　　　　　←虫かごは入らないようにしてください。
				RETURN 0
			;まんべんなく移動する
			CASEELSE
				RETURN 80
		ENDSELECT
	;仕事時間帯
	CASE 5
		IF !CHARA_HOLIDAY(NO:58) && BASE:(NO:58):仕事量 && TIME >= 360　　　←仕事があるのでテンプレから改変。詳しくは別記仕事解説を参照。
			RETURN 1
		ELSE
			RETURN 0
		ENDIF
	CASE 6
		;行く地域（1=寺2=里3=館4=竹5=森6=湖7=山8=守9=地）
		SELECTCASE ARG:2
			CASE 3,6　　　　　　　　　　　　　　　　　　　　　　←紅魔館と霧の湖に出現する
				RETURN 1
			CASEELSE
				RETURN 0
		ENDSELECT
	CASE 7
		;出現場所
		SELECTCASE ARG:2
			CASE 31 TO 35,61,62,63　　　　　　　　　　　　　　←Strファイルの6000番台を参考に、下二桁が場所にあたる
				RETURN 1　　　　　　　　　　　　　　　　　31～35（紅魔館の地下室以外）と61～63（霧の湖の廃洋館以外）に出現
			CASEELSE
				RETURN 0　　　　　　　　　　　　　　　　←※行く地域/よく行く地域で指定した地域内から必ず１つは出現するようにしないとエラーが出るので注意！
		ENDSELECT
ENDSELECT

-------------------------------------------
③オサレ
-------------------------------------------
CLOTHESファイルで服装を指定できます。
普段着の@CLOTHES_SETTING(ARG)とパジャマの@CLOTHES_SETTING_NIGHT(ARG)がありますが、
後者は指定しなくても全裸になったりしないので前者だけで十分。
美鈴はこんな感じ。

	CASE 158
		EQUIP:ARG:帽子 = 1
		EQUIP:ARG:靴 = 1
		EQUIP:ARG:靴下 = 1
		EQUIP:ARG:下半身下着２ = 今日のぱんつ(ARG)
		EQUIP:ARG:上半身下着１ = 1
		EQUIP:ARG:ズボン = 1
		EQUIP:ARG:ワンピース = 4

服の種類について詳しく書きたいならCLOTHESファイルの下の方を弄りませう。

-------------------------------------------
④アプデとか
-------------------------------------------
せっかく色々設定しても、era内でキャラを追加する処理をしなければキャラが出てきません。
SHOP関連フォルダのUPDATEファイルにこれを追記。

SIF GETCHARA (XX) == -1
	ADDCHARA XX

CSVに書いたCFLAGはニューゲームでないと反映されないので、UPDATEファイルにこれも追記する。

CFLAG:XX:よく行く地域 = X
CFLAG:XX:自宅位置 = X
CFLAG:XX:来訪時間 = X
CFLAG:XX:帰宅時間 = X
CFLAG:XX:起床時間 = X
CFLAG:XX:就寝時間 = X

そしてキャラメイク.ERBの24行目あたり、

;EX_APPEND_CHARACTER
FOR LOCAL,1,103
ADDCHARA LOCAL
NEXT

これの103のところをキャラを追加しただけ増やす。

-------------------------------------------
⑤AA追加
-------------------------------------------
小傘とか一部キャラだけステータス画面にAA出てずるい！！！！！１
それは口上を制作して貰って嬉しいので贔屓しているからです。あなたもお気に入りのキャラを贔屓してAAをつけましょう。
※意訳：面倒くさくて全員分なんかやってらんねーから誰かやってくれ

ステータス表示関連フォルダのAAファイルの
@AA有無(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 28,40,49,63
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

となっているのは、キャラ番号28,40,49,63にはAAがあるので表示しますという意味です。
よって加筆してCASE 28,40,49,63,XXにします。

続いて@PRINT_AA(ARG)に

CASE XX
（そのキャラのAAコピペ）
を加筆し、AAのすべての行の頭にPRINTL(+半角スペース)をつければAAが表示されるようになります。