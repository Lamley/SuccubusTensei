@TURN_RESET
;処女喪失
FOR LOCAL, 0, CHARANUM
	SIF !TCVAR:LOCAL:破瓜
		CONTINUE
	IF TCVAR:LOCAL:破瓜 == 1
		IF TALENT:LOCAL:処女 == 1
			TALENT:LOCAL:処女 = 0
			EX:LOCAL:処女喪失チェック = 1
		ENDIF
		IF FLAG:時間停止 || TCVAR:LOCAL:泥酔 || CFLAG:LOCAL:睡眠
			TALENT:LOCAL:処女 = -1
			EX:LOCAL:処女喪失チェック = 1
			CFLAG:292 = 1
		ENDIF
	ENDIF
	IF TCVAR:LOCAL:破瓜 == 2 && TALENT:LOCAL:処女 == 2
		TALENT:LOCAL:処女 = 0
		EX:LOCAL:処女喪失チェック = 2
	ENDIF
	IF TCVAR:LOCAL:破瓜 == -1 && TALENT:LOCAL:処女 == -1
		TALENT:LOCAL:処女 = 0
		EX:LOCAL:処女喪失チェック = 1
	ENDIF
NEXT

;童貞喪失
IF TALENT:MASTER:2 & 2
	SELECTCASE SELECTCOM
		CASE 60, 61, 65, 67, 68, 320, 322
			TALENT:MASTER:1 |= 1
		CASE 62, 63, 66, 69, 70, 323
			TALENT:MASTER:1 |= 2
	ENDSELECT
ENDIF
IF TALENT:2 & 2
	SELECTCASE SELECTCOM
		CASE 64
			TALENT:1 |= 1
		CASE 92, 93, 94
			TALENT:1 |= 2
		CASE 71
			IF MASTER_POSE(SET_V, 1, 1)
				TALENT:1 |= 2
			ELSE
				TALENT:1 |= 1
			ENDIF
	ENDSELECT
ENDIF

;既成事実
IF FLAG:時間停止 == 0
	FOR LOCAL, 1, CHARANUM
		SIF CFLAG:LOCAL:うふふ && !TCVAR:LOCAL:発情 && !TCVAR:LOCAL:媚薬 && !TCVAR:LOCAL:泥酔 && !CFLAG:MASTER:イタズラ && !TCVAR:LOCAL:無理矢理
			SETBIT CFLAG:LOCAL:既成事実 , 1
	NEXT
ENDIF
;キス成功
;合意取得
SIF (SELECTCOM == 312 || SELECTCOM == 602) && TFLAG:193 != 99 && !CFLAG:MASTER:イタズラ
		SETBIT CFLAG:既成事実, 0

;告白成功
;恋人取得
IF SELECTCOM == 352 && TFLAG:193
	SETBIT CFLAG:既成事実, 2
	SETBIT CFLAG:MASTER:既成事実, 2
	TALENT:MASTER:恋人 = NO:TARGET
	TALENT:恋人 = NO:TARGET
	;セフレ削除
	IF TALENT:TARGET:セフレ == 1
		TALENT:TARGET:セフレ = 0
	ENDIF
ENDIF

;食事
IF SELECTCOM == 414 || SELECTCOM == 415
	FLAG:料理 = 0
	DISHQUALITY = 0
	DISHTYPE = 0
	DISHNAME =
ENDIF
;調教履歴を1ターン保存
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	TCVAR:LOCAL:110 = TCVAR:LOCAL:10
	TCVAR:LOCAL:112 = TCVAR:LOCAL:12

	SIF TCVAR:LOCAL:102
		TCVAR:LOCAL:102 --
	SIF !TCVAR:LOCAL:102
		TCVAR:LOCAL:103 = -1
	SIF TCVAR:LOCAL:104
		TCVAR:LOCAL:104 --
	SIF !TCVAR:LOCAL:104
		TCVAR:LOCAL:105 = -1
	;体位の保存
	FOR LOCAL:1, 1, 11
		TEQUIP:LOCAL:(110 + LOCAL:1) = TEQUIP:LOCAL:(100 + LOCAL:1)
		TEQUIP:LOCAL:(LOCAL:1) = 0
	NEXT
	;体勢制御
	IF TCVAR:LOCAL:体勢制御
		TCVAR:LOCAL:体勢制御 --
		SIF TCVAR:LOCAL:体勢制御 == 0
			TCVAR:LOCAL:体勢 = 0
	ENDIF
	;位置関係制御
	IF TCVAR:LOCAL:位置関係制御
		TCVAR:LOCAL:位置関係制御 --
		SIF TCVAR:LOCAL:位置関係制御 == 0
			TCVAR:LOCAL:位置関係 = 0
	ENDIF
	;ついでに射精処理用文字列変数の初期化
	CSTR:LOCAL:10 = /
	CSTR:LOCAL:11 = /
	;服装フラグ
	IF FLAG:時間停止 == 1
	ELSE
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	ENDIF
	IF BATHROOM(CFLAG:LOCAL:現在位置) && GET_MAPID(CFLAG:LOCAL:現在位置) == MAIN_MAP
		;風呂フラグはMOVEMENT共通処理へ移動しました
;		CFLAG:LOCAL:風呂 = 0
		RESET_STAIN LOCAL
		;アヌスを洗う
		STAIN:LOCAL:4 = 0
		CFLAG:LOCAL:膣内射精 = 0
		CFLAG:LOCAL:アナル射精 = 0
		CFLAG:LOCAL:口に精液 = 0
		CFLAG:LOCAL:顔に精液 = 0
		CFLAG:LOCAL:手に精液 = 0
	ELSEIF CFLAG:LOCAL:風呂 > 120 && !CFLAG:LOCAL:うふふ
		STAIN:LOCAL:Ａ |= 汚れ_アナル
	ENDIF
NEXT
;BASEの変動
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	;勃起度
	;加算後
	CFLAG:LOCAL:勃起度２ = BASE:LOCAL:勃起
	BASE:LOCAL:勃起 = MIN(MAXBASE:LOCAL:勃起, BASE:LOCAL:勃起)
	IF NOWEX:LOCAL:射精
		IF !MASTER_POSE(SET_V, 1) && !MASTER_POSE(SET_A, 1)
			BASE:LOCAL:勃起 = BASE:LOCAL:勃起 * (ABL:LOCAL:欲望 + 1) * (BASE:LOCAL:精力 + 200) / ((MAXBASE:LOCAL:精力 + 200) * (ABL:LOCAL:欲望 + 10))
		ELSE
			BASE:LOCAL:勃起 = 1000
		ENDIF
	ENDIF
NEXT

SIF CFLAG:MASTER:うふふ
	CALL うふふ中特殊イベント

;フラグのリセット
TFLAG:193 = 0
TFLAG:194 = 0
TSTR:2 = 


;TCVARのリセット
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == SUKIMA()
		CONTINUE
	FOR LOCAL:1, 0, 100
		TCVAR:LOCAL:(LOCAL:1) = 0
	NEXT
;挿入履歴
	TCVAR:LOCAL:100 = TEQUIP:LOCAL:50
	TCVAR:LOCAL:101 = TEQUIP:LOCAL:51
NEXT

TFLAG:110 = TCVAR:MASTER:馴れ合い

;FIRSTTIMEにSELECTCOMをぶっこむ
CALLF FIRSTTIME(SELECTCOM)
FOR LOCAL, 1, CHARANUM
	SIF EXP_UP(15, LOCAL)
		CALLF FIRSTTIME("精飲", 0, LOCAL)
NEXT
;射精箇所
FOR LOCAL:1, 0, 60
	IF GETBIT(TFLAG:1, LOCAL:1)
		LOCALS = "射精"{LOCAL:1}
		CALLF FIRSTTIME(LOCALS, 0, TARGET)
	ENDIF
NEXT
PREVCOM = SELECTCOM
;派生コマンドの履歴
TFLAG:151 = TFLAG:50
;前回のTARGET
TFLAG:103 = TARGET
TFLAG:150 = ASSIPLAY

;経験値取表示用
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	;現在位置の保存
	CFLAG:LOCAL:前ターン位置 = CFLAG:LOCAL:現在位置
	FOR LOCAL:1, 0, 100
		TCVAR:LOCAL:(400 + LOCAL:1) = EXP:LOCAL:(LOCAL:1)
	NEXT
NEXT
IF BATHCHECK(CFLAG:MASTER:現在位置) && !FLAG:時間停止 && AT_HOME(MASTER)
	FOR LOCAL,1,CHARANUM
		SIF 風呂追い出し条件(LOCAL) && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
			CALL 風呂から追い出し(LOCAL)
	NEXT
ENDIF
;TFLAGのリセット
REPEAT 100
	TFLAG:COUNT = 0
REND
TFLAG:400 = 0
TFLAG:移動不能メッセージ = 0
;眠いと気力が減少していく
IF TIME_PROGRESS(TIME:3 + DAY * 1440) >= 960 && !CFLAG:MASTER:睡眠
	BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 3 * TIME_PROGRESS(TIME:1) , 0)
	BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 5 * TIME_PROGRESS(TIME:1) , 0)
ENDIF


@うふふ中特殊イベント
#DIM 奪主導権確率
;あなた主導
IF CFLAG:TARGET:うふふ == 1 && CFLAG:溜まってる度 > 300
;攻守逆転
	IF SHIRAHU(TARGET) && TEQUIP:TARGET:体位 == 3
		;強気なほど確率が高い→分母は小さい
		;1/確率
		奪主導権確率 = 15
		奪主導権確率 -= TALENT:TARGET:度胸 * 2
		奪主導権確率 -= TALENT:TARGET:応答 * 2
		奪主導権確率 -= TALENT:TARGET:プライド * 2
		奪主導権確率 += TALENT:TARGET:自制心 * 3
		奪主導権確率 -= TALENT:TARGET:性的興味 * 3
		奪主導権確率 += TALENT:TARGET:羞恥心 * 2
		奪主導権確率 -= TALENT:TARGET:サド * 5
		奪主導権確率 += TALENT:TARGET:マゾ * 5

		;RANDに渡すので最低値は1
		SIF 奪主導権確率 < 1
			奪主導権確率 = 1
		IF !RAND(奪主導権確率)
			CFLAG:TARGET:うふふ = 2
			CFLAG:MASTER:うふふ = 2
			TFLAG:COMABLE管理 = 3
			PRINTFORML 気が付くと、%CALLNAME:TARGET%に主導権を奪われていた
		ENDIF
	ENDIF
;相手主導
ELSEIF CFLAG:TARGET:うふふ == 2
	IF BASE:気力 <= 0
		PRINTFORML %CALLNAME:TARGET%はちょっと疲れたと言って%CALLNAME:MASTER%を解放した
		CALL 押し倒され終了("疲れた")
	ELSEIF CFLAG:溜まってる度 <= 0 && !TCVAR:TARGET:好きにして
		PRINTFORML %CALLNAME:TARGET%は満足したようだ
		CALL KIGEN_CHANGE(TARGET,100,1)
		BASE:怒り = 0
		CFLAG:不機嫌 = 0
		CALL ASK_M("抜け出す",1,"こっちはまだ満足してない",1,"もっと攻めて",ABL:サドっ気 >= 3)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%を解放した
					CALL 押し倒され終了("満足")
				CASE 1
					PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%を押し倒した
					CALL KOJO_MESSAGE_SEND("EVENT",30,TARGET,3)
					FOR LOCAL,0,CHARANUM
						CFLAG:(TARGET:LOCAL):うふふ = 1
						IF INROOM(CFLAG:MASTER:現在位置) == 2
							CALL DATUI_SHOES(LOCAL,0)
							CALL DATUI_SHOES(MASTER,0)
						ENDIF
					NEXT
				CASE 2
					PRINTFORML %CALLNAME:TARGET%は目を細め、それなら壊れるまでしてあげると囁いた
					CALL KOJO_MESSAGE_SEND("EVENT",30,TARGET,4)
					TCVAR:TARGET:好きにして = 1
			ENDSELECT
	ELSEIF OAZUKE(TARGET)
		PRINTFORML おあずけを食らった%CALLNAME:TARGET%は不満そうだ…
		SOURCE:反感 += 3000
		SOURCE:屈従 += 500
		SOURCE:恭順 -= 1500
	ENDIF
ENDIF

@風呂追い出し条件(ARG)
#FUNCTION
SIF FLAG:70
	RETURNF 0
SIF !GETBIT(TALENT:MASTER:2,1) && HETEROSEX(MASTER,ARG)
	RETURNF 0
SIF TALENT:ARG:羞恥心 < 0 && ABL:ARG:親密 >= 5
	RETURNF 0
SIF CFLAG:ARG:態度 < 2
	RETURNF 1
SIF CFLAG:ARG:既成事実 < 1
	RETURNF 1
SIF !TALENT:ARG:恋慕
	RETURNF 1

RETURNF 0

@OAZUKE(ARG)
#FUNCTION
SELECTCASE TCVAR:ARG:20
	CASE 41
	;正常位V
		SIF SELECTCOM != 320
			RETURNF 1
	CASE 43
	;バックV
		SIF SELECTCOM != 322
			RETURNF 1
	CASE 44
	;バックA
		SIF SELECTCOM != 323
			RETURNF 1
ENDSELECT
RETURNF 0