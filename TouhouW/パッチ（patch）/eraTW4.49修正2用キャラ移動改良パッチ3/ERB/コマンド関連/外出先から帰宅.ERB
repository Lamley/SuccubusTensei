@外出先から帰宅
#DIM DIST_TO_HOME
IF !FLAG:デート相手 && !FLAG:70 && BASE:MASTER:TSP >= TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3
	PRINTL TSPを消費して、時間経過なしで帰宅できます。
	PRINTL TSPを使いますか？
	PRINTL [0] いいえ
	PRINTFORML [1] はい（TSP{TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3}消費）
	INPUT
	IF RESULT == 1
		IF BASE:MASTER:TSP < TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3
			PRINTW TSPが足りなかった
		ELSE
			BASE:MASTER:TSP -= TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3
			GOTO WARP
		ENDIF
	ENDIF
ENDIF
IF !FLAG:70
	CALL 帰宅の時間経過
ELSEIF BASE:MASTER:TSP < TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3
	PRINTW TSPが足りないので時を止めたままは帰れない
	RETURN -1
ELSE
	BASE:MASTER:TSP -= TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) * 3
ENDIF

$WARP
PRINTFORMW \@ CFLAG:MASTER:デート中 != CFLAG:TARGET:デート中 || TARGET == MASTER ? 外出 # デート \@から帰って来ました
CALL 守矢くじデート相手のリセット()
FOR LOCAL,1,CHARANUM
	IF CHK_DATENOW(CFLAG:LOCAL:デート中)
		CALL DATE_EVENT(LOCAL)
		CALL SET_DATE(99,LOCAL)
	ENDIF
NEXT
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	CALL SET_DATE(99,MASTER)

CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)

@帰宅の時間経過
;残り時間算出
LOCAL = (GET_DATE_PLACE(CFLAG:MASTER:現在位置)) * 10
IF LOCAL && (TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) - LOCAL) > 0
	;道中の場合
	TIME = TIME + (TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000) - LOCAL)
ELSE
	;デート先到着済みの場合
	TIME = TIME + TIME_REQUIRED(CFLAG:MASTER:デート中 + 5000)
ENDIF
CALL 守矢くじデート相手のリセット()

@外出先から誘えるか
LOCAL = FLAG:デート相手
IF LOCAL != TARGET && LOCAL != MASTER
	PRINTFORM %CALLNAME:LOCAL%が%CALLNAME:MASTER%
	;気弱系
	IF 素質計算(LOCAL) == 1
		PRINTFORML の服の裾を掴んで首を横に振っている
	;強気系
	ELSEIF 素質計算(LOCAL) == 2
		PRINTFORML をぎらりと光る目で見つめてくる
	;素直系
	ELSEIF 素質計算(LOCAL) == 3
		PRINTFORML を見つめながら私だけを見て欲しいと告げてくる
	;真面目系
	ELSEIF 素質計算(LOCAL) == 4
		PRINTFORML にソレはダメだと口を出してくる
	;その他
	ELSEIF 素質計算(LOCAL) == 5
		PRINTFORML へため息を向けて、首を横に振った
	ENDIF
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%を誘うのを諦めた
	RETURN 0
ENDIF
IF TARGET == 93 && !ITEM:38
	PRINTW 彼女は連れて行けない…
	RETURN 0
ELSEIF BASE:TARGET:気力 < 500
	PRINTFORMW %CALLNAME:TARGET%は疲れているようだ
	RETURN 0
ELSEIF !CHARA_HOLIDAY(TARGET) && BASE:TARGET:仕事量 && TCVAR:TARGET:仕事開始 < TIME + 180
	PRINTFORMW %CALLNAME:TARGET%はこれから仕事がある
	RETURN 0
ELSE
	SETBIT FLAG:(50 + (TARGET) / 64),(TARGET) % 64
	RETURN 1
ENDIF