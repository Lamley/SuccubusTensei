;-------------------------------------------------
;移動
;-------------------------------------------------
@COM400
#DIM 入浴拒否
#DIM 虫かご
#DIM 忍び込み
#DIM 立ち止まり対象
#DIM BUTTON戻る
#DIM BUTTON切替左 
#DIM BUTTON切替中 
#DIM BUTTON切替右
#DIM 入れません
BUTTON戻る = MAXROOM + 1
BUTTON切替左 = MAXROOM + 2
BUTTON切替中 = MAXROOM + 4
BUTTON切替右 = MAXROOM + 4
入れません = 0

IF IN_HOME(TFLAG:195) != 1 && NEXTCOM != -1
	NEXTCOM = -1
	RETURN 0
ENDIF
;人数カウントをリセット
VARSET COUNTCHARA
;部屋の人数カウントを設定
FOR LOCAL,1,CHARANUM
	COUNTCHARA:(CFLAG:LOCAL:現在位置) ++
NEXT
入浴拒否 = 0
CALL TURN_RESET
;遠距離移動中の処理
IF TFLAG:195
	IF TFLAG:195 == CFLAG:MASTER:現在位置
		NEXTCOM = -1
		TFLAG:195 = 0
		DEBUGPRINTL 【COM400】RETURNPOINT A
		RETURN 0
	ENDIF
	IF !FLAG:70
		;キャラの位置更新(NEXTCOM設定中は@SHOW_STATUSが走らないのでこっちで処理)
		CALL CHARA_MOVEMENT
		TIME:1 = TIME + 1440 * DAY
		;CHARA_MOVEMENTで追い出された時にRETURNする
		IF TFLAG:195 == 0
			NEXTCOM = -1
			DEBUGPRINTL 【COM400】RETURNPOINT G
			RETURN 0
		ENDIF
	ENDIF
	;キャラをCFLAG:310の昇順に並び替え
	VARSET TARGET
	CALL SORT_CFLAG(310)
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在位置 != CFLAG:(RESULT:LOCAL):現在位置
			CONTINUE
		LOCAL:1 ++
		TARGET:(LOCAL:1) = RESULT:LOCAL
	NEXT
	立ち止まり対象 = 0
	;誰かいないかチェック
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):同行
			CONTINUE
		SIF TFLAG:運搬 == TARGET:LOCAL
			CONTINUE
		立ち止まり対象 = TARGET:LOCAL
	NEXT
	IF 立ち止まり対象 && CFLAG:MASTER:現在位置 != TFLAG:206
		PRINTFORMS NAME_FROM_PLACE(CFLAG:MASTER:現在位置)
		PRINTFORML で%CALLNAME:(立ち止まり対象)%\@ FLAG:立ち止まり ? をみかけた # と出会った \@
		IF FLAG:70 == 1
			RESULT = 0
			SIF FLAG:立ち止まり == 0
				RESULT = 1
			SIF FLAG:立ち止まり == 1
				TRYCALLFORM M_KOJO_K{NO:(立ち止まり対象)}
			IF RESULT
				PRINTL ……　どうしますか？
				PRINTL [1] 無視して移動する
				PRINTL [2] 立ち止まる
				INPUT

				IF RESULT == 1
				ELSE
					;実際は0以外全部立ち止まる扱い
					NEXTCOM = -1
					DEBUGPRINTL 【COM400】RETURNPOINT C
					RETURN 0
				ENDIF
			ENDIF
		;ELSEIF TARGET > 0 && RAND:2 && SHIRAHU(立ち止まり対象) && !CFLAG:(立ち止まり対象):パンツ2 && STRLENS(CSTR:MASTER:12) 
		ELSEIF TARGET > 0 && RAND:2 && SHIRAHU(立ち止まり対象) && !CFLAG:(立ち止まり対象):パンツ2 && FLAG:パンツ輸送
			CALL パンツ没収(立ち止まり対象)
			NEXTCOM = -1
			DEBUGPRINTL 【COM400】RETURNPOINT B
			RETURN 0
		ELSE
			RESULT = 0
			SIF FLAG:立ち止まり == 0
				RESULT = 1
			SIF FLAG:立ち止まり == 1
				TRYCALLFORM M_KOJO_K{NO:(立ち止まり対象)}
			IF RESULT
				PRINTL ……　どうしますか？
				PRINTL [0] 会釈して移動する
				PRINTL [1] 無視して移動する
				PRINTL [2] 立ち止まる
				INPUT
				IF RESULT == 0
					CALL KOJO_MESSAGE_SEND("EVENT",4,立ち止まり対象,2,0)
				ELSEIF RESULT == 1
				;無視する場合
				;何もしないでいいかな…無視されて怒らせる？
					;CALL KOJO_EVENT(10, 立ち止まり対象, 0)
					CALL KOJO_MESSAGE_SEND("EVENT",4,立ち止まり対象,3,0)
				ELSE
				;実際は0以外全部立ち止まる扱い
					;CALL KOJO_EVENT(10, 立ち止まり対象, 1)
					CALL KOJO_MESSAGE_SEND("EVENT",4,立ち止まり対象,1,0)
					NEXTCOM = -1
					DEBUGPRINTL 【COM400】RETURNPOINT C
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	
	LOCAL:1 = IN_ROOM_MEMBER("MIN", TFLAG:195, "CFLAG", 6)
;-------------------------遠距離移動------------------------------------------------------
	IF CAN_MOVE(CFLAG:MASTER:現在位置, TFLAG:195) & 1
		;トイレ
		IF IN_TOILET(TFLAG:195)
			CALL トイレ侵入(TFLAG:195,1)
		;浴場
		ELSEIF BATHCHECK(TFLAG:195) && LOCAL:1 && !CFLAG:MASTER:同行
			IF 風呂追い出し条件(LOCAL:1)
				PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				NEXTCOM = -1
				DEBUGPRINTL 【COM400】RETURNPOINT D
				RETURN 0
			ELSE
				CFLAG:MASTER:現在位置 = TFLAG:195
				;遠距離移動フラグ消去
				IF FLAG:70 == 1
					PRINTFORMW %CALLNAME:(LOCAL:1)%が入浴中だったようだ…
				ELSE
					PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%と目が合った…
					CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)
				ENDIF
				NEXTCOM = -1
			ENDIF
		;浴場連れだし
		ELSEIF BATHCHECK(TFLAG:195)
			FOR LOCAL, 1, CHARANUM
				IF 風呂追い出し条件(LOCAL) && CFLAG:LOCAL:同行
					PRINTFORMW %CALLNAME:LOCAL%とお風呂に入ろうとしたが断られた
					CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
					入浴拒否 ++
				ELSEIF LOCAL:1 && 風呂追い出し条件(LOCAL:1)
					PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
					CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
					NEXTCOM = -1
					DEBUGPRINTL 【COM400】RETURNPOINT D
					RETURN 0
				ELSEIF CFLAG:LOCAL:同行
					PRINTFORMW %CALLNAME:LOCAL%は一緒にお風呂に入ってくれそうだ
					CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
				ENDIF
			NEXT
			IF 入浴拒否
				NEXTCOM = -1
				DEBUGPRINTL 【COM400】RETURNPOINT D
				RETURN 0
			ELSE
				CFLAG:MASTER:現在位置 = (TFLAG:195)
				NEXTCOM = -1
			ENDIF
		;虫かご
		ELSEIF TFLAG:195 == 54
			CALL 虫かご侵入(TFLAG:195,1)
		ELSEIF LOCK:(TFLAG:195)
			IF FLAG:70
				FOR LOCAL, 1, CHARANUM
					IF CFLAG:LOCAL:311 == TFLAG:195 && CFLAG:LOCAL:睡眠
						PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
					ENDIF
				NEXT
				CFLAG:MASTER:現在位置 = (TFLAG:195)
				;遠距離移動フラグ消去
				NEXTCOM = -1
			ELSEIF !FLAG:70
				入れません = 0
				FOR LOCAL, 1, CHARANUM
					IF CFLAG:LOCAL:311 == TFLAG:195 && CFLAG:LOCAL:睡眠
						PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
					ELSEIF CFLAG:LOCAL:311 == TFLAG:195 && !CFLAG:LOCAL:睡眠
						PRINTFORML どうやら%CALLNAME:LOCAL%は眠っているようだ
						PRINTFORMW 少し物音も聞こえる…
					ENDIF
					SIF CFLAG:LOCAL:同行
						入れません = 1
				NEXT
				;同行中は忍び込めない
				IF 入れません
					NEXTCOM = -1
					DEBUGPRINTL 【COM400】RETURNPOINT E
					RETURN 0
				ENDIF
				PRINTL [0] 部屋に忍び込む
				PRINTL [1] やめておく
				$INPUT_SNEAK02
				INPUT
				IF RESULT == 0
					CALL 部屋に忍び込む(TFLAG:195,1)
				ELSEIF RESULT == 1
					NEXTCOM = -1
				ELSE
					CLEARLINE 1
					GOTO INPUT_SNEAK02
				ENDIF
			ENDIF
		ELSE
			CFLAG:MASTER:現在位置 = (TFLAG:195)
			;遠距離移動フラグ消去
			NEXTCOM = -1
		ENDIF
		;同行処理
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:LOCAL:同行
				CFLAG:LOCAL:現在位置 = CFLAG:MASTER:現在位置
		NEXT
	ELSE
		CALL FARMOVE(TFLAG:195)
		;現在位置を更新し、次も移動の扱いにする
		CFLAG:MASTER:現在位置 = RESULT
		NEXTCOM = 400
	ENDIF
	IF FLAG:70 == 1
		SIF TALENT:MASTER:100 < -4
		BASE:MASTER:TSP -= 15
		BASE:MASTER:TSP -= 5
		IF TFLAG:運搬
			BASE:MASTER:TSP -= 3
			CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
		ENDIF
	ELSE
		;小人同士で同行しててもいつも通りの速度になるのは小人の知恵のおかげだ
		SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
		TIME += 7
		TIME += 3
		IF CFLAG:93:同行 && !GETBIT (FLAG:プール, 0)
			TIME += 2
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5,500)
		ENDIF
	ENDIF
	DEBUGPRINTL 【COM400】RETURNPOINT F
	CALL MOVEMENT共通処理(MASTER)
	RETURN 0
ENDIF

DRAWLINE
;
;	// 自宅マップの表示
;
CALL MAP_VIEWING

PRINTL 
PRINTFORM 現在{(CFLAG:MASTER:現在位置)}:
PRINTFORMS NAME_FROM_PLACE(CFLAG:MASTER:現在位置)
PRINTL にいます
SIF !CAN_MOVE(CFLAG:MASTER:現在位置, CFLAG:MASTER:311) && CFLAG:MASTER:現在位置 != CFLAG:MASTER:311
	PRINTL [0] - 私室へ戻る
FOR LOCAL, MINROOM(), MAXROOM
	IF CAN_MOVE(CFLAG:MASTER:現在位置, LOCAL) & 1
		SELECTCASE FLAG:移動先表示
		CASE 0, 5, 6
			IF LOCAL == 54 && TALENT:MASTER:100 >= -4
			ELSEIF LOCAL >= 53 && LOCAL <=55 && !FLAG:改築
			ELSEIF NAME_FROM_PLACE(LOCAL) != ""
			PRINTFORM [{LOCAL}] - 
			PRINTFORMSL NAME_FROM_PLACE(LOCAL)
			ENDIF
		CASE 1, 2
			IF LOCAL == 54 && TALENT:MASTER:100 >= -4
			ELSEIF LOCAL >= 53 && LOCAL <=55 && !FLAG:改築
			ELSEIF NAME_FROM_PLACE(LOCAL) != ""
			PRINTFORM [{LOCAL}]-%NAME_FROM_PLACE(LOCAL),5,LEFT% 
			ENDIF
		CASE 3
		ENDSELECT
	ENDIF
NEXT
SIF FLAG:移動先表示 == 1 || FLAG:移動先表示 == 2
PRINTL
IF !CAN_MOVE(CFLAG:MASTER:現在位置, ENTRANCE) & 1 && CFLAG:MASTER:現在位置 != ENTRANCE
	PRINTFORM [{ENTRANCE}] - 
	PRINTFORMSL NAME_FROM_PLACE(ENTRANCE)
ENDIF
PRINTFORM [{BUTTON戻る}] - 戻る　　
PRINTBUTTON "＜]", BUTTON切替左
PRINTBUTTON "表示切り替え",BUTTON切替中
PRINTFORM ({FLAG:移動先表示})
PRINTBUTTON "[＞",BUTTON切替右
SIF TALENT:MASTER:居場所察知 || TALENT:MASTER:汚部屋察知
	PRINTFORM 　　[200] - 察知モード
IF FLAG:察知モード == 1
	PRINT (居)
ELSEIF FLAG:察知モード == 2
	PRINT (汚)
ENDIF
SIF TALENT:MASTER:居場所察知 && FLAG:察知モード == 1
	PRINTFORM [811] - 居場所察知　　
SIF TALENT:MASTER:汚部屋察知 && FLAG:察知モード == 2
	PRINTFORM [812] - 汚部屋察知　　
IF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
	PRINTL
	PRINT （歩くのも一苦労だ…）
ELSEIF TALENT:MASTER:100 < -4 && CFLAG:MASTER:同行
	PRINTL
	PRINT （乗っけてもらえば楽だ）
ENDIF
$INPUT_LOOP
INPUTS
SIF RESULTS == ""
	GOTO INPUT_LOOP
RESULT = TOINT(RESULTS) - (TOINT(RESULTS) == 0 && RESULTS != "0")
IF RESULT == -1
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, MINROOM() , MAXROOM
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
IF RESULT == BUTTON戻る || RESULT == CFLAG:MASTER:現在位置
	NEXTCOM = -1
	RETURN -1
ELSEIF RESULT == BUTTON切替左
	FLAG:移動先表示 -= 1
	SIF FLAG:移動先表示 < 0
		FLAG:移動先表示 = 6
	RESTART
ELSEIF RESULT == BUTTON切替右
	FLAG:移動先表示 += 1
	SIF FLAG:移動先表示 >= 7
		FLAG:移動先表示 = 0
	RESTART
;MASTERの部屋
ELSEIF RESULT == 0
	IF IN_HOME(CFLAG:MASTER:初期位置) != 1
		PRINTW 初期位置が不正です。私室に変更します。
		CFLAG:MASTER:初期位置 = デフォルト初期位置
	ENDIF
	CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
;居場所察知
ELSEIF RESULT == 811 && TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL HITOSAGASI()
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 鍵がかかっている
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		NEXTCOM = GETNUM(TRAINNAME, "移動")
		TFLAG:195 = RESULT
	ENDIF
	RESTART
ELSEIF RESULT == 812 && TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:うふふ
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL OHEYA_CHECK
	INPUTS
	RESULT = TOINT(RESULTS)
	IF RESULT == 0
		REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
		FOR LOCAL, 1, 19
			SIF STR:LOCAL == RESULTS
				RESULT = LOCAL
		NEXT
	ENDIF
	IF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW 鍵がかかっている
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		NEXTCOM = GETNUM(TRAINNAME, "移動")
		TFLAG:195 = RESULT
	ENDIF
	RESTART
ELSEIF RESULT == 200
	IF TALENT:MASTER:居場所察知 && TALENT:MASTER:汚部屋察知
		FLAG:察知モード += 1
		SIF FLAG:察知モード >= 3
			FLAG:察知モード = 0
	ELSEIF TALENT:MASTER:居場所察知
		FLAG:察知モード = !FLAG:察知モード
	ELSEIF TALENT:MASTER:汚部屋察知
		FLAG:察知モード = (!FLAG:察知モード) * 2
	ENDIF
	RESTART
;移動範囲外
ELSEIF IN_HOME(RESULT) != 1
	PRINTW 移動範囲外
	CLEARLINE 2
	REUSELASTLINE 
	GOTO INPUT_LOOP
;移動不能な
ELSEIF MAP_CAN_MOVE(RESULT) == 0
	IF TFLAG:移動不能メッセージ == 999
		PRINTFORMW 鍵がかかっている
		CLEARLINE 2
		REUSELASTLINE 
	ELSE
		TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
	ENDIF
	TFLAG:移動不能メッセージ = 0
	GOTO INPUT_LOOP
ELSEIF CAN_SKIPMOVE(MAIN_MAP)
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	IF FLAG:70 == 1
		SIF TALENT:MASTER:100 < -4
			BASE:MASTER:TSP -= 15 * RESULT
		BASE:MASTER:TSP -= 5 * RESULT
		IF TFLAG:運搬
			BASE:MASTER:TSP -= 3 * RESULT
			CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
		ENDIF
	ELSE
		SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
			TIME += 7 * RESULT
		TIME += 3 * RESULT
		IF CFLAG:93:同行 && !GETBIT (FLAG:プール, 0)
			TIME += 2 * RESULT
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (5 * RESULT), 500)
		ENDIF
	ENDIF
	PREVCOM = 400
	STR:0 = 移動
	RETURN
;直接行けるかチェック
ELSEIF CAN_MOVE(CFLAG:MASTER:現在位置, RESULT) & 1
	DEBUGPRINTL 直接移動するよ
	;トイレ
	IF IN_TOILET(RESULT)
		CALL トイレ侵入(RESULT)
	;浴場
	ELSEIF BATHCHECK(RESULT)
		忍び込み = RESULT
		LOCAL:1 = IN_ROOM_MEMBER("MIN", RESULT, "CFLAG", 6)
		IF LOCAL:1 && 風呂追い出し条件(LOCAL:1) && !CFLAG:MASTER:同行
			IF !FLAG:70 == 1
				PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				RESTART
			ELSE
				CFLAG:MASTER:現在位置 = RESULT
				IF FLAG:70 == 1
					PRINTFORMW %CALLNAME:(LOCAL:1)%が入浴中だったようだ…
				ELSE
					PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%と目が合った…
					CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)
				ENDIF
			ENDIF
		ELSE
			FOR LOCAL, 1, CHARANUM
				IF 風呂追い出し条件(LOCAL) && CFLAG:LOCAL:同行
					PRINTFORMW %CALLNAME:LOCAL%とお風呂に入ろうとしたが断られた
					CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
					RESTART
				ELSEIF LOCAL:1 && 風呂追い出し条件(LOCAL:1)
					PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
					CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
					NEXTCOM = -1
					DEBUGPRINTL 【COM400】RETURNPOINT D
					RETURN 0
				ELSEIF CFLAG:LOCAL:同行
					PRINTFORMW %CALLNAME:LOCAL%とお風呂に入った
					CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
					BREAK
				ENDIF
			NEXT
		ENDIF
		CFLAG:MASTER:現在位置 = 忍び込み
	;虫かご
	ELSEIF RESULT == 54
		CALL 虫かご侵入(RESULT)
	ELSEIF LOCK:RESULT
		;行き先を忍び込みへ退避
		忍び込み = RESULT
		IF FLAG:70 == 1
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:311 == 忍び込み && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
					PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
				ENDIF
			NEXT
			;移動成立
			CFLAG:MASTER:現在位置 = 忍び込み
		ELSEIF FLAG:70 == 0
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:311 == 忍び込み && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
					PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
				ELSEIF CFLAG:LOCAL:311 == 忍び込み && !CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
					PRINTFORML どうやら%CALLNAME:LOCAL%は眠っているようだ
					PRINTFORMW 少し物音も聞こえる…
				ENDIF
				IF CFLAG:LOCAL:同行
					入れません = 1
				ELSE
					入れません = 0
				ENDIF
			NEXT
			IF 入れません
				RESTART
			ELSE
				PRINTL [0] 部屋に忍び込む
				PRINTL [1] やめておく
				$INPUT_SNEAK01
				INPUT
				IF RESULT == 0
					CALL 部屋に忍び込む(忍び込み)
				ELSEIF RESULT == 1
					RESTART
				ELSE
					CLEARLINE 1
					GOTO INPUT_SNEAK01
				ENDIF
			ENDIF
		ENDIF
	ELSE
		CFLAG:MASTER:現在位置 = RESULT
	ENDIF
ELSE
	DEBUGPRINTL FARMOVEするよ
	;目的地が離れているので、最終目的地として設定
	TFLAG:195 = RESULT
	CALL FARMOVE(RESULT)
	;現在位置を更新し、次も移動の扱いにする
	CFLAG:MASTER:現在位置 = RESULT
	NEXTCOM = 400
ENDIF
IF FLAG:70 == 1
	SIF TALENT:MASTER:100 < -4
	BASE:MASTER:TSP -= 15
	BASE:MASTER:TSP -= 5
	IF TFLAG:運搬
		BASE:MASTER:TSP -= 3
		CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
	ENDIF
ELSE
	;小人同士で同行しててもいつも通りの速度になるのは小人の知恵のおかげだ
	SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:同行
	TIME += 7
	TIME += 3
	IF CFLAG:93:同行 && !GETBIT (FLAG:プール, 0)
		TIME += 2
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5,500)
	ENDIF
ENDIF
	;隠密解除処理
;	要検証
;IF CFLAG:MASTER:隠密中
;	FOR LOCAL, 1, CHARANUM
;		;MASTERの前ターン位置が私室だった場合
;		IF (CFLAG:LOCAL:311 == CFLAG:MASTER:前ターン位置) || (CFLAG:LOCAL:現在位置 == CFLAG:MASTER:311 && CFLAG:MASTER:前ターン位置 == CFLAG:MASTER:311)
;			CFLAG:LOCAL:隠密中 = 0
;			CFLAG:LOCAL:イタズラ = 0
;		ENDIF
;		;MASTERの現在位置が私室だった場合
;		SIF ((CFLAG:MASTER:現在位置 == CFLAG:LOCAL:311) && CFLAG:LOCAL:睡眠) || ((CFLAG:LOCAL:現在位置 == CFLAG:MASTER:311 && CFLAG:MASTER:現在位置 == CFLAG:MASTER:311) && CFLAG:LOCAL:睡眠)
;			CFLAG:LOCAL:隠密中 = 1
;	NEXT
;	;私室以外は隠密出来ない…ハズ
;	IF CFLAG:MASTER:現在位置 < 13
;		CFLAG:MASTER:隠密中 = 0
;		CFLAG:MASTER:イタズラ = 0
;	ENDIF
;ENDIF
PREVCOM = 400
STR:0 = 移動
RETURN 0

;移動
@COM_ABLE400
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:うふふ == 2
	RETURN 0
SIF CFLAG:MASTER:イタズラ
	RETURN 0
SIF CFLAG:MASTER:添い寝中
	RETURN 0
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	RETURN 0
;自宅でない
SIF AT_HOME(MASTER) == 0
	RETURN 0
RETURN 1

@SKIP_MOVE(ARG, ARG:1)
#DIM 暫定目的地
#DIM 直前位置
#DIM 経過時間
#DIM 中継地点,20
#DIM 中継回数
;ARG:0	出発位置
;ARG:1	最終目的地
VARSET LOCAL
VARSET 中継地点
暫定目的地 = ARG:1
経過時間 = 0
中継回数 = 0
IF (CAN_MOVE(CFLAG:MASTER:現在位置, 暫定目的地) & 1)
	直前位置 = ARG
ELSE
	TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(ARG:1)
	直前位置 = RESULT
ENDIF
WHILE !(CAN_MOVE(CFLAG:MASTER:現在位置, 暫定目的地) & 1)
	経過時間 += 1
	中継回数 ++
	TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(暫定目的地)
	[IF_DEBUG]
	PRINTFORML {暫定目的地}に直接行けないから{RESULT}を目指すよ	
	[ENDIF]
	中継地点:中継回数 = RESULT
	暫定目的地 = RESULT
WEND
LOCAL:4 = 中継回数
$MIKAKETA
IF 中継回数
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == 中継地点:中継回数
			LOCAL:3 = LOCAL
			BREAK
		ENDIF
	NEXT
	PRINTFORM {LOCAL:4 - 中継回数 + 1}番目の中継地の%NAME_FROM_PLACE(中継地点:中継回数)%
	IF LOCAL:3
		TRYCALLFORM M_KOJO_K{(LOCAL:3)}
		PRINTFORML で%CALLNAME:(LOCAL:3)%をみかけた
		IF FLAG:立ち止まり == 2
			;立ち止まらない
		ELSEIF FLAG:立ち止まり == 1 && !RESULT
			;口上がある場合のみ
		ELSE
			CALL ASK_M("会釈して移動する",!FLAG:70,"無視して移動する",1,"立ち止まる",1)
			IF RESULT == 2
				;PRINTFORML まだその機能はない
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,1,0)
				経過時間 = LOCAL:4 - 中継回数 + 1
				CFLAG:MASTER:現在位置 = 中継地点:中継回数
				RESULT = 中継地点:中継回数
				GOTO STOP
			ELSEIF RESULT == 1
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,3,0)
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,2,0)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML には誰もいなかった
	ENDIF
	中継回数 --
	LOCAL:3 = 0
	GOTO MIKAKETA
ENDIF
経過時間 += 1
CFLAG:MASTER:現在位置 = ARG:1
RESULT = ARG:1
$STOP
IF IN_TOILET(RESULT)
	CALL トイレ侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF BATHCHECK(RESULT)
	LOCAL:1 = IN_ROOM_MEMBER("MIN", RESULT, "CFLAG", 6)
	IF LOCAL:1 && 風呂追い出し条件(LOCAL:1) && !CFLAG:MASTER:同行
		PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
		CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
		CFLAG:MASTER:現在位置 = 直前位置
	ELSEIF LOCAL:1 && !CFLAG:MASTER:同行
		IF FLAG:70 == 1
			PRINTFORMW %CALLNAME:(LOCAL:1)%が入浴中だったようだ…
		ELSE
			PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%と目が合った…
			CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)
		ENDIF
		CFLAG:MASTER:現在位置 = ARG:1
	ELSE
		FOR LOCAL, 1, CHARANUM
			IF 風呂追い出し条件(LOCAL) && CFLAG:LOCAL:同行
				PRINTFORMW %CALLNAME:LOCAL%とお風呂に入ろうとしたが断られた
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ELSEIF LOCAL:1 && 風呂追い出し条件(LOCAL:1)
				PRINTFORMW 浴場に居た%CALLNAME:(LOCAL:1)%に追い出されてしまった…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				CFLAG:MASTER:現在位置 = 直前位置
				GOTO END
			ENDIF
		NEXT
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:同行
				PRINTFORMW %CALLNAME:LOCAL%は一緒にお風呂に入ってくれそうだ
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
				BREAK
			ENDIF
		NEXT
		CFLAG:MASTER:現在位置 = ARG:1
	ENDIF
ELSEIF RESULT == 54
	CALL 虫かご侵入(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:現在位置 = 直前位置
ELSEIF LOCK:(RESULT)
	LOCAL:4 = RESULT
	IF FLAG:70 == 1
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
			ENDIF
		NEXT
		;移動成立
		CFLAG:MASTER:現在位置 = ARG:1
	ELSEIF FLAG:70 == 0
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORMW どうやら%CALLNAME:LOCAL%は眠っているようだ
			ELSEIF CFLAG:LOCAL:311 == RESULT && !CFLAG:LOCAL:睡眠 && !CFLAG:LOCAL:出禁
				PRINTFORML どうやら%CALLNAME:LOCAL%は眠っているようだ
				PRINTFORMW 少し物音も聞こえる…
			ENDIF
			IF CFLAG:LOCAL:同行
				LOCAL:2 = 1
			ELSE
				LOCAL:2 = 0
			ENDIF
		NEXT
		IF LOCAL:2
			CFLAG:MASTER:現在位置 = 直前位置
		ELSE
			PRINTL [0] 部屋に忍び込む
			PRINTL [1] やめておく
			$INPUT_SNEAK01
			INPUT
			IF RESULT == 0
				CALL 部屋に忍び込む(LOCAL:4)
				GOTO END
			ELSEIF RESULT == 1
				CFLAG:MASTER:現在位置 = 直前位置
			ELSE
				CLEARLINE 1
				GOTO INPUT_SNEAK01
			ENDIF
		ENDIF
	ENDIF
ENDIF
$END
RETURN 経過時間

