;紅魔カジノ
;とりあえず格好だけ作ったが景品がないのはどうしたもんか
;為替市場もここにいれてます
;各ゲームコーナーで保有カリスマを確認できないのはカリスマを搾取するための罠である

@CASINO_ENTRANCE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM EXIT
EXIT = 0
CALL CASINO_CHECK_EVENT("ENTRANCE")
PRINTFORML 
PRINTFORML ～紅魔カジノ～
PRINTFORML 　あなたの保有￥　　　　{MONEY, 10, RIGHT}
PRINTFORML 　あなたの保有カリスマ　{MONEY:2, 10, RIGHT}
PRINTFORML 
;SIF ITEM:紅魔カジノVIP会員証
;	PRINTFORML [6] - VIPルーム
;PRINTFORML [5] - 景品交換
IF FUNC_CASINO("CLOSE_CASINO")
	PRINTFORML [-] - スロットで遊ぶ（休業中）
	PRINTFORML [-] - ポーカーで遊ぶ（休業中）
	PRINTFORML [-] - ブラックジャックで遊ぶ（休業中）
	PRINTFORML [-] - カードで遊ぶ（休業中）
	PRINTFORML [-] - ダイスで遊ぶ（休業中）
	PRINTFORML [-] - クイズで遊ぶ（休業中）
ELSE
	PRINTFORML [6] - スロットで遊ぶ
	PRINTFORML [5] - ポーカーで遊ぶ
	PRINTFORML [4] - ブラックジャックで遊ぶ
	PRINTFORML [3] - カードで遊ぶ
	PRINTFORML [2] - ダイスで遊ぶ
	PRINTFORML [1] - クイズで遊ぶ
ENDIF
PRINTFORML 
PRINTFORML [0] - もどる
PRINTFORML [9] - せつめい
SIF FLAG:会員ランク
	PRINTFORML [8] - カリスマ取引
DO
	INPUT
	;休業中はゲーム系は閉まってる
	SIF FUNC_CASINO("CLOSE_CASINO") && INRANGE(RESULT, 1, 6)
		GOTO RETRY
	SELECTCASE RESULT
	CASE 9
		CALL CASINO_CHECK_EVENT("HELP")
	CASE 8
	SIF FLAG:会員ランク
		CALL CASINO_TRADE
	CASE 0
		;IF MONEY:2 >= TFLAG:カジノ入場時カリスマ + 100
		;	PRINTFORML 「カリスマを現金に交換しますか？」
		;	PRINTFORML 「今ですと手数料がお得ですよ」
		;ENDIF
		EXIT = 1
	CASE 6
		CALL CASINO_SLOT
	CASE 5
		CALL CASINO_POKER
	CASE 4
		CALL CASINO_BLACKJACK
	CASE 3
		CALL CASINO_CARD
	CASE 2
		CALL CASINO_DICE
	CASE 1
		CALL CASINO_QUIZ
	CASEELSE
		$RETRY
		REUSELASTLINE 無効な入力です
		CONTINUE
	ENDSELECT
	SIF NEMUKE() >= 700
		EXIT = 1
	BREAK
LOOP 1
SIF !EXIT
	RESTART
CALL CASINO_CHECK_EVENT("EXIT")



;カジノ系イベントの独自関数化
;説明パネルもここに突っ込んだ
;可読性の確保のためにマジックナンバーを無理矢理排除した結果珍妙な構文になりますた
@CASINO_CHECK_EVENT(TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM COUNT_ENTER ;本日入店した回数
#DIM COUNT_EXIT  ;本日店を出た回数
SELECTCASE TYPE
CASE "ENTRANCE"
	;紅魔館メンバーに対してそっけない反応ってどうなのよって思った
	;金銭的に盛りすぎなのは承知だが
	;ロールプレイ的にはこうなる気がする
	IF !FLAG:カジノ訪問日
	;初回
		PRINTFORML 
		SELECTCASE CSVCALLNAME(FLAG:なりきり)
		CASE "レミリア", "フラン"
			PRINTFORML メイド妖精
			PRINTFORML 　%CALLNAME:MASTER%様！
			PRINTFORML 　ようこそおいで下さいました
			PRINTFORML 　こちら、お預かりしていた250カリスマでございます
			PRINTFORMW 　ごゆっくりお楽しみ下さいませ
			MONEY:2 += 250
		CASE "パチュリー"
			PRINTFORML メイド妖精
			PRINTFORML 　ようこそ%CALLNAME:MASTER%様
			PRINTFORML 　はい、お陰様で大盛況です
			PRINTFORMW 　お嬢様から200カリスマを預かっておりますのでどうぞお使い下さい
			MONEY:2 += 200
		CASE "咲夜"
			PRINTFORML メイド妖精
			PRINTFORML 　ようこそ%CALLNAME:MASTER%さん
			PRINTFORML 　店内視察でしょうか？皆、一生懸命に働いておりますよ
			PRINTFORMW 　お嬢様から200カリスマを預かっておりますのでどうぞお使い下さい
			MONEY:2 += 200
		CASE "美鈴", "小悪魔"
			PRINTFORML メイド妖精
			PRINTFORML 　ようこそ%CALLNAME:MASTER%さん
			IF GET_DAY() == "日" || GET_DAY() == "土"
				PRINTFORML 　今日はお休みですか？程々にしておいて下さいね
			ELSE
				PRINTFORML 　お仕事はいいんですか？後で怒られても知りませんよ
			ENDIF
			PRINTFORMW 　お嬢様から150カリスマを預かってますからどうぞ使って下さい
			MONEY:2 += 150
		CASE "魔理沙"
		;図書館がらみで出禁くらっててもおかしくないよね
			PRINTFORML メイド妖精
			PRINTFORML 　%CALLNAME:MASTER%さんでしたか
			PRINTFORML 　こちら、100カリスマとなります
			PRINTFORMW 　初めて来店するお客様に歓迎の印として渡すようにと言いつかっております
			PRINTFORML 　しかしながら、貴女はお嬢様にあまり歓迎されてはおりません
			PRINTFORMW 　当カジノ店内ではどうかお行儀よくお楽しみいただきますようお願いします
			MONEY:2 += 100
		CASE "あなた"
			IF MAIN_MAP == 3
				PRINTFORML メイド妖精
				PRINTFORML 　あら、うちで働いてるひとよね？
				PRINTFORML 　ここは専属の清掃員がいるから掃除しなくてもいいんですよ
				PRINTFORML 　それとも遊びに来たのかしら？
				PRINTFORMW 　お嬢様から120カリスマが支給されてるわ、大事に使ってね
				MONEY:2 += 120
			ELSE
				PRINTFORML メイド妖精
				PRINTFORML 　紅魔カジノへようこそ。お客様は当カジノのご利用は初めてですね？
				PRINTFORMW 　初めてのお客様には、記念にお嬢様から100カリスマが贈呈されます
				MONEY:2 += 100
			ENDIF
		CASEELSE
			PRINTFORML メイド妖精
			PRINTFORML 　紅魔カジノへようこそ。お客様は当カジノのご利用は初めてですね？
			PRINTFORMW 　初めてのお客様には、記念にお嬢様から100カリスマが贈呈されます
			MONEY:2 += 100
		ENDSELECT
		CM_LIMIT = 100
		COUNT_EXIT = 0
		COUNT_ENTER = 1
	ELSEIF DAY - FLAG:カジノ訪問日
	;1日1回だけ
		PRINTFORML 
		SELECTCASE DAY - FLAG:カジノ訪問日
		CASE 1 TO 5
			IF MAIN_MAP == 3 && (CSVCALLNAME(FLAG:なりきり) == "美鈴" || CSVCALLNAME(FLAG:なりきり) == "小悪魔")
				PRINTFORML メイド妖精
				PRINTFORML 　%CALLNAME:MASTER%さん、困りますよぉ
				PRINTFORMW 　あんまり遊んでると私達まで叱られちゃうんですからねー？
			ELSEIF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "あなた"
				PRINTFORML メイド妖精
				PRINTFORML 　あら、また来たの？
				PRINTFORMW 　程々に楽しんでいってね
			ELSE
				PRINTFORML メイド妖精
				PRINTFORML 　いらっしゃいませ
				PRINTFORMW 　お楽しみいただけているようで何よりです
			ENDIF
		CASEELSE
			IF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "あなた"
				PRINTFORML メイド妖精
				PRINTFORML 　お掃除お疲れ様
				PRINTFORMW 　楽しんでいってね
			ELSE
				PRINTFORML メイド妖精
				PRINTFORML 　いらっしゃいませ
				PRINTFORMW 　ごゆっくりお楽しみ下さい
			ENDIF
		ENDSELECT
		COUNT_EXIT = 0
		COUNT_ENTER = 1
	ELSEIF COUNT_ENTER == COUNT_EXIT
	;再来店時
		PRINTFORML 
		IF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "あなた"
			PRINTFORML メイド妖精
			PRINTFORML 　あら、また来たの？
			PRINTFORMW 　程々に楽しんでいってね
		ELSE
			PRINTFORML メイド妖精
			PRINTFORML 　おかえりなさいませ
			PRINTFORMW 　ごゆっくりお楽しみ下さい
		ENDIF
		COUNT_ENTER ++
	ELSE
		IF !RAND:10
		;この手の賑やかしってあると楽しいと思うんだ
			;はぐれたETC
			PRINTDATAL
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　人里よりお越しの××××さま
				DATAFORM 　お連れのお客さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　太陽の畑よりお越しの××××さま
				DATAFORM 　お連れのひまわり妖精さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　迷いの竹林よりお越しの××××さま
				DATAFORM 　お連れの兎さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　月よりお越しの××××さま
				DATAFORM 　お連れの兎さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　妖怪の山よりお越しの××××さま
				DATAFORM 　お連れの天狗さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　玄武の沢よりお越しの××××さま
				DATAFORM 　お連れの河童さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　触手宮よりお越しの××××さま
				DATAFORM 　お連れの触手さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
			;字面だけみるとすげーコワイ！
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　三途の川よりお越しの××××さま
				DATAFORM 　お連れの死神さまがお待ちです
				DATAFORM 　受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　三途の川よりお越しの××××さま
				DATAFORM 　閻魔さまがお待ちです
				DATAFORM 　大至急、受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　えー、ご案内申し上げます
				DATAFORM 　妖怪の山よりお越しの××××さま
				DATAFORM 　大天狗さまがお待ちです
				DATAFORM 　大至急、受付までお越しください
			ENDLIST
			
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　きゅうりが大量に入った紙袋を預かっております
				DATAFORM 　お心当たりのある方は受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　飛頭蛮の方の「首から下」の落し物です
				DATAFORM 　こちらに「首から上」がいらっしゃいますので
				DATAFORM 　「首から下」を見かけた方はお近くのメイド妖精までお知らせください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　傘の落し物です
				DATAFORM 　茄子色で目と長い舌のある珍妙な傘を預かっております
				DATAFORM 　お心当たりのある方は受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　えーと、手の平大の水晶球に屋根がついたような
				DATAFORM 　オブジェのようなものを預かっております
				DATAFORM 　お心当たりのある方は受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　えーと、何だろコレ
				DATAFORM 　ピンク色でぶよぶよした手の平大の塊を預かっております
				DATAFORM 　お心当たりのある方は受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 店内アナウンスが流れた
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　落し物のご案内を申し上げます
				DATAFORM 　杵の落し物です
				DATAFORM 　先っぽが赤く染まった使いふるしの杵を預かっております
				DATAFORM 　お心当たりのある方は受付までお越しください
			ENDLIST
			DATALIST
				DATAFORM 受付のメイド妖精は別のメイド妖精と話をしている
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　あ、ちょっといいかしら？
				DATAFORM 　そうなの、この子うちで働きたいらしいのよ
				DATAFORM 　咲夜さんのところまで案内してもらえる？
				DATAFORM 　うん、よろしくね
			ENDLIST
			DATALIST
				DATAFORM 受付のメイド妖精は別のメイド妖精と話をしている
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　ねえ、ちょっといい？
				DATAFORM 　この人が妹様と遊びたいんだって
				DATAFORM 　何か美味しいものでも食べさせてあげて
				DATAFORM 　うん、よろしくね
			ENDLIST
			DATALIST
				DATAFORM 受付のメイド妖精は別のメイド妖精と話をしている
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　ねえ、ちょっといい？
				DATAFORM 　そうよ、お嬢様とのお食事の出席者の方
				DATAFORM 　いつもどおりによろしくね
			ENDLIST
			DATALIST
				DATAFORM 受付のメイド妖精は別のメイド妖精と話をしている
				DATAFORM 
				DATAFORM メイド妖精
				DATAFORM 　そっか、そろそろ交代の時間だっけ？
				DATAFORM 　それじゃ、あとはよろしくね
				DATAFORM 　お先に失礼しまーす
			ENDLIST
			
			ENDDATA
		ELSE
			PRINTFORML 受付のメイド妖精は接客中だ
			PRINTFORML 
			PRINTFORML メイド妖精
			PRINTDATAL
			DATALIST
				DATAFORM 　あら、そちらのお客様は魔法使いの方でしょうか？
				DATAFORM 　いらっしゃいませ
				DATAFORM 　教養が活かせるゲームですか？
				DATAFORM 　それではクイズゲームなどはいかがでしょうか？
			ENDLIST
			DATALIST
				DATAFORM 　おや、その立派なツノは鬼のお客様ですね
				DATAFORM 　「一発当たるとデカイ博打」ですか？
				DATAFORM 　それではポーカーなどはいかがでしょうか？
				DATAFORM 　ロイヤルストレートの役では配当はなんと300倍になりますよ
			ENDLIST
			DATALIST
				DATAFORM 　いらっしゃいませ
				DATAFORM 　「一番稼げるゲーム」ですか？
				DATAFORM 　それではダイスゲームなどはいかがでしょうか？
				DATAFORM 　勝ち続ければ天井知らずで配当金を増やしていくことが出来ますよ
			ENDLIST
			DATALIST
				DATAFORM 　いらっしゃいませ
				DATAFORM 　いつもご利用ありがとうございます
				DATAFORM 　「新しく出来たスロットマシンを探している」？
				DATAFORM 　ええ、それでしたら向こうの奥にございます
			ENDLIST
			DATALIST
				DATAFORM 　あの、お客様？
				DATAFORM 　カジノ内への死体の持ち込みはご遠慮いただけますか？
				DATAFORM 　ええ、生きている人間のお客様もみえますので
			ENDLIST
			DATALIST
				DATAFORM 　まあ、今日は随分とツイていらしたんですね！
				DATAFORM 　楽しんでいただけたようで何よりです
				DATAFORM 　またのお越しをお待ちしております
			ENDLIST
			DATALIST
				DATAFORM 　ついに人型に化けることができるようになったんですね！
				DATAFORM 　おめでとうございます
				DATAFORM 　これからもよろしくお願いしますね
			ENDLIST
			ENDDATA
		ENDIF
	ENDIF
	FLAG:カジノ訪問日 = DAY
CASE "EXIT"
	COUNT_EXIT ++
	CALL CASINO_MEMBER_RANKUP
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　ご利用ありがとうございました
	PRINTDATAW
		DATAFORM 　気をつけてお帰り下さいね
		DATAFORM 　またいらして下さいね
		DATAFORM 　またのご来店をお待ちしております
	ENDDATA
CASE "HELP"
;へるぷ機能
	PRINTFORML 
	PRINTFORML カジノの入り口に説明のパネルが置いてあった
	PRINTFORML 
	PRINTFORML [9] - 迷惑行為について
	PRINTFORML [8] - カリスマについて
	PRINTFORML 
	PRINTFORML [4] - ブラックジャックについて
	PRINTFORML [3] - カードゲームについて
	PRINTFORML [2] - ダイスゲームについて
	PRINTFORML [1] - クイズゲームについて
	PRINTFORML 
	PRINTFORML [0] - もどる
	DO
		INPUT
		SELECTCASE RESULT
		CASE 9
			;魔理沙分岐を仕込んだことで注意書きに「窃盗」が無かったなと気づく
			PRINTFORML ☆迷惑行為について
			PRINTFORML 　魔法や特殊な能力の使用、偽造通貨の使用、窃盗、詐欺、暴力、脅迫、恐喝、セクハラ、
			PRINTFORML 　店内での弾幕ごっこ、スペルカードの使用、ぴちゅーん、宴会、読経、清掃、入浴、お茶淹れなど
			PRINTFORML 　店内の秩序を著しく乱す行為を行ったと認められたお客様につきましては
			PRINTFORML 　お嬢様のお食事にご招待させて頂く、もしくは妹様の遊び相手になって頂く場合がございますのでご注意下さい
			PRINTFORML 
			PRINTFORML 　どうか皆様くれぐれもお行儀よくお楽しみいただきますよう
			PRINTFORMW 　店員一同、心よりお願い申し上げます
		CASE 8
			PRINTFORML ☆カリスマについて
			PRINTFORML 　当カジノでは不正行為の防止のため専用通貨“カリスマ”を使用してお遊び頂いております
			PRINTFORML 　お気軽にフロントにてお求め下さい
			PRINTFORMW 　なお現金を直接使って遊ぶことはできませんので予めご了承下さい
			PRINTFORML 
			PRINTFORML 　現在のカリスマ払い対応店舗
			PRINTFORML 　・清く正しい　　「文々。新聞」
			PRINTFORML 　・人里の貸本屋　「鈴奈庵」
			PRINTFORML 　・紅魔館ホフゴブリン水やり代行サービス
			PRINTFORMW 　・紅魔館ホフゴブリン出張清掃サービス
		CASE 4
			PRINTFORML ☆ブラックジャックについて
			PRINTFORML 　※現実のブラックジャックとはルールが異なります
			PRINTFORML 　カードの点数を競い、より高い点数を獲得した方の勝ちです
			PRINTFORML 　ただし２１を超過してはいけません
			PRINTFORML 　２１を超えてしまったプレイヤーは負けです
			PRINTFORML 　
			PRINTFORML 　点数の計算は以下のとおりです
			PRINTFORML 　　１～10：数字通りの点数
			PRINTFORML 　　ＪＱＫ：10点固定
			PRINTFORML 　　　　Ａ：点数が２１を超えなければ１０点、超えてしまう場合は１点
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　まずディーラーとあなたに２枚ずつカードが配られます
			PRINTFORML 　この時ディーラーのカードのうち１枚は表になります
			PRINTFORML 　あなたには以下の選択肢が与えられます
			PRINTFORML 　　　　ヒット：カードを１枚追加して勝負します
			PRINTFORML 　　　スタンド：カードを追加せずに勝負します
			PRINTFORML 　　　　ダブル：賭け金を２倍にした上でカードを１枚追加して勝負します
			PRINTFORML 　　サレンダー：賭け金の半分を受け取りゲームを降ります
			PRINTFORML 　
			PRINTFORML 　カードを引いた時点で２１を超えてしまったらその時点であなたの負けです
			PRINTFORML 　そうでない場合はディーラーの伏せたカードを表にします
			PRINTFORML 　このとき１７点未満だった場合、ディーラーは必ずカードを１枚追加します
			PRINTFORML 　ディーラーが２１を超えてしまったらあなたの勝ちです
			PRINTFORML 　
			PRINTFORML 　ディーラーの点数が１７点以上、あるいはカードを１枚追加して２１点以下だった場合
			PRINTFORML 　点数の高いプレイヤーの勝ちになります
			PRINTFORML 　引き分けた場合は賭け金が返ってきます
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　２枚のカードで２１点（Ａ＋10点のカード）の場合、ブラックジャックという役が成立します
			PRINTFORML 　ブラックジャックはカード３枚の２１点に勝ちます
			PRINTFORML 　さらにブラックジャックで勝った場合ボーナスが付きます
			PRINTFORML 　
			PRINTFORML 　ディーラーの表になったカードがＡだった場合、インシュアランス（INS）を選択することができます
			PRINTFORML 　INSは賭け金の半分を保険金として追加で賭けます
			PRINTFORML 　ディーラーがブラックジャックであなたがINSをしていた場合、賭け金と保険金がそのまま返ってきます
			PRINTFORML 　ディーラーがブラックジャックでなかった場合、保険金は返ってきません
			PRINTFORML 　
			PRINTFORML 　INSを選択でき、あなたがブラックジャックだった場合、代わりにイーブンマネーを選択することができます
			PRINTFORML 　イーブンマネーを選択すると賭け金の２倍の配当を受け取りゲームを終了します
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　配当は以下のとおりです
			PRINTFORML 　　　BJで勝ち：2.5倍
			PRINTFORML 　　　　　勝ち：2.0倍
			PRINTFORML 　　　　　負け：0.0倍
			PRINTFORML 　　サレンダー：0.5倍
			PRINTFORML 　　　 INS成功：1.0倍
			PRINTFORMW ※イカサマは未実装です
		CASE 3
			PRINTFORML ☆カードゲームについて
			PRINTFORML 　イカサマありのトランプゲームです
			PRINTFORML 　この館のメイド長が監修したゲームです
			PRINTFORML 　それぞれが三回行動を行った後に勝敗結果が確定し精算となります
			PRINTFORML 　ゲーム開始時に1～50カリスマを賭け金として預け、
			PRINTFORML 　メイド妖精に勝つことができれば賭け金の2.5倍の金額をその場で受け取ることができます
			PRINTFORML 
			PRINTFORML 　ゲーム中では三種類の行動を選ぶことができ、
			PRINTFORML 　これらは三すくみの関係になっています（イカサマ＞正々堂々＞注視＞イカサマ）
			PRINTFORML 　　イカサマ：イカサマで有利にゲームを進めます
			PRINTFORML 　　正々堂々：ルールに則ってゲームを進めます
			PRINTFORML 　　　注視　：相手のイカサマを阻止します
			PRINTFORMW 　ちなみにイカサマを仕掛けるには料理技能を、イカサマを見抜くには清掃技能を使用します
		CASE 2
			PRINTFORML ☆ダイスゲームについて
			PRINTFORML 　いわゆる丁半博打です
			PRINTFORML 　この館の当主が自ら監修したゲームです
			PRINTFORML 　ゲーム開始時に手持ちのカリスマから100カリスマを配当金として預け、
			PRINTFORML 　予想的中直後に精算を行うことで増加した配当金を受け取ることができます
			PRINTFORML 　丁半を的中させるたびに配当金の金額は1.5倍となっていきますが、予想が外れると0となります
			PRINTFORML 　また、ゲームの続行のためには毎回100カリスマを支払う必要があります
			PRINTFORML 　
			PRINTFORML 　メイド妖精は正解を的確に見極めて教えてくれますが嘘をついているかもしれません
			PRINTFORMW 　話術に長けているならばその嘘を見抜くこともできるでしょう
		CASE 1
			PRINTFORML ☆クイズゲームについて
			PRINTFORML 　何の変哲もないクイズゲームです
			PRINTFORML 　この館の当主の親友が監修したゲームです
			PRINTFORML 　料金は難易度に関わらず1つの問題につき5カリスマです
			PRINTFORML 　問題に正解した時点で難易度に応じた賞金が支払われ、次の問題の難易度が開示されます
			PRINTFORMW 　特に駆け引きの要素はなく、純粋に知識が問われます
		CASE 0
			RETURN
		CASEELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
	RESTART
ENDSELECT



;D/Pはディーラー、プレイヤー
;ディーラーの能力は咲夜さんより少し弱いくらいでかなり優秀
;POINT 注:9、正:12、イ:15
;戦闘収支
;　注　vs正＝-3、vsイ＝-6or+9
;　正　vs注＝+3、vsイ＝-3
;　イ　vs注＝+6or-9、vs正＝+3
;注視には清掃、イカサマには料理が必要
;当然ながら対応技能がディーラーを下回ってると全然成功しない
;
;じゃんけんとして捉えるとわかりやすいが
;システム上、対応技能が4以下で挑むと長期的な損益は必ずマイナスになる
;イカサマ関連の仕様で「グーがチョキに勝てなかったりする」ことがあるためである
;あんま詳しく検証してないがたぶん技能が拮抗しているときの勝率が1/3未満

@CASINO_CARD
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT
#DIM D_POINT
#DIM D_ACT
#DIM D_STOP_IKASAMA
#DIM D_DO_IKASAMA
#DIM P_BET
#DIM P_POINT
#DIM P_ACT
#DIM P_STOP_IKASAMA
#DIM P_DO_IKASAMA
D_STOP_IKASAMA = POWER(4, 3)
D_DO_IKASAMA = POWER(4, 3)
P_STOP_IKASAMA = POWER(MAX(1, ABL:MASTER:清掃技能), 3)
P_DO_IKASAMA = POWER(MAX(1, ABL:MASTER:料理技能), 3)
IF MONEY:2 <= 0
	PRINTFORML メイド妖精
	PRINTFORML 　お客様、当カジノ店内では専用通貨“カリスマ”のみを使用してお遊びいただいております
	PRINTFORMW 　お手数ですがカリスマの購入をお願いいたします
	RETURN
ENDIF
PRINTFORML メイド妖精
PRINTFORML 　カードゲームコーナーへようこそ
PRINTFORML 　このゲームはお客様と当館メイド長直伝の技術を持つわたくしとが腕を競い合う
PRINTFORMW 　幻想郷随一の“家事のゲーム”となっております
PRINTFORML 
PRINTFORML メイド妖精
PRINTFORML 　お客様の腕前がわたくしを上回った場合、賭けた金額を3倍にしてお返しいたします
PRINTFORML 　まずは賭け金をご入力下さい
PRINTFORML 
CALL CASINO_INPUTLOOP("カード賭け金")
IF !RESULT
	GOTO CANCEL
ELSE
	P_BET = RESULT
	CALL CASINO_MONEY("カリスマ", -P_BET)
	CALL CASINO_MONEY("カモ", P_BET)
ENDIF
PRINTFORML メイド妖精
PRINTFORML 　承りました
PRINTFORML 　それでははじめましょう
PRINTFORMW 　よろしくお願いいたします
DO
	TIME += 5
	D_POINT = 0
	P_POINT = 0
	PRINTFORML 
	FOR LCOUNT, 1, 4
		PRINTFORML ～{LCOUNT}巡目～
		;[3]イカサマ[2]正々堂々[1]注視
		CALL CASINO_INPUTLOOP("カード戦術")
		P_ACT = RESULT
		D_ACT = RAND(1, 4)
		SELECTCASE P_ACT
		CASE 1
		;注視
			P_POINT += 9
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				PRINTFORML あなたはメイド妖精と見つめ合った
				PRINTFORML メイド妖精は頬を染めて目をそらした
			CASE 2
			;正々堂々
				D_POINT += 12
				PRINTFORML メイド妖精には特に気になる様子は見られない
			CASE 3
			;イカサマ
				PRINTFORML どことなく不審な動きを見せるメイド妖精を注視した
				LOCAL = D_DO_IKASAMA + P_STOP_IKASAMA
				IF RAND:LOCAL <= D_DO_IKASAMA
					D_POINT += 15
					PRINTFORML メイド妖精は余裕しゃくしゃくだ
				ELSE
					PRINTFORML メイド妖精は苛立った様子だ
				ENDIF
			ENDSELECT
		CASE 2
		;正々堂々
		P_POINT += 12
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				PRINTFORML こちらを見張るメイド妖精の視線を感じながら堂々とゲームを進めた
			CASE 2
			;正々堂々
				D_POINT += 12
				PRINTFORML ゲームは滞り無く進行していく
			CASE 3
			;イカサマ
				D_POINT += 15
				PRINTFORML 口の端に笑みを浮かべるメイド妖精を無視して普通にゲームを進めた
			ENDSELECT
		CASE 3
		;イカサマ
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				LOCAL = D_STOP_IKASAMA + P_DO_IKASAMA
				PRINTFORML こちらを見張るメイド妖精の視線を感じながらイカサマを試みた
				IF RAND:LOCAL <= D_STOP_IKASAMA
					PRINTFORML しかし、失敗してしまった
				ELSE
					P_POINT += 15
					PRINTFORML イカサマは成功した
				ENDIF
			CASE 2
			;正々堂々
				D_POINT += 12
				P_POINT += 15
				PRINTFORML 手際よくゲームを進行するメイド妖精の目を盗んでイカサマを仕込んだ
			CASE 3
			;イカサマ
				D_POINT += 15
				P_POINT += 15
				PRINTFORML メイド妖精とあなたは仲良くイカサマを仕込んだ
			ENDSELECT
		ENDSELECT
		WAIT
	NEXT
	PRINTFORML 
	PRINTFORML ～ゲーム結果～
	IF D_POINT >= P_POINT
		PRINTFORMW あなたは負けてしまった
	ELSE
		PRINTFORMW あなたの勝利だ
		PRINTFORML 
		PRINTFORML メイド妖精
		PRINTFORML 　おめでとうございます
		PRINTFORML 　では約束通り、こちらをお受取り下さい
		PRINTFORML 
		PRINTFORMW 手持ちのカリスマが{P_BET * 3}増えた
		CALL CASINO_MONEY("カリスマ", P_BET * 3)
	ENDIF
	IF NEMUKE() >= 700
		PRINTFORML 
		PRINTFORML メイド妖精
		PRINTFORML 　あの、お客様？
		PRINTFORML 　随分とお疲れのご様子ですよ
		PRINTFORML 　今日はもうゲームを終えたほうが良いのではないかしら
		PRINTFORMW 　またのお越しをお待ちしておりますわ
		RETURN
	ELSEIF MONEY:2 <= 0
		PRINTFORML 
		PRINTFORML メイド妖精
		PRINTFORML 　お手持ちのカリスマが尽きてしまったようですわね
		PRINTFORMW 　またのお越しをお待ちしておりますわ
		RETURN
	ENDIF
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　まだ勝負なさいますか？
	PRINTFORML 
	;[2]賭け金の再設定[1]続行[0]やめる
	CALL CASINO_INPUTLOOP("カード続行")
	SELECTCASE RESULT
	CASE 0
		GOTO CANCEL
	CASE 1
		IF MONEY:2 < P_BET
			PRINTFORML メイド妖精
			PRINTFORML 　お手持ちのカリスマが足りないようですわ
			PRINTFORML 　賭け金を再設定なさって下さい
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			IF !RESULT
				GOTO CANCEL
			ELSE
				P_BET = RESULT
				PRINTFORML 
			ENDIF
		ENDIF
	CASE 2
		PRINTFORML メイド妖精
		PRINTFORML 　それでは賭け金をご入力下さい
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード賭け金")
		IF !RESULT
			GOTO CANCEL
		ELSE
			P_BET = RESULT
			PRINTFORML 
		ENDIF
	ENDSELECT
	CALL CASINO_MONEY("カリスマ", -P_BET)
	CALL CASINO_MONEY("カモ", P_BET)
	PRINTFORML メイド妖精
	PRINTFORML 　はい、賭け金{P_BET}カリスマ確かに頂戴いたしました
	PRINTFORMW 　それでは引き続きよろしくお願いいたします
LOOP 1
$CANCEL
PRINTFORML 
PRINTFORML メイド妖精
PRINTFORML 　かしこまりました
PRINTFORMW 　またのお越しをお待ちしておりますわ



;D/Pはディーラーとプレイヤー
;
;適当に作ったがコレかなり巧妙な手口だなぁ……
;・実は2勝、3勝の賞金よりも1勝の賞金が多い
;・獲得機会の多いであろう3勝以下の賞金は雀の涙
;・不規則なタイミングで正解が判明するためワンチャンあるかもと錯覚する
;
;賞金×1.5の操作とプレイヤー収入
;01回_150　__50
;02回_225　__25
;03回_337　__37
;04回_506　_106
;05回_759　_259
;06回1139　_539
;07回1708　1008
;08回2562　1762
;09回3840　2940
;10回5766　4766
;
;ディーラーの話術は4、これもわりとぶっ飛んだ性能の人だ
;
;プレイヤーの能力に応じた正誤検知率と誤答率
;話術1＝_1→検知率_1/16＝_6％、不明率94％÷2＝誤答率47％
;話術2＝_4→検知率_4/17＝23.5％、不明率76.5％÷2＝誤答率38％
;話術3＝_9→検知率_9/_25＝36％、不明率64％÷2＝誤答率32％
;話術4＝16→検知率_16/32＝50％、不明率50％÷2＝誤答率25％
;話術5＝25→検知率25/41＝61％、不明率39％÷2＝誤答率20％
;話術6＝36→検知率_36/_52＝69％、不明率31％÷2＝誤答率16％

;カンストでの10連勝率 17.4%

;2乗ではなく単純計算だと

;話術1→検知率_1/5＝_20％、不明率80％÷2＝誤答率40％
;話術2→検知率_2/6＝33.3％、不明率66.6％÷2＝誤答率33.3％
;話術3→検知率_3/_7＝36％、不明率42.8％÷2＝誤答率21.4％
;話術4→検知率_4/8＝50％、不明率50％÷2＝誤答率25％
;話術5→検知率5/9＝55.6％、不明率44.4％÷2＝誤答率22.2％
;話術6→検知率6/10＝60％、不明率40％÷2＝誤答率20％

;カンストでの10連勝率 10.7%


;単純計算でディーラーを5に

;話術1→検知率_1/6 不明率83％÷2＝誤答率41.5％
;話術2→検知率_2/7 不明率71.4％÷2＝誤答率35.7％
;話術3→検知率_3/8 不明率62.5％÷2＝誤答率31.3％
;話術4→検知率_4/9 不明率55.6％÷2＝誤答率27.8％
;話術5→検知率5/10 不明率50％÷2＝誤答率25％
;話術6→検知率6/11 不明率45.5％÷2＝誤答率22.8％

;カンストでの10連勝率 7.5%

;

;
;話術6で200回くらいやれば当たることは当たるのかな
;額面は4766だが2000を引いたら儲けは半分くらいだ
@CASINO_DICE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM HAITOU
#DIM DICE_1
#DIM DICE_2
#DIM DICE_SUM
#DIM D_YOSOU
#DIM D_TALK
#DIM P_YOSOU
#DIM P_TALK
D_TALK = POWER(4, 2)
P_TALK = POWER(MAX(ABL:MASTER:話術技能, 1), 2)
IF MONEY:2 <= 0
	PRINTFORML メイド妖精
	PRINTFORML 　あちゃー、お客さんカリスマ持ってないじゃん
	PRINTFORMW 　先に買ってこないと遊べないよ
	RETURN
ELSEIF MONEY:2 < 100
	PRINTFORML メイド妖精
	PRINTFORML 　これ1回100カリスマなんだよねー
	PRINTFORMW 　ごめんねー
	RETURN
ENDIF
PRINTFORML メイド妖精
PRINTFORML 　ダイスゲームコーナーです
PRINTFORMW 　どっかそこら辺に座っちゃってください
HAITOU = 100
DO
	TIME += 5
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　ではまずはゲームの参加料100カリスマをもらいますねー
	CALL CASINO_MONEY("カリスマ", -100)
	CALL CASINO_MONEY("カモ", 100)
	DICE_1 = FUNC_CASINO("DICE", 1, 6)
	DICE_2 = FUNC_CASINO("DICE", 1, 6)
	DICE_SUM = DICE_1 + DICE_2
	D_YOSOU = RAND(1, 3)
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　じゃー適当にコロコロっと
	PRINTFORML 　はーい、丁半どっちでしょうか
	IF D_YOSOU % 2
		PRINTFORML 　では私の予想、半
	ELSE
		PRINTFORML 　では私の予想、丁
	ENDIF
	WAIT
	PRINTFORML 
	LOCAL = D_TALK + P_TALK
	IF RAND:LOCAL <= D_TALK
		PRINTDATAL
			DATAFORM これは嘘だろうか？
			DATAFORM これは本当？……いや、わからない
			DATAFORM 眉毛の動きが不自然だ、嘘か？？？
			DATAFORM 考えれば考える程わからなくなってきた
			DATAFORM メイド妖精はふぁ～っと大きなあくびをした
			DATAFORM メイド妖精はぼけーっとこちらの顔を眺めている
			DATAFORM メイド妖精はダルそうで何を考えてるのかよくわからない
			DATAFORM メイド妖精の小鼻がピクピクしている……と思ったら可愛らしいくしゃみをした
		ENDDATA
	ELSE
		IF DICE_SUM % 2 == D_YOSOU % 2
			PRINTFORML この妖精の言うことは本当だ！
		ELSE
			PRINTFORML この妖精は嘘をついている！
		ENDIF
	ENDIF
	PRINTFORML 
	;[2]丁[1]半
	CALL CASINO_INPUTLOOP("丁半予想")
	P_YOSOU = RESULT
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　それじゃー結果発表のお時間でーす
	PRINTFORML 　パカっと開けると……どうだっ？
	PRINTFORML 
	PRINTFORML ～結果～
	IF DICE_SUM % 2
		PRINTFORML 出た目は{DICE_1}と{DICE_2}で半だった
	ELSE
		PRINTFORML 出た目は{DICE_1}と{DICE_2}で丁だった
	ENDIF
	
	IF DICE_SUM % 2 == P_YOSOU % 2
	;客の勝ち
		TIMES HAITOU, 1.50
		PRINTFORML メイド妖精
		PRINTFORML 　おー的中、配当金が1.5倍の{HAITOU}になります
		IF MONEY:2 < 100
			PRINTFORML 　お客さんは続行の料金払えないからここでいちど精算だね
			CALL CASINO_MONEY("カリスマ", HAITOU)
			SIF 500000 - FLAG:カリスマ流通 < 10000
				GOTO CLOSE_SHOP
			PRINTFORML 　さぁて、まだ続けます？それとも降りますか？
			;[1]続行[0]やめる
			CALL CASINO_INPUTLOOP("続行")
			IF !RESULT
				PRINTFORML メイド妖精
				PRINTFORMW 　また来てくださいねぇ
				RETURN
			ELSE
				PRINTFORML メイド妖精
				PRINTFORMW 　そーですよ、そーこなくっちゃ
				HAITOU = 100
			ENDIF
		ELSEIF NEMUKE() >= 700
			CALL CASINO_MONEY("カリスマ", HAITOU)
			$EXIT_NEMUI
			PRINTFORML 
			PRINTFORML メイド妖精
			PRINTFORML 　ん～、お客さんも眠いのかな
			PRINTFORML 　今日のゲームはここまでってことで
			PRINTFORMW 　またね～
			RETURN
		ELSEIF 500000 - FLAG:カリスマ流通 - HAITOU * 15 / 10 < 10000
			CALL CASINO_MONEY("カリスマ", HAITOU)
			$CLOSE_SHOP
			PRINTFORML 
			PRINTFORML メイド妖精
			PRINTFORML 　おっと、お店のお金がすっからかんだ
			PRINTFORML 　今日はもう続けらんないや
			PRINTFORMW 　また来てくださいねぇ
			RETURN
		ELSE
			PRINTFORML 　さぁて、まだ続けます？それとも降りますか？
			;[2]続行[1]精算して続行[0]精算してやめる
			CALL CASINO_INPUTLOOP("ダイス続行")
			SELECTCASE RESULT
			CASE 0
				PRINTFORML メイド妖精
				PRINTFORML 　では配当金{HAITOU}カリスマをお受け取りくださいな
				PRINTFORMW 　また来てくださいねぇ
				CALL CASINO_MONEY("カリスマ", HAITOU)
				RETURN
			CASE 1
				PRINTFORML メイド妖精
				PRINTFORML 　慎重ですねぇ
				PRINTFORMW 　では配当金{HAITOU}カリスマをお受け取りくださいな
				CALL CASINO_MONEY("カリスマ", HAITOU)
				HAITOU = 100
			CASE 2
				PRINTFORML メイド妖精
				PRINTFORMW 　そーですよ、そーこなくっちゃ
			ENDSELECT
		ENDIF
	ELSE
	;客の負け
		PRINTFORML メイド妖精
		PRINTFORML 　あらま残念
		IF MONEY:2 < 100
			PRINTFORMW 　カリスマが無いんじゃ続けられませんねぇ
			RETURN
		ELSEIF NEMUKE() >= 700
			WAIT
			GOTO EXIT_NEMUI
		ELSE
			PRINTFORML 　100カリスマ払って続けますか？
			;[1]続行[0]やめる
			CALL CASINO_INPUTLOOP("続行")
			IF !RESULT
				PRINTFORML メイド妖精
				PRINTFORMW 　また来てくださいねぇ
				RETURN
			ELSE
				PRINTFORML メイド妖精
				PRINTFORMW 　そーですよ、そーこなくっちゃ
				HAITOU = 100
			ENDIF
		ENDIF
	ENDIF
LOOP 1



;D/Pはディーラーとプレイヤー
;最高賞金は90で消費カリスマの4.5倍という屈指のコスパを誇るも教養6でようやく正答率50％
;さらに動く額面が固定して小さいため大して稼げない
;確率で正解が決定するためいかに教養を上げても誤答率は0にはならないという地味な罠もある
@CASINO_QUIZ
#LOCALSIZE 1
#LOCALSSIZE 10
#DIM Q_LV
#DIM Q_COUNT
#DIM Q_CHECK
Q_COUNT = 1
SIF !STRLENS(LOCALS)
	SPLIT "/妖精でもわかる問題/ふつうの人間むきな問題/ふつうでない人間むきの問題/魔法使いならわかってほしい問題/魔法使いも頭を抱える難問/誰もが唸る奇問", "/", LOCALS
PRINTFORML メイド妖精
PRINTFORML 　さぁさぁ始まりました！
PRINTFORML 　『動かない大図書館』こと魔法使いパチュリー・ノーリッジ様完全監修！
PRINTFORMW 　“パチュリークイズ”の時間です！！
IF MONEY:2 < 5
	PRINTFORMW 　……ってあれー、お客さんカリスマ足りませんよー
	RETURN
ENDIF

Q_LV = 1
PRINTFORML メイド妖精
PRINTFORMW 　それでは早速第1問！
DO
	TIME += 5
	CALL CASINO_MONEY("カリスマ", -5)
	CALL CASINO_MONEY("カモ", 5)
	PRINTFORML 　・
	PRINTFORML 　・
	PRINTFORML 　・
	PRINTFORML 
	PRINTFORMW あなたはクイズに答えた
	PRINTFORML 

	Q_CHECK = POWER(Q_LV, 3)
	LOCAL = Q_CHECK + POWER(MAX(ABL:MASTER:教養, 1), 3)
	IF RAND:LOCAL <= Q_CHECK
		PRINTFORML メイド妖精
		PRINTFORML 　ざんねーん！不正解でーす
	ELSE
		PRINTFORML メイド妖精
		PRINTFORML 　すごーい、大正解！
		PRINTFORML 　賞金{Q_LV * 5}もってけー！！！
		CALL CASINO_MONEY("カリスマ", Q_LV * 5)
	ENDIF
	WAIT
	PRINTFORML 
	IF MONEY:2 < 5
		PRINTFORML メイド妖精
		PRINTFORML 　あーっと、ここでお客さんがカリスマ切れだー！！
		PRINTFORMW 　また遊びに来て下さいね～
		RETURN
	ELSEIF FUNC_CASINO("CLOSE_CASINO")
		PRINTFORML メイド妖精
		PRINTFORML 　おーっと、本日はここらで閉店のようです！！
		PRINTFORMW 　また遊びに来て下さいね～
		RETURN
	ELSEIF NEMUKE() >= 700
		PRINTFORML メイド妖精
		PRINTFORML 　おやおや？お客さんずいぶんお疲れのご様子ですね！！
		PRINTFORMW 　また遊びに来て下さいね～
		RETURN
	ENDIF

	Q_COUNT ++
	SELECTCASE RAND(1, 101)
	CASE 1 TO 8
		Q_LV = 1
	CASE 9 TO 22
		Q_LV = 2
	CASE 23 TO 50
		Q_LV = 3
	CASE 51 TO 78
		Q_LV = 4
	CASE 79 TO 92
		Q_LV = 5
	CASE 93 TO 100
		Q_LV = 6
	ENDSELECT
	PRINTFORML メイド妖精
	PRINTFORML 　続いて第{Q_COUNT}問！
	PRINTFORML 　この問題は%LOCALS:Q_LV%です
	PRINTFORML 　ここでビビって止めちゃっても構いませんよ～
	PRINTFORML 　どうしますかー！？
	PRINTFORML 
	;[1]続行[0]やめる
	CALL CASINO_INPUTLOOP("続行")
	IF !RESULT
		PRINTFORML メイド妖精
		PRINTFORML 　ご利用ありがとうございましたー！！
		PRINTFORMW 　また遊びに来て下さいね～
		RETURN
	ENDIF
	PRINTFORML メイド妖精
	PRINTFORMW 　それでは次の問題を読み上げます！
LOOP 1



;カジノ用ファンクション
@FUNC_CASINO(TYPE, ARG, ARG:1)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM LCOUNT
#DIM YEN
#DIM CHARISMA
SELECTCASE TYPE
CASE "DICE"
;汎用で使えるがダイス関数
;個人的に改造のたびになんやかんや毎回作って入れてるような気がする
	LOCAL = 0
	FOR LCOUNT, 0, ARG
		LOCAL += RAND(1, ARG:1)
	NEXT
CASE "TRADE"
;紅魔館用の為替取引レート
;基本レート￥200 == 1カリスマ
	LOCAL = CHARISMA_NOW()
CASE "CLOSE_CASINO"
;カジノ休業判定
;廃止になったような気もするが、読みやすいように
	IF 500000 - FLAG:カリスマ流通 < 10000
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
ENDSELECT
RETURNF LOCAL



;カジノ金銭取引関数
;あなたが1万￥を得た場合には
;CALL CASINO_MONEY("￥", 10000)
;あなたが100カリスマを失った場合には
;CALL CASINO_MONEY("カリスマ", -100)
;
;あなたの最高収入はたぶん一ヶ月で100万には届かない
;紅魔館側で1億＄と50万カリスマを用意
@CASINO_MONEY(TYPE, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
SELECTCASE TYPE
CASE "￥"
	;払い戻し上限
	IF ARG > 0
		LOCAL = MIN(ARG, 100000000 - FLAG:￥流通)
		SIF LOCAL < 0
			LOCAL = 0
	ELSE
		LOCAL = ARG
	ENDIF
	MONEY += LOCAL
	FLAG:￥流通 += LOCAL
CASE "カリスマ"
	;払い戻し上限
	IF ARG > 0
		LOCAL = MIN(ARG, 500000 - FLAG:カリスマ流通)
		SIF LOCAL < 0
			LOCAL = 0
	ELSE
		LOCAL = ARG
	ENDIF
	MONEY:2 += LOCAL
	FLAG:カリスマ流通 += LOCAL
CASE "カモ"
	FLAG:カモられ指数 += ARG
ENDSELECT



;汎用性もクソもねぇ
;さして意味もないDO-LOOPがうざかった
@CASINO_INPUTLOOP(TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM C_MAX
#DIM LCOUNT
SELECTCASE TYPE
CASE "カード賭け金"
	C_MAX = MIN(50, MONEY:2, (500000 - FLAG:カリスマ流通 - 10000) / 2)
	PRINTFORML 手持ちカリスマ：{MONEY:2}（上限{C_MAX}）
	SIF C_MAX >= 1
		PRINTFORML [{1, LOG10(C_MAX) + 1}] - 1カリスマを賭ける
	SIF C_MAX >= 10
		PRINTFORML [{10, LOG10(C_MAX) + 1}] - 10カリスマを賭ける
	SIF C_MAX >= 4
		PRINTFORML [{C_MAX / 2, LOG10(C_MAX) + 1}] - 最大値の半分を賭ける
	PRINTFORML [{C_MAX, LOG10(C_MAX) + 1}] - 最大値を賭ける
	DO
		INPUT
		IF INRANGE(RESULT, 0, C_MAX)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
CASE "カード戦術"
	PRINTFORML あなたの戦術は……
	PRINTFORML [3] - イカサマ
	PRINTFORML [2] - 正々堂々
	PRINTFORML [1] - 注視
	DO
		INPUT
		IF INRANGE(RESULT, 1, 3)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
CASE "カード続行"
	PRINTFORML [2] - 賭け金の変更
	PRINTFORML [1] - 続行
	PRINTFORML [0] - やめる
	DO
		INPUT
		IF INRANGE(RESULT, 0, 2)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
CASE "続行"
	PRINTFORML [1] - 続ける
	PRINTFORML [0] - やめる
	DO
		INPUT
		IF INRANGE(RESULT, 0, 1)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
CASE "丁半予想"
	PRINTFORML ～あなたの予想～
	PRINTFORML [2] - 丁
	PRINTFORML [1] - 半
	DO
		INPUT
		IF INRANGE(RESULT, 1, 2)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
CASE "ダイス続行"
	PRINTFORML [2] - 続行
	PRINTFORML [1] - 精算して続行
	PRINTFORML [0] - 精算してやめる
	DO
		INPUT
		IF INRANGE(RESULT, 0, 2)
			BREAK
		ELSE
			REUSELASTLINE 無効な入力です
			CONTINUE
		ENDIF
	LOOP 1
ENDSELECT
LOCAL = RESULT
RETURN LOCAL



;カジノ用情報表示関数
;主にブラックジャック関連
@CASINO_DISP(TYPE, ARG, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM PL
#DIMS HAND_OP
SELECTCASE TYPE
CASE "BJ_CLOSE"
;	DRAWLINEFORM ━
;	PRINTFORML 山札残り{52 - CARD_DECK:0}
	DRAWLINEFORM ━
	PRINTFORML 【メイド妖精】
	CALL CARD_DISP("CARD", CARD_HAND:0:0)
	CALL CARD_DISP("CARD_L")
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_PL_HAND")
	DRAWLINEFORM ━
CASE "BJ_OPEN"
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_DL_HAND")
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_PL_HAND")
	DRAWLINEFORM ━
CASE "BJ_PL_HAND"
	PRINTFORML 【%CALLNAME:MASTER%】
	CALL CARD_DISP("HAND", 1)
	PRINTFORML 
	CALL CASINO_DISP("BJ_POINT", 1)
	PRINTFORML 
CASE "BJ_DL_HAND"
	PRINTFORML 【メイド妖精】
	CALL CARD_DISP("HAND", 0)
	PRINTFORML 
	CALL CASINO_DISP("BJ_POINT", 0)
	PRINTFORML 
CASE "BJ_POINT"
	PL = ARG
	PRINTFORM 　点数：
	;情報更新のタイミングに注意
	IF CARD_PLAYER("GET_BJ_BUST", PL)
		SETCOLOR 255, 0, 0
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2% BUST!!
		RESETCOLOR
	ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
		SETCOLOR 255, 255, 0
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2% BLACK JACK!!
		RESETCOLOR
	ELSE 
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2%
	ENDIF
CASE "RESULT"
	IF ARG
		PRINTL 　
		PRINTFORML ～リザルト～
		PRINTFORML 　賭け金総額： {ARG:0, 10}
		PRINTFORML 　　配当総額： {ARG:1, 10}
		PRINTFORML 　　　　収支：\@ ARG:1 - ARG:0 >= 0 ? + #   \@{ARG:1 - ARG:0, 10}
		PRINTW
	ENDIF
ENDSELECT



;ここからカード関連関数群
;カジノ以外でも使えることは使えるはず

;カードゲーム系は汎用化する旨味が大きいと思った


;カード（トランプ）用汎用関数
@PLAY_CARDS(TYPE, ARG:0, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM  PL_ID
SELECTCASE TYPE
CASE "SHUFFLE"
;カードの山札をシャッフルする
;引数＝使用するジョーカーの数（上限8）
	VARSET CARD_DECK
	NUM_JOKER = LIMIT((ARG:0), 0, MAX_JOKER)
	NUM_CARD  = 52 + NUM_JOKER
	;0～52までつくって0を除去する
	CALL FISHER_YATES_SHAFFLE(NUM_CARD + 1)
	ARRAYREMOVE SHAFFLE_ARRAY, FINDELEMENT(SHAFFLE_ARRAY, 0), 1
	ARRAYCOPY "SHAFFLE_ARRAY", "CARD_DECK"
	NEW_CARD = 0
CASE "RESET_HAND"
;全てのプレイヤーの手札を破棄する
;NEW_CARDの数値はリセットしない
	VARSET NUM_HAND
	VARSET CARD_HAND
CASE "HIT"
;特定プレイヤーがカードを山札から引く
;引数＝プレイヤー番号
	CARD_HAND:(ARG:0):(NUM_HAND:(ARG:0)) = CARD_DECK:NEW_CARD
	NUM_HAND:(ARG:0) ++
	NEW_CARD ++
CASE "HIT_BJ"
;ブラックジャック用
	CALL PLAY_CARDS("HIT", (ARG:0))
	CALLF CARD_PLAYER("RENEW_BJ_POINT", (ARG:0))

CASE "NUM_HIT"
;ARG:0のプレイヤーの手札をARG:1枚だけ補充する
	FOR LOCAL, NUM_HAND:(ARG:0) , NUM_HAND:(ARG:0) + ARG:1
		CALL PLAY_CARDS("HIT", ARG)
	NEXT
CASE "REHIT"
;ARG:0のプレイヤーの手札、ARG:1を「上書き」する
;元の手札は参照できない（「捨てる」ではない）
;手札枚数は変わらない
;現状で捨て札の記録が必要になるシステムは無いのでとりあえずこれで実装
	CARD_HAND:(ARG:0):(ARG:1) = CARD_DECK:NEW_CARD
	NEW_CARD ++

CASE "POKER_FIRST_HAND"
;ポーカーの最初の手札、ARG:0のプレイヤーの手札を5枚
	CALL PLAY_CARDS("NUM_HIT", ARG:0, 5)

ENDSELECT



;カード表示関数
@CARD_DISP(TYPE, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM PL
#DIMS HAND_OP
#DIM C_HAND

SELECTCASE TYPE
CASE "CARD", "CARD_L"
;汎用のカード表示用
	;0とJKは別扱い
	IF !ARG
		PRINT ［ ？ ］
		SIF TYPE == "CARD_L"
			PRINTL
		RETURN
	ELSEIF CARD_CHECK("JOKER", ARG)
		PRINT ［ JK ］
		SIF TYPE == "CARD_L"
			PRINTL
		RETURN
	ENDIF
	PRINT ［
	;スート表示, スペード, クラブ, ハート, ダイヤの順
	SELECTCASE CARD_CHECK("SUIT", ARG)
	CASE 1
		SETCOLOR C_BLUE
	CASE 2
		SETCOLOR C_GREEN
	CASE 3
		SETCOLOR C_RED
	CASE 4
		SETCOLOR C_YELLOW
	ENDSELECT
	PRINTFORM %CARD_SUIT:CARD_CHECK("SUIT", ARG)% 
	RESETCOLOR
	;数字表示, あまり0は13, 遊びで９を⑨にしたり色を変えたりしてます
	;記号やＡＡで絵札を表現するのも面白そう（めんどくさい）
	SELECTCASE CARD_CHECK("RANK", ARG)
	CASE 1
		SETCOLOR 255, 0, 0
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
		RESETCOLOR
	CASE 9
		SETCOLOR 0, 255, 255
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
		RESETCOLOR
	CASEELSE
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
	ENDSELECT
	PRINT ］　
	SIF TYPE == "CARD_L"
		PRINTL
CASE "HAND", "HAND_L", "HAND_BUTTON"
;汎用の手札表示用
	PL = ARG
	HAND_OP = %TYPE%
	FOR C_HAND, 0, NUM_HAND:PL
		SIF CARD_HAND:PL:C_HAND == 0
			BREAK
		SIF HAND_OP == "HAND_BUTTON"
			PRINTFORM [{C_HAND, 2}]
		IF HAND_OP == "HAND_L"
			CALL CARD_DISP("CARD_L", CARD_HAND:PL:C_HAND)
		ELSE
			CALL CARD_DISP("CARD", CARD_HAND:PL:C_HAND)
		ENDIF
	NEXT
ENDSELECT



;カードのプレイヤー変数の管理用F関数
;"GET"のないTYPEはCALLFで使う
@CARD_PLAYER(TYPE, PL)
#FUNCTION
#DIMS TYPE     ;処理内容
#DIM DYNAMIC PL;判定対象プレイヤー
#DIM PL_HAND   ;手札判定ループ用
#DIM PL_ID     ;プレイヤー判定ループ用

;ブラックジャック用変数
;バースト・ブラックジャックあたりはBITつかえば1変数で済むけどこれでいいや
#DIM P_BJ,    MAX_CARD_PL ;各プレイヤーの得点
#DIM BJ_ACE,  MAX_CARD_PL ;所持するエースの数
#DIM BJ_BJ,   MAX_CARD_PL ;ブラックジャックを達成しているかどうか
#DIM BJ_BUST, MAX_CARD_PL ;バーストしたかどうか

;ポーカー用変数
;とりあえず複数人プレイは考慮していない
#DIM PL_RANK,  5  ;手札情報の一時取得用
#DIM PL_SUIT,  5  ;手札情報の一時取得用
#DIM PK_GROUP     ;連番判定用
#DIM CNT_RANK, 14 ;各数字の枚数取得
#DIM CNT_SUIT, 5  ;各スートの枚数取得
#DIM PK_RATE      ;得られた倍率、戻り値として使用（％）
SELECTCASE TYPE

;こっからブラックジャック用
CASE "CLEAR_BJ_ALL"
;計算用数値の初期化
	VARSET P_BJ
	VARSET BJ_ACE
	VARSET BJ_BJ
	VARSET BJ_BUST
CASE "CLEAR_BJ"
;特定プレイヤー向け
	P_BJ:PL = 0
	BJ_ACE:PL = 0
	BJ_BJ:PL = 0
	BJ_BUST:PL = 0
CASE "RENEW_BJ_POINT_ALL"
;全プレイヤーの状況を再計算
	CALLF CARD_PLAYER("CLEAR_BJ_ALL", PL)
	FOR PL_ID, 0, NUM_CARD_PL
		CALLF CARD_PLAYER("CALC_BJ_POINT", PL_ID)
	NEXT
CASE "RENEW_BJ_POINT"
;特定プレイヤーの状況を再計算
	CALLF CARD_PLAYER("CLEAR_BJ", PL)
	CALLF CARD_PLAYER("CALC_BJ_POINT", PL)
CASE "CALC_BJ_POINT"
;ブラックジャックの点数計算用
;特定のプレイヤーの現在の手札の得点を計算する
	;12枚で確実にバーストするが一応計算はする
	FOR PL_HAND, 0, NUM_HAND:PL
		;引いてない（ありえなそうだが一応）
		SIF CARD_HAND:PL:PL_HAND == 0
			BREAK
		;ジョーカー分岐だけつくったけどちょっとまだ対応してないよ
		IF CARD_CHECK("JOKER", CARD_HAND:PL:PL_HAND)
		ELSEIF CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND) == 1
			BJ_ACE:PL ++
		ELSE
			P_BJ:PL += CARD_CHECK("BJ_POINT", CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND))
		ENDIF
	NEXT
	IF BJ_ACE:PL
	;4パターン、1or11,2or12,3or13,4or14
	;2枚以上を11扱いするとそれだけでバーストなので考慮する必要はない
		IF P_BJ:PL + 10 + BJ_ACE:PL <= 21
			P_BJ:PL += 10 + BJ_ACE:PL
		ELSE
			P_BJ:PL += BJ_ACE:PL
		ENDIF
	ENDIF
	SIF NUM_HAND:PL == 2 && P_BJ:PL == 21
		BJ_BJ:PL = 1
	SIF P_BJ:PL > 21
		BJ_BUST:PL = 1
CASE "GET_BJ_POINT"
;特定のプレイヤーの得点
	RETURNF P_BJ:PL
CASE "GET_BJ_BJ"
;特定のプレイヤーがブラックジャックを達成しているかどうか
	RETURNF BJ_BJ:PL
CASE "GET_BJ_BUST"
;特定のプレイヤーがバーストしたかどうか
	RETURNF BJ_BUST:PL


CASE "GET_POKER_RATE"
;ポーカーのレート計算
;いまんとこ手札5枚の1人プレイしか想定してない
;ベットが出来たほうが「らしい」けどAIとか評価関数とか考えたくないよ
	VARSET PL_RANK
	VARSET PL_SUIT
	VARSET PK_GROUP
	VARSET CNT_RANK
	VARSET CNT_SUIT
	VARSET PK_RATE
	;手札配列のコピー
	FOR PL_HAND, 0, NUM_HAND:PL
		PL_RANK:PL_HAND = CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND)
		PL_SUIT:PL_HAND = CARD_CHECK("SUIT", CARD_HAND:PL:PL_HAND)
	NEXT
	PK_GROUP = 0
	;枚数カウント
	FOR LOCAL, 1, 14
		CNT_RANK:LOCAL = MATCH(PL_RANK, LOCAL)
		;連番判定
		IF LOCAL == 1 || PK_GROUP == 5
		ELSEIF CNT_RANK:(LOCAL - 1) && CNT_RANK:(LOCAL)
			PK_GROUP ++
		ELSE
			PK_GROUP = 0
		ENDIF
		;ついでにスートのカウントもやっちゃう
		SIF !INRANGE(LOCAL, 1, 4)
			CONTINUE
		CNT_SUIT:LOCAL = MATCH(PL_SUIT, LOCAL)
	NEXT
	;10, J, Q, K, Aはストレート扱い
	SIF GROUPMATCH(1, CNT_RANK:1, CNT_RANK:10, CNT_RANK:11, CNT_RANK:12, CNT_RANK:13) == 5
		PK_GROUP = 5
	
	;レートは％表記デス（小数は返せないので）
	IF MATCH(CNT_SUIT, 5) && GROUPMATCH(1, CNT_RANK:1, CNT_RANK:10, CNT_RANK:11, CNT_RANK:12, CNT_RANK:13) == 5
	;Royal Straight
		PK_RATE = 30000
	ELSEIF MATCH(CNT_SUIT, 5) && PK_GROUP == 5
	;Straight Flush
		PK_RATE = 5000
	ELSEIF MATCH(CNT_RANK, 4)
	;Four of a Kind
		PK_RATE = 3000
	ELSEIF MATCH(CNT_RANK, 3) && MATCH(CNT_RANK, 2)
	;Full House
		PK_RATE = 2000
	ELSEIF MATCH(CNT_SUIT, 5)
	;Flush
		PK_RATE = 1000
	ELSEIF PK_GROUP == 5
	;Straight
		PK_RATE = 500
	ELSEIF MATCH(CNT_RANK, 3)
	;Three of a Kind
		PK_RATE = 300
	ELSEIF MATCH(CNT_RANK, 2) == 2
	;Two Pair
		PK_RATE = 100
	ELSEIF MATCH(CNT_RANK, 2)
	;One Pair
		PK_RATE = 50
	ELSE
	;No Pair
		PK_RATE = 0
	ENDIF
	RETURNF PK_RATE
ENDSELECT



;カードゲーム判定F関数
;カードの一枚一枚に対する判定
@CARD_CHECK(TYPE, D_ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM DYNAMIC D_ARG ;適当にDYNAMIC化したかっただけなんだどいい名前が思いつかない
#DIM P_BJ
#DIM ACE_COUNT
#DIM C_HAND
SELECTCASE TYPE
CASE "JOKER"
;RANK/SUITは戻り値0でジョーカー/数値無しの判別が不能のため
;先にJOKERをチェックして下さい
	SIF D_ARG > 52
		RETURNF 1
	RETURNF 0
CASE "RANK"
;戻り値1～13
;ジョーカーのとき0
	SIF !D_ARG
		RETURNF
	SIF D_ARG > 52
		RETURNF 0
	SIF !(D_ARG % 13)
		RETURNF 13
	RETURNF D_ARG % 13
CASE "SUIT"
;戻り値1～4
;ジョーカーのとき0
	SIF !D_ARG
		RETURNF
	SIF D_ARG > 52
		RETURNF 0
	RETURNF (D_ARG - 1) / 13 + 1

CASE "BJ_POINT"
;ブラックジャックの点数計算用
;ACEのポイントが手札次第で変わってしまってこの関数だけでは扱えない
;変数の上書き問題が面倒なので独立させた
;引数はRANKにして下さい
;ちなジョーカー非対応
	SIF GROUPMATCH(D_ARG, 11, 12, 13)
		RETURNF 10
	RETURNF D_ARG

CASE "BJ_BUST"
	SIF D_ARG > 21
		RETURNF 1
	RETURNF 0
ENDSELECT


@CASINO_MEMBER_RANKUP


IF FLAG:カモられ指数 >= 100 && FLAG:会員ランク < 1
	PRINTFORMW 店を出ようとするとメイド妖精に呼び止められた
	PRINTFORML 「いつも当カジノをご愛顧いただき、誠にありがとうございます」
	PRINTFORM 「ただいまをもちまして、お客様の会員ランクが
	CALL COLORMESSAGE("ブロンスカリスマ会員",C_YELLOW)
	PRINTFORML となり、カリスマ保有上限の上昇、およびカリスマの売却が解禁されました」
	PRINTFORMW 「今後もご贔屓を賜りますようお願いします」
	FLAG:会員ランク = 1
	CM_COMMISSION = 25
	CM_LIMIT = 500
	FLAG:カモられ指数 = 0
ENDIF
IF FLAG:カモられ指数 >= 3000 && FLAG:会員ランク < 2
	PRINTFORMW 店を出ようとするとメイド妖精に呼び止められた
	PRINTFORML 「いつも当カジノをご愛顧いただき、誠にありがとうございます」
	PRINTFORM 「ただいまをもちまして、お客様の会員ランクが
	CALL COLORMESSAGE("シルバーカリスマ会員",C_YELLOW)
	PRINTFORML となり、カリスマ保有上限の上昇とカリスマの売却手数料の引き下げが適用されます」
	PRINTFORMW 「今後もご贔屓を賜りますようお願いします」
	FLAG:会員ランク = 2
	CM_COMMISSION = 20
	CM_LIMIT = 1000
	FLAG:カモられ指数 = 0
ENDIF

IF FLAG:カモられ指数 >= 10000 && FLAG:会員ランク < 3
	PRINTFORMW 店を出ようとするとメイド妖精に呼び止められた
	PRINTFORML 「いつも当カジノをご愛顧いただき、誠にありがとうございます」
	PRINTFORM 「ただいまをもちまして、お客様の会員ランクが
	CALL COLORMESSAGE("ゴールドカリスマ会員",C_YELLOW)
	PRINTFORML となり、さらなるカリスマ保有上限の上昇とカリスマの売却手数料の引き下げが適用されます」
	PRINTFORMW 「今後もご贔屓を賜りますようお願いします」
	FLAG:会員ランク = 3
	CM_COMMISSION = 18
	CM_LIMIT = 3000
	FLAG:カモられ指数 = 0
ENDIF
[SKIPSTART]
IF !ITEM:紅魔カジノVIP会員証
PRINTFORML 
PRINTFORMW 店を出ようとするとメイド妖精に呼び止められた
PRINTFORML 
;VIPるーむの仕様が固まりきらないので封印
;封印しといて文章だけ拡充するのってどうなんだ
;れみふらさくやは顔パスでも良さそうだけどな
	SELECTCASE CSVCALLNAME(FLAG:なりきり)
	CASE "レミリア", "フラン", "パチュリー"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%様、少しお時間を頂いてもよろしいでしょうか？
		PRINTFORML 　ご存知のこととは思いますが当店にはVIPルームがございます
		PRINTFORML 　随分とカジノをお気に召したご様子でしたのでVIP会員証を用意させていただきました
	CASE "咲夜"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%さん、VIP会員証をお持ちしました
		PRINTFORMW 　VIPルームにご案内しますね
	CASE "美鈴", "小悪魔"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%さん、完全にハマっちゃってますよね
		PRINTFORML 　はい、これVIP会員証です
		PRINTFORMW 　VIPルームに案内しますからついてきて下さい
	CASE "あなた"
		IF MAIN_MAP == 3
			PRINTFORML メイド妖精
			PRINTFORML 　もうすっかり常連さんですね
			PRINTFORML 　うちのディーラーになっちゃいますか？
			PRINTFORML 　はい、これVIP会員証です
			PRINTFORMW 　VIPルームに案内しますからついてきて下さい
		ELSE
			PRINTFORML メイド妖精
			PRINTFORML 　少しお時間を頂いてもよろしいでしょうか？
			PRINTFORML 　いつも当店をご利用いただきありがとうございます
			PRINTFORML 　お客様は当店のVIP会員として認められました
			PRINTFORMW 　こちらがVIP会員証でございます
		ENDIF
	CASEELSE
			PRINTFORML メイド妖精
			PRINTFORML 　少しお時間を頂いてもよろしいでしょうか？
			PRINTFORML 　いつも当店をご利用いただきありがとうございます
			PRINTFORML 　お客様は当店のVIP会員として認められました
			PRINTFORMW 　こちらがVIP会員証でございます
	ENDSELECT
	PRINTFORML 
	PRINTFORML メイド妖精はあなたに会員証を手渡すとついてくるようにと促した
	PRINTFORMW 案内された先は何の変哲もない紅い壁の前に見えるが……？
	PRINTFORML 
	SELECTCASE CSVCALLNAME(FLAG:なりきり)
	CASE "レミリア", "フラン", "パチュリー"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%様、先ほどの会員証をこちらの壁の前におかざしになって下さい
	CASE "咲夜"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%さん、先ほどの会員証をこちらの壁の前にかざしてみて下さい
	CASE "美鈴", "小悪魔"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%さん、さっきの会員証をこちらの壁の前にかざしてみて下さい
	CASE "あなた"
		IF MAIN_MAP == 3
			PRINTFORML メイド妖精
			PRINTFORMW 　さっきの会員証をこちらの壁の前にかざしてみて下さい
		ELSE
			PRINTFORML メイド妖精
			PRINTFORMW 　お客様、先ほどの会員証をこちらの壁の前におかざしになって下さい
		ENDIF
	CASEELSE
			PRINTFORML メイド妖精
			PRINTFORMW 　お客様、先ほどの会員証をこちらの壁の前におかざしになって下さい
	ENDSELECT
	PRINTFORML 
	PRINTFORMW %CALLNAME:MASTER%が言われたとおりにすると、かすかな機械音と共に目の前の壁が開いた
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　こちら、当店自慢のVIPルームでございます
	PRINTFORML 　VIPルーム内部ではさまざまなサービスをご提供いたしております
	PRINTFORML 　貴重なお時間をいただきありがとうございました
	PRINTFORML 　それでは気をつけてお帰り下さいませ
	PRINTFORMW 　店員一同、またのご来店を心よりお待ちしていおります
	PRINTFORML 
	PRINTFORMW VIPルームが利用可能になった
	ITEM:紅魔カジノVIP会員証 = 1
ENDIF
[SKIPEND]