;
;スロットのメインプログラム
;


@CASINO_SLOT
;リール配列
;	左  	中  	右 
;1	REP 	MEL 	BEL
;2	BL7 	BL7 	BL7
;3	CHE 	REP 	MEL
;4	MEL 	BEL 	CHE
;5	BEL 	CHE 	REP
;6	REP 	CHE 	BEL
;7	CHE 	BAR 	RD7
;8	BAR 	REP 	MEL
;9	MEL 	BEL 	BAR
;10	BEL 	CHE 	REP
;11	REP 	RD7 	BEL
;12	BAR 	CHE 	BEL
;13	CHE 	REP 	MEL
;14	MEL 	BEL 	CHE
;15	BEL 	BAR 	REP
;16	REP 	MEL 	BEL
;17	RD7 	CHE 	BAR
;18	CHE 	REP 	MEL
;19	MEL 	BEL 	CHE
;20	BEL 	CHE 	REP

;絵柄一覧
;1 RD7 赤7(ボーナス図柄)
;2 BL7 青7(ボーナス図柄)
;3 BEL ベル
;4 CHE チェリー
;5 MEL スイカ
;6 BAR BAR(ボーナス図柄)
;7 REP リプレイ
;8 NUL 未選択

;=============================
;変数定義
;=============================
#DIM 初期所持カリスマ
#DIM 総回転数
#DIM 前回当選からの回転数
#DIM 短周期連開始時所持カリスマ

#DIM オートモード
#DIM フリーズ回数
#DIM CONST 検定回数 = 100000

#DIM 当選履歴, 11
#DIM 当選種類履歴, 11
#DIM 当選枚数履歴, 11

#DIM 小役カウンタ, 100

#DIM 情報パネル表示ページ

;一回転分の掛け金は3カリスマ
#DIM CONST 掛け金 = 3

;各絵柄の略称(B_A_Rのみ5文字なのに注意!!)
#DIM CONST RD7 = 1
#DIM CONST BL7 = 2
#DIM CONST BEL = 3
#DIM CONST CHE = 4
#DIM CONST MEL = 5
#DIM CONST B_A_R = 6
#DIM CONST REP = 7
#DIM CONST NUL = 8

;当選小役の種類
#DIM CONST 当選小役_赤7ボーナス = 0
#DIM CONST 当選小役_青7ボーナス = 1
#DIM CONST 当選小役_レギュラーボーナス = 2
#DIM CONST 当選小役_中チェリー = 3
#DIM CONST 当選小役_強チェリー = 4
#DIM CONST 当選小役_弱チェリー = 5
#DIM CONST 当選小役_強スイカ = 6
#DIM CONST 当選小役_弱スイカ = 7
#DIM CONST 当選小役_ベル = 8
#DIM CONST 当選小役_リプレイ = 9
#DIM CONST 当選小役_はずれ = 99
;枠の色
#DIM 色_枠
#DIM CONST 削除行数 = 51

#DIM 絵柄_左上
#DIM 絵柄_左中
#DIM 絵柄_左下
#DIM 絵柄_中上
#DIM 絵柄_中中
#DIM 絵柄_中下
#DIM 絵柄_右上
#DIM 絵柄_右中
#DIM 絵柄_右下
#DIM 暗転_左上
#DIM 暗転_左中
#DIM 暗転_左下
#DIM 暗転_中上
#DIM 暗転_中中
#DIM 暗転_中下
#DIM 暗転_右上
#DIM 暗転_右中
#DIM 暗転_右下

;NULがDIM中で使用できないので直打ち
#DIM 絵柄_左上_回転中
#DIM 絵柄_左中_回転中
#DIM 絵柄_左下_回転中
#DIM 絵柄_中上_回転中
#DIM 絵柄_中中_回転中
#DIM 絵柄_中下_回転中
#DIM 絵柄_右上_回転中
#DIM 絵柄_右中_回転中
#DIM 絵柄_右下_回転中
#DIM 暗転_左上_回転中
#DIM 暗転_左中_回転中
#DIM 暗転_左下_回転中
#DIM 暗転_中上_回転中
#DIM 暗転_中中_回転中
#DIM 暗転_中下_回転中
#DIM 暗転_右上_回転中
#DIM 暗転_右中_回転中
#DIM 暗転_右下_回転中

#DIM 設定
#DIM 内部状態
#DIM 短周期連継続率
#DIM 短周期連セット数
#DIM 累計短周期連セット数
#DIM 前兆
#DIM 当選した小役
#DIM 当選したBB種類
#DIM 小役抽選しないよ
#DIM 投入カリスマ

;=============================
;各変数の初期化
;=============================
;のちのち状況に応じて枠の色を変える予定あり
色_枠 = 0xE6B422
初期所持カリスマ = MONEY:2
短周期連開始時所持カリスマ = 0
総回転数 = 0
前回当選からの回転数 = 0

オートモード = 0
フリーズ回数 = 0

短周期連セット数 = 0
累計短周期連セット数 = 0

情報パネル表示ページ = 0

;0回転当選が存在しないため0で初期化
VARSET 当選履歴, 0

VARSET 当選種類履歴, -1
VARSET 当選枚数履歴, 0

VARSET 小役カウンタ, 0

絵柄_左上 = REP
絵柄_左中 = RD7
絵柄_左下 = CHE
絵柄_中上 = CHE
絵柄_中中 = RD7
絵柄_中下 = CHE
絵柄_右上 = BEL
絵柄_右中 = RD7
絵柄_右下 = MEL
暗転_左上 = 0
暗転_左中 = 0
暗転_左下 = 0
暗転_中上 = 0
暗転_中中 = 0
暗転_中下 = 0
暗転_右上 = 0
暗転_右中 = 0
暗転_右下 = 0

絵柄_左上_回転中 = NUL
絵柄_左中_回転中 = NUL
絵柄_左下_回転中 = NUL
絵柄_中上_回転中 = NUL
絵柄_中中_回転中 = NUL
絵柄_中下_回転中 = NUL
絵柄_右上_回転中 = NUL
絵柄_右中_回転中 = NUL
絵柄_右下_回転中 = NUL
暗転_左上_回転中 = 0
暗転_左中_回転中 = 0
暗転_左下_回転中 = 0
暗転_中上_回転中 = 0
暗転_中中_回転中 = 0
暗転_中下_回転中 = 0
暗転_右上_回転中 = 0
暗転_右中_回転中 = 0
暗転_右下_回転中 = 0

;数字によって色んな期待値が変わるアレ(内部状態とは異なる)
;設定 = RAND(6) + 1
CALL 設定決定(設定)


;内部状態:機械内の状態を表す
;1:通常,2:高確(各フラグによる抽選が2回になる)
;3超高確:(各フラグによる抽選が3回になる),4:短周期連(天井３２Ｇ),5フリーズ
内部状態 = 1

;短周期連継続率:内部状態が３２Ｇ連のときのループ継続率
;0～95の値が入る予定
短周期連継続率 = 0
;発生していないときは-1,発生した場合1ゲームごとに値を減らし0になると当選
前兆 = -1

;1が立っている場合小役抽選を行わない
;情報パネルの操作を行うとさっき成立した小役による各種抽選をまた行ってしまうためこのフラグを立てる
;無抽選モードとかにも悪用できそう
小役抽選しないよ = 0

;当選した小役 
;0:スカーレットBB当選
;1:十六夜BB当選
;2:REG当選
;3:中段チェリー当選
;4:強チェリー当選
;5:弱チェリー当選
;6:強スイカ当選
;7:弱スイカ当選
;8:ベル当選
;9:リプレイ当選
;99:はずれ
当選した小役 = 当選小役_はずれ

;当選したBB種類　-1:当選してないよ,0:スカーレットBB当選，1:十六夜BB当選，2:REG当選
当選したBB種類 = -1

;=============================
;初期文章表示
;=============================
PRINTW こちらはスロットのコーナーです。
PRINTW ごゆっくりお楽しみください。

$INPUT_LOOP
CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
DRAWLINE
;=================================
;PRINTL1行を使ってメッセージを挿入
;=================================
CALL 一言メッセージ(短周期連セット数,設定,前兆,当選したBB種類,内部状態,短周期連継続率,当選した小役,前回当選からの回転数,短周期連開始時所持カリスマ)

PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
PRINTFORML [0] - スロットをやめる
PRINTFORML [2] - 情報パネルの操作
PRINTFORML [1] - スロットを回す

;=============================
;いろんなものの修正処理
;=============================
;当選していた場合前兆,当選したBB種類を-1にセットし直す
SIF 前兆 == 0
	前兆 = -1
SIF 当選したBB種類 > -1
	当選したBB種類 = -1

;フリーズした場合３２Ｇ連状態にセット
SIF 内部状態 == 5
	内部状態 = 4
;短周期連継続率が95％を超えた場合95％に戻す(レア小役引きすぎるとこうなる)
SIF 短周期連継続率 > 95
	短周期連継続率 = 95
	
;情報パネルの操作ボタンを押してここに戻ってきた場合さっき引いた小役でもう一回抽選を行うため
;このフラグが立っていたら強制的に小役による抽選をスキップする(情報パネルの操作選択時にフラグは立つ)
IF 小役抽選しないよ
	小役抽選しないよ = 0
	GOTO 抽選終わり
ENDIF
;=====================================
;今回当たっていた小役に対応した抽選
;=====================================
$小役抽選開始
;～～～～～～～～～～～～～～～～～～～～～～～
;３２Ｇ連最終ゲームの場合
;～～～～～～～～～～～～～～～～～～～～～～～
IF 内部状態 == 4 && 前回当選からの回転数 == 31
	
	;成立したフラグによって変化
	SELECTCASE 当選した小役
	
	;3:中チェリー当選の場合(継続確定)
	CASE 当選小役_中チェリー
		
		;短周期連継続率変更抽選
		;短周期連継続率を95％に上書き
		短周期連継続率 = 95
		
		;継続確定抽選
		IF RAND(100) < 中段チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			
			GOTO 抽選終わり
		ENDIF
	
	;4:強チェリー当選の場合
	CASE 当選小役_強チェリー
		
		;短周期連継続率変更抽選
		;短周期連継続率を増加
		短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;5:弱チェリー当選の場合
	CASE 当選小役_弱チェリー
		
		;短周期連継続率変更抽選
		;50％の確率で短周期連継続率を増加
		SIF RAND(100) < 50
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;6:強スイカ当選の場合
	CASE 当選小役_強スイカ
		
		;短周期連継続率変更抽選
		;短周期連継続率を増加
		短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強スイカ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;7:弱スイカ当選の場合
	CASE 当選小役_弱スイカ
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱スイカ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;短周期連継続率変更抽選(ベルでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < ベル_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;9:リプレイ当選の場合
	CASE 当選小役_リプレイ
		
		;短周期連継続率変更抽選(リプレイでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < リプレイ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;99:はずれ当選の場合
	CASE 当選小役_はずれ
		
		;短周期連継続率変更抽選
		;50％の確率で短周期連継続率を増加
		SIF RAND(100) < 50
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < はずれ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			前兆 = 0
			GOTO 抽選終わり
		ENDIF
	
	;その他(デフォルトでは存在しないはず)
	CASEELSE
		PRINTFORMW 存在しない小役が当選したことになっています 小役番号:{当選した小役}
	ENDSELECT
	
	
	;======================================================================
	;短周期連(32Ｇ連)終了確定
	;ここに来たということは短周期連終了は確定しているので内部状態振り分け
	;======================================================================
	;設定によって内部状態振り分けは異なる
	IF 前兆 == -1
		内部状態 = 短周期連終了時状態振り分け(設定)
		短周期連セット数 = 0
	ENDIF
	

;～～～～～～～～～～～～～～～～～～～～～～～
;３２Ｇ連中(最終ゲーム除く)の場合
;～～～～～～～～～～～～～～～～～～～～～～～
ELSEIF 内部状態 == 4
	
	;成立したフラグによって変化
	SELECTCASE 当選した小役
	
	;ボーナス当選時はボーナス当選直後に継続抽選を行う
	;0:スカーレットBB当選の場合
	CASE 当選小役_赤7ボーナス
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
	
	;ボーナス当選時はボーナス当選直後に継続抽選を行う
	;1:十六夜BB当選の場合
	CASE 当選小役_青7ボーナス
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
	
	;ボーナス当選時はボーナス当選直後に継続抽選を行う
	;2:REG当選の場合
	CASE 当選小役_レギュラーボーナス
		短周期連セット数 ++
		SIF RAND(100) < 短周期連継続率
			前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
	
	;レア小役
	;3:中チェリー当選の場合(継続確定)
	CASE 当選小役_中チェリー
		
		;短周期連継続率変更抽選
		;短周期連継続率を95％に上書き
		短周期連継続率 = 95
		
		;継続確定抽選
		IF RAND(100) < 中段チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			GOTO 抽選終わり
		ENDIF
	
	;4:強チェリー当選の場合
	CASE 当選小役_強チェリー
		
		;短周期連継続率変更抽選
		;40％の確率で短周期連継続率を増加
		SIF RAND(100) < 40
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;5:弱チェリー当選の場合
	CASE 当選小役_弱チェリー
		
		;短周期連継続率変更抽選
		;20％の確率で短周期連継続率を増加
		SIF RAND(100) < 20
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱チェリー_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;6:強スイカ当選の場合
	CASE 当選小役_強スイカ
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 強スイカ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;7:弱スイカ当選の場合
	CASE 当選小役_弱スイカ
		
		;短周期連継続率変更抽選
		;10％の確率で短周期連継続率を増加
		SIF RAND(100) < 10
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < 弱スイカ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;短周期連継続率変更抽選(ベルでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < ベル_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;9:リプレイ当選の場合
	CASE 当選小役_リプレイ
		
		;短周期連継続率変更抽選(リプレイでは行わない)
		
		
		;継続確定抽選
		IF RAND(1000) < リプレイ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;99:はずれ当選の場合
	CASE 当選小役_はずれ
		
		;短周期連継続率変更抽選
		;25％の確率で短周期連継続率を増加
		SIF RAND(100) < 25
			短周期連継続率 += 短周期連継続率変更(設定)
		
		;継続確定抽選
		IF RAND(100) < はずれ_32G連中(設定)
			;次ゲームボーナス当選(継続確定)
			IF 前兆 == -1
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数)
			ELSE
				前兆 = MIN(前兆設定(内部状態),32 - 前回当選からの回転数,前兆)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;その他(デフォルトでは存在しないはず)
	CASEELSE
		PRINTFORMW 存在しない小役が当選したことになっています 小役番号:{当選した小役}
	ENDSELECT
	
;～～～～～～～～～～～～～～～～～～～～～～～
;通常,高確,超高確の場合
;～～～～～～～～～～～～～～～～～～～～～～～
ELSE
	;成立したフラグによって変化
	SELECTCASE 当選した小役
	
	;ボーナス当選時はボーナス当選直後に短周期連突入抽選を行う
	;0:スカーレットBB当選の場合(1/2で短周期連突入)
	CASE 当選小役_赤7ボーナス
		IF !RAND(2)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持カリスマ = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;ボーナス当選時はボーナス当選直後に短周期連突入抽選を行う
	;1:十六夜BB当選の場合(1/3で短周期連突入)
	CASE 当選小役_青7ボーナス
		IF !RAND(3)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持カリスマ = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;ボーナス当選時はボーナス当選直後に短周期連突入抽選を行う
	;2:REG当選の場合(1/5で短周期連突入)
	CASE 当選小役_レギュラーボーナス
		IF !RAND(5)
			;内部状態を短周期連に
			内部状態 = 4
			;継続率の決定
			短周期連継続率 = 短周期連継続率設定(設定)
			;短周期連セット数の初期化(GOTOした後に1足されるのでここでは0)
			短周期連セット数 = 0
			短周期連開始時所持カリスマ = MONEY:2
			SIF 内部状態 == 4
				累計短周期連セット数 ++
			;小役抽選開始地点に戻す
			GOTO 小役抽選開始
		ENDIF
	
	;レア小役当選時はボーナス当選抽選を行う
	;3:中チェリー当選の場合
	;中段チェリーの処理は特別なものになる
	;スカーレットBB確定＆継続率90％以上短周期連突入確定
	CASE 当選小役_中チェリー
		前兆 = 0
		GOTO 抽選終わり
	
	;レア小役当選時は内部状態抽選とボーナス当選抽選を行う
	;4:強チェリー当選の場合
	CASE 当選小役_強チェリー
		
		;内部状態抽選
		;強チェリーは50％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(2)
				内部状態 ++
		ENDIF
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とボーナス当選抽選を行う
	;5:弱チェリー当選の場合
	CASE 当選小役_弱チェリー
		
		;内部状態抽選
		;弱チェリーは30％で内部状態が昇格する
		IF 内部状態 < 3
			SIF RAND(10) < 3
				内部状態 ++
		ENDIF
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とボーナス当選抽選を行う
	;6:強スイカ当選の場合
	CASE 当選小役_強スイカ
		
		;内部状態抽選
		;強スイカは50％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(2)
				内部状態 ++
		ENDIF
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;レア小役当選時は内部状態抽選とボーナス当選抽選を行う
	;7:弱スイカ当選の場合
	CASE 当選小役_弱スイカ
		
		;内部状態抽選
		;弱スイカは25％で内部状態が昇格する
		IF 内部状態 < 3
			SIF !RAND(4)
				内部状態 ++
		ENDIF
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とボーナス当選抽選を行う
	;8:ベル当選の場合
	CASE 当選小役_ベル
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とボーナス当選抽選を行う
	;9:リプレイ当選の場合
	CASE 当選小役_リプレイ
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;小役当選時は内部状態抽選とボーナス当選抽選を行う
	;99:はずれ当選の場合
	CASE 当選小役_はずれ
		
		;内部状態抽選
		;はずれは2％で内部状態が降格する
		IF 内部状態 > 1
			SIF !RAND(50)
				内部状態 --
		ENDIF
		
		;ボーナス当選抽選
		IF ボーナス抽選_非32G連(当選した小役,設定,内部状態)
			;次ゲームボーナス当選
			;前兆中にボーナス当選した場合前兆の短いほうが選ばれもう片方は捨てられる
			IF 前兆 != -1
				前兆 = MIN(前兆,前兆設定(内部状態))
			ELSE
				前兆 = 前兆設定(内部状態)
			ENDIF
			
			GOTO 抽選終わり
		ENDIF
	
	;その他(デフォルトでは存在しないはず)
	CASEELSE
		PRINTFORMW 存在しない小役が当選したことになっています 小役番号:{当選した小役}
	ENDSELECT
ENDIF


;当選ゲーム数の設定(短周期連→通常転落時)
IF 前兆 == -1 && 内部状態 < 4
	前兆 = 当選ゲーム数設定()
	前回当選からの回転数 = 0
ENDIF
$抽選終わり

IF オートモード == 1 && 総回転数 < 検定回数
	PRINTL 
	RESULT = 1
ELSE
	INPUT
ENDIF


IF RESULT == 1
	;=============================
	;所持カリスマが足りない場合
	;※スロットをやめさせる
	;=============================
	IF MONEY:2 < 掛け金
		PRINTL カリスマが足りなくてスロットを回せない
		GOTO GAME_END
	ENDIF
	
	
	;=============================
	;一回転分の掛け金徴収
	;=============================
	MONEY:2 -= 掛け金
	
	;=============================
	;前回のスロット画面を消去
	;=============================
	CLEARLINE 削除行数
	;=============================
	;回転数を1増加
	;=============================
	総回転数 ++
	前回当選からの回転数 ++
	SIF 前兆 > 0
		前兆 --
	
	;=============================
	;フリーズ(1/131072)
	;フリーズした場合は処理が特別になるのでここで記述
	;フリーズ恩恵　スカーレットBB確定 継続率95％連
	;=============================
	IF !RAND(131072)
		;全リール暗転
		暗転_左上 = 1
		暗転_左中 = 1
		暗転_左下 = 1
		暗転_中上 = 1
		暗転_中中 = 1
		暗転_中下 = 1
		暗転_右上 = 1
		暗転_右中 = 1
		暗転_右下 = 1
		
		;フリーズしたので内部状態を5にする
		内部状態 = 5
		
		CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
		CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
		DRAWLINE
		CALL 一言メッセージ(短周期連セット数,設定,前兆,当選したBB種類,内部状態,短周期連継続率,当選した小役,前回当選からの回転数,短周期連開始時所持カリスマ)
		PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
		PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
		PRINTL 
		PRINTL 
		PRINTL 
		
		;3秒間停止させる
		TWAIT 3000,1
		;=============================
		;前回のスロット画面を消去
		;=============================
		CLEARLINE 削除行数
		
		;全リール明るいままでのスカーレットBB(通常時とは異なる)
		絵柄_左上 = REP
		絵柄_左中 = RD7
		絵柄_左下 = CHE
		絵柄_中上 = CHE
		絵柄_中中 = RD7
		絵柄_中下 = CHE
		絵柄_右上 = BEL
		絵柄_右中 = RD7
		絵柄_右下 = MEL
		暗転_左上 = 0
		暗転_左中 = 0
		暗転_左下 = 0
		暗転_中上 = 0
		暗転_中中 = 0
		暗転_中下 = 0
		暗転_右上 = 0
		暗転_右中 = 0
		暗転_右下 = 0
		
		;スカーレットBB当選
		当選した小役 = 0
		
		フリーズ回数 ++
		;フリーズ恩恵 スカーレットBB確定 継続率95％連をセット
		当選したBB種類 = 0
		
		
		;=============================
		;カリスマの獲得
		;=============================
		CALL 小役払い戻し(当選した小役)
		
		
		;=============================
		;小役カウンタ
		;=============================
		小役カウンタ:当選した小役 ++
		
		FOR LOCAL,0,10
			SIF 当選種類履歴:(9-LOCAL) == -1
				CONTINUE
			当選種類履歴:(10-LOCAL) = 当選種類履歴:(9-LOCAL)
			当選履歴:(10-LOCAL) = 当選履歴:(9-LOCAL)
			当選枚数履歴:(10-LOCAL) = 当選枚数履歴:(9-LOCAL)
		NEXT
		
		SIF 内部状態 == 5
			累計短周期連セット数 ++
		当選種類履歴:0 = 当選した小役
		当選履歴:0 = 前回当選からの回転数
		IF 短周期連セット数
			当選枚数履歴:0 = MONEY:2 - 短周期連開始時所持カリスマ
		ELSE
			SELECTCASE 当選した小役
			CASE 当選小役_赤7ボーナス
				当選枚数履歴:0 = 400
			CASE 当選小役_青7ボーナス
				当選枚数履歴:0 = 200
			CASE 当選小役_レギュラーボーナス
				当選枚数履歴:0 = 100
			CASEELSE
			ENDSELECT
		ENDIF
		
		前回当選からの回転数 = 0
		短周期連継続率 = 95
		
		SIF 短周期連セット数 == 0
			短周期連開始時所持カリスマ = MONEY:2
		
		;INPUT_LOOPへ
		GOTO INPUT_LOOP
	ENDIF
	
	;=============================
	;前回の結果を初期化
	;=============================
	絵柄_左上 = NUL
	絵柄_左中 = NUL
	絵柄_左下 = NUL
	絵柄_中上 = NUL
	絵柄_中中 = NUL
	絵柄_中下 = NUL
	絵柄_右上 = NUL
	絵柄_右中 = NUL
	絵柄_右下 = NUL
	暗転_左上 = 0
	暗転_左中 = 0
	暗転_左下 = 0
	暗転_中上 = 0
	暗転_中中 = 0
	暗転_中下 = 0
	暗転_右上 = 0
	暗転_右中 = 0
	暗転_右下 = 0
	
	;=============================
	;回転最中のスロット表示
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	CALL SLOT_全体表示(色_枠,絵柄_左上_回転中,絵柄_左中_回転中,絵柄_左下_回転中,絵柄_中上_回転中,絵柄_中中_回転中,絵柄_中下_回転中,絵柄_右上_回転中,絵柄_右中_回転中,絵柄_右下_回転中,暗転_左上_回転中,暗転_左中_回転中,暗転_左下_回転中,暗転_中上_回転中,暗転_中中_回転中,暗転_中下_回転中,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
	PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
	PRINTL  
	PRINTL  
	PRINTFORML [1] - リールを止める
	;リール回転
	IF オートモード == 1 && 総回転数 < 検定回数
		PRINTL 
		RESULT = 1
	ELSE
		INPUT
	ENDIF
	CLEARLINE 削除行数
	
	;=============================
	;特殊な状態の場合は個々に記述する
	;=============================
	
	;=============================
	;前兆が0GならばBB当選
	;=============================
	IF 前兆 == 0
		SIF 当選した小役 == 当選小役_中チェリー
			GOTO スカーレットBB
		IF RAND(2)
			;合計1/3で十六夜BB(200)
			IF RAND(3)
$十六夜BB
				;=============================
				;当選したBB種類に1(十六夜BB)を代入
				;=============================
				当選したBB種類 = 1
				
				;=============================
				;左絵柄決定
				;=============================
				絵柄_左上 = REP
				絵柄_左中 = BL7
				絵柄_左下 = CHE
				暗転_左上 = 1
				暗転_左中 = 0
				暗転_左下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
				PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - リールを止める
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;中絵柄決定
				;=============================
				絵柄_中上 = MEL
				絵柄_中中 = BL7
				絵柄_中下 = REP
				暗転_中上 = 1
				暗転_中中 = 0
				暗転_中下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
				PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - リールを止める
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;右絵柄決定
				;=============================
				絵柄_右上 = BEL
				絵柄_右中 = BL7
				絵柄_右下 = MEL
				暗転_右上 = 1
				暗転_右中 = 0
				暗転_右下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				
				;十六夜BB当選
				当選した小役 = 1
				
				;=============================
				;カリスマの獲得
				;=============================
				CALL 小役払い戻し(当選した小役)
			;合計1/6でスカーレットBB(400)
			ELSE
$スカーレットBB
				;=============================
				;当選したBB種類に0(スカーレットBB)を代入
				;=============================
				当選したBB種類 = 0
				
				;=============================
				;左絵柄決定
				;=============================
				絵柄_左上 = REP
				絵柄_左中 = RD7
				絵柄_左下 = CHE
				暗転_左上 = 1
				暗転_左中 = 0
				暗転_左下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
				PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - リールを止める
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;中絵柄決定
				;=============================
				絵柄_中上 = CHE
				絵柄_中中 = RD7
				絵柄_中下 = CHE
				暗転_中上 = 1
				暗転_中中 = 0
				暗転_中下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
				CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				DRAWLINE
				PRINTL 
				PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
				PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
				PRINTL  
				PRINTL  
				PRINTFORML [1] - リールを止める
				;リール回転
				IF オートモード == 1 && 総回転数 < 検定回数
					PRINTL 
					RESULT = 1
				ELSE
					INPUT
				ENDIF
				CLEARLINE 削除行数
				
				;=============================
				;右絵柄決定
				;=============================
				絵柄_右上 = BEL
				絵柄_右中 = RD7
				絵柄_右下 = MEL
				暗転_右上 = 1
				暗転_右中 = 0
				暗転_右下 = 1
				;=============================
				;回転最中のスロット表示
				;=============================
				;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
				
				;=============================
				;中段チェリーから直接ここへ来た場合の処理
				;=============================
				IF 当選した小役 == 当選小役_中チェリー
					内部状態 = 4
					;短周期連セット数の初期化
					短周期連セット数 = 1
					短周期連開始時所持カリスマ = MONEY:2
					短周期連継続率 = 90
					SIF !RAND(10)
						短周期連継続率 = 95
				ENDIF
				
				
				
				;スカーレットBB当選
				当選した小役 = 0
				
				;=============================
				;カリスマの獲得
				;=============================
				CALL 小役払い戻し(当選した小役)
			ENDIF
		;合計1/2でREG(100)
		ELSE
$REG
			;=============================
			;当選したBB種類に2(REG)を代入
			;=============================
			当選したBB種類 = 2
			;=============================
			;左絵柄決定
			;=============================
			絵柄_左上 = REP
			絵柄_左中 = RD7
			絵柄_左下 = CHE
			暗転_左上 = 1
			暗転_左中 = 0
			暗転_左下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
			CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			DRAWLINE
			PRINTL 
			PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
			PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
			PRINTL  
			PRINTL  
			PRINTFORML [1] - リールを止める
			;リール回転
			IF オートモード == 1 && 総回転数 < 検定回数
				PRINTL 
				RESULT = 1
			ELSE
				INPUT
			ENDIF
			CLEARLINE 削除行数
			
			;=============================
			;中絵柄決定
			;=============================
			絵柄_中上 = CHE
			絵柄_中中 = RD7
			絵柄_中下 = CHE
			暗転_中上 = 1
			暗転_中中 = 0
			暗転_中下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
			CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			DRAWLINE
			PRINTL 
			PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
			PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
			PRINTL  
			PRINTL  
			PRINTFORML [1] - リールを止める
			;リール回転
			IF オートモード == 1 && 総回転数 < 検定回数
				PRINTL 
				RESULT = 1
			ELSE
				INPUT
			ENDIF
			CLEARLINE 削除行数
			
			;=============================
			;右絵柄決定
			;=============================
			絵柄_右上 = BEL
			絵柄_右中 = B_A_R
			絵柄_右下 = MEL
			暗転_右上 = 1
			暗転_右中 = 0
			暗転_右下 = 1
			;=============================
			;回転最中のスロット表示
			;=============================
			;CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
			
			;REG当選
			当選した小役 = 2
			
			;=============================
			;カリスマの獲得
			;=============================
			CALL 小役払い戻し(当選した小役)
		ENDIF
		
		;=============================
		;小役カウンタ
		;=============================
		小役カウンタ:当選した小役 ++
		
		FOR LOCAL,0,10
			SIF 当選種類履歴:(9-LOCAL) == -1
				CONTINUE
			当選種類履歴:(10-LOCAL) = 当選種類履歴:(9-LOCAL)
			当選履歴:(10-LOCAL) = 当選履歴:(9-LOCAL)
			当選枚数履歴:(10-LOCAL) = 当選枚数履歴:(9-LOCAL)
		NEXT
		
		SIF 内部状態 == 4
			累計短周期連セット数 ++
		当選種類履歴:0 = 当選した小役
		当選履歴:0 = 前回当選からの回転数
		IF 内部状態 == 4
			当選枚数履歴:0 = MONEY:2 - 短周期連開始時所持カリスマ
		ELSE
			SELECTCASE 当選した小役
			CASE 当選小役_赤7ボーナス
				当選枚数履歴:0 = 400
			CASE 当選小役_青7ボーナス
				当選枚数履歴:0 = 200
			CASE 当選小役_レギュラーボーナス
				当選枚数履歴:0 = 100
			CASEELSE
			ENDSELECT
		ENDIF
		;=============================
		;各種情報の初期化
		;=============================
		前回当選からの回転数 = 0
		GOTO INPUT_LOOP
	ENDIF
	;=============================
	;BB当選時の記述ここまで
	;=============================
	
	;=============================
	;子役抽選
	;=============================
	当選した小役 = 小役抽選(内部状態)
	
	CALL スロット絵柄決定(当選した小役,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上,絵柄_右中,絵柄_右下)
	
	
	;=============================
	;回転最中のスロット表示(左停止)
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	;中リール右リールには「_回転中」が付いた変数を入れる
	CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上_回転中,絵柄_中中_回転中,絵柄_中下_回転中,絵柄_右上_回転中,絵柄_右中_回転中,絵柄_右下_回転中,暗転_左上,暗転_左中,暗転_左下,暗転_中上_回転中,暗転_中中_回転中,暗転_中下_回転中,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
	PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
	PRINTL  
	PRINTL  
	PRINTL  
	
	;左リールが通常と違う場合更に1秒間停止させる(目押し感の演出)
	SIF 絵柄_左上 != BEL
		TWAIT 1000,0
	;0.1秒間停止させる
	TWAIT 100,0
	;以下、コメントアウト
	;PRINTFORML [1] - リールを止める
	;リール回転
	;INPUT
	;CLEARLINE 削除行数
	
	CLEARLINE 削除行数 - 1
	
	;=============================
	;回転最中のスロット表示(中停止)
	;=============================
	CALL SLOT_情報パネル表示(情報パネル表示ページ,総回転数,前回当選からの回転数,累計短周期連セット数,当選履歴,当選種類履歴,当選枚数履歴,小役カウンタ)
	;右リールには「_回転中」が付いた変数を入れる
	CALL SLOT_全体表示(色_枠,絵柄_左上,絵柄_左中,絵柄_左下,絵柄_中上,絵柄_中中,絵柄_中下,絵柄_右上_回転中,絵柄_右下_回転中,絵柄_右下_回転中,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上_回転中,暗転_右中_回転中,暗転_右下_回転中)
	DRAWLINE
	PRINTL 
	PRINTFORML 所持カリスマ {MONEY:2} カリスマ差枚 {MONEY:2-初期所持カリスマ}
	PRINTFORML 回転数 {総回転数}G 前回の大当たりから{前回当選からの回転数}G
	PRINTL  
	PRINTL  
	PRINTL  
	
	;左リールが通常と違う場合更に1秒間停止させる(目押し感の演出)
	;左リールスイカ滑りからの中段ベルが存在するので「絵柄_中中 != BEL」が追加
	SIF 絵柄_左上 != BEL && 絵柄_中中 != BEL
		TWAIT 1000,0
	;0.1秒間停止させる
	TWAIT 100,0
	;以下、コメントアウト
	;PRINTFORML [1] - リールを止める
	;リール回転
	;INPUT
	;CLEARLINE 削除行数
	
	CLEARLINE 削除行数 - 1
	
	CALL 暗転設定(当選した小役,暗転_左上,暗転_左中,暗転_左下,暗転_中上,暗転_中中,暗転_中下,暗転_右上,暗転_右中,暗転_右下)
	
	;=============================
	;小役カウンタ
	;=============================
	小役カウンタ:当選した小役 ++
	;=============================
	;カリスマ払い戻し
	;=============================
	CALL 小役払い戻し(当選した小役)
	
	
	
	;=============================================================
	;全停止後のリール表示はINPUT_LOOPにループしたあと206行目で行う
	;=============================================================
	GOTO INPUT_LOOP

ELSEIF RESULT == 2
	情報パネル表示ページ ++
	情報パネル表示ページ %= 2
	CLEARLINE 削除行数
	小役抽選しないよ = 1
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
ELSE
	CLEARLINE 1
	REUSELASTLINE 無効な数字
	GOTO INPUT_LOOP
ENDIF
;カリスマ会員用のカモられ指数を上昇
;リプレイは in 0なのでそれを引いた（ゲーム数 * 3）が投入カリスマ
投入カリスマ = (総回転数 - 小役カウンタ:当選小役_リプレイ) * 3
CALL CASINO_MONEY("カモ", 投入カリスマ)
$GAME_END