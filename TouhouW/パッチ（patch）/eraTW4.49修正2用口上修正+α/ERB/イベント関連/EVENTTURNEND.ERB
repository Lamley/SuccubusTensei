@EVENTTURNEND
#DIM 養育費
SIF TFLAG:妄想中
	DELCHARA (CHARANUM - 1)
;宴会当日に一日が終わると宴会フラグOFF
;IF DAY >= FLAG:34 && FLAG:宴会開催フラグ
IF TIME + 1440 * DAY >= FLAG:開始時間 + 1440 * FLAG:開始日 && FLAG:宴会開催フラグ
	;会場となった場所は荒れ果てる
	SIF YOGORE:(FLAG:32) < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:宴会開催フラグ = 0
	FLAG:31 = 0
	FLAG:32 = 0
	FLAG:33 = 0
	FLAG:34 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	FLAG:宴会差し入れ = 0
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:仕事量 = 0
		BASE:LOCAL:仕事量 = 0
		CFLAG:LOCAL:宴会参加 = 0
		CFLAG:LOCAL:行動 = 0
	NEXT
ENDIF
;霊廟フラグOFF
FLAG:17 = 0
FOR LOCAL,1,CHARANUM
	;恋慕取得
	IF CFLAG:LOCAL:2 > 3000 && ABL:LOCAL:親密 >= 10 && !TALENT:LOCAL:恋慕 && CFLAG:LOCAL:4 >= 500
		;面霊気は特別あつかい
		IF LOCAL == 57
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%への思いを自覚すると同時、口元を緩めた
			PRINTFORMW 自然な笑顔を浮かべる%CALLNAME:LOCAL%は、表情を得た様だ
			PRINTFORMW %CALLNAME:LOCAL%の\@ TALENT:LOCAL:思慕 ? [思慕] # [愛欲] \@は[恋慕]になった
			FLAG:10 ++
		;愛欲の有無で分岐
		ELSEIF TALENT:LOCAL:愛欲
			PRINTFORMW %CALLNAME:LOCAL%は身も心も%CALLNAME:MASTER%に満たされることに悦びを感じている…
			PRINTL 爛れた関係の果てに実った恋心、それもまた一つの恋のカタチ…
			PRINTFORMW %CALLNAME:LOCAL%の[愛欲]は[恋慕]になった
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%と過ごす時間に幸せを感じているようだ…
			PRINTFORMW %CALLNAME:LOCAL%の[思慕]は[恋慕]になった
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:時姦刻印 && !TALENT:LOCAL:愛欲
			CFLAG:LOCAL:清い交際 = 1
		TALENT:LOCAL:恋慕 = 1
		TALENT:LOCAL:思慕 = 0
		TALENT:LOCAL:愛欲 = 0
		TALENT:LOCAL:セフレ = 0
		CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,1,0)
	;思慕取得
	;セフレ／愛人の取得で分岐
	ELSEIF CFLAG:LOCAL:2 > 1500 && ABL:LOCAL:親密 >= 5 && !TALENT:LOCAL:思慕 && CFLAG:LOCAL:4 >= 150 && !TALENT:LOCAL:恋慕 && !TALENT:LOCAL:愛欲
		IF TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%の身体以外のことにも興味が有るようだ
			PRINTFORMW %CALLNAME:LOCAL%は[思慕]を得た
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%のことが気になるようだ…
			PRINTFORMW %CALLNAME:LOCAL%は[思慕]を得た
		ENDIF
		TALENT:LOCAL:思慕 = 1
		CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,2,0)
	;愛欲取得
	;奉仕快楽の分岐がまだないので、取得条件に併合
	ELSEIF CFLAG:LOCAL:2 > 3000 && ABL:LOCAL:欲望 >= 10 && !TALENT:LOCAL:恋慕 && !TALENT:LOCAL:愛欲 && TALENT:LOCAL:セフレ && (EXP:LOCAL:絶頂経験 - EXP:LOCAL:無自覚絶頂経験) > 500 && (ABL:LOCAL:欲望 - ABL:LOCAL:親密) >= 2
		;思慕持ち、心より身体を選んだ的な
		IF TALENT:LOCAL:思慕
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%と心以上に体のつながりに満足しているようだ…
			PRINTFORML 目に見えない絆よりも目に見える証を欲してこれからも身体を重ねるのだろう
			PRINTFORMW %CALLNAME:LOCAL%は[愛欲]を得た
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%との情欲におぼれているようだ…
			PRINTFORMW %CALLNAME:LOCAL%は[愛欲]を得た
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:愛欲 = 1
			TALENT:LOCAL:思慕 = 0
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,3,0)
	;セフレ／愛人取得
	;セフレと愛人の違いは思慕の有無
	ELSEIF CFLAG:LOCAL:2 > 1500 && ABL:LOCAL:欲望 >= 5 && !TALENT:LOCAL:恋慕 && (EXP:LOCAL:絶頂経験 - EXP:LOCAL:無自覚絶頂経験) > 200 && (ABL:LOCAL:欲望 - ABL:LOCAL:親密) >= 1
		;思慕取得済＋セフレ未取得
		IF TALENT:LOCAL:思慕 && !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%となら快楽に溺れてもいいと思い始めたようだ…
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%の[愛人]になった
			TALENT:LOCAL:セフレ = 2
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,4,0)
		;思慕取得済＋セフレ取得済
		ELSEIF TALENT:LOCAL:思慕 && TALENT:LOCAL:セフレ == 1
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%に単なる肉体関係以上のものを望んでいるようだ…
			PRINTFORMW %CALLNAME:LOCAL%は[愛人]になった
			TALENT:LOCAL:セフレ = 2
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,4,0)
		;思慕未取得＋セフレ未取得ならまずセフレ取得
		ELSEIF !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%から与えられる快楽に溺れつつある…
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%の[セフレ]になってしまった
			TALENT:LOCAL:セフレ = 1
			CALL KOJO_MESSAGE_SEND("EVENT",21,LOCAL,5,0)
		ENDIF
	ENDIF
	IF EXP:LOCAL:演奏経験 >= 1000 && ABL:LOCAL:技巧 > 3 && !TALENT:LOCAL:音楽知識
		PRINTFORMW %CALLNAME:LOCAL%は音楽に造詣深くなった
		PRINTFORMW %CALLNAME:LOCAL%は[音楽知識]を得た
		TALENT:LOCAL:音楽知識 = 1
	ELSEIF EXP:LOCAL:演奏経験 >=5000 && !TALENT:LOCAL:音感
		PRINTFORMW %CALLNAME:LOCAL%の音感はかなり鍛えられてきたようだ
		PRINTFORMW %CALLNAME:LOCAL%は[相対音感]を得た
		TALENT:LOCAL:音感 = 1
	ENDIF
	IF EXP:LOCAL:キス経験 > 300 && EXP:LOCAL:愛情経験 > 500 && !TALENT:LOCAL:キス魔
		PRINTFORMW %CALLNAME:LOCAL%は[キス魔]を得た
		TALENT:LOCAL:キス魔 = 1
	ENDIF
	IF EXP:LOCAL:調教自慰経験 > 200 && EXP:LOCAL:自慰経験 > 500 && !TALENT:LOCAL:自慰狂い
		PRINTFORMW %CALLNAME:LOCAL%は[自慰狂い]を得た
		TALENT:LOCAL:自慰狂い = 1
	ENDIF
	IF CFLAG:LOCAL:不機嫌
		CFLAG:LOCAL:不機嫌 --
		IF !CFLAG:LOCAL:不機嫌
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:不機嫌 ++
		ENDIF
	ENDIF
	;EXキャラの恋慕時はメンバー追加イベントフラグを立てる
	IF LOCAL >= 11 && LOCAL <= CHARANUM && LOCAL != 57 && LOCAL != 71
		SIF TALENT:LOCAL:恋慕 && EXP:LOCAL:愛情経験 >= 200
			FLAG:1 = 3
	ENDIF
NEXT
IF EXP:MASTER:異常清掃経験 > (500 + 100 * FLAG:850) && ABL:MASTER:清掃技能 > 3 && !TALENT:MASTER:清掃中毒
	PRINTFORMW %CALLNAME:MASTER%は恐ろしいことに気付いてしまった
	PRINTFORMW 寝ても覚めても、時を止めている間も、少女たちと肌を重ねている時ですら、
	PRINTFORMW 部屋が散らかっていないか気になって仕方がないのだ
	PRINTFORMW もはや%CALLNAME:MASTER%の心が安らぐのはこうして掃除をしている間のみ
	PRINTFORMW %CALLNAME:MASTER%の掃除へのこだわりは、取り返しのつかない域に達していた
	PRINTFORMW %CALLNAME:MASTER%は[清掃中毒]を得た
	TALENT:MASTER:清掃中毒 = 1
ENDIF
;MASTERを除く全員のプリセット処理
FOR LOCAL,1,CHARANUM
	;気力の回復
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;回復早い、回復遅い
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)
	
	;添い寝してたら回復速度向上
	SIF CFLAG:LOCAL:添い寝中
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:徹夜
		LOCAL:1 /= 2
	CFLAG:LOCAL:徹夜 = 0
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;南無三カード発行者が居たら発行フラグOFF
	CFLAG:LOCAL:328 = 0
NEXT
;全員のプリセット処理
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:精力 = MIN(MAXBASE:LOCAL:精力, BASE:LOCAL:精力 + MAXBASE:LOCAL:精力 * BASE:LOCAL:体力 / MAXBASE:LOCAL:体力)
	
	;隠密中のキャラが居たらフラグOFF
	CALL SET_SNEAK(0,LOCAL,1)
	;CFLAG:LOCAL:隠密中 = 0
	
	;イタズラ中のキャラが居たらフラグOFF
	CALL SET_PRANK(0,LOCAL,1)
	;CFLAG:LOCAL:イタズラ = 0
	
NEXT

;宴会の開催判定
SIF FLAG:3 && FLAG:宴会開催フラグ == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:育児人数
	養育費 = CFLAG:MASTER:育児人数 * 500 / (1 + FLAG:甲斐性無)
	PRINTFORML 子供の養育費に\\{養育費}かかった
	MONEY -= 養育費
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0


BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;清潔度
LOCAL:2 = EX:MASTER:今日の掃除回数 / 10
PRINTFORML 清掃によるボーナス \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【大掃除ボーナス】
	LOCAL:2 *= 2
ENDIF
;拠点の主の好感度と信頼度が上昇
LOCAL:3 = MIN(10, EX:MASTER:今日の掃除回数 / 2000)
IF LOCAL:3
	PRINTFORML 掃除を頑張ったことで、%CALLNAME:拠点_大家%からの評価が上がった
	CFLAG:拠点_大家:2 += LOCAL:3 * 2
	CFLAG:拠点_大家:4 += LOCAL:3
ENDIF
PRINTL  
;手伝い報酬
FOR LOCAL,1,CHARANUM
	LOCAL:4 += TCVAR:LOCAL:手伝った量
NEXT

SIF LOCAL:4 > 0
	PRINTFORML 仕事手伝いによるボーナス \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;好感度上昇
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 好感度上昇ボーナス ＋{LOCAL:5}カリスマ
	MONEY:2 += LOCAL:5
	PRINTFORML 現在のカリスマ：{LOCAL:5}　カリスマ保有上限：{CM_LIMIT}
ENDIF
FLAG:12 = 0

PRINTW  