;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND
#LATER

PRINTL 一日が終わりました
PRINTFORML 
;SIF STRLENS(TSTR:1)
;	CALL SELL_VIDEO

;睡眠フラグ
CALL SET_SLEEP(1,MASTER,0)

CFLAG:MASTER:286 = 0
CFLAG:MASTER:287 = 0
CFLAG:MASTER:288 = 0
CFLAG:MASTER:289 = 0
CFLAG:MASTER:290 = 0
CFLAG:MASTER:ノーパン = 0

;おみくじを引けるようにフラグリセット
CLEARBIT FLAG:999, 1




;中出しフラグリセット
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:286 = 0
	CFLAG:LOCAL:287 = 0
	CFLAG:LOCAL:288 = 0
	CFLAG:LOCAL:289 = 0
	CFLAG:LOCAL:290 = 0
	;願掛けによる感度上昇解除
	SIF GROUPMATCH(TCVAR:LOCAL:願掛け補正,101,102,103,104,106)
		TALENT:LOCAL:(TCVAR:LOCAL:願掛け補正) -= 1
	;好きな体位決定
	SIF CFLAG:LOCAL:好きな体位 > 10 
		CFLAG:LOCAL:好きな体位 = 0
	FOR COUNT,1,10
		SIF EXP:LOCAL:(COUNT + 100) > EXP:LOCAL:(CFLAG:LOCAL:好きな体位 + 100) + 50
			CFLAG:LOCAL:好きな体位 = COUNT
	NEXT
	;パンツフラグ
	CFLAG:LOCAL:ノーパン = 0
	SIF TCVAR:LOCAL:本来の服装
		CFLAG:LOCAL:基本服装セット = TCVAR:LOCAL:本来の服装
	SELECTCASE ABL:LOCAL:精液中毒
		CASE 0
			LOCAL:1 = 5
		CASE 1 TO 3
			LOCAL:1 = 7
		CASE 4 TO 7
			LOCAL:1 = 10
		CASEELSE
			LOCAL:1 = 20
	ENDSELECT
	SIF TALENT:MASTER:濃厚精液
		LOCAL:1 *= 2
	SETCOLOR 0xEE60EE
	IF EX:LOCAL:膣内精液 > 200 || EX:LOCAL:アナル内精液 > 200
		PRINTFORM %CALLNAME:MASTER%にたっぷり注ぎ込まれた精液で、%CALLNAME:LOCAL%の
		IF EX:LOCAL:膣内精液 > 200 && EX:LOCAL:アナル内精液 > 200
			PRINTL 膣内とアナルが熱く疼いている…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
		ELSEIF EX:LOCAL:膣内精液 > 200
			PRINTL 膣内が熱く疼いている…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
		ELSE
			PRINTL アナルが熱く疼いている…
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
		ENDIF
	ENDIF
	IF (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) > 0
		PRINTFORM %CALLNAME:LOCAL%は
		SIF EX:LOCAL:20
			PRINT クリトリス
		SIF EX:LOCAL:20 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT と
		SIF EX:LOCAL:21
			PRINT 膣内
		SIF EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINT と
		SIF EX:LOCAL:22
			PRINT アナル
		SIF EX:LOCAL:22 && (EX:LOCAL:23 || EX:LOCAL:24)
			PRINT と
		SIF EX:LOCAL:23
			PRINT 胸
		SIF EX:LOCAL:23 && EX:LOCAL:24
			PRINT と
		SIF EX:LOCAL:24
			PRINT 唇
		PRINTFORMW の疼きに悶々としている…
		CFLAG:LOCAL:溜まってる度 += (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) * 100
	ENDIF
	IF (TALENT:LOCAL:150 || TALENT:LOCAL:151) && !TALENT:LOCAL:初潮 && EXP:LOCAL:Ｖ絶頂経験 > 200 && !RAND:10 && EX:LOCAL:膣内精液
		PRINTFORML 幼い身体が知るには早すぎた快楽が、%CALLNAME:LOCAL%を「女」へと変えていく……
		PRINTFORMW %CALLNAME:LOCAL%は初潮を迎えた
		PRINTFORMW %CALLNAME:LOCAL%は子を孕める身体となった
		TALENT:LOCAL:初潮 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 < 3
		IF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 10000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10
			PRINTFORML 幾度となく精液を注ぎ込まれた%CALLNAME:LOCAL%の身体は、%CALLNAME:MASTER%の精液の味を覚えてしまった…
			PRINTL 今後は無意識でも自分から欲しがるようになるだろう
			PRINTFORMW %CALLNAME:LOCAL%の精液中毒が3になった
			ABL:LOCAL:精液中毒 = 3
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 5000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && ABL:LOCAL:精液中毒 < 2
			PRINTFORML %CALLNAME:LOCAL%の身体は、精液に馴染みつつある…
			PRINTFORMW %CALLNAME:LOCAL%の精液中毒が2になった
			ABL:LOCAL:精液中毒 = 2
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 2000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !ABL:LOCAL:精液中毒
			PRINTFORML %CALLNAME:LOCAL%の身体は、精液の味を知ってしまった…
			PRINTFORMW %CALLNAME:LOCAL%の精液中毒が1になった
			ABL:LOCAL:精液中毒 = 1
		ENDIF
	ENDIF
	IF CFLAG:LOCAL:累計精飲 > 4000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:精愛味覚
		PRINTFORML 精液を飲まされ続けた%CALLNAME:LOCAL%の身体は、精液の味そのものに悦びを覚えるようになってしまった…
		PRINTFORMW %CALLNAME:LOCAL%は【精愛味覚】を得た
		TALENT:LOCAL:精愛味覚 = 1
	ENDIF
	IF ABL:LOCAL:精液中毒 >= 3
		IF CFLAG:LOCAL:累計膣内精液 > 1500 * ABL:LOCAL:膣射中毒 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 &&  EX:LOCAL:膣内精液 > 200 && ABL:LOCAL:膣射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%の媚粘膜に精液が染み渡ってゆく…
			PRINTFORMW %CALLNAME:LOCAL%の膣は\@ ABL:LOCAL:膣射中毒 ? より強く# \@%CALLNAME:MASTER%の精液を欲するようになった
			ABL:LOCAL:膣射中毒 ++
		ELSEIF CFLAG:LOCAL:累計アナル精液 > 1500 * ABL:LOCAL:肛射中毒 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && EX:LOCAL:アナル内精液 > 200 && ABL:LOCAL:肛射中毒 < 3
			PRINTFORMW %CALLNAME:LOCAL%の直腸に精液が染み渡ってゆく…
			PRINTFORMW %CALLNAME:LOCAL%のアナルは\@ ABL:LOCAL:肛射中毒 ? より強く# \@%CALLNAME:MASTER%の精液を欲するようになった
			ABL:LOCAL:肛射中毒 ++
		ENDIF
	ENDIF
	RESETCOLOR
	IF FLAG:妊娠スイッチ == 1
		SIF CFLAG:LOCAL:妊娠２ == 0 && !FLAG:避妊 && !TCVAR:LOCAL:ピル
			CALL GET_CHILD(LOCAL)
	ELSE
	;
	ENDIF
	CALL 出産経過(LOCAL)
	SIF CFLAG:LOCAL:600
		CFLAG:LOCAL:600 = 0
NEXT

FOR LOCAL,MINROOM(),MAXROOM
	YOGORE:LOCAL += RAND:50
NEXT
IF FLAG:加護
	FLAG:加護 = 0
	PRINTFORMW 神の加護の効果が切れた
ENDIF
IF FLAG:異常気象
	FLAG:異常気象 = 0
	PRINTFORMW 異常気象は収まった
ENDIF

IF TFLAG:出張お掃除
	PRINTFORML 頼んでおいたお掃除サービスがやってきた
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%中がきれいになった！
	FOR LOCAL,MINROOM(),MAXROOM
		YOGORE:LOCAL /= 4
	NEXT
ENDIF

;依頼の期限切れをチェック
FOR LOCAL,1,6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	SIF IRAI_DEADLINE:LOCAL > DAY
		CONTINUE
	PRINTFORML %CALLNAME:(IRAI_CLIENT:LOCAL)%からの依頼の期限が切れた……
	CALL COLORMESSAGE("【依頼失敗】",C_YELLOW,2,1)
	CFLAG:(IRAI_CLIENT:LOCAL):依頼状況 = 4
NEXT

;デイリー
SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:MASTER:デート中 == MAIN_MAP
	CALL DAIRYEVENT
;畑の管理
CALL YASAI

;貸本屋
CALL BOOKLEND

CALL ONANISM

CALL ONAKIN

CALL 酒造



CALL MOENY_HENDOU

IF MONEY:1 >= 30000
ELSE
	MONEY:1 += MAXBASE:MASTER:TSP - BASE:MASTER:TSP
	IF MONEY:1 >= 30000 && MAXBASE:MASTER:TSP == 2500
		PRINTFORML 時間停止能力が極限進化した！
		MAXBASE:MASTER:TSP = 4000
	ELSEIF MONEY:1 >= 10000 && MAXBASE:MASTER:TSP == 1500
		PRINTFORML 時間停止能力の扱いが最高に上手くなった！
		MAXBASE:MASTER:TSP = 2500
	ELSEIF MONEY:1 >= 5000 && MAXBASE:MASTER:TSP == 800
		PRINTFORML 時間停止能力の扱いがかなり上手くなった！
		MAXBASE:MASTER:TSP = 1500
	ELSEIF MONEY:1 >= 1800 && MAXBASE:MASTER:TSP == 500
		PRINTFORML 時間停止能力の扱いが上手くなった！
		MAXBASE:MASTER:TSP = 800
	ELSEIF MONEY:1 >= 500 && MAXBASE:MASTER:TSP == 200
		PRINTFORML 時間停止能力の扱いが少し上手くなった！
		MAXBASE:MASTER:TSP = 500
	ELSEIF MONEY:1 >= 150 && MAXBASE:MASTER:TSP == 100
		PRINTFORML 時間停止能力の扱いが僅かに上手くなった！
		MAXBASE:MASTER:TSP = 200
	ENDIF
ENDIF
BASE:MASTER:TSP = MAXBASE:MASTER:TSP

SIF FLAG:酒屋買取強化 > 0
	FLAG:酒屋買取強化 --
;何の珠を得られたか
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
	CALL JUEL_CHECK(LOCAL)
	CALL DENIAL_CHECK(LOCAL)
NEXT
BEGIN ABLUP



@JUEL_CHECK(ARG)
FOR LOCAL,0,100
	IF PALAM:ARG:LOCAL < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:1*3
		LOCAL:1 = 1
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2
		LOCAL:1 = 2
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2*3
		LOCAL:1 = 10
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3
		LOCAL:1 = 20
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3*2
		LOCAL:1 = 100
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:4
		LOCAL:1 = 200
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:5
		LOCAL:1 = 1000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:6
		LOCAL:1 = 2000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:7
		LOCAL:1 = 3000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:8
		LOCAL:1 = 5000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:9
		LOCAL:1 = 8000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:10
		LOCAL:1 = 12000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:11
		LOCAL:1 = 25000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:12
		LOCAL:1 = 40000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:13
		LOCAL:1 = 60000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:14
		LOCAL:1 = 100000
	ELSE
		LOCAL:1 = 150000
	ENDIF
	;快感系
	IF LOCAL >= 0 && LOCAL < 9
		GOTJUEL:ARG:LOCAL = LOCAL:1 + EX:ARG:LOCAL * 1000
	;その他
	ELSEIF LOCAL < 30
		GOTJUEL:ARG:LOCAL = LOCAL:1
	;否定の珠
	ELSEIF LOCAL == 30 || LOCAL == 31 || LOCAL == 32
		GOTJUEL:ARG:100 += LOCAL:1
	ENDIF
NEXT
JUEL:ARG:100 += GOTJUEL:ARG:100
FOR LOCAL,0,100
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF LOCAL != 9 &&LOCAL != 30 && LOCAL != 31 && LOCAL != 32
		JUEL:ARG:LOCAL += GOTJUEL:ARG:LOCAL
NEXT
@DENIAL_CHECK(ARG)
SIF JUEL:ARG:100 == 0
	RETURN 0
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恭順 && !JUEL:ARG:欲情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恥情 && !JUEL:ARG:苦痛 && !JUEL:ARG:恐怖 && !JUEL:ARG:好意 && !JUEL:ARG:優越)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:5
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 14
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
		CASE 4
			LOCAL:2 = 21
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

@ONANISM
#DIM 欲求不満度
#DIM 満足度
#DIM 嫉妬度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加自慰
#DIM 自慰タイプ
#DIM 自慰発生閾値
VARSET LOCAL

嫉妬度 = 0

;親密な人の数が多いほど嫉妬して欲求不満度が上がる
;FOR LOCALの中にFOR LOCALいれたらエラー出たので独立させる
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF TALENT:LOCAL:恋慕
		嫉妬度 += 3
	SIF TALENT:LOCAL:思慕
		嫉妬度 += 1
NEXT
DEBUGPRINTFORML 嫉妬度{嫉妬度}
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	部位指定 = 0
	自慰タイプ = 0
	追加自慰 = 0
	自慰発生閾値 = LIMIT(750 - 200 * TALENT:LOCAL:自慰しやすい - 100 * TALENT:LOCAL:快感応答 - 100 * TALENT:LOCAL:自己愛 + 200 * TALENT:LOCAL:自制心,500,999)
	自慰発生閾値 -= 300 * TALENT:LOCAL:自慰狂い
	IF CFLAG:LOCAL:オナバレ
		PRINTFORML %CALLNAME:LOCAL%は枕に顔を埋めてばたばたしている…
	ELSEIF CFLAG:LOCAL:溜まってる度 > 自慰発生閾値
		IF CFLAG:LOCAL:既成事実 & 合意_うふふ && !RAND:(5 - TALENT:LOCAL:自制心)
			PRINTFORML %CALLNAME:LOCAL%は体の疼きに悶々としながらも、明日%CALLNAME:MASTER%に抱いてもらおうと考え、己を強いて眠りに就いた……
			自慰タイプ = 7
			;CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,7)
			CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 100, 0 ,1000)
			SIF !CFLAG:LOCAL:神社在住 
				SETBIT FLAG:(550 + LOCAL / 64),LOCAL % 64
		ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初期位置
			PRINTFORML 夜中、%CALLNAME:MASTER%は物音を聞いて目を覚ました
			PRINTFORML ……%CALLNAME:LOCAL%が%CALLNAME:MASTER%の手を握りながら声を押し殺して自慰に耽っていた……
			自慰タイプ = 2
		ELSEIF ABL:LOCAL:精液中毒 > 2 && EX:LOCAL:膣内精液 && !RAND:4
			PRINTFORML 我慢できなくなった%CALLNAME:LOCAL%は、膣内に残る%CALLNAME:MASTER%の精液を刷り込み馴染ませるように自慰をした……
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:無知
			LOCAL:1 = 0
			LOCAL:1 = EXP:自慰経験 - EXP:調教自慰経験
			IF LOCAL:1 >= 25
				PRINTFORMW とうとう%CALLNAME:LOCAL%は保護者に相談した……
				PRINTFORMW %CALLNAME:LOCAL%は無知を失った……
				TALENT:LOCAL:無知 = 0
			ELSEIF LOCAL:1 >= 20
				PRINTFORML %CALLNAME:LOCAL%は自慰を自慰とわからないまま自慰に没頭している……
			ELSEIF LOCAL:1 >= 15
				PRINTFORML %CALLNAME:LOCAL%は机の角にお股を押し当てながら腰を振り、秘所を刺激した……
			ELSEIF LOCAL:1 >= 10
				PRINTFORML %CALLNAME:LOCAL%は本を股に挟み幼い秘裂を刺激した……
			ELSEIF LOCAL:1 >= 5
				PRINTFORML %CALLNAME:LOCAL%は勉強中に筆を股間に当てて、ぷにぷにの割れ目を擦りあげた……
			ELSE
				PRINTFORML %CALLNAME:LOCAL%は寝床に横になりながら太腿を擦り合わせ悶々としている……
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && (TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人 || TALENT:LOCAL:愛欲 || TALENT:LOCAL:セフレ) && !RAND:3
			PRINTFORML どうにも我慢できなくなった%CALLNAME:LOCAL%は、服に染み付いた%CALLNAME:MASTER%の匂いを嗅ぎながら激しい自慰をした……
			LOCAL:2 = RAND:5
			IF (TALENT:LOCAL:Ｃ感度 == 1 || ABL:LOCAL:Ｃ感覚 >= 10) && LOCAL:2 == 4
				PRINTFORM 陰核を抓み、潮を噴出しながら只管、行為に没頭したが
				部位指定 = 1
			ELSEIF (TALENT:LOCAL:Ｖ感度 == 1 || ABL:LOCAL:Ｖ感覚 >= 10) && LOCAL:2 == 3
				PRINTFORM 蜜壷に指を深々と挿入して愛液を掻き出し続けたが
				部位指定 = 2
			ELSEIF (TALENT:LOCAL:Ａ感度 == 1 || ABL:LOCAL:Ａ感覚 >= 10) && LOCAL:2 == 2
				PRINTFORM 秘所だけでなく尻穴をも嬲り続けたが
				部位指定 = 3
			ELSEIF (TALENT:LOCAL:Ｂ感度 == 1 || ABL:LOCAL:Ｂ感覚 >= 10) && LOCAL:2 == 1
				IF TALENT:LOCAL:バストサイズ >= 1
					PRINTFORM 自らの豊満な乳房を咥えて吸い上げ、
				ELSEIF TALENT:LOCAL:バストサイズ <= -1
					PRINTFORM 薄い胸をなで回しながら乳首をこすり、
				ELSE
					PRINTFORM やわらかな乳房をやわやわと揉みしだき、
				ENDIF
				PRINTFORM 幾度も絶頂を迎えるも
				部位指定 = 4
			ELSE
				;PRINTFORML しかし、どんなに快感を得ようとも%CALLNAME:MASTER%への想いは治まることはなく、%CALLNAME:LOCAL%は朝まで悶々とした時間を過ごした……
				PRINTFORM 体中に響く悦楽に身を震わせながらも
				部位指定 = 5
			ENDIF
			IF TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人
				PRINTFORML 、%CALLNAME:MASTER%への想いは治まることはなかった……
			ELSE
				PRINTFORML 、燃え上がった体の熱は収まらなかった……
			ENDIF
			追加自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:LOCAL:自慰中毒 > 3 && !RAND:2
			追加自慰 = 1
			PRINTFORM もはや指では満足できなくなった%CALLNAME:LOCAL%は、河童から買い求めた玩具
			IF ABL:LOCAL:Ａ感覚 >= 5
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM を膣とアヌスの奥深くまで咥え込んで
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM を秘所と肛門に押し付けて
				ELSE
					PRINTFORM で秘所と尻穴を弄って
				ENDIF
				部位指定 = 1
			ELSE
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM を膣の奥深くまで咥え込んで
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM を秘所に押し付けて
				ELSE
					PRINTFORM で秘所を弄って
				ENDIF
				部位指定 = 2
			ENDIF
			PRINTFORML 自慰に耽っている…
			自慰タイプ = 6
		ELSE
			PRINTFORML 体の疼きを抑えきれなくなった%CALLNAME:LOCAL%は、秘所を自らの指で弄った…
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,自慰タイプ,部位指定)
		SIF 自慰タイプ == 7
			CONTINUE
		EXP:LOCAL:自慰経験 += ABL:LOCAL:自慰中毒 * 2 + 1
		CALL COLORMESSAGE(@"自慰経験＋{ABL:LOCAL:自慰中毒 * 2 + 1}",C_PINK,1)
		SELECTCASE ABL:LOCAL:自慰中毒
			CASE 0 TO 2
				珠入手倍率 = 5
			CASE 3 TO 5
				珠入手倍率 = 3
			CASEELSE
				珠入手倍率 = 1
		ENDSELECT
		;快C
		CALL JUEL_UP_SPECIAL(LOCAL,0,0,珠入手倍率,1)
		;恥情
		CALL JUEL_UP_SPECIAL(LOCAL,14,30,珠入手倍率 * 2,1)
		;欲情
		CALL JUEL_UP_SPECIAL(LOCAL,11,11,珠入手倍率,1)
		SIF 追加自慰
			CALL JUEL_UP_SPECIAL(LOCAL,部位指定,部位指定,珠入手倍率,1)
		CFLAG:LOCAL:溜まってる度 /= 4
		PRINTFORML 
	ENDIF
	;まず欲望で基礎値を決める
	SELECTCASE ABL:LOCAL:欲望
		CASE 0 TO 2
			欲求不満度 = 10
		CASE 3 TO 5
			欲求不満度 = 20
		CASE 6 TO 10
			欲求不満度 = 30
		CASE 10 TO 15
			欲求不満度 = 50		
		CASEELSE
			欲求不満度 = 80
	ENDSELECT
	IF TALENT:LOCAL:恋慕
		欲求不満度 += 嫉妬度 * 2
	ELSEIF TALENT:LOCAL:思慕
		欲求不満度 += 嫉妬度
	ENDIF
	欲求不満度 += ABL:LOCAL:自慰中毒 * 7 + (ABL:LOCAL:膣射中毒 + ABL:LOCAL:肛射中毒) * 3
	欲求不満度 = 欲求不満素質補正(LOCAL,欲求不満度)
	SELECTCASE 欲求不満度
		CASE IS > 400
			欲求不満度 = (欲求不満度 - 400) / 4 + 400
		CASE IS > 200
			欲求不満度 = (欲求不満度 - 200) / 2 + 200
	ENDSELECT
	SIF 自慰タイプ
		欲求不満度 /= 4
	;溜まってる度は0~1000の間に
	CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 欲求不満度, 0, 1000)
	DEBUGPRINTFORML %CALLNAME:LOCAL%の満足度：{満足度}　欲求不満上昇値：{欲求不満度}　溜まってる度：{CFLAG:LOCAL:溜まってる度}
NEXT

@ONAKIN
IF BASE:MASTER:精力 == MAXBASE:MASTER:精力 && TALENT:MASTER:2 & 2
	FLAG:71 ++
	IF FLAG:71 > 2
		TALENT:MASTER:120 = 1
		PRINTL そういえばここのところ抜いていない…
	ENDIF
ELSE
	FLAG:71 = 0
	TALENT:MASTER:120 = 0
ENDIF

@DAIRYEVENT
;とりあえず30だけ。
#DIM EVENT_DAY,30

CALL FISHER_YATES_SHAFFLE(30)
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_DAY"

FOR LOCAL,0,30
	RESULT = 0
	TRYCALLFORM DAIRY_CONDITION_EV{EVENT_DAY:LOCAL}
	IF RESULT > 0
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}が発生したよ
		BREAK
	ENDIF
	SIF RESULT == -1
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}は発生条件を満たさなかったんだぜ
NEXT
PRINTFORMW 
DRAWLINE
PRINTFORML 


@欲求不満素質補正(ARG,ARG:1)
#FUNCTION
LOCAL = ARG:1
SIF TALENT:ARG:自制心
	LOCAL /= 2
SIF TALENT:ARG:自己愛 < 0
	LOCAL /= 2
SIF TALENT:ARG:自己愛 > 0
	LOCAL = LOCAL * 5 / 4
SIF TALENT:ARG:幼稚 && !TALENT:ARG:初潮
	LOCAL /= 4
SIF TALENT:ARG:妊娠
	LOCAL /= 4
SIF TALENT:ARG:育児中
	LOCAL /= 2
SIF CFLAG:ARG:オナバレ
	LOCAL /= 4
RETURNF LOCAL