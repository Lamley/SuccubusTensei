@HOLIDAY
;;;1	土日
;;;2	春休み
;;;3	夏休み
;;;4	冬休み
;;;5	正月
;;;6	成人の日	１月第２月曜日
;;;7	建国記念の日	２月１１日
;;;8	春分の日	３月２１日ころ
;;;9	昭和の日	４月２９日
;;;10	憲法記念日	５月３日
;;;11	みどりの日	５月４日
;;;12	こどもの日	５月５日
;;;13	海の日		7月第3月曜日
;;;14	敬老の日	9月第3月曜日
;;;15	秋分の日	９月２３日ころ
;;;16	体育の日	10月第2月曜日
;;;17	文化の日	１１月３日
;;;18	勤労感謝の日	１１月２３日
;;;19	天皇诞生日	１２月２３日
;;;20	国民の休日は再現しません、克里斯マスイブが休みとなります。（リア・・・・ろ）
#FUNCTION
DAY:4 = DAY:3 + 6
SELECTCASE DAY:2
	CASE 1
		SIF DAY:3 < 4
			RETURNF 5
		SIF ((DAY:4 / 7) == 2) && ((DAY % 7) == 1)
			RETURNF 6
		SIF DAY:3 < 7
			RETURNF 4
	CASE 2
		SIF DAY:3 == 11
			RETURNF 7
	CASE 3
		IF DAY:1 % 4 == 0 || DAY:1 % 4 == 1
			SIF DAY:3 == 20
				RETURNF 8
		ELSE
			SIF DAY:3 == 21
				RETURNF 8
		ENDIF
		SIF DAY:3 > 24
			RETURNF 2
	CASE 4
		SIF DAY:3 < 6
			RETURNF 2
		SIF DAY:3 == 29
			RETURNF 9
	CASE 5
		SIF DAY:3 == 3
			RETURNF 10
		SIF DAY:3 == 4
			RETURNF 11
		SIF DAY:3 == 5
			RETURNF 12
	CASE 7
		SIF ((DAY:4 / 7) == 3) && ((DAY % 7) == 1)
			RETURNF 13
		SIF DAY:3 > 20
			RETURNF 3
	CASE 8
		RETURNF 3
	CASE 9
		SIF ((DAY:4 / 7) == 3) && ((DAY % 7) == 1)
			RETURNF 14
		SIF DAY:3 == 22
			RETURNF 15
	CASE 10
		SIF ((DAY:4 / 7) == 2) && ((DAY % 7) == 1)
			RETURNF 16
	CASE 11
		SIF DAY:3 == 3
			RETURNF 17
		SIF DAY:3 == 23
			RETURNF 18
	CASE 12
		SIF DAY:3 == 23
			RETURNF 19
		SIF DAY:3 == 24
			RETURNF 20
		SIF DAY:3 > 24
			RETURNF 4
ENDSELECT
;土日、休日だと1を返す
SIF DAY % 7 == 0 || DAY % 7 == 6
	RETURNF 1	

RETURNF 0

@MAX_MAP
#FUNCTION
RETURNF 3


@MAX_ROOM
#FUNCTION
RETURNF 30

@GETMAP(ARG)
FOR LOCAL, 1, CHARANUM
	IF CFLAG:MASTER:現在位置 == ARG
		SETCOLOR 0 , 255, 0
	;ELSEIF CFLAG:MASTER:現在地区 == CFLAG:LOCAL:現在地区 && CFLAG:LOCAL:現在位置 == ARG
		;SETCOLOR 255 , 255, 0
	ENDIF
NEXT
PRINTBUTTON (ARG > 9 ? TOSTR(ARG) # TOFULL(TOSTR(ARG))),ARG
RESETCOLOR

@MAP_NAME_BUTTON(ARG,ARG:1)
#FUNCTIONS
RETURNF @"\@ARG:1 > 9 ? %TOSTR(ARG:1)% # %TOFULL(TOSTR(ARG:1))%\@\:%PLACE_NAME(ARG,ARG:1),16,LEFT%"

@EXP_UP(ARG,ARG:1)
#FUNCTION
IF ARG == 1 && TALENT:(ARG:1):处女 == 1
	SIF !(TCVAR:(ARG:1):被挿入)
		RETURNF 0
ENDIF
RETURNF EXP:(ARG:1):ARG - TCVAR:(ARG:1):(400 + ARG)

;-------------------------------------------------
;関数名:SOURCE_REVISION1
;快楽系SOURCEの快楽系ABLによる補正
;1/10される
;-------------------------------------------------
@SOURCE_REVISION1(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0
		RETURNF 5
	CASE 1 TO 3
		RETURNF 10 * ARG
	CASE 4 TO 9
		RETURNF 20 * ARG - 30
	CASEELSE
		RETURNF 150
ENDSELECT
;-------------------------------------------------
;関数名:SOURCE_REVISION2
;快楽系SOURCEに依存するUP:欲望のABL:欲望による補正
;1/100される
;-------------------------------------------------
@SOURCE_REVISION2(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0 TO 5
		RETURNF 50 + 10 * ARG
	CASE 6 TO 9
		RETURNF 75 + 5 * ARG
	CASEELSE
		RETURNF 140
ENDSELECT

;-------------------------------------------------
;関数名:GET_REVISION
;概　要:汎用補正値取得関数
;引　数:ARG:0…参照する数値
;       ARG:1…上限
;       ARG:2…上昇率
;戻り値:補正後の値
;備　考:式中関数
;ある数値を、上限値に漸近していく数値に変換したいときに
;ARG:2が小さいとすぐに上限に達する、大きいと逆になかなか上限まで近づかない
;-------------------------------------------------
@GET_REVISION(ARG:0, ARG:1, ARG:2)
#FUNCTION
RETURNF ARG:1 - ARG:2 * ARG:1 / (ARG:2 + ARG:0)

@GET_REVISION2(ARG:0, ARG:1, ARG:2)
#FUNCTION
RETURNF ARG:1 * (ARG:0 + 1) / (ARG:2 + ARG:0 + 1)

;-------------------------------------------------
;関数名:HETEROSEX
;ARGとARG:1の性別が同じかどうかを返すほんとはHOMOSEXだけど語感が悪いので
;女性器を持つものは女、持たないものを男とする
;戻り値0=異性
;戻り値1=同性(レズ)
;戻り値2=同性(ゲイ)
;-------------------------------------------------
@HETEROSEX(ARG,ARG:1)
#FUNCTION
IF GETBIT(TALENT:ARG:2,0) == GETBIT(TALENT:(ARG:1):2,0)
	IF GETBIT(TALENT:ARG:2,0)
		RETURNF 1
	ELSE
		RETURNF 2
	ENDIF
ELSE
	RETURNF 0
ENDIF

@HETEROSEX_EXP(ARG,ARG:1,ARG:2)
;同性愛　レズなら戻り値1ゲイなら戻り値2
SELECTCASE HETEROSEX(ARG,ARG:1)
	CASE 1
		EXP:ARG:百合经验 += ARG:2
		EXP:(TCVAR:ARG:行為対象):百合经验 += ARG:2
	CASE 2
		EXP:ARG:ゲイ経験 += ARG:2
		EXP:(TCVAR:ARG:行為対象):ゲイ経験 += ARG:2
ENDSELECT

@SAME_ROOM_CHECK
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
;;;	SIF NO:LOCAL == 34
;;;		CONTINUE
;;;	SIF TALENT:LOCAL:200
;;;		CONTINUE
	IF !TCVAR:LOCAL:衰弱 && !TCVAR:LOCAL:睡眠 && TCVAR:LOCAL:イベント == TCVAR:MASTER:イベント && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:LOCAL:現在地区 == CFLAG:MASTER:現在地区
		IF (CFLAG:LOCAL:現在位置 != 30 || CFLAG:LOCAL:現在地区 != 0)
;;;20120211 追加　同室で情欲度に＋１（マスター）まだマスター分は未実装
;;;			TA:MASTER:LOCAL:4 += 1
			TCVAR:LOCAL:同室 = 1
			LOCAL:1++
		ENDIF
	ELSE
		TCVAR:LOCAL:同室 = 0
	ENDIF
NEXT
IF LOCAL:1
	TFLAG:同室 = 1
ELSE
	TFLAG:同室 = 0
ENDIF


@LOVE_CHECK(ARG,ARG:1)
#FUNCTION
IF TA:ARG:(ARG:1):3 > 200 && TA:ARG:(ARG:1):4 > 200
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@OBEY_CHECK(ARG,ARG:1)
#FUNCTION
SELECTCASE TA:ARG:(ARG:1):2 - TA:(ARG:1):ARG:2
	CASE IS < 80
		RETURNF 0
	CASE IS < 150
		RETURNF 1
	CASEELSE
		RETURNF 2
ENDSELECT

@WAKE_UP_CHECK
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:睡眠 || CFLAG:LOCAL:待機時間
		CONTINUE
	IF ((TIME >= CFLAG:LOCAL:起床時間 && TIME <= CFLAG:LOCAL:起床時間 + 180) || (BASE:LOCAL:眠気 == 0 && !CFLAG:LOCAL:主睡眠 )) && BASE:LOCAL:眠気 < 1000 && (!CFLAG:LOCAL:衰弱 || BASE:LOCAL:体力 == MAXBASE:LOCAL:体力)
			CFLAG:LOCAL:睡眠 = 0
			CFLAG:LOCAL:衰弱 = 0
			CFLAG:LOCAL:主睡眠 = 0
	ENDIF
NEXT

[SKIPSTART]
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:睡眠
		CONTINUE
	IF CFLAG:LOCAL:就寝時間 >= CFLAG:LOCAL:起床時間
		IF TIME >= CFLAG:LOCAL:起床時間 && TIME < CFLAG:LOCAL:就寝時間 && (!CFLAG:LOCAL:衰弱 || BASE:LOCAL:体力 == MAXBASE:LOCAL:体力)
			CFLAG:LOCAL:睡眠 = 0
			CFLAG:LOCAL:衰弱 = 0
			;PRINTFORML %CALLNAME:LOCAL%起床了
		ENDIF
	ELSE
		IF TIME >= CFLAG:LOCAL:起床時間 || TIME < CFLAG:LOCAL:就寝時間 && (!CFLAG:LOCAL:衰弱 || BASE:LOCAL:体力 == MAXBASE:LOCAL:体力)
			CFLAG:LOCAL:睡眠 = 0
			CFLAG:LOCAL:衰弱 = 0
			;PRINTFORML %CALLNAME:LOCAL%起床了
		ENDIF
	ENDIF
NEXT
[SKIPEND]
@SLEEP_CHECK
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:睡眠 || TCVAR:LOCAL:イベント
		CONTINUE
	SIF LOCAL == MASTER
		CONTINUE
	IF CFLAG:LOCAL:就寝時間 >= CFLAG:LOCAL:起床時間
		IF TIME < CFLAG:LOCAL:起床時間 || TIME >= CFLAG:LOCAL:就寝時間
			CFLAG:LOCAL:睡眠 = 1
			CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:開始位置
			CFLAG:LOCAL:現在地区 = CFLAG:LOCAL:開始地区
			CFLAG:LOCAL:乗車 = 0
			TCVAR:LOCAL:目的地区 = 0
			TCVAR:LOCAL:目的位置 = 0
			CFLAG:LOCAL:到着地区 = 0
			CFLAG:LOCAL:到着位置 = 0
			;PRINTFORML %CALLNAME:LOCAL%睡了
		ENDIF

	ELSE
		IF TIME < CFLAG:LOCAL:起床時間 && TIME >= CFLAG:LOCAL:就寝時間
			CFLAG:LOCAL:睡眠 = 1
			CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:開始位置
			CFLAG:LOCAL:現在地区 = CFLAG:LOCAL:開始地区
			CFLAG:LOCAL:乗車 = 0
			TCVAR:LOCAL:目的地区 = 0
			TCVAR:LOCAL:目的位置 = 0
			CFLAG:LOCAL:到着地区 = 0
			CFLAG:LOCAL:到着位置 = 0
			;PRINTFORML %CALLNAME:LOCAL%睡了
		ENDIF
	ENDIF
	
NEXT
@WEAKNESS_CHECK
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:睡眠 || CFLAG:LOCAL:衰弱 || TCVAR:LOCAL:イベント == 2
		CONTINUE
	IF BASE:LOCAL:体力 < 500
		J:115 = 0
		J:116 = 0
		CFLAG:LOCAL:睡眠 = 1
		CFLAG:LOCAL:衰弱 = 1
		TCVAR:LOCAL:衰弱 = 1
		CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:開始位置
		CFLAG:LOCAL:現在地区 = CFLAG:LOCAL:開始地区
		CFLAG:LOCAL:乗車 = 0
		TCVAR:LOCAL:目的地区 = 0
		TCVAR:LOCAL:目的位置 = 0
		CFLAG:LOCAL:到着地区 = 0
		CFLAG:LOCAL:到着位置 = 0
		CFLAG:LOCAL:到着時間 = 0
		IF TCVAR:LOCAL:イベント
			CALL EVENT_ESCAPE(LOCAL)
			SIF LOCAL != MASTER
				PRINTFORMW %CALLNAME:LOCAL%精疲力竭的回去了
		ENDIF
		SIF LOCAL == MASTER
			PRINTW 累倒了･･････
	ENDIF
NEXT

@EVENT_ESCAPE(ARG)
VARSET LOCAL
IF ARG == MASTER
	CALL EVENT_ESCAPE_ALL
ELSE
	TCVAR:ARG:イベント = 0
	TCVAR:ARG:行為対象 = 0
	TCVAR:ARG:同行 = 0
	CFLAG:ARG:受能動 = 0
	CFLAG:ARG:押さえつけ = 0
	CALL SET_CLOTHES(ARG)
	IF TARGET == ARG
		TCVAR:MASTER:行為対象 = 0
		TCVAR:MASTER:同行 = 0
		TARGET = 0
		FOR LOCAL,1,CHARANUM
			IF TCVAR:LOCAL:行為対象 == MASTER
				TCVAR:MASTER:行為対象 = LOCAL
				TARGET = LOCAL
				BREAK
			ENDIF
		NEXT
		IF !TCVAR:MASTER:行為対象
			CALL EVENT_ESCAPE_ALL
		ENDIF
	ENDIF
ENDIF
@EVENT_ESCAPE_ALL
VARSET LOCAL

FOR LOCAL,1,CHARANUM
	TCVAR:LOCAL:イベント = 0
	TCVAR:LOCAL:行為対象 = 0
	TCVAR:LOCAL:同行 = 0
	CFLAG:LOCAL:受能動 = 0
	CFLAG:LOCAL:押さえつけ = 0
	TEQUIP:LOCAL:绳子 = 0
	TEQUIP:LOCAL:口塞球 = 0
	CFLAG:LOCAL:ＶオンリーＴ = 0
	CFLAG:LOCAL:ＶオンリーＰ = 0
	CFLAG:LOCAL:ＡオンリーＴ = 0
	CFLAG:LOCAL:ＡオンリーＰ = 0
	CALL SET_CLOTHES(LOCAL)
	IF CFLAG:LOCAL:乗車 == 1
		CFLAG:LOCAL:待機時間 = CFLAG:LOCAL:到着時間 - TIME
	ENDIF
NEXT

TARGET = 0
TFLAG:騒ぎ = 0
@EVENT_ESCAPE_NPC(ARG,ARG:1)
TCVAR:ARG:イベント = 0
TCVAR:ARG:行為対象 = 0
TCVAR:ARG:同行 = 0
CFLAG:ARG:受能動 = 0
CFLAG:ARG:押さえつけ = 0
TEQUIP:ARG:绳子 = 0
TEQUIP:ARG:口塞球 = 0
CFLAG:ARG:ＶオンリーＴ = 0
CFLAG:ARG:ＶオンリーＰ = 0
CFLAG:ARG:ＡオンリーＴ = 0
CFLAG:ARG:ＡオンリーＰ = 0
CALL SET_CLOTHES(ARG)
IF CFLAG:ARG:乗車 == 1
	CFLAG:ARG:待機時間 = CFLAG:ARG:到着時間 - TIME
ENDIF

TCVAR:(ARG:1):イベント = 0
TCVAR:(ARG:1):行為対象 = 0
TCVAR:(ARG:1):同行 = 0
CFLAG:(ARG:1):受能動 = 0
CFLAG:(ARG:1):押さえつけ = 0
TEQUIP:(ARG:1):绳子 = 0
TEQUIP:(ARG:1):口塞球 = 0
CFLAG:(ARG:1):ＶオンリーＴ = 0
CFLAG:(ARG:1):ＶオンリーＰ = 0
CFLAG:(ARG:1):ＡオンリーＴ = 0
CFLAG:(ARG:1):ＡオンリーＰ = 0
CALL SET_CLOTHES((ARG:1))
IF CFLAG:(ARG:1):乗車 == 1
	CFLAG:(ARG:1):待機時間 = CFLAG:(ARG:1):到着時間 - TIME
ENDIF

@EVENT_JOIN(ARG,ARG:1,ARG:2)
;ARG 実行者 ,ARG:1 対象者 ,ARG:2 イベント番号
VARSET LOCAL
TCVAR:ARG:イベント = ARG:2
TCVAR:(ARG:1):イベント = ARG:2
TCVAR:ARG:行為対象 = ARG:1
CFLAG:ARG:受能動 = 1
CFLAG:(ARG:1):受能動 = 2
SIF ARG:1 == MASTER && !TARGET
	TARGET = ARG
IF !TARGET
	IF ARG == MASTER
		TARGET = ARG:1
	ELSEIF ARG:1 == MASTER
		TARGET = ARG
	ENDIF
ENDIF
TCVAR:(ARG:1):行為対象 = ARG

TCVAR:(ARG:1):目的位置 = 0
TCVAR:(ARG:1):目的地区 = 0
TCVAR:ARG:目的位置 = 0
TCVAR:ARG:目的地区 = 0
CFLAG:(ARG:1):待機時間 = 0
SIF ARG:2 == 4 && K:104
	CFLAG:ARG:待機時間 = 6

@STATIN_POINT(ARG,ARG:1)
#FUNCTION
VARSET LOCAL
;愛液の汚れ
SIF STAIN:ARG:(ARG:1) & 1
	LOCAL+= 1
;精液の汚れ
SIF STAIN:ARG:(ARG:1) & 4
	LOCAL += 3
;アナルの汚れ
SIF STAIN:ARG:(ARG:1) & 8
	LOCAL += 7
SIF STAIN:ARG:(ARG:1) & 16
	LOCAL += 15
RETURNF LOCAL

@FAVOR_RANK(ARG,ARG:1)
;ARG 被行為者
;ARG:1 行為者
#FUNCTION
SELECTCASE TA:ARG:(ARG:1):3
	CASE IS <= 29
		RETURNF 0
	CASE 30 TO 99
		RETURNF 1
	CASE 100 TO 199
		RETURNF 2
	CASE 200 TO 499
		RETURNF 3
	CASE 500 TO 1999
		RETURNF 4
	CASEELSE
		RETURNF 5
ENDSELECT

@OBEY_RANK(ARG,ARG:1)
;ARG 被行為者
;ARG:1 行為者
#FUNCTION
SELECTCASE TA:ARG:(ARG:1):2
	CASE IS <= 0
		RETURNF 0
	CASE 1 TO 9
		RETURNF 1
	CASE 10 TO 29
		RETURNF 2
	CASE 30 TO 79
		RETURNF 3
	CASE 80 TO 149
		RETURNF 4
	CASEELSE
		RETURNF 5
ENDSELECT

@PLACE_NAME(ARG,ARG:1)
#FUNCTIONS
SELECTCASE ARG
CASE 0
	RETURNF PLACE_NAME_0(ARG:1)
CASE 1
	RETURNF PLACE_NAME_1(ARG:1)
CASE 2
	RETURNF PLACE_NAME_2(ARG:1)
ENDSELECT

@ATTACK_POWER(ARG,ARG:1)
#FUNCTION

;クリティカルとファンブル
SELECTCASE RAND(20)
	CASE 0
		TIMES ARG , 3.50
		RETURNF ARG
	CASE 19
		RETURNF 0
ENDSELECT
SELECTCASE ARG:1
	CASE IS < 0
		RETURNF 1
	CASE 0
		TIMES ARG , 0.50
		RETURNF ARG
	CASE 1
		RETURNF ARG
	CASE 2
		TIMES ARG , 1.40
		RETURNF ARG
	CASE 3
		TIMES ARG , 1.80
		RETURNF ARG
	CASE 4
		TIMES ARG , 2.20
		RETURNF ARG
	CASE 5
		TIMES ARG , 2.60
		RETURNF ARG
	CASE IS > 5
		TIMES ARG , 3.00
		RETURNF ARG
ENDSELECT

@ATTACK_SUCCESS_RATE(ARG)
#FUNCTION
;クリティカルとファンブル
SELECTCASE RAND(10)
	CASE 0
		RETURNF 1
	CASE 9
		RETURNF 0
ENDSELECT

SELECTCASE ARG
	CASE IS < 0
		RETURNF 0
	CASE 0
		IF RAND(10) == 0
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE 1
		IF RAND(10) == 0
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE 2
		IF RAND(10) <= 2
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE 3
		IF RAND(10) <= 4
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE 4
		IF RAND(10) <= 6
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE 5
		IF RAND(10) <= 8
			RETURNF 1
		ELSE
			RETURNF 0
		ENDIF
	CASE IS > 5
			RETURNF 1

ENDSELECT


@ATTACK_SUCCESS(ARG,ARG:1)
#FUNCTION
;ARG 攻撃基本値 ARG:1 技量差

SELECTCASE ARG:1
	CASE IS < 0
		SELECTCASE RAND(10)
			CASE 0 TO 2
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1

				RETURNF ARG
			CASE 3 TO 4
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 5 TO 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT
	CASE 0
		SELECTCASE RAND(10)
			CASE 0
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 1 TO 4
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 5 TO 6
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 7 TO 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT

	CASE 1
		SELECTCASE RAND(10)
			CASE 0
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 1 TO 3
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 4 TO 7
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 8 TO 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT

	CASE 2
		SELECTCASE RAND(10)
			CASE 0 TO 1
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 2 TO 3
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 4 TO 7
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 8 TO 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT

	CASE 3
		SELECTCASE RAND(10)
			CASE 0 TO 2
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 3 TO 4
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 5 TO 8
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT

	CASE 4
		SELECTCASE RAND(10)
			CASE 0 TO 3
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 4 TO 5
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 6 TO 8
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 9
				TFLAG:COM用変数 = 0

				RETURNF 0
		ENDSELECT

	CASE 5
		SELECTCASE RAND(10)
			CASE 0 TO 4
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 5 TO 6
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
			CASE 7 TO 9
				TFLAG:COM用変数 = 2
				RETURNF ARG
		ENDSELECT

	CASE IS > 5
		SELECTCASE RAND(10)
			CASE 0 TO 6
				TIMES ARG,2.00
				TFLAG:COM用変数 = 3
				RETURNF ARG
			CASE 7 TO 8
				TFLAG:COM用変数 = 2
				RETURNF ARG
			CASE 9
				TIMES ARG,0.50
				TFLAG:COM用変数 = 1
				RETURNF ARG
		ENDSELECT

ENDSELECT

