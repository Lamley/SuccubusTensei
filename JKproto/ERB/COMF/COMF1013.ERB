@COM_ABLE1013(ARG)
VARSET LOCAL
LOCAL = ARG
VARSET ARG
SIF !CFLAG:LOCAL:部活
	RETURN 0
SIF CFLAG:LOCAL:現在位置 != CLUB_PLACE(CFLAG:LOCAL:部活) || CFLAG:LOCAL:現在地区 != 1
	RETURN 0
SIF !(TIME >= 960 && TIME < 1130)
	RETURN 0
RETURN GLOBAL_COMABLE(1013)
@COM1013(ARG)
VARSET LOCAL
PRINTL 
PRINTFORML %"=" * 107%
PRINTL 部活動
;部活動
CFLAG:ARG:待機時間 += 1130 - TIME

;TRYCALLFORM CLUB_PALAM_MOVE{CFLAG:ARG:部活}(ARG)
SELECTCASE CFLAG:ARG:部活
	CASE IS < 30
		EXP:ARG:運動経験 += CFLAG:ARG:待機時間 * 2 / 60
		MAXBASE:ARG:体力 += CFLAG:ARG:待機時間 * 3 / 60
		DOWNBASE:ARG:体力 += CFLAG:ARG:待機時間 * 40 / 60
		DOWNBASE:ARG:气力 -= CFLAG:ARG:待機時間 * 20 / 60
		DOWNBASE:ARG:疲労 -= CFLAG:ARG:待機時間 * 50 / 60
	CASEELSE
		BASE:ARG:学习成绩 = MIN(BASE:ARG:学习成绩 + CFLAG:ARG:待機時間 / 6,MAXBASE:ARG:学习成绩)
		EXP:ARG:学習経験 += CFLAG:ARG:待機時間 * 4 / 60 / 5
		MAXBASE:ARG:气力 += CFLAG:ARG:待機時間 * 3 / 60
		DOWNBASE:ARG:体力 -= CFLAG:ARG:待機時間 * 20 / 60
		DOWNBASE:ARG:气力 += CFLAG:ARG:待機時間 * 40 / 60
		DOWNBASE:ARG:疲労 -= CFLAG:ARG:待機時間 * 40 / 60
ENDSELECT
TB:(CFLAG:ARG:現在地区):(CFLAG:ARG:現在位置):ARG += 2

M:101 = 0
VARSET ARG
RETURN 1


