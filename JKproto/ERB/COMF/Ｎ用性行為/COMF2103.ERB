@COM_ABLE2103(ARG)
VARSET LOCAL
LOCAL = ARG
VARSET ARG
SIF !TCVAR:LOCAL:行為対象
	RETURN 0
SIF !GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1)
	RETURN 0
SIF !CAN_COM2103(TCVAR:LOCAL:行為対象,LOCAL)
	RETURN 0
SIF TEQUIP:(TCVAR:LOCAL:行為対象):下半身下着 == 373
	RETURN 0
;道具男性器反抗時は取り敢えずスルー
SIF TEQUIP:LOCAL:アナル == 79
	RETURN 0
;男性器反抗時は取り敢えずスルー
SIF TEQUIP:LOCAL:アナル == 78
	RETURN 0
;男性器のみ反抗時CFLAGは取り敢えずスルー
SIF CFLAG:LOCAL:ＡオンリーＴ == 1
	RETURN 0
;対象者が男性器で名器装着時は不可
SIF (GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1)) && (TEQUIP:(TCVAR:LOCAL:行為対象):クリトリス == 80)
	RETURN 0
;対象者が绳子装着時は不可
SIF TEQUIP:(TCVAR:LOCAL:行為対象):绳子
	RETURN 0
;;;2012/02/07 対象の　従順3以上　または　従順2かつ屈服刻印2以上　以外は無理にした
SIF !(ABL:(TCVAR:LOCAL:行為対象):従順 >= 3) && !((ABL:(TCVAR:LOCAL:行為対象):従順 == 2) && (MARK:(TCVAR:LOCAL:行為対象):屈服刻印 >= 2))
	RETURN 0
;対象者が男性器なし且つ両者共が捆绑式假阴茎なし
SIF !GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1) && EQUIP:LOCAL:捆绑式假阴茎 == 0 && EQUIP:(TCVAR:LOCAL:行為対象):捆绑式假阴茎 == 0
	RETURN 0

RETURN GLOBAL_COMABLE(2103)
;-------------------------------------------------
;后背位Ａさせる
;-------------------------------------------------
@COM2103(ARG)
VARSET LOCAL

PRINTL 
PRINTFORML %"=" * 107%
PRINTFORML 后背位Ａさせる(%CALLNAME:ARG%,%CALLNAME:(TCVAR:ARG:行為対象)%)

;道具のみ反抗時はCFLAG立て
IF TEQUIP:ARG:アナル == 77
	CFLAG:ARG:ＡオンリーＰ = 1
ENDIF

TEQUIP:ARG:アナル = 0
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、气力
DOWNBASE:ARG:体力 += 40
DOWNBASE:ARG:气力 += 80
DOWNBASE:(TCVAR:ARG:行為対象):体力 += 80
DOWNBASE:(TCVAR:ARG:行為対象):气力 += 120

SOURCE:(TCVAR:ARG:行為対象):性行動 = 120
SOURCE:(TCVAR:ARG:行為対象):快Ｃ = 300

SOURCE:ARG:快Ａ = 400
SOURCE:ARG:情愛 = 100
SOURCE:ARG:痛苦 = 900
SOURCE:ARG:露出 = 80
;SIF CFLAG:193
;	SOURCE:露出 = 300
SOURCE:ARG:逸脱 = 200
SOURCE:(TCVAR:ARG:行為対象):反感 = 500

SOURCE:(TCVAR:ARG:行為対象):不潔 = STATIN_POINT(ARG,1) * (2 - TALENT:(TCVAR:ARG:行為対象):汚臭耐性) * 5 + 60
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;压抑、反抗持ちには、抑郁ソース追加
SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:反抗
	SOURCE:ARG:鬱屈 += 500

;Aへの痛苦は先に処理しておく
CALL PAIN_CHECK_A(ARG)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:(TCVAR:ARG:行為対象):2 |= STAIN:ARG:4
STAIN:(TCVAR:ARG:行為対象):2 |= STAIN:ARG:7
STAIN:ARG:4 |= STAIN:(TCVAR:ARG:行為対象):2
STAIN:ARG:7 |= STAIN:(TCVAR:ARG:行為対象):2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;;;EXP:ARG:Ａ经验 ++
EXP:ARG:Ａ性交经验 ++
EXP:(TCVAR:ARG:行為対象):Ｃ插入经验 ++

CALL HETEROSEX_EXP(TCVAR:ARG:行為対象,ARG,4)
TCVAR:(TCVAR:ARG:行為対象):挿入 = 2
CFLAG:ARG:待機時間 += 2


VARSET ARG
RETURN 1

@CAN_COM2103(ARG,ARG:1)
#FUNCTION
;;;VARSET LOCAL

;绳子で縛られている場合は無視（绳子が無い時のみ実行判定を行う）
SIF TEQUIP:(ARG:1):绳子 == 1
	RETURNF 1

A = 0
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
A = COM_ORDER(ARG,ARG:1)

;ABL:欲望
IF ABL:(ARG:1):欲望
	A += ABL:(ARG:1):欲望 * 3
ENDIF
;ABL:Ａ感觉
IF ABL:(ARG:1):Ａ感觉
	A += ABL:(ARG:1):Ａ感觉 * 2
ENDIF
;快乐刻印
IF MARK:(ARG:1):快乐刻印
	A += MARK:(ARG:1):快乐刻印 * 3
ENDIF
;PALAM:润滑（の不足）
IF PALAM:(ARG:1):润滑 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:情欲
IF GETPALAMLV(PALAM:(ARG:1):情欲,5)
	A += GETPALAMLV(PALAM:(ARG:1):情欲,5) * 3
ENDIF
;害羞
IF TALENT:(ARG:1):羞恥心 > 0
	A -= 2
ENDIF
;否定快感
IF TALENT:(ARG:1):快感応答 < 0 && !TALENT:(ARG:1):淫乱
	A -= 5
ENDIF
;讨厌男人（恋慕、相亲相爱で無効）
IF TALENT:(ARG:1):性別嗜好 & 1 && !(TALENT:ARG:2 & 1) && !LOVE_CHECK(ARG:1,ARG)
	A -= 12
ENDIF
;淫乱
IF TALENT:(ARG:1):淫乱
	A += 15
ENDIF
;A経験
IF EXP:(ARG:1):Ａ经验 < EXPLV:2
	A -= 30
ENDIF
;調教者が扶她
IF (TALENT:ARG:2 & 3) == 3
	A += 4
ENDIF

;春药
IF TEQUIP:(ARG:1):春药
	A += 6
ENDIF

;汚れあり
IF STATIN_POINT(ARG,2)
	A -= STATIN_POINT(ARG,2) * (2 - TALENT:(ARG:1):汚臭耐性) / 2
ENDIF


;実行できない
SIF A < 32
	RETURNF 0
RETURNF 1
