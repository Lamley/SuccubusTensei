@COM_ABLE1000(ARG)
SIF TCVAR:MASTER:イベント && !TCVAR:MASTER:同行
	RETURN 0
SIF CFLAG:MASTER:乗車
	RETURN 0
RETURN GLOBAL_COMABLE(1000)



;-------------------------------------------------
;移動
;-------------------------------------------------
@COM1000(ARG)
VARSET LOCAL
IF TCVAR:MASTER:目的位置
	IF TCVAR:MASTER:目的位置 == CFLAG:MASTER:現在位置
		;NEXTCOM = -1
		TCVAR:MASTER:目的位置 = 0
		IF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),1)
			SKIPDISP 0
			PRINTFORML %PLACE_NAME(CFLAG:MASTER:現在地区,CFLAG:MASTER:現在位置)%进去么？
			PRINTL [0] - 是
			PRINTL [1] - 否
			INPUT
			SIF RESULT == 0
				CALL IN_ROOM
			SKIPDISP 1
		ELSEIF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),0)
			SKIPDISP 0
			PRINTFORML 从%PLACE_NAME(CFLAG:MASTER:現在地区,CFLAG:MASTER:現在位置)%出去么？
			PRINTL [0] - 是
			PRINTL [1] - 否
			INPUT
			SIF RESULT == 0
				CALL OUT_ROOM
			SKIPDISP 1
		ENDIF
		RETURN 0
	ENDIF
	;CALL CHARA_MOVEMENT
	CALLFORM FARMOVE_{CFLAG:MASTER:現在地区}(MASTER,TCVAR:MASTER:目的位置)
	LOCAL:2 = RESULT
	IF GETBIT(DA:(CFLAG:MASTER:現在地区):(LOCAL:2),9) && !(CFLAG:MASTER:鍵)
		PRINTW 鍵がかかっている
		;NEXTCOM = -1
		TCVAR:MASTER:目的位置 = 0
		TFLAG:移動口上制御 = 0
		RETURN 0
	ELSE
		CALLFORM CAN_MOVE_{CFLAG:MASTER:現在地区}(CFLAG:MASTER:現在位置,LOCAL:2)
		CFLAG:MASTER:待機時間 = RESULT
		CFLAG:MASTER:現在位置 = LOCAL:2
		;CALL EVENT_CHECK
		;NEXTCOM = 1000
		RETURN 1
	ENDIF
ENDIF

LOCAL:4 = 0
CALLFORM GET_MAP_{CFLAG:MASTER:現在地区}
FOR LOCAL,0,MAX_ROOM()
CALLFORM CAN_MOVE_{CFLAG:MASTER:現在地区}(CFLAG:MASTER:現在位置,LOCAL)
	IF RESULT
		LOCAL:4++
		PRINTFORMLC [{LOCAL,3}] - %PLACE_NAME(CFLAG:MASTER:現在地区,LOCAL)%
		SIF LOCAL:4 % 2 == 0
			PRINTL 
	ENDIF
NEXT
PRINTL 
SIF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),0)
	PRINTL [800] - 外出
SIF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),1)
	PRINTL [801] - 进入
SIF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),2)
	PRINTL [802] - 乘电车
SIF (CFLAG:MASTER:現在地区 == 0 && CFLAG:MASTER:現在位置 == 1) && (TIME >= 360 && TIME < 1260) && MONEY:MASTER >= 200
	PRINTL [803] - 直接去教室
SIF CFLAG:MASTER:現在地区 != 0 && MONEY:MASTER >= 200
	PRINTL [804] - 直接回家
SIF CFLAG:MASTER:現在地区 == 1
	PRINTL [806] - 出学校
SIF CFLAG:MASTER:現在地区 != 2
	PRINTL [807] - 去繁华街游玩
SIF CFLAG:1:現在地区 == 0 && TIME < 1260 && CLUB_PLACE(CFLAG:1:部活) && MONEY:MASTER >= 200
	PRINTL [808] - 直接去社团く
SIF CFLAG:1:現在地区 == 0 && CFLAG:1:所属 && (TIME >= 360 && TIME < 1260) && MONEY:MASTER >= 200
	PRINTL [809] - 去格斗场





PRINTL [900] - 戻る
$INPUT_LOOP



INPUT
LOCAL:1 = RESULT
SIF RESULT == 900
	RETURN 0
IF RESULT == 800 && GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),0)
	CALL OUT_ROOM
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 801 && GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),1)
	CALL IN_ROOM
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 802 && GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),2)
	CALL TAKE_TRAIN
	M:101 = 0
	IF RESULT
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ELSEIF RESULT == 803
	MONEY:MASTER -= 200
	CFLAG:MASTER:現在地区 = 1
	CFLAG:MASTER:現在位置 = 2
	CFLAG:MASTER:待機時間 = 50
	IF !(D:101)
		PRINTFORML 校门口で%CALLNAME:(40 + CHARANUM -41)%打招呼了
		PRINTL 「早上好！」
	ENDIF
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 804
	MONEY:MASTER -= 200
	CFLAG:MASTER:現在地区 = 0
	CFLAG:MASTER:現在位置 = 1
	CFLAG:MASTER:待機時間 = 50
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 806
	CFLAG:MASTER:現在地区 = 2
	CFLAG:MASTER:現在位置 = 1
	CFLAG:MASTER:待機時間 = 10
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 807
	IF MONEY:MASTER < 200 && CFLAG:MASTER:現在地区 == 0
		PRINTL お金が足りません
		GOTO INPUT_LOOP
	ENDIF
	SIF CFLAG:MASTER:現在地区 == 0
		MONEY:MASTER -= 200
	CFLAG:MASTER:現在地区 = 2
	CFLAG:MASTER:現在位置 = 4
	CFLAG:MASTER:待機時間 = 45
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 808
	MONEY:MASTER -= 200
	CFLAG:MASTER:現在地区 = 1
	CFLAG:MASTER:現在位置 = CLUB_PLACE(CFLAG:MASTER:部活)
	CFLAG:MASTER:待機時間 = 50
	M:101 = 0
	RETURN 1
ELSEIF RESULT == 809
	MONEY:MASTER -= 200
	CFLAG:MASTER:現在地区 = 2
	CFLAG:MASTER:現在位置 = 28
	CFLAG:MASTER:待機時間 = 50
	M:101 = 0
	RETURN 1
ELSEIF CFLAG:MASTER:現在位置 == RESULT || PLACE_NAME(CFLAG:MASTER:現在地区,RESULT) == ""
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALLFORM CAN_MOVE_{CFLAG:MASTER:現在地区}(CFLAG:MASTER:現在位置,LOCAL:1)
IF RESULT
	LOCAL:2 = RESULT
	IF GETBIT(DA:(CFLAG:MASTER:現在地区):(LOCAL:1),9) && !(CFLAG:MASTER:鍵)
		PRINTW 鍵がかかっている
		RESTART
	ELSE
		CFLAG:MASTER:現在位置 = LOCAL:1
		CFLAG:MASTER:待機時間 = LOCAL:2
		IF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),1)
			PRINTFORML 从%PLACE_NAME(CFLAG:MASTER:現在地区,CFLAG:MASTER:現在位置)%进去么？
			PRINTL [0] - 是
			PRINTL [1] - 否
			INPUT
			SIF RESULT == 0
				CALL IN_ROOM
		ELSEIF GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),0)
			PRINTFORML 从%PLACE_NAME(CFLAG:MASTER:現在地区,CFLAG:MASTER:現在位置)%から出来么？
			PRINTL [0] - 是
			PRINTL [1] - 否
			INPUT
			SIF RESULT == 0
				CALL OUT_ROOM
		ENDIF

		RETURN 1
	ENDIF

ELSEIF LOCAL:1 > 0 && LOCAL:1<= MAX_ROOM()
	TCVAR:MASTER:目的位置 = LOCAL:1
	CALLFORM FARMOVE_{CFLAG:MASTER:現在地区}(MASTER,LOCAL:1)
	LOCAL:3 = RESULT
	CALLFORM CAN_MOVE_{CFLAG:MASTER:現在地区}(CFLAG:MASTER:現在位置,LOCAL:3)
	CFLAG:MASTER:待機時間 = RESULT
	CFLAG:MASTER:現在位置 = LOCAL:3
	;NEXTCOM = 1000
	M:101 = 0
	RETURN 1
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
M:101 = 0

RETURN 1

