@COM_ABLE1006(ARG)
;買い物の処理。どこで何を買えるか。買える場所を増やすにはFIELDも弄る。
SIF !GETBIT(DA:(CFLAG:MASTER:現在地区):(CFLAG:MASTER:現在位置),7)
	RETURN 0
RETURN GLOBAL_COMABLE(1006)

@COM1006(ARG)
PRINTL 
PRINTFORML %"=" * 107%
;買い物
VARSET LOCAL
$INPUT_LOOP3
DRAWLINE
PRINTFORML 买什么？(所持金:\\{MONEY:ARG})
FOR LOCAL , 1, 21
	SIF SHOP_HANDLING_CATEGORY(LOCAL)
		PRINTFORML [{LOCAL,3}] - %ITEM_CATEGORY_NAME(LOCAL)%
NEXT
PRINTL 
PRINTFORML [999] - \@LOCAL:4?#何も買わずに\@店を出る
$INPUT_LOOP
INPUT

IF RESULT == 999
	IF LOCAL:4
		CFLAG:MASTER:待機時間 = LOCAL:4 * 2
		VARSET LOCAL
		RETURN 1
	ELSE
		VARSET LOCAL
		RETURN 0
	ENDIF
ELSEIF RESULT > 0 && RESULT < 21 && SHOP_HANDLING_CATEGORY(RESULT)
	LOCAL:1 = RESULT
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

DRAWLINE
PRINTFORML 何を買いますか？(所持金:\\{MONEY:ARG})
LOCAL:2 = 0
FOR LOCAL ,0,1000
	IF ITEM_EXIST(LOCAL:1,LOCAL) && ITEM_HANDLING_CHECK(LOCAL)
		LOCAL:5 = SUGGESTIVE_POINT(LOCAL)
		SIF LOCAL >= 620 && LOCAL < 820
			LOCAL:5 *= 2
		PRINTFORM [{LOCAL,3}] - %ITEMNAME:LOCAL,20,LEFT%\\{ITEMPRICE:LOCAL,6}
		SIF (LOCAL < 150 || LOCAL >= 200) && LOCAL < 901
			PRINTFORM  (刺激度:{LOCAL:5,3})   
		LOCAL:2++
		SIF LOCAL:2 % 2 == 0
			PRINTL 
	ENDIF
NEXT
PRINTL 
PRINTL [9999] - 戻る
$INPUT_LOOP2
INPUT

IF RESULT == 9999
	GOTO INPUT_LOOP3
ELSEIF RESULT > 0 && RESULT < 1000 && ITEM_EXIST(LOCAL:1,RESULT) && ITEM_HANDLING_CHECK(RESULT)
	LOCAL:3 = RESULT
	IF LOCAL:3 == 181 || LOCAL:3 == 182 || LOCAL:3 == 183
		IF EQUIP:MASTER:美味的便当 || EQUIP:MASTER:普通的便当 || EQUIP:MASTER:可怕的便当
			PRINTW これ以上は食べられない
			CLEARLINE 2
			GOTO INPUT_LOOP2
		ELSE
			PRINTFORMW %ITEMNAME:(LOCAL:3)%を買いました
			LOCAL:4++
			EQUIP:MASTER:(LOCAL:3)++
			MONEY:ARG -= ITEMPRICE:(LOCAL:3)
			GOTO INPUT_LOOP3
		ENDIF
	ELSEIF LOCAL:3 >= 150 && LOCAL:3 < 200
		PRINTFORML いくつ買いますか？(1～{MONEY:ARG / ITEMPRICE:(LOCAL:3)} 0はキャンセル)
		$INPUT_LOOP4
		INPUT
		IF RESULT == 0
			GOTO INPUT_LOOP3
		ELSEIF RESULT > 0 && ITEMPRICE:(LOCAL:3) * RESULT <= MONEY:ARG
			PRINTFORMW %ITEMNAME:(LOCAL:3)%を{RESULT}個買いました
			LOCAL:4++
			MONEY:ARG -= ITEMPRICE:(LOCAL:3) * RESULT
			EQUIP:ARG:(LOCAL:3) += RESULT
			GOTO INPUT_LOOP3
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP4
		ENDIF
	ELSE
		IF MONEY:ARG < ITEMPRICE:(LOCAL:3)
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP2
		ELSE
			PRINTFORMW %ITEMNAME:(LOCAL:3)%を買いました
			LOCAL:4++
			EQUIP:MASTER:(LOCAL:3)++
			MONEY:ARG -= ITEMPRICE:(LOCAL:3)
			GOTO INPUT_LOOP3
		ENDIF
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP2
ENDIF


RETURN 1

@ITEM_EXIST(ARG,ARG:1)
#FUNCTION
;ARG アイテムの種類 1,調教アイテム 2,薬品類 3消耗品(その他) 4上半身上衣 5下半身上衣 6上半身下着
;7下半身下着 8帽子 9靴 10袜子 11眼鏡 12頭 13耳 14首 15腕 16指 17その他 18上下一体型上衣 19上下一体型下着 20家電品
;ARG:1 アイテム番号

SIF ITEMNAME:(ARG:1) == "" || (EQUIP:MASTER:(ARG:1) && !(ARG:1 >= 150 && ARG:1 < 200))
	RETURNF 0
SELECTCASE ARG
	CASE 1
		SIF ARG:1 >= 1 && ARG:1 < 150
			RETURNF 1
	CASE 2
		SIF ARG:1 >= 150 && ARG:1 < 175
			RETURNF 1
	CASE 3
		SIF ARG:1 >= 175 && ARG:1 < 200
			RETURNF 1
	CASE 4
		SIF ARG:1 >= 200 && ARG:1 < 250
			RETURNF 1
	CASE 5
		SIF ARG:1 >= 250 && ARG:1 < 300
			RETURNF 1
	CASE 6
		SIF ARG:1 >= 300 && ARG:1 < 350
			RETURNF 1
	CASE 7
		SIF ARG:1 >= 350 && ARG:1 < 400
			RETURNF 1
	CASE 8
		SIF ARG:1 >= 400 && ARG:1 < 420
			RETURNF 1
	CASE 9
		SIF ARG:1 >= 420 && ARG:1 < 440
			RETURNF 1
	CASE 10
		SIF ARG:1 >= 440 && ARG:1 < 460
			RETURNF 1
	CASE 11
		SIF ARG:1 >= 460 && ARG:1 < 480
			RETURNF 1
	CASE 12
		SIF ARG:1 >= 480 && ARG:1 < 500
			RETURNF 1
	CASE 13
		SIF ARG:1 >= 500 && ARG:1 < 520
			RETURNF 1
	CASE 14
		SIF ARG:1 >= 520 && ARG:1 < 540
			RETURNF 1
	CASE 15
		SIF ARG:1 >= 540 && ARG:1 < 560
			RETURNF 1
	CASE 16
		SIF ARG:1 >= 560 && ARG:1 < 580
			RETURNF 1
	CASE 17
		SIF ARG:1 >= 580 && ARG:1 < 620
			RETURNF 1
	CASE 18
		SIF ARG:1 >= 620 && ARG:1 < 720
			RETURNF 1
	CASE 19
		SIF ARG:1 >= 720 && ARG:1 < 820
			RETURNF 1
	CASE 20
		SIF ARG:1 >= 901 && ARG:1 < 1000
			RETURNF 1
ENDSELECT

RETURNF 0
@SHOP_CATEGORY
#FUNCTION
VARSET LOCAL
;薬屋
SIF (CFLAG:MASTER:現在地区 == 0 && CFLAG:MASTER:現在位置 == 8) || (CFLAG:MASTER:現在地区 == 1 && CFLAG:MASTER:現在位置 == 7) ||(CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 10)
	SETBIT LOCAL,0
;服屋
SIF (CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 9)
	SETBIT LOCAL,1
;装饰品店铺
SIF (CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 21)
	SETBIT LOCAL,2
;成人商店
SIF (CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 20)
	SETBIT LOCAL,3
;家電屋
SIF CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 24
	SETBIT LOCAL,4
;小卖部
SIF CFLAG:MASTER:現在地区 == 1 && CFLAG:MASTER:現在位置 == 26
	SETBIT LOCAL,5
;书店
SIF CFLAG:MASTER:現在地区 == 0 && CFLAG:MASTER:現在位置 == 16 || (CFLAG:MASTER:現在地区 == 2 && CFLAG:MASTER:現在位置 == 15)
	SETBIT LOCAL,6

RETURNF LOCAL


@SHOP_HANDLING_CATEGORY(ARG)
;店の種類ごとにどのカテゴリーの商品を売るか
#FUNCTION
SELECTCASE ARG
	CASE 1
		SIF GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 2
		SIF GETBIT(SHOP_CATEGORY(),0) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 3
		SIF GETBIT(SHOP_CATEGORY(),3) || GETBIT(SHOP_CATEGORY(),4) || GETBIT(SHOP_CATEGORY(),5) || GETBIT(SHOP_CATEGORY(),6)
			RETURNF 1
	CASE 4
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),5)
			RETURNF 1
	CASE 5
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),5)
			RETURNF 1
	CASE 6
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 7
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 8
		SIF GETBIT(SHOP_CATEGORY(),1)
			RETURNF 1
	CASE 9
		SIF GETBIT(SHOP_CATEGORY(),1)
			RETURNF 1
	CASE 10
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 11
		SIF GETBIT(SHOP_CATEGORY(),2)
			RETURNF 1
	CASE 12
		SIF GETBIT(SHOP_CATEGORY(),2)
			RETURNF 1
	CASE 13
		SIF GETBIT(SHOP_CATEGORY(),2)
			RETURNF 1
	CASE 14
		SIF GETBIT(SHOP_CATEGORY(),2) || GETBIT(SHOP_CATEGORY(),3) || GETBIT(SHOP_CATEGORY(),1)
			RETURNF 1
	CASE 15
		SIF GETBIT(SHOP_CATEGORY(),2) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 16
		SIF GETBIT(SHOP_CATEGORY(),2)
			RETURNF 1
	CASE 17
		SIF GETBIT(SHOP_CATEGORY(),3) || GETBIT(SHOP_CATEGORY(),1)
			RETURNF 1
	CASE 18
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),5)
			RETURNF 1
	CASE 19
		SIF GETBIT(SHOP_CATEGORY(),1) || GETBIT(SHOP_CATEGORY(),3)
			RETURNF 1
	CASE 20
		SIF GETBIT(SHOP_CATEGORY(),4)
			RETURNF 1

ENDSELECT
RETURNF 0



@ITEM_CATEGORY_NAME(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "調教アイテム"
	CASE 2
		RETURNF "薬品"
	CASE 3
		RETURNF "消耗品"
	CASE 4
		RETURNF "上半身上衣"
	CASE 5
		RETURNF "下半身上衣"
	CASE 6
		RETURNF "上半身下着"
	CASE 7
		RETURNF "下半身下着"
	CASE 8
		RETURNF "帽子"
	CASE 9
		RETURNF "靴"
	CASE 10
		RETURNF "袜子"
	CASE 11
		RETURNF "眼鏡"
	CASE 12
		RETURNF "髪飾り"
	CASE 13
		RETURNF "耳飾り"
	CASE 14
		RETURNF "首飾り"
	CASE 15
		RETURNF "腕輪"
	CASE 16
		RETURNF "指輪"
	CASE 17
		RETURNF "その他"
	CASE 18
		RETURNF "上下一体型上衣"
	CASE 19
		RETURNF "上下一体型下着"
	CASE 20
		RETURNF "家電品"
ENDSELECT


@ITEM_HANDLING_CHECK(ARG)
#FUNCTION
;SETBIT LOCAL 0薬屋取り扱い 1服屋取り扱い 2装饰品店铺取り扱い 3成人商店取り扱い 4家電屋取り扱い 5小卖部取り扱い 6书店取り扱い

VARSET LOCAL
SELECTCASE ARG
	CASE 1 TO 149,150,175,320,322,365,370,371,372,373,374,450,524,528,529,530,542,585,722,730,731,732
		SETBIT LOCAL,3
	CASE 150 TO 174
		SETBIT LOCAL,0
	CASE 460 TO 530, 532 TO 579
		SETBIT LOCAL,2
	CASE 176,901,902,903
		SETBIT LOCAL,4
	CASE 182,200,201,219,240,250,284,290,638,639,641
		SETBIT LOCAL,5
	CASE 190 TO 195
		SETBIT LOCAL,6
	CASEELSE
		SETBIT LOCAL,1
ENDSELECT

IF LOCAL == SHOP_CATEGORY()
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

