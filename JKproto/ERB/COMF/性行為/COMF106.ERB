@COM_ABLE106(ARG)
VARSET LOCAL
LOCAL = ARG
VARSET ARG
SIF !TCVAR:LOCAL:行為対象
	RETURN 0
SIF !GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1)
	RETURN 0
SIF !CAN_COM106(TCVAR:LOCAL:行為対象,LOCAL)
	RETURN 0
SIF J:115
	RETURN 1
SIF TEQUIP:(TCVAR:LOCAL:行為対象):下半身下着 == 373
	RETURN 0
;道具男性器反抗時は取り敢えずスルー
SIF TEQUIP:LOCAL:アナル == 79
	RETURN 0
;男性器反抗時は取り敢えずスルー
SIF TEQUIP:LOCAL:アナル == 78
	RETURN 0
;男性器のみ反抗時CFLAGは取り敢えずスルー
SIF CFLAG:LOCAL:ＡオンリーＴ == 1
	RETURN 0
;対象者が男性器で名器装着時は不可
SIF (GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1)) && (TEQUIP:(TCVAR:LOCAL:行為対象):クリトリス == 80)
	RETURN 0
;行為者＆対象者が绳子装着時は不可（３Ｐを考え付けておく）
SIF (TEQUIP:LOCAL:绳子) && (TEQUIP:(TCVAR:LOCAL:行為対象):绳子)
	RETURN 0
;対象者が男性器なし
SIF !GETBIT(TALENT:(TCVAR:LOCAL:行為対象):2,1)
	RETURN 0

RETURN GLOBAL_COMABLE(106)
;-------------------------------------------------
;骑乘位Ａさせる
;-------------------------------------------------
@COM106(ARG)
VARSET LOCAL
PRINTL 
PRINTFORML %"=" * 107%
PRINTFORML 骑乘位Ａさせる(%CALLNAME:ARG%)

;道具のみ反抗時はCFLAG立て
IF TEQUIP:ARG:アナル == 77
	CFLAG:ARG:ＡオンリーＰ = 1
ENDIF

TEQUIP:ARG:アナル = 0
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、气力
DOWNBASE:ARG:体力 += 200
DOWNBASE:ARG:气力 += 80
DOWNBASE:(TCVAR:ARG:行為対象):体力 += 40
DOWNBASE:(TCVAR:ARG:行為対象):气力 += 100

SOURCE:(TCVAR:ARG:行為対象):性行動 = 150
SOURCE:(TCVAR:ARG:行為対象):快Ｃ = 400

SOURCE:ARG:快Ａ = 500
SOURCE:ARG:情愛 = 250
SOURCE:ARG:性行動 = 250
SOURCE:ARG:達成 = 250
SOURCE:ARG:痛苦 = 800
SOURCE:ARG:露出 = 120
SOURCE:ARG:逸脱 = 700

SOURCE:(TCVAR:ARG:行為対象):反感 = 900

SOURCE:(TCVAR:ARG:行為対象):不潔 = STATIN_POINT(ARG,1) * (2 - TALENT:(TCVAR:ARG:行為対象):汚臭耐性) * 5 + 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;压抑、反抗持ちには、抑郁ソース追加
SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:反抗
	SOURCE:ARG:鬱屈 += 900

;Aへの痛苦は先に処理しておく
CALL PAIN_CHECK_A(ARG)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:(TCVAR:ARG:行為対象):2 |= STAIN:ARG:4
STAIN:(TCVAR:ARG:行為対象):2 |= STAIN:ARG:7
STAIN:ARG:4 |= STAIN:(TCVAR:ARG:行為対象):2
STAIN:ARG:7 |= STAIN:(TCVAR:ARG:行為対象):2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;A経験
;;;EXP:ARG:Ａ经验 ++

;性交经验
EXP:ARG:Ａ性交经验 ++
;骑乘位の時は相手腰使い経験無し
;;;EXP:(TCVAR:ARG:行為対象):Ｃ插入经验 ++

CALL HETEROSEX_EXP(TCVAR:ARG:行為対象,ARG,4)
TCVAR:(TCVAR:ARG:行為対象):挿入 = 2
CFLAG:ARG:待機時間 += 2


VARSET ARG
RETURN 1

@CAN_COM106(ARG,ARG:1)
#FUNCTION
;;;VARSET LOCAL

;绳子で縛られている場合は無視（绳子が無い時のみ実行判定を行う）
SIF TEQUIP:(ARG:1):绳子 == 1
	RETURNF 0

A = 0
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
A = COM_ORDER(ARG,ARG:1)

;ABL:欲望
IF ABL:(ARG:1):欲望
	A += ABL:(ARG:1):欲望 * 3
ENDIF
;ABL:Ａ感觉
IF ABL:(ARG:1):Ａ感觉
	A += ABL:(ARG:1):Ａ感觉 * 2
ENDIF
;ABL:侍奉精神
IF ABL:(ARG:1):侍奉精神
	A += ABL:(ARG:1):侍奉精神 * 4
ENDIF

;快乐刻印
IF MARK:(ARG:1):快乐刻印
	A += MARK:(ARG:1):快乐刻印 * 3
ENDIF
;PALAM:润滑（の不足）
IF PALAM:(ARG:1):润滑 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:情欲
IF GETPALAMLV(PALAM:(ARG:1):情欲,5)
	A += GETPALAMLV(PALAM:(ARG:1):情欲,5) * 3
ENDIF
;害羞
IF TALENT:(ARG:1):羞恥心 > 0
	A -= 2
ENDIF
;否定快感
IF TALENT:(ARG:1):快感応答 < 0 && !TALENT:(ARG:1):淫乱
	A -= 5
ENDIF
;讨厌男人（恋慕、相亲相爱で無効）
IF TALENT:(ARG:1):性別嗜好 & 1 && !(TALENT:ARG:2 & 1) && !LOVE_CHECK(ARG:1,ARG)
	A -= 12
ENDIF
;淫乱
IF TALENT:(ARG:1):淫乱
	A += 15
ENDIF
;A経験
IF EXP:(ARG:1):Ａ经验 < EXPLV:2
	A -= 30
ENDIF
;調教者が扶她
IF (TALENT:ARG:2 & 3) == 3
	A += 4
ENDIF

;春药
IF TEQUIP:(ARG:1):春药
	A += 6
ENDIF

;汚れあり
IF STATIN_POINT(ARG,2)
	A -= STATIN_POINT(ARG,2) * (2 - TALENT:(ARG:1):汚臭耐性) / 2
ENDIF


;実行できない
SIF A < 45
	RETURNF 0
RETURNF 1


