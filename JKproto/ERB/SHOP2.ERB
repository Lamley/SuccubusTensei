@LIFE_LIST(ARG)
DRAWLINE
FOR LOCAL,1, CHARANUM
	;SIF !CFLAG:LOCAL:存在フラグ
	;	CONTINUE
	PRINTFORM \@LOCAL == MASTER?★#　\@[{LOCAL,2}]
	PRINTFORM %CALLNAME:LOCAL,16,LEFT%

	SELECTCASE FLAG:1003
		;基礎表示選択
		CASE 2
			PRINTFORM \@(FLAG:1004) ? 气力#体力\@
			A = BASE:LOCAL:(FLAG:1004)
			SIF BASE:LOCAL:(FLAG:1004) < 0
				A = 0
			BAR A,MAXBASE:LOCAL:(FLAG:1004),10
			PRINTFORM ({A}/{MAXBASE:LOCAL:(FLAG:1004)})

		;能力表示選択or性技表示選択
		CASE 3, 4
			IF FLAG:1003 == 4 && !(TALENT:LOCAL:2 & 1) && (FLAG:1004 == 52 || FLAG:1004 == 54)
				PRINTFORM %ABLNAME:(FLAG:1004),12%:* 
			ELSE
				PRINTFORM %ABLNAME:(FLAG:1004),12%:{ABL:LOCAL:(FLAG:1004),5,LEFT} 
			ENDIF
		;刻印表示選択
		CASE 5
			PRINTFORM %MARKNAME:(FLAG:1004),12%:LV{MARK:LOCAL:(FLAG:1004),5,LEFT} 
		;経験表示選択
		CASE 6
			IF (!(TALENT:LOCAL:2 & 1) && FLAG:1004 == 51) || (TALENT:LOCAL:2 & 1 && FLAG:1004 == 52)
				PRINTFORM %EXPNAME:(FLAG:1004),12%:* 
			ELSE
				PRINTFORM %EXPNAME:(FLAG:1004),12%:{EXP:LOCAL:(FLAG:1004),5,LEFT} 
			ENDIF
		;宝珠表示選択
		CASE 7
			LOCALS = {JUEL:LOCAL:(FLAG:1004)}
			SIF JUEL:LOCAL:(FLAG:1004) > 1000000
				LOCALS = {JUEL:LOCAL:(FLAG:1004) / 1000}k
			SIF JUEL:LOCAL:(FLAG:1004) > 1000000000
				LOCALS = {JUEL:LOCAL:(FLAG:1004) / 1000000}m
			PRINTFORM %PALAMNAME:(FLAG:1004),6%の珠:%LOCALS,7,LEFT%
		;その他表示選択
		CASE 8
			SELECTCASE FLAG:1004
				;既成事実
				CASE 1
					VARSET LOCALS
					SIF GETBIT(CFLAG:LOCAL:(FLAG:1004),0) 
						LOCALS += "告白成功　"
					SIF GETBIT(CFLAG:LOCAL:(FLAG:1004),1) 
						LOCALS += "うふふ経験あり　"
					PRINTFORM %LOCALS%
				;好感度
				CASE 2
					PRINTFORM 好感度:{CFLAG:LOCAL:(FLAG:1004),5,LEFT} 
				;弱势
				CASE 10
					VARSET LOCALS
					SIF GETBIT(CFLAG:LOCAL:(FLAG:1004),0) 
						LOCALS += "ぱんつを没収された　"
					SIF GETBIT(CFLAG:LOCAL:(FLAG:1004),1) 
						LOCALS += "情事を見られた　"
					PRINTFORM %LOCALS%
			ENDSELECT
	ENDSELECT

	PRINTL 
NEXT
PRINTL 

;ステータス表示
@SHOW_CHARADATA
R = 0
CALL LIFE_LIST
;PRINTLC  [2000]基礎表示切替
;PRINTLC  [3000]能力表示切替
;PRINTLC  [4000]性技表示切替
;PRINTLC  [5000]刻印表示切替
;PRINTLC  [6000]経験表示切替
;PRINTLC  [7000]宝珠表示切替
;PRINTLC  [8000]その他表示切替
PRINTFORML  [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 2000 || RESULT == 3000 || RESULT == 4000 || RESULT == 5000 || RESULT == 6000 || RESULT == 7000 || RESULT == 8000
	CALL SHOW_CHARADATA2, RESULT
	RESTART
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF
CALL PRINT_STATE(RESULT)
WAIT
RESTART

@SHOW_CHARADATA2, ARG
VARSET LOCAL, 0
LOCAL:999 = ARG/1000
DRAWLINE
DRAWLINE

SELECTCASE LOCAL:999
	;基礎表示選択
	CASE 2
		PRINTLC  [   0] 体力
		PRINTLC  [   1] 气力
		LOCAL:0 = 1
		LOCAL:1 = 1
		LOCAL:998 = 1
	;能力表示選択(性技は別）
	CASE 3
		FOR LOCAL:998,0,40
			SIF !STRLENS(ABLNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
	;性技表示選択
	CASE 4
		FOR LOCAL:998,50,60
			SIF !STRLENS(ABLNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
	;刻印表示選択
	CASE 5
		FOR LOCAL:998,0,5
			SIF !STRLENS(MARKNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %MARKNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
	;経験表示選択
	CASE 6
		FOR LOCAL:998,0,100
			SIF !STRLENS(EXPNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %EXPNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
	;宝珠表示選択
	CASE 7
		FOR LOCAL:998,0,22
			SIF !STRLENS(PALAMNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %PALAMNAME:(LOCAL:998),6%の珠
			LOCAL:(LOCAL:998) = 1
		NEXT
		PRINTLC  [ 100] 否定の珠
		LOCAL:100 = 1
		LOCAL:998 = 100
	;その他表示選択
	CASE 8
		PRINTLC  [   1]既成事実
		PRINTLC  [   2]好感度
		PRINTLC  [  10]弱势
		LOCAL:1 = 1
		LOCAL:2 = 1
		LOCAL:10 = 1
		LOCAL:998 = 10
ENDSELECT
PRINTL  [1000] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT > LOCAL:998 || LOCAL:RESULT <= 0
	GOTO INPUT_LOOP
ENDIF
FLAG:1003 = LOCAL:999
FLAG:1004 = RESULT

