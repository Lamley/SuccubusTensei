@CHANGE_CLOTHES(ARG,ARG:1)
VARSET LOCAL
CALL SUGGESTIVE_CHECK(ARG)
LOCAL = LIMIT(BASE:ARG:刺激度 / 100,0,10)
PRINTFORM 刺激度:
SIF BASE:ARG:刺激度 >= 600
	SETCOLOR 255,51,153
SIF BASE:ARG:刺激度 >= 900
	SETCOLOR 255,0,0
PRINTFORML %UNICODE(0x2665) * LOCAL%
RESETCOLOR
PRINTFORML 内部刺激度:{BASE:ARG:内部刺激度}、刺激度:{BASE:ARG:刺激度}、容姿：{BASE:ARG:容姿}
PRINTL 
FOR LOCAL,200,218
	SIF LOCAL == 216 && !GETBIT(TALENT:ARG:2,0)
		CONTINUE
	LOCALS = %TEQUIPNAME:LOCAL%
	SIF LOCAL == 215 && GETBIT(TALENT:ARG:2,1)
		LOCALS = ペニス
	PRINTFORML [{LOCAL}] - %LOCALS%:%EQUIPNAME:(TEQUIP:ARG:LOCAL)%
NEXT
PRINTL 
PRINTL [900] - 今の服装を登録する
PRINTL [901] - 登録してある服装に着替える
PRINTL [999] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 999
	IF BASE:ARG:刺激度 >= 500 + ABL:ARG:露出癖 * 100
		PRINTW この格好では恥ずかしい･･････
		RESTART
	ENDIF
	RETURN 0
ELSEIF RESULT == 900
	CALL CLOTHES_REGISTRATION(ARG)
	RESTART
ELSEIF RESULT == 901
	CALL CHANGE_CLOTHES_REGISTRATION(ARG,ARG:1)
	RESTART
ELSEIF RESULT < 200 || RESULT > 217 || (RESULT == 216 && !GETBIT(TALENT:ARG:2,0))
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSE
	LOCAL:1 = RESULT
	SELECTCASE RESULT
		CASE 200
			LOCAL:2 = 200
			LOCAL:3 = 249
		CASE 201
			LOCAL:2 = 250
			LOCAL:3 = 299
		CASE 202
			LOCAL:2 = 300
			LOCAL:3 = 349
		CASE 203
			LOCAL:2 = 350
			LOCAL:3 = 399
		CASE 204
			LOCAL:2 = 400
			LOCAL:3 = 419
		CASE 205
			LOCAL:2 = 420
			LOCAL:3 = 439
		CASE 206
			LOCAL:2 = 440
			LOCAL:3 = 459
		CASE 207
			LOCAL:2 = 460
			LOCAL:3 = 479
		CASE 208
			LOCAL:2 = 480
			LOCAL:3 = 499
		CASE 209
			LOCAL:2 = 500
			LOCAL:3 = 519
		CASE 210
			LOCAL:2 = 520
			LOCAL:3 = 539
		CASE 211
			LOCAL:2 = 540
			LOCAL:3 = 559
		CASE 212
			LOCAL:2 = 560
			LOCAL:3 = 579
		CASE 213
			LOCAL:2 = 580
			LOCAL:3 = 619
		CASE 214
			LOCAL:2 = 20
			LOCAL:3 = 39
		CASE 215
			IF GETBIT(TALENT:ARG:2,1)
				LOCAL:2 = 80
				LOCAL:3 = 89
			ELSE
				LOCAL:2 = 40
				LOCAL:3 = 59
			ENDIF
		CASE 216
			LOCAL:2 = 1
			LOCAL:3 = 19
		CASE 217
			LOCAL:2 = 60
			LOCAL:3 = 79

	ENDSELECT
	DRAWLINE
	IF LOCAL:1 >= 200 && LOCAL:1 <= 217
		FOR LOCAL , LOCAL:2, LOCAL:3 +1
			SIF EQUIP:ARG:LOCAL && CAN_EQUIP(LOCAL)
				PRINTFORM [{LOCAL}] - %EQUIPNAME:LOCAL,20,LEFT% (刺激度:{SUGGESTIVE_POINT(LOCAL),3})
		NEXT
		PRINTL 
		IF LOCAL:1 == 200 || LOCAL:1 == 201
			DRAWLINE
			PRINTL 一体型
			FOR LOCAL,620,720
				SIF EQUIP:ARG:LOCAL
					PRINTFORM [{LOCAL}] - %EQUIPNAME:LOCAL,20,LEFT% (刺激度:{SUGGESTIVE_POINT(LOCAL) * 2,3})
			NEXT
		ENDIF
		IF LOCAL:1 == 202 || LOCAL:1 == 203
			DRAWLINE
			PRINTL 一体型
			FOR LOCAL,720,820
				SIF EQUIP:ARG:LOCAL
					PRINTFORM [{LOCAL}] - %EQUIPNAME:LOCAL,20,LEFT% (刺激度:{SUGGESTIVE_POINT(LOCAL) * 2,3})
			NEXT
		ENDIF
		
		PRINTL 
		PRINTL [900] - なし
		PRINTL [999] - 戻る
		$INPUT_LOOP2
		INPUT
		LOCAL:4 = RESULT
		IF RESULT >= 620 && RESULT < 720 && EQUIP:ARG:RESULT && (LOCAL:1 == 200 || LOCAL:1 == 201)
			;一体型上衣
			IF !ARG:1
				CFLAG:ARG:200 = RESULT
				CFLAG:ARG:201 = RESULT
			ENDIF
			TEQUIP:ARG:200 = RESULT
			TEQUIP:ARG:201 = RESULT
			RESTART
		ELSEIF RESULT >= 720 && RESULT < 820 && EQUIP:ARG:RESULT && (LOCAL:1 == 202 || LOCAL:1 == 203)
			;一体型下着
			IF !ARG:1
				CFLAG:ARG:202 = RESULT
				CFLAG:ARG:203 = RESULT
			ENDIF
			TEQUIP:ARG:202 = RESULT
			TEQUIP:ARG:203 = RESULT
			RESTART
		ELSEIF RESULT >= LOCAL:2 && RESULT <= LOCAL:3 && EQUIP:ARG:RESULT && CAN_EQUIP(RESULT)
			IF (!ARG:1 && CFLAG:ARG:(LOCAL:1) >= 620 && CFLAG:ARG:(LOCAL:1) < 720) || (ARG:1 && TEQUIP:ARG:(LOCAL:1) >= 620 && TEQUIP:ARG:(LOCAL:1) < 720)
				IF !ARG:1
					CFLAG:ARG:200 = 0
					CFLAG:ARG:201 = 0
				ENDIF
				TEQUIP:ARG:200 = 0
				TEQUIP:ARG:201 = 0
			ELSEIF (!ARG:1 && CFLAG:ARG:(LOCAL:1) >= 720 && CFLAG:ARG:(LOCAL:1) < 820) || (ARG:1 && TEQUIP:ARG:(LOCAL:1) >= 720 && TEQUIP:ARG:(LOCAL:1) < 820)
				IF !ARG:1
					CFLAG:ARG:202 = 0
					CFLAG:ARG:203 = 0
				ENDIF
				TEQUIP:ARG:202 = 0
				TEQUIP:ARG:203 = 0
			ENDIF
			SIF !ARG:1
				CFLAG:ARG:(LOCAL:1) = RESULT
			TEQUIP:ARG:(LOCAL:1) = RESULT
			RESTART
		ELSEIF RESULT == 900
			IF (!ARG:1 && CFLAG:ARG:(LOCAL:1) >= 620 && CFLAG:ARG:(LOCAL:1) < 720) || (ARG:1 && TEQUIP:ARG:(LOCAL:1) >= 620 && TEQUIP:ARG:(LOCAL:1) < 720)
				IF !ARG:1
					CFLAG:ARG:200 = 0
					CFLAG:ARG:201 = 0
				ENDIF
				TEQUIP:ARG:200 = 0
				TEQUIP:ARG:201 = 0
			ELSEIF (!ARG:1 && CFLAG:ARG:(LOCAL:1) >= 720 && CFLAG:ARG:(LOCAL:1) < 820) || (ARG:1 && TEQUIP:ARG:(LOCAL:1) >= 720 && TEQUIP:ARG:(LOCAL:1) < 820)
				IF !ARG:1
					CFLAG:ARG:202 = 0
					CFLAG:ARG:203 = 0
				ENDIF
				TEQUIP:ARG:202 = 0
				TEQUIP:ARG:203 = 0
			ENDIF
			SIF !ARG:1
				CFLAG:ARG:(LOCAL:1) = 0
			TEQUIP:ARG:(LOCAL:1) = 0
			RESTART
		ELSEIF RESULT == 999
			RESTART
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF

	ENDIF
ENDIF





@SET_CLOTHES(ARG)
VARSET LOCAL
;設定してある服装に着替える
FOR LOCAL,200,218
	TEQUIP:ARG:LOCAL = CFLAG:ARG:LOCAL
NEXT

@CLOTHES_REGISTRATION(ARG)
VARSET LOCAL
PRINTL 何番に登録しますか？
FOR LOCAL,1,10
	PRINTFORM [{LOCAL,2}] - 
	IF CSTR:ARG:LOCAL == ""
		PRINT 未登録
	ELSE
		PRINT 登録済み
	ENDIF
	PRINTL 
NEXT
PRINTL [10] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 10
	RETURN 0
ELSEIF RESULT < 1 || RESULT >= 10
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
PRINTL コメントが書けます(書かなくても可)
INPUTS
SIF RESULTS != ""
	CSTR:ARG:(LOCAL:1 + 10) = %RESULTS%
CSTR:ARG:(LOCAL:1) = ""

FOR LOCAL,200,218
	LOCALS = /%TOSTR(LOCAL)%,%TOSTR(TEQUIP:ARG:LOCAL)%
	CSTR:ARG:(LOCAL:1) += LOCALS
NEXT
CSTR:ARG:(LOCAL:1) += "/"

@CHANGE_CLOTHES_REGISTRATION(ARG,ARG:1)
VARSET LOCAL
PRINTL 何番の服装に着替えますか？
FOR LOCAL,0,10
	PRINTFORM [{LOCAL,2}] - 
	IF CSTR:ARG:LOCAL == ""
		PRINT 未登録
	ELSE
		IF CSTR:ARG:(LOCAL + 10) == ""
			PRINT 登録済み
		ELSE
			PRINTFORM %CSTR:ARG:(LOCAL + 10)%
		ENDIF
	ENDIF
	PRINTL 
NEXT
PRINTL [10] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 10
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= 10 || CSTR:ARG:RESULT == ""
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
LOCAL:2 = RESULT

FOR LOCAL,200,218
	FOR LOCAL:1,0,1000
		LOCALS = /{LOCAL},{LOCAL:1}/
		IF STRCOUNT(CSTR:ARG:(LOCAL:2),LOCALS)
			SIF !ARG:1
				CFLAG:ARG:LOCAL = LOCAL:1
			TEQUIP:ARG:LOCAL = LOCAL:1
			BREAK
		ENDIF
	NEXT
NEXT
