@CUSTOM_CHARAMAKE(ARG)

CALL NAME_CUSTOM(ARG)

CALL CUSTOM_TERMINAL(ARG)

@CUSTOM_TERMINAL(ARG)
;排他処理
IF TALENT:ARG:2 & 1
	ABL:ARG:ホモっ気 = 0
	ABL:ARG:ゲイ中毒 = 0
	EXP:ARG:ゲイ経験 = 0
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精经验 = 0
ELSE
	ABL:ARG:Ｖ感觉 = 0
	ABL:ARG:百合属性 = 0
	ABL:ARG:百合成瘾 = 0
	EXP:ARG:百合经验 = 0
	EXP:ARG:喷乳经验 = 0
	EXP:ARG:Ｖ经验 = 0
	EXP:ARG:乳交経験 = 0
	EXP:ARG:Ｖ性交经验 = 0
	EXP:ARG:Ｖ扩张经验 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:处女 = 0
	TALENT:ARG:母乳体质 = 0
	TALENT:ARG:胸围サイズ = 0
ENDIF
IF TALENT:ARG:2 & 2
	;MAXBASE:ARG:精力 = 10000
	;BASE:ARG:精力 = 10000
ELSE
	TALENT:ARG:童貞 = 0
	;MAXBASE:ARG:精力 = 0
	;BASE:ARG:精力 = 0
ENDIF
IF TALENT:ARG:处女 == 1
	ABL:ARG:Ｖ感觉 = 0
	EXP:ARG:Ｖ经验 = 0
	EXP:ARG:Ｖ性交经验 = 0
	EXP:ARG:Ｖ扩张经验 = 0
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ经验 == 0
	EXP:ARG:Ｖ经验 = 1
ENDIF

DRAWLINE
PRINTSL NAME:ARG
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
PRINT 气力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
PRINTL 
CALL PRINT_STATE_ABL(ARG)
PRINTL 
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
CALL PRINT_STATE_EXP(ARG)
PRINTL
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL これで本当によろしいですか？
;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG,LOCAL:101)
;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ENDIF

DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ? はい # 決定\@  [1001] - 名前の変更  [1002] - 体力、气力の設定
PRINTL [1003] - 素質の設定  [1004] - 能力の設定  [1005] - 経験の設定
INPUT
IF RESULT == 1000 && LOCAL:100 == 1000
	VARSET LOCAL
	RETURN 1
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF
;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 3000
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 3000
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
;素質設定
ELSEIF LOCAL:100 == 1003
	;ページ送り
	SIF RESULT == 998 && LOCAL:101
		LOCAL:101 --
	SIF RESULT == 999
		LOCAL:101 ++
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;陥落素質ほか
	SIF (RESULT >= 3 && RESULT <= 5) || RESULT == 9 || RESULT == 34 || (RESULT >= 72 && RESULT <= 76) || RESULT == 84 || RESULT == 85 || (RESULT >= 153 && RESULT <= 155)
		RESTART
	;性別
	IF RESULT == 2
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;性別嗜好
	ELSEIF RESULT == 81
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
	;量的素質
	ELSE
		;性別
		IF LOCAL == 2
			IF TALENT:ARG:RESULT == 3
				TALENT:ARG:RESULT = 1
			ELSE
				TALENT:ARG:RESULT ++
			ENDIF
		;端まで行くと-1を掛ける
		ELSEIF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;中毒関係
	SIF LOCAL >= 30
		RESTART
	;奇数(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
	;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 51
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 52
		RESTART
	ENDIF
	PRINTFORM %EXPNAME:LOCAL%を入力してください      
	PRINTBUTTON "[キャンセル]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT,5000)
ELSEIF LOCAL:100 == 1006
	
ENDIF
RESTART

;名前の設定
@NAME_CUSTOM(ARG)
PRINTL 请输入角色的名字
IF STRLENS(NAME:ARG)
	PRINTFORM 現在のNAME：%NAME:ARG%      
	PRINTBUTTON "[不变]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 请输入角色的昵称（常用这个称呼）
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在のCALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[不变]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
PRINT 气力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 3000
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
;素質の設定
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS,35,LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 前のページ                 ",998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 前のページ",35,LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 3
	PRINTBUTTON "[999] - 次のページ",999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 次のページ",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 51
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 52
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

;------------------------------------------------------------
;家族構成設定
@CUSTOM_FAMILY
VARSET LOCAL
LOCAL:0 = 19
	;0:姐姐 1:姐姐 2:姐姐 3:姐姐 4:妹妹 5:妹妹

PRINTL 决定家庭的构成吧
$INPUT_LOOP
PRINTL ---------------------------
FOR LOCAL:1, 0,6

	LOCALS = [{(LOCAL:1+1),3}]
	
	SELECTCASE LOCAL:1
		CASE 0
			LOCALS += "姐姐"
		CASE 1
			LOCALS += "姐姐"
		CASE 2
			LOCALS += "姐姐"
		CASE 3
			LOCALS += "姐姐"
		CASE 4
			LOCALS += "妹妹"
		CASE 5
			LOCALS += "妹妹"
		CASEELSE
			LOCALS += "????"
	ENDSELECT
	
	GETBIT LOCAL:0, LOCAL:1
	SIF RESULT:0 != 1
		SETCOLOR 100,100,100
	
	PRINTFORM %LOCALS,15,LEFT%

	SIF LOCAL:1 % 2 == 1
	PRINTL
	
	RESETCOLOR
NEXT
PRINTL ---------------------------

PRINTBUTTON "[1000] - 決定",1000
	PRINTL
INPUT

PRINTL

IF RESULT == 1000 

	FOR LOCAL:2,2,100
		
		SELECTCASE LOCAL:2
		CASE 10
			GETBIT LOCAL:0, 0
		CASE 11
			GETBIT LOCAL:0, 1
		CASE 14
			GETBIT LOCAL:0, 2
		CASE 15
			GETBIT LOCAL:0, 3
		CASE 12
			GETBIT LOCAL:0, 4
		CASE 13
			GETBIT LOCAL:0, 5
		CASEELSE
			RESULT:0 = 1
		ENDSELECT
		SIF RESULT:0 == 1 && EXISTCSV(LOCAL:2,0)
			ADDCHARA LOCAL:2

	NEXT

	RETURN 0
ELSE
	LOCAL:2 = RESULT-1
	;PRINTFORM RESULT
	INVERTBIT LOCAL:0, LOCAL:2
	GOTO INPUT_LOOP
ENDIF


;------------------------------------------------------------
;他人の初期値設定
@CUSTM_OTHER
VARSET LOCAL
PRINTL 要改变别人的初期值么？
PRINTL [0] - 是
PRINTL [1] - 不用
$INPUT_LOOP
INPUT

IF RESULT == 0

ELSEIF RESULT == 1
	RETURN 1
ELSE
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

$INPUT_LOOP2
PRINTL 请选择要改变的人
FOR LOCAL,2,CHARANUM
	PRINTFORML [{LOCAL,2}] - %CALLNAME:LOCAL%
NEXT
PRINTL
PRINTL [1000] - 決定
INPUT

IF RESULT == 1000
	RETURN 1
ELSEIF RESULT > 1 && RESULT < CHARANUM
	CALL CUSTOM_CHARAMAKE(RESULT)
	GOTO INPUT_LOOP2
ELSE
	REUSELASTLINE
	GOTO INPUT_LOOP2
ENDIF

