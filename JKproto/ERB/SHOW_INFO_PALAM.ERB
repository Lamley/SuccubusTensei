@SHOW_INFO_PALAM
PRINTL
VARSET LOCAL
IF TARGET
	PRINTL                         体力 气力 興奮 理性 Ｃ Ｖ Ａ Ｂ 潤 恭 欲 屈 習 恥 痛 恐 好 越 反 不 抑

	FOR LOCAL,1,CHARANUM
		SIF LOCAL != MASTER && TCVAR:LOCAL:行為対象 != MASTER
			CONTINUE
		PRINTFORM %\@LOCAL == TARGET?☆#　\@%
		IF LOCAL == MASTER
			PRINTFORM       
		ELSE
			PRINTFORM [{LOCAL + 2000}]
		ENDIF
		PRINTFORM %CALLNAME:LOCAL,16,LEFT%
		PRINTFORM {BASE:LOCAL:体力,4} 
		PRINTFORM {BASE:LOCAL:气力,4} 
		PRINTFORM {BASE:LOCAL:興奮度,4} 
		PRINTFORM {BASE:LOCAL:理性,4} 

		FOR LOCAL:1 , 0,100
			SIF !STRLENS(PALAMNAME:(LOCAL:1))
				CONTINUE
			PRINTFORM {GETPALAMLV(PALAM:LOCAL:(LOCAL:1),15),2} 
		NEXT
		PRINTL 
	NEXT


ELSE
	FOR LOCAL,0,100
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		PRINTFORM  %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:MASTER:LOCAL,15),2}[
		IF GETPALAMLV(PALAM:MASTER:LOCAL,15) >= 15
			PRINT ********
		ELSE
			;現在のPALAMLV
			LOCAL:1 = PALAMLV:(GETPALAMLV(PALAM:MASTER:LOCAL,15) + 1) - PALAMLV:(GETPALAMLV(PALAM:MASTER:LOCAL,15))
			FOR LOCAL:10,0,8
				IF PALAM:MASTER:LOCAL - PALAMLV:(GETPALAMLV(PALAM:MASTER:LOCAL,15)) > LOCAL:1 * LOCAL:10 / 8
					PRINT *
				ELSE
					PRINT .
				ENDIF
			NEXT
		ENDIF
		PRINT ]
		LOCAL:1 = PALAM:MASTER:LOCAL
		LOCAL:2 = 0
		;これだけ特殊
		IF LOCAL == 9 && LOCAL:1 >= 20000
			PRINTFORM %"摩擦 0",6% 
		ELSE
			;桁合わせ
			FOR LOCAL:10,0,10
				IF LOCAL:1 > 999999
					LOCAL:1 /= 1000
					LOCAL:2 ++
				ENDIF
			NEXT
			PRINTFORM {LOCAL:1,6}
			SELECTCASE LOCAL:2
				CASE 0
					PRINT  
				CASE 1
					PRINT k
				CASE 2
					PRINT M
				CASE 3
					PRINT G
				CASE 4
					PRINT T
				CASE 5
					PRINT P
			ENDSELECT
		ENDIF
		LOCAL:3 ++
		SIF LOCAL:3 % PRINTCPERLINE() == 0
			PRINTL 
	NEXT
	PRINTL
ENDIF